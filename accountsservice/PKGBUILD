
pkgname=accountsservice
pkgver=22.04.62
pkgrel=1
pkgdesc="Set of D-Bus interfaces for querying and manipulating user account information."
arch=('x86_64')
url="https://gitlab.freedesktop.org/accountsservice/accountsservice"
license=('GPL3')
depends=('dbus' 'glib2' 'polkit')
makedepends=('meson' 'ninja')
source=("https://www.freedesktop.org/software/accountsservice/${pkgname}-${pkgver}.tar.xz")
sha256sums=('e8ee6b95aa073fcbd5007726676de16aba98373c20d44560df192c0a58cb7bb5')

build() {
  mkdir -p build
  cd build

  meson ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    --libexecdir=lib \
    -Dadmin_group=wheel \
    -Dintrospection=false
  ninja
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
}

