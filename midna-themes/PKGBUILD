
pkgname=midna-themes
pkgver=6.0.1
_commit=753f2e6ecbce29343bf4095c31f7af4316c44b40
pkgrel=1
pkgdesc='KaOS Plasma 5 Look & Feel theme files'
url='https://kaosx.us'
arch=('x86_64')
license=('LGPL')
depends=('plasma-framework' 'qtcurve' 'ttf-raleway' 'kvantum')
makedepends=('extra-cmake-modules')
conflicts=('kde-kdm-themes-midna'
           'kde-ksplash-themes-midna'
           'kde-plasma-themes-midna' 
           'midna-colors' 
           'wallpapers-midna')
replaces=('kde-kdm-themes-midna'
          'kde-ksplash-themes-midna'
          'kde-plasma-themes-midna' 
          'midna-colors' 
          'wallpapers-midna')
source=("https://github.com/KaOSx/midna/archive/${_commit}.zip")
md5sums=('98958fae426b3270fa1281b6f36bb111')

build() {
  cmake -B build -S midna-${_commit} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON  
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
