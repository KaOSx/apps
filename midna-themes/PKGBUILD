
pkgname=midna-themes
pkgver=6.0.6
_commit=4a481b078659053ce31619060261d79772a65a67
pkgrel=1
pkgdesc='KaOS Plasma 5 Look & Feel theme files'
url='https://kaosx.us'
arch=('x86_64')
license=('LGPL')
depends=('plasma-framework' 'qtcurve' 'ttf-raleway' 'kvantum')
makedepends=('extra-cmake-modules')
conflicts=('kde-kdm-themes-midna'
           'kde-ksplash-themes-midna'
           'kde-plasma-themes-midna' 
           'midna-colors' 
           'wallpapers-midna')
replaces=('kde-kdm-themes-midna'
          'kde-ksplash-themes-midna'
          'kde-plasma-themes-midna' 
          'midna-colors' 
          'wallpapers-midna')
source=("https://github.com/KaOSx/midna/archive/${_commit}.zip")
md5sums=('f45e76944a87c2130f98a48557693065')

build() {
  cmake -B build -S midna-${_commit} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON  
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
