
pkgname=midna-themes
pkgver=7.0.1
_commit=29589d787d057ca16828daf430258a780e3e00fe
pkgrel=1
pkgdesc='KaOS Plasma 6 Look & Feel theme files'
url='https://kaosx.us'
arch=('x86_64')
license=('LGPL')
depends=('libplasma' 'ttf-raleway' 'kvantum-qt6')
makedepends=('extra-cmake-modules')
conflicts=('midna-themes6')
replaces=('midna-themes6')
source=("https://github.com/KaOSx/midna/archive/${_commit}.zip")
md5sums=('e95d7f6be87fd2b61d12a142d8f8e274')

prepare() {
  cd midna-${_commit}

  sed -i -e 's|WindowSystem))|WindowSystem)|' CMakeLists.txt
}

build() {
  cmake -B build -S midna-${_commit} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DQT_MAJOR_VERSION=6
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
