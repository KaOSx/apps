
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=plasma-desktop
pkgver=${_plasmaver}
pkgrel=1
pkgdesc='KDE Plasma Desktop'
arch=('x86_64')
url='https://github.com/KDE/plasma-desktop'
license=('LGPL')
depends=('qt5-graphicaleffects' 'libxkbfile' 'libusb' 'libcanberra' 'polkit-kde-agent-1'
         'systemsettings' 'ksysguard' 'powerdevil' 'kmenuedit' 'kpeople' 
         'kdbusaddons' 'plasma-integration' 'xorg-server' 'signon-oauth2' 'accountsservice'
         'kactivities-stats' 'kaccounts-integration' 'kio-fuse' 'xf86-input-evdev' 'xf86-input-libinput')
makedepends=('extra-cmake-modules' 'kdoctools' 'xf86-input-synaptics' 'boost' 'intltool')
groups=('kdeplasma5')
conflicts=('user-kcm')
replaces=('user-kcm')
provides=('user-kcm')
source=("https://download.kde.org/stable/plasma/5.18.1/${pkgname}-${pkgver}.tar.xz"
        'layout_desktop_panel.diff'
        'main_desktop_kicker.diff'
        "https://invent.kde.org/plasma/plasma-desktop/-/commit/64546c81d43f8b10487d9c6c5bb0fab4de457d13.diff")
md5sums=(`grep ${pkgname}-${_plasmaver}.tar.xz ../kf5.md5 | cut -d" " -f1`
         '4cee67971dd83b85ab313acd152bfcd2'
         '22b66401945f92bd9f99e6d334bc306e'
         '893b8a7f6a45023f29b58b85f2ee31bc')

prepare() {
  cd ${pkgname}-${pkgver}
  # adjust default settings for panel, kicker & desktop view
  patch -p1 -i ${srcdir}/layout_desktop_panel.diff
  patch -p1 -i ${srcdir}/main_desktop_kicker.diff
  sed -i 's|org.kde.plasma.folder|org.kde.desktopcontainment|' desktoppackage/contents/defaults
  # 5.21 keyboard repeat migration fail
  #patch -p1 -i ${srcdir}/64546c81d43f8b10487d9c6c5bb0fab4de457d13.diff
}

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_LIBEXECDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_QMLDIR=/usr/lib/qt5/qml \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
