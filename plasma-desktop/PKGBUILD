
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=plasma-desktop
pkgver=${_plasmaver}
pkgrel=2
pkgdesc='KDE Plasma Desktop'
arch=('x86_64')
url='https://github.com/KDE/plasma-desktop'
license=('LGPL')
depends=('qt5-graphicaleffects' 'libxkbfile' 'libusb' 'libcanberra' 'polkit-kde-agent-1'
         'systemsettings' 'ksysguard' 'powerdevil' 'kmenuedit' 'kpeople' 
         'kdbusaddons' 'plasma-integration' 'xf86-input-synaptics' 'xorg-server'
         'kactivities-stats' 'xf86-input-evdev' 'xf86-input-libinput')
makedepends=('extra-cmake-modules' 'kdoctools' 'boost')
groups=('kdeplasma5')
conflicts=('kde-workspace' 'kcm-touchpad')
replaces=('kde-workspace' 'kcm-touchpad')
source=("https://download.kde.org/stable/plasma/5.14.4/${pkgname}-${pkgver}.tar.xz"
#source=("${pkgname}-${pkgver}.tar.gz::https://github.com/KDE/${pkgname}/archive/${_branch}.tar.gz"
        'layout_desktop_panel.diff'
        'main_desktop_kicker.diff')
md5sums=(`grep ${pkgname}-${_plasmaver}.tar.xz ../kf5.md5 | cut -d" " -f1`
         '5a22b33c2f9ed93c8b8f580601e00a8e'
         '22b66401945f92bd9f99e6d334bc306e')

prepare() {
  cd ${pkgname}-${pkgver}
  # prep for plasma 5.5, move to noto, no longer use oxygen-fonts in KaOS, uncomment with 5.5
  sed -i -e 's|QFont f1("Oxygen Mono", 9); // fixed font|QFont f1("DejaVu Sans Mono", 9); // fixed font|' kcms/fonts/fonts.cpp
  # adjust default settings for panel, kicker & desktop view
  patch -p1 -i ${srcdir}/layout_desktop_panel.diff
  patch -p1 -i ${srcdir}/main_desktop_kicker.diff
  sed -i 's|org.kde.plasma.folder|org.kde.desktopcontainment|' desktoppackage/contents/defaults
  
  rm -rf po/id/docs
}

build() {
  mkdir -p build
  
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_LIBEXECDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_QMLDIR=/usr/lib/qt5/qml \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build

  make DESTDIR=${pkgdir} install
}
