
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=plasma-desktop
pkgver=${_plasmaver}
pkgrel=1
pkgdesc='KDE Plasma Desktop'
arch=('x86_64')
url='https://invent.kde.org/plasma/plasma-desktop'
license=('LGPL')
depends=('qt6-svg' 'libxkbfile' 'libusb' 'libcanberra' 'polkit-kde-agent-1'
         'systemsettings' 'ksystemstats' 'powerdevil' 'kmenuedit' 'kpeople6' 
         'kdbusaddons6' 'qqc2-desktop-style6' 'xorg-server' 'signon-oauth2'
         'plasma-activities-stats' 'kirigami-addons' 'xf86-input-evdev' 
         'xf86-input-libinput' 'xdg-user-dirs' 'plasma-integration' 
         'kaccounts-integration' 'kio-fuse' 'noto-emoji')
makedepends=('extra-cmake-modules' 'kdoctools6' 'boost' 'intltool')
conflicts=('plasma-desktop6' 'latte-dock' 'xf86-input-synaptics')
replaces=('plasma-desktop6' 'latte-dock' 'xf86-input-synaptics')
groups=('kdeplasma6')
source=("https://download.kde.org/stable/plasma/${pkgver}/${pkgname}-${pkgver}.tar.xz"
        'layout_desktop_panel.diff'
        'main_desktop_kicker.diff'
        'main_desktop_kickoff.diff'
        'main_taskmanager.diff'
        "https://invent.kde.org/plasma/plasma-desktop/-/commit/1dfba7045c82ae2553be2cb7018fcd893c19d7bc.diff")
md5sums=(`grep ${pkgname}-${_plasmaver}.tar.xz ../kf6.md5 | cut -d" " -f1`
         '6e13e785600f1a6c1e181ab2934f1558'
         'a5d83777cc835399a7c825088548cb5d'
         'c07c318e7b8a3b9616924ff6502de1ef'
         '46b4248270d75f1c7e481145c5c797a7')

prepare() {
  cd ${pkgname}-${pkgver}
  # adjust default settings for panel, kickoff, kicker & desktop view
  patch -p1 -i ${srcdir}/layout_desktop_panel.diff
  patch -p1 -i ${srcdir}/main_desktop_kicker.diff
  patch -p1 -i ${srcdir}/main_desktop_kickoff.diff
  patch -p1 -i ${srcdir}/main_taskmanager.diff
  sed -i 's|org.kde.plasma.folder|org.kde.desktopcontainment|' desktoppackage/contents/defaults
  #patch -p1 -i ${srcdir}/1dfba7045c82ae2553be2cb7018fcd893c19d7bc.diff
}

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_LIBEXECDIR=/usr/lib \
    -DBUILD_TESTING=OFF \
    -DCMAKE_DISABLE_FIND_PACKAGE_PackageKitQt6=ON
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
