
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=plasma-desktop
pkgver=${_plasmaver}
pkgrel=2
pkgdesc='KDE Plasma Desktop'
arch=('x86_64')
url='https://invent.kde.org/plasma/plasma-desktop'
license=('LGPL')
depends=('qt5-graphicaleffects' 'libxkbfile' 'libusb' 'libcanberra' 'polkit-kde-agent-1'
         'systemsettings' 'ksystemstats' 'powerdevil' 'kmenuedit' 'kpeople' 
         'kdbusaddons' 'plasma-integration' 'xorg-server' 'signon-oauth2'
         'kactivities-stats' 'kaccounts-integration' 'kio-fuse' 'xf86-input-evdev' 
         'xf86-input-libinput' 'xdg-user-dirs' 'kdelibs4support')
makedepends=('extra-cmake-modules' 'kdoctools' 'xf86-input-synaptics' 'boost' 'intltool')
groups=('kdeplasma5')
conflicts=('user-kcm')
replaces=('user-kcm')
provides=('user-kcm')
source=("https://download.kde.org/stable/plasma/5.23.90/${pkgname}-${pkgver}.tar.xz"
        'layout_desktop_panel.diff'
        'main_desktop_kicker.diff'
        'main_desktop_kickoff.diff'
        'automountersettings.diff')
md5sums=(`grep ${pkgname}-${_plasmaver}.tar.xz ../kf5.md5 | cut -d" " -f1`
         '6e13e785600f1a6c1e181ab2934f1558'
         '63f0088dd9e7a9dfc341650b0f91c447'
         'bc7c07f2754de81b64ead90aa20b1020'
         '71302e4853361fcb25afa6994a4852e1')

prepare() {
  cd ${pkgname}-${pkgver}
  # adjust default settings for panel, kickoff, kicker & desktop view
  patch -p1 -i ${srcdir}/layout_desktop_panel.diff
  patch -p1 -i ${srcdir}/main_desktop_kicker.diff
  patch -p1 -i ${srcdir}/main_desktop_kickoff.diff
  sed -i 's|org.kde.plasma.folder|org.kde.desktopcontainment|' desktoppackage/contents/defaults
  # 5.24
  #patch -p1 -i ${srcdir}/automountersettings.diff
}

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_LIBEXECDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_QMLDIR=/usr/lib/qt5/qml \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF \
    -DCMAKE_DISABLE_FIND_PACKAGE_PackageKitQt5=ON
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
