
# perl issue on server, build locally only
pkgname=pidgin
pkgver=2.14.12
pkgrel=2
pkgdesc="Multi-protocol instant messaging client"
arch=('x86_64')
url="https://pidgin.im/"
license=('GPL')
depends=('startup-notification' 'gtk2' 'libxss' 'hicolor-icon-theme'
         'libsm' 'libidn' 'avahi' 'gst-plugins-good' 'farstream' 'libgadu')
makedepends=('nss' 'libsasl' 'python3' 'tk' 'ca-certificates' 'intltool' )
optdepends=('kde-gtk-config: gtk integration settings for Plasma')
conflicts=('libpurple')
provides=('libpurple')
replaces=('libpurple')
options=('!libtool')
source=("https://downloads.sourceforge.net/pidgin/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('2b05246be208605edbb93ae9edc079583d449e2a9710db6d348d17f59020a4b7')

build() {
  cd ${pkgname}-${pkgver}

    ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --disable-schemas-install \
        --disable-meanwhile \
        --disable-gnutls \
        --enable-cyrus-sasl \
        --disable-nm \
        --with-system-ssl-certs=/etc/ssl/certs \
        --disable-gtkspell \
        --disable-gevolution
    make
}

package(){
    cd ${pkgbase}-${pkgver}

    make DESTDIR=${pkgdir} install
}


