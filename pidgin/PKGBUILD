
# perl issue on server, build locally only
pkgname=pidgin
pkgver=2.14.7
pkgrel=1
pkgdesc="Multi-protocol instant messaging client"
arch=('x86_64')
url="https://pidgin.im/"
license=('GPL')
depends=("libpurple=${pkgver}" 'startup-notification' 'gtk2' 'libxss' 'hicolor-icon-theme'
         'libsm' 'libidn' 'avahi' 'gst-plugins-good' 'farstream')
makedepends=('nss' 'libsasl' 'python3' 'tk' 'ca-certificates' 'intltool' )
optdepends=('kde-gtk-config: gtk integration settings for Plasma')
options=('!libtool')
source=("https://downloads.sourceforge.net/pidgin/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('fea6ab4f0572fe24646049c2b3fecbdca27abca6d06e95bd655e44db99bd69fe')

build() {
  cd ${pkgname}-${pkgver}

    ./configure --prefix=/usr \
                --sysconfdir=/etc \
                --disable-schemas-install \
                --disable-meanwhile \
                --disable-gnutls \
                --enable-cyrus-sasl \
                --disable-nm \
                --with-python=/usr/bin/python3 \
                --with-system-ssl-certs=/etc/ssl/certs \
                --disable-gtkspell \
                --disable-gevolution
    make
}

package(){
    cd ${pkgbase}-${pkgver}
    #for linking
    make -C libpurple DESTDIR=${pkgdir} install-libLTLIBRARIES

    make -C pidgin DESTDIR=${pkgdir} install
    make -C doc DESTDIR=${pkgdir} install

    #clean up libpurple
    make -C libpurple DESTDIR=${pkgdir} uninstall-libLTLIBRARIES

    install -D -m 0644 pidgin/data/pidgin.desktop ${pkgdir}/usr/share/applications/pidgin.desktop

    rm -f ${pkgdir}/usr/share/man/man1/finch.1
}


