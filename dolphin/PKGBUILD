
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=dolphin
pkgver=${_kdever}
pkgrel=1
pkgdesc="KDE File Manager"
arch=('x86_64')
url='https://projects.kde.org/projects/kde/applications/dolphin'
license=('LGPL')
depends=('kinit' 'kcmutils' 'knewstuff' 'kcoreaddons' 'ki18n' 'kdbusaddons' 'kbookmarks' 'kconfig' 'kio' 'kparts' 'solid'
         'kiconthemes' 'kcompletion' 'ktextwidgets' 'kwindowsystem' 'knotifications' 'baloo-widgets' 'kactivities' 
         'kfilemetadata' 'kde-cli-tools') 
makedepends=('extra-cmake-modules' 'kdoctools')
source=("$_mirror/${pkgname}-${_kdever}.tar.xz"
        "https://github.com/KDE/dolphin/commit/0bdd8e0b0516555c6233fdc7901e9b417cf89791.diff"
        'dolphinui.diff')
md5sums=(`grep ${pkgname}-${_kdever}.tar.xz ../kde-sc.md5 | cut -d" " -f1`
         '213f3dbcdd374f1f74a87a08d28de0fd'
         '8e49d1b5ba044959f2fb24f1cf80aaaa')
groups=('kde-system')

prepare() {
  cd $pkgname-${pkgver}
  # keep kdesu option for dolphin, comment out for now, wait for upstream decision
  patch -R -p1 -i ${srcdir}/0bdd8e0b0516555c6233fdc7901e9b417cf89791.diff
  # set KaOS defaults
  patch -p1 -i ${srcdir}/dolphinui.diff
}

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DKDE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build

  make DESTDIR="${pkgdir}" install
}
