
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=dolphin
pkgver=${_kdever}
pkgrel=2
pkgdesc="KDE File Manager"
arch=('x86_64')
url='https://projects.kde.org/projects/kde/applications/dolphin'
license=('LGPL')
depends=('kinit' 'kcmutils' 'knewstuff' 'kcoreaddons' 'ki18n' 'kdbusaddons' 'kbookmarks' 'kconfig' 'kio' 'kparts' 'solid'
         'kiconthemes' 'kcompletion' 'ktextwidgets' 'kwindowsystem' 'knotifications' 'baloo-widgets' 'kactivities' 
         'kfilemetadata' 'kde-cli-tools') 
makedepends=('extra-cmake-modules' 'kdoctools' 'ruby')
groups=('kde-system')
source=("$_mirror/${pkgname}-${_kdever}.tar.xz"
        'kdesu.diff'
        'dolphinui.diff'
        "https://invent.kde.org/system/dolphin/-/commit/57909ebe66fd4aa83d1fbaf2c746d408451e80cb.diff")
md5sums=(`grep ${pkgname}-${_kdever}.tar.xz ../kde-sc.md5 | cut -d" " -f1`
         'b4f0491bacf3c9f923f2ee0810cc3576'
         'e0211cc01ceb848101e19cbd0bd95fcf'
         'b03e2661a955a44e278030d01dbd44d1')

prepare() {
  cd $pkgname-${pkgver}
  # keep kdesu option for dolphin, comment out for now, wait for upstream decision
  patch -p1 -i ${srcdir}/kdesu.diff
  # set KaOS defaults
  patch -p1 -i ${srcdir}/dolphinui.diff
  #sed -i 's|5.81.0|5.82.0|' CMakeLists.txt
  patch -p1 -i ${srcdir}/57909ebe66fd4aa83d1fbaf2c746d408451e80cb.diff
}

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DKDE_INSTALL_LIBDIR=lib
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
