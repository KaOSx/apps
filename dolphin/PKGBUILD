
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=dolphin
pkgver=${_kdever}
pkgrel=1
pkgdesc="KDE File Manager"
arch=('x86_64')
url='https://projects.kde.org/projects/kde/applications/dolphin'
license=('LGPL')
depends=('kinit' 'kcmutils' 'knewstuff' 'kcoreaddons' 'ki18n' 'kdbusaddons' 'kbookmarks' 'kconfig' 'kio' 'kparts' 'solid'
         'kiconthemes' 'kcompletion' 'ktextwidgets' 'kwindowsystem' 'knotifications' 'baloo-widgets' 'kactivities' 
         'kfilemetadata' 'kde-cli-tools') 
makedepends=('extra-cmake-modules' 'kdoctools' 'ruby')
groups=('kde-system')
source=("$_mirror/${pkgname}-${_kdever}.tar.xz"
        'kdesu.diff'
        'dolphinui.diff'
        "https://invent.kde.org/system/dolphin/-/commit/27bfcde4efaf936243fc41e4a61d0cac32105ef6.diff"
        "https://github.com/KDE/dolphin/commit/ab9644d0ba0efac2f2e7512abc937a5b3e4c96c4.diff")
md5sums=(`grep ${pkgname}-${_kdever}.tar.xz ../kde-sc.md5 | cut -d" " -f1`
         '759a01a07a1f4379d182b60dcb84fae9'
         'e0211cc01ceb848101e19cbd0bd95fcf'
         '0c321ada7e9b729ebbd887f534c52fd0'
         'a910c075a70f054baf5f22a12e8ebab3')

prepare() {
  cd $pkgname-${pkgver}
  # keep kdesu option for dolphin, comment out for now, wait for upstream decision
  patch -p1 -i ${srcdir}/kdesu.diff
  # set KaOS defaults
  patch -p1 -i ${srcdir}/dolphinui.diff
  # KDE Bug 441072
  sed -i 's|5.81.0|5.82.0|' CMakeLists.txt
  patch -p1 -i ${srcdir}/27bfcde4efaf936243fc41e4a61d0cac32105ef6.diff
  # KDE Bug 442815
  #patch -p1 -i ${srcdir}/ab9644d0ba0efac2f2e7512abc937a5b3e4c96c4.diff
}

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DKDE_INSTALL_LIBDIR=lib
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
