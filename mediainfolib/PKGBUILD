
pkgname=mediainfolib
_pkgname=MediaInfoLib
pkgver=19.04
pkgrel=1
pkgdesc="Convenient unified display of the most relevant technical and tag data for video and audio files, shared libraries"
arch=('x86_64')
url="https://github.com/MediaArea/MediaInfoLib"
license=('GPL')
depends=('curl' 'libmms' 'zenlib')
makedepends=('libtool')
conflicts=('libmediainfo')
replaces=('libmediainfo')
source=("https://github.com/MediaArea/MediaInfoLib/archive/v${pkgver}.tar.gz")
md5sums=('138f92c1a6416982359453ef4327058f')

build() {
  cd ${_pkgname}-${pkgver}/Project/GNU/Library
  
  ./autogen.sh
  ./configure --prefix=/usr \
    --enable-shared \
    --disable-static \
    --with-libcurl \
    --with-libmms
  make
}
package() {
  cd ${_pkgname}-${pkgver}/Project/GNU/Library
  make DESTDIR=${pkgdir} install
  
  cd ../../..

  install -dm 755 ${pkgdir}/usr/lib/pkgconfig
  install -m 644 Project/GNU/Library/libmediainfo.pc $pkgdir/usr/lib/pkgconfig
}
