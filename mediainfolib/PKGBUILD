
pkgname=mediainfolib
_pkgname=libmediainfo
__pkgname=MediaInfoLib
pkgver=22.03
pkgrel=1
pkgdesc="Convenient unified display of the most relevant technical and tag data for video and audio files, shared libraries"
arch=('x86_64')
url="https://github.com/MediaArea/MediaInfoLib"
license=('GPL')
depends=('curl' 'libmms' 'zenlib')
makedepends=('libtool')
conflicts=('libmediainfo')
replaces=('libmediainfo')
#source=("https://github.com/MediaArea/MediaInfoLib/archive/v${pkgver}.tar.gz")
source=("https://old.mediaarea.net/download/source/libmediainfo/${pkgver}/${_pkgname}_${pkgver}.tar.xz")
md5sums=('905424105bcb27da7c5a50ab152ed273')

build() {
  cd ${__pkgname}/Project/GNU/Library

  ./autogen.sh
  ./configure --prefix=/usr \
    --enable-shared \
    --disable-static \
    --with-libcurl \
    --with-libmms
  make
}
package() {
  cd ${__pkgname}/Project/GNU/Library
  make DESTDIR=${pkgdir} install

  cd ../../..

  install -dm 755 ${pkgdir}/usr/lib/pkgconfig
  install -m 644 Project/GNU/Library/libmediainfo.pc $pkgdir/usr/lib/pkgconfig
}
