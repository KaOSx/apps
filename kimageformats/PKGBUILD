
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=kimageformats
pkgver=${_kf5ver}
pkgrel=4
pkgdesc='Plugins to allow QImage to support extra file formats.'
arch=('x86_64')
url='https://invent.kde.org/frameworks/kimageformats'
license=('LGPL')
depends=('qt5-base' 'openexr' 'karchive' 'libavif' 'jpeg-xl')
makedepends=('extra-cmake-modules')
groups=('kf5')
source=("http://download.kde.org/stable/frameworks/5.99/${pkgname}-${pkgver}.tar.xz"
        "https://invent.kde.org/frameworks/kimageformats/-/commit/350ce1b990460cb2178f369f22fe80803f5645f3.diff"
        "https://invent.kde.org/frameworks/kimageformats/-/commit/1190e53e9b69da6f9663ceb75c4813c5708b7cbd.diff"
        "https://invent.kde.org/frameworks/kimageformats/-/commit/9ac923ad09316dcca0fc11e0be6b3dfc6cce6ca0.diff")
md5sums=(`grep ${pkgname}-$_kf5ver.tar.xz ../kf5.md5 | cut -d" " -f1`
         '22945362bb6aceff48998429140af608'
         '85fb82637acc0a62e47b6322816fb02b'
         '3b366594eeb9df45634d03a370b11249')

prepare() {
  cd ${pkgname}-${pkgver}

  patch -p1 -i ${srcdir}/350ce1b990460cb2178f369f22fe80803f5645f3.diff
  patch -p1 -i ${srcdir}/1190e53e9b69da6f9663ceb75c4813c5708b7cbd.diff
  # libavif 011.0 needed
  patch -R -p1 -i ${srcdir}/9ac923ad09316dcca0fc11e0be6b3dfc6cce6ca0.diff
}

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
