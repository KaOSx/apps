
pkgname=webcamoid
pkgver=9.1.1
_commit=0e85ca398e26752d3cae7c0de200eabd72ac3268
pkgrel=1
pkgdesc="Webcamoid is a full featured webcam capture application."
url='https://webcamoid.github.io/'
license=('GPL')
arch=('x86_64')
depends=('qt5-quickcontrols' 'qt5-quickcontrols2' 'qt5-svg' 'ffmpeg' 'v4l-utils'
         'kdesu' 'pipewire')
makedepends=('qt5-tools' 'cmake')
source=("https://github.com/webcamoid/webcamoid/archive/${pkgver}.tar.gz"
        "https://github.com/webcamoid/webcamoid/commit/3a6426a0d489732fa79648a08aaf9d85c0f1dc8d.diff")
#source=("https://github.com/webcamoid/webcamoid/archive/${_commit}.zip")
md5sums=('39ca28c17a9c1c6380c59e41989ebb0e'
         '3d1a62f176c0b2bbb58526ab6366cff6')

prepare() {
  cd ${pkgname}-${pkgver}
  # ffmpeg 5 needed
  patch -p1 -i ${srcdir}/3a6426a0d489732fa79648a08aaf9d85c0f1dc8d.diff
}

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}

