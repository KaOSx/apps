
pkgname=plasma-applet-weather-widget
pkgver=3.0.3
_commit=b8622221b78abce81de6106e212d24dd721f95bc
pkgrel=1
pkgdesc="Updated version of the plasma-applet-weather-widget by Kotelnik, using the Met.No API instead of the Yr.No API which is no longer operational."
arch=('x86_64')
url="https://github.com/blackadderkate/weather-widget-2/"
license=('GPL')
depends=('libplasma' 'plasma5support')
makedepends=('extra-cmake-modules')
#source=("https://github.com/blackadderkate/weather-widget-2/archive/refs/tags/v${pkgver}.tar.gz")
source=("https://github.com/blackadderkate/weather-widget-2/archive/${_commit}.zip")
md5sums=('74e7c991cb41254d10cec4b1c5b3411f')

build() {
  cmake -B build -S weather-widget-2-${_commit} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
