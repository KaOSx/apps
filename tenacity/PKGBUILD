
pkgname=tenacity
pkgver=1.3.2
_pkgver=1.3.2
# libnyquist
_commit=3678ee6bfff9e0edc721a1b8865ab625e40f9ce4
pkgrel=1
pkgdesc="Easy-to-use, cross-platform multi-track audio editor/recorder"
arch=('x86_64')
url="https://tenacityaudio.org/"
license=('GPL')
depends=('libvorbis' 'libmad' 'mpg123' 'libid3tag' 'lame' 'hicolor-icon-theme' 'ffmpeg'
         'shared-mime-info' 'desktop-file-utils' 'redland' 'jack2' 'soundtouch' 'libsoxr'
         'lilv' 'flac' 'libsndfile' 'libsamplerate' 'suil' 'portmidi' 'wxwidgets'
         'vamp-plugin-sdk' 'portsmf' 'portaudio' 'wavpack' 'libmatroska')
makedepends=('cmake' 'zip' 'pkgconfig')
provides=('audacity')
replaces=('audacity')
source=("https://codeberg.org/tenacityteam/tenacity/archive/v${_pkgver}.tar.gz"
        "https://codeberg.org/tenacityteam/libnyquist/archive/${_commit}.tar.gz")
sha256sums=('a77fe87e0f96adbe7f62956165d96af8e54753b09c661431fd45463441103829'
            '86d9b2d753752a97e169d377da04e843e91131192e2b871c1e600effde032379')

prepare() {

    rm -rf tenacity/lib-src/libnyquist
    ln -s ${srcdir}/libnyquist ${srcdir}/tenacity/lib-src/libnyquist
}

build() {
  cd tenacity
  cmake -B build \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DUSE_FFMPEG=ON\
    -DUSE_SBSMS=OFF \
    -DUSE_LIBTWOLAME=OFF
    #-DPCH=OFF
    #-DCMAKE_SKIP_BUILD_RPATH=ON \
    #-DALWAYS_ALLOW_CONAN_FALLBACK=ON
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install tenacity/build

  rm ${pkgdir}/usr/share/pixmaps/gnome-mime-application-x-audacity-project.xpm
}
