
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=cantor
pkgver=${_kdever}
pkgrel=4
pkgdesc="KDE Frontend to Mathematical Software"
url="https://apps.kde.org/en/cantor"
arch=('x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kconfig' 'kcrash' 'kcompletion' 'knewstuff' 'kiconthemes' 'ktexteditor' 'kcoreaddons'
         'karchive' 'kparts' 'syntax-highlighting' 'ktextwidgets' 'kio' 'kxmlgui' 'ki18n'
         'kalgebra' 'libspectre' 'octave' 'python3' 'libqalculate' 'r' 'poppler' 'qtwebengine')
makedepends=('extra-cmake-modules' 'kdoctools' 'icu' 'qt5-tools')
opdepends=('scilab' 'maxima') 
groups=('kde' 'kde-edu' 'kde-uninstall' 'education')
source=("$_mirror/${pkgname}-${_kdever}.tar.xz")
md5sums=(`grep ${pkgname}-${_kdever}.tar.xz ../kde-sc.md5 | cut -d" " -f1`)

prepare() {
  cd ${pkgname}-${pkgver}

  sed -i -e 's|add_subdirectory(sage)|#add_subdirectory(sage)|' src/backends/CMakeLists.txt
}

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
    -DBUILD_TESTING=OFF 
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
