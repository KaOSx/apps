
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=cantor
pkgver=${_kdever}
pkgrel=1
pkgdesc="KDE Frontend to Mathematical Software"
url="https://apps.kde.org/en/cantor"
arch=('x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kconfig6' 'kcrash6' 'kcompletion6' 'knewstuff6' 'kiconthemes6' 'ktexteditor6' 'kcoreaddons6'
         'karchive6' 'kparts6' 'syntax-highlighting6' 'ktextwidgets6' 'kio6' 'kxmlgui6' 'ki18n6'
         'kalgebra' 'libspectre' 'octave' 'python3' 'libqalculate' 'r' 'poppler' 'qt6-webengine'
         'qt6-5compat')
makedepends=('extra-cmake-modules' 'kdoctools6' 'icu' 'qt6-tools')
opdepends=('scilab' 'maxima') 
groups=('kde' 'kde-edu' 'kde-uninstall' 'education')
source=("$_mirror/${pkgname}-${_kdever}.tar.xz")
md5sums=(`grep ${pkgname}-${_kdever}.tar.xz ../kde-sc.md5 | cut -d" " -f1`)

prepare() {
  cd ${pkgname}-${pkgver}

  sed -i -e 's|add_subdirectory(sage)|#add_subdirectory(sage)|' src/backends/CMakeLists.txt
}

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF 
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
