
pkgname=qpwgraph
pkgver=0.7.2
pkgrel=1
pkgdesc="Graph manager dedicated to PipeWire, based on and pretty much the same of QjackCtl."
url="https://gitlab.freedesktop.org/rncbc/qpwgraph"
arch=('x86_64')
license=('GPL')
depends=('alsa-lib' 'pipewire' 'qt6-base' 'qt6-svg')
makedepends=('cmake' 'qt6-tools')
options=('!makeflags')
groups=('multimedia')
source=("https://www.rncbc.org/archive/${pkgname}-${pkgver}.tar.gz")
md5sums=('72f0753cf4e9992c4a1d598cfed4522f')

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCONFIG_WAYLAND=ON \
    -DCONFIG_QT6=ON
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
