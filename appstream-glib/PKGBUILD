 
pkgname=appstream-glib
pkgver=0.7.18
pkgrel=2
pkgdesc="Library that provides objects and helper methods to help reading and writing AppStream metadata."
arch=('x86_64')
url="https://github.com/hughsie/appstream-glib"
license=('LGPL')
depends=('gcab' 'gtk3' 'libyaml' 'libsoup' 'pacman' 'gperf' 'json-glib' 'gdk-pixbuf2' 'libarchive')
makedepends=('meson' 'ninja' 'gobject-introspection')
source=("https://people.freedesktop.org/~hughsient/appstream-glib/releases/${pkgname}-${pkgver}.tar.xz")
sha256sums=('ca1ed22e3bde3912cb903aaa7de085d55771da454f1c0573fd9608e1de9c4002')

build() {
  mkdir -p build
  cd build

   meson ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    --libexecdir=/usr/lib \
    -Dalpm=true \
    -Dstemmer=false \
    -Drpm=false 
  ninja
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
}
