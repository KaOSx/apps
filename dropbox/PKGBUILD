
pkgname=dropbox
pkgver=111.4.472
pkgrel=1
epoch=1
pkgdesc="Free service that lets you bring your photos, docs, and videos anywhere and share them easily."
arch=("x86_64")
url="https://www.dropbox.com"
license=('custom')
depends=('dbus-glib' 'qt5-base' 'libsm' 'libxmu')
options=('!strip' '!upx')
#source=("https://dl-web.dropbox.com/u/17/${pkgname}-lnx.x86_64-${pkgver}.tar.gz"
# get latest builds at https://www.dropboxforum.com/t5/Desktop-client-builds/bd-p/101003016
source=("https://clientupdates.dropboxstatic.com/dbx-releng/client/dropbox-lnx.x86_64-${pkgver}.tar.gz"
        'dropbox.png'
        'dropbox.desktop'
        'terms.txt'
        'dropbox.service')
md5sums=('d456ed3ef149e2801d4640b5d60c1634'
         '9ec50da2ce59ed8c17606394b9c5e1c0'
         '20c97082211cf02af494043b93d5feb8'
         '61802705d14aadc1082a9bdbdd1fe323'
         '9061a4055570fc33d059dddec508b14a')

package() {
	install -d ${pkgdir}/opt
	cp -R ${srcdir}/.dropbox-dist/dropbox-lnx.x86_64-${pkgver} ${pkgdir}/opt/dropbox

	find ${pkgdir}/opt/dropbox/ -type f -exec chmod 644 {} \;
	chmod 755 ${pkgdir}/opt/dropbox/dropboxd
	chmod 755 ${pkgdir}/opt/dropbox/dropbox
	chmod 755 ${pkgdir}/opt/dropbox/*.so

	install -d ${pkgdir}/usr/bin
	ln -s /opt/dropbox/dropboxd ${pkgdir}/usr/bin/dropboxd

	install -Dm644 ${srcdir}/dropbox.desktop ${pkgdir}/usr/share/applications/dropbox.desktop
	install -Dm644 ${srcdir}/dropbox.png ${pkgdir}/usr/share/pixmaps/dropbox.png
	install -Dm644 ${srcdir}/terms.txt ${pkgdir}/usr/share/licenses/$pkgname/terms.txt
	install -Dm644 ${srcdir}/dropbox.service ${pkgdir}/usr/lib/systemd/system/dropbox@.service
}
