
pkgname=fcitx-gtk3
_pkgname=fcitx
pkgver=4.2.9.9
pkgrel=1
pkgdesc="A Flexible Input Method Framework"
arch=('x86_64')
license=('GPL')
url="https://fcitx-im.org/wiki/Fcitx"
depends=('fcitx' 'gtk3' 'gtk-update-icon-cache')
makedepends=('extra-cmake-modules' 'doxygen' 'gobject-introspection')
groups=('localization')
install=${pkgname}.install
source=("https://download.fcitx-im.org/${_pkgname}/${_pkgname}-${pkgver}_dict.tar.xz")
md5sums=('187fc08d5e14267419c825e83297cbc2')

build() {
  cmake -B build -S ${_pkgname}-${pkgver} \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_BUILD_TYPE=Release \
      -DENABLE_QT=OFF \
      -DENABLE_GTK2_IM_MODULE=OFF \
      -DENABLE_GTK3_IM_MODULE=ON
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build/src/frontend/gtk3
}

