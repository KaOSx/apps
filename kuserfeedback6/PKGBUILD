
pkgname=kuserfeedback6
_pkgname=kuserfeedback
pkgver=1.2.0
pkgrel=1
pkgdesc="Framework for collecting user feedback for applications via telemetry and surveys"
arch=('x86_64')
url="https://invent.kde.org/libraries/kuserfeedback/"
license=('MIT')
depends=('qt6-declarative' 'qt6-svg') # 'php' optional, check what needs with plasma 5.15
makedepends=('extra-cmake-modules' 'qt6-tools' 'clang')
source=("https://download.kde.org/stable/kuserfeedback/${_pkgname}-${pkgver}.tar.xz"
        "https://invent.kde.org/libraries/kuserfeedback/-/commit/17db7d75643b4f3dccd5b36a6ab175f03ba74297.diff")
sha256sums=('76aac922b153249b274680a6f4c72c238ef14e3df04bad00cb64158b1063f264'
            '50fe80c634fc685798bb8ba07ff1424ef67e8e378b18477514c4a01b2925b0ea')

prepare() {
  cd ${_pkgname}-${pkgver}
  sed -i -e 's|QMLLINT_EXECUTABLE qmllint|QMLLINT_EXECUTABLE qmllint-qt6|' cmake/FindQmlLint.cmake
  #patch -p1 -i ${srcdir}/17db7d75643b4f3dccd5b36a6ab175f03ba74297.diff
}

build() {
  cmake -B build -S ${_pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_LIBEXECDIR=/usr/lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_WITH_QT6=ON \
    -DEXCLUDE_DEPRECATED_BEFORE_AND_AT=${_kf5ver} \
    -DQT_MAJOR_VERSION=6
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
