
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=kuserfeedback6
_pkgname=kuserfeedback
pkgver=${_kf6ver}
pkgrel=2
pkgdesc="Framework for collecting user feedback for applications via telemetry and surveys"
arch=('x86_64')
url="https://invent.kde.org/libraries/kuserfeedback/"
license=('MIT')
depends=('qt6-declarative' 'qt6-svg') # add once added to repo 'qt6-charts' # 'php' optional
makedepends=('extra-cmake-modules6' 'qt6-tools' 'clang')
groups=('kf6')
source=("https://download.kde.org/stable/frameworks/5.99/${_pkgname}-${pkgver}.tar.xz")
md5sums=(`grep ${_pkgname}-${_kf6ver}.tar.xz ../kf6.md5 | cut -d" " -f1`)

prepare() {
  cd ${_pkgname}-${pkgver}
  sed -i -e 's|QMLLINT_EXECUTABLE qmllint|QMLLINT_EXECUTABLE qmllint-qt6|' cmake/FindQmlLint.cmake
  #patch -p1 -i ${srcdir}/17db7d75643b4f3dccd5b36a6ab175f03ba74297.diff
}

build() {
  cmake -B build -S ${_pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_LIBEXECDIR=/usr/lib \
    -DBUILD_TESTING=OFF \
    -DQT_MAJOR_VERSION=6 \
    -DENABLE_CONSOLE=OFF \
    -DENABLE_CLI=OFF \
    -DENABLE_DOCS=OFF
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
