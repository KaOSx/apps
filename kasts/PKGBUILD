
pkgname=kasts
pkgver=22.11
_pkgver=22.11
pkgrel=1
arch=('x86_64')
pkgdesc="Kirigami-based podcast player"
url="https://invent.kde.org/plasma-mobile/kasts"
license=('GPL2')
depends=('qt5-multimedia' 'qt5-quickcontrols2' 'qt5-svg' 'kirigami' 'networkmanager-qt'
         'kcoreaddons' 'syndication' 'kconfig' 'ki18n' 'qtkeychain' 'threadweaver')
makedepends=('extra-cmake-modules' 'qt5-tools')
source=("https://download.kde.org/stable/plasma-mobile/${_pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('2912b4a8864e35a7dadc16b9368988b6163870a0358e020c3b80b31222002711')

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_BUILD_TYPE=Release \
      -DKDE_INSTALL_LIBDIR=lib \
      -DBUILD_TESTING=OFF
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
