
# 1.x odd numbers are dev, not stable
pkgname=xdg-desktop-portal
pkgver=1.16.0
pkgrel=1
pkgdesc="Portal frontend service for Flatpak and possibly other desktop containment frameworks."
url="https://github.com/flatpak/xdg-desktop-portal"
arch=("x86_64")
license=('LGPL2.1')
depends=('fuse3' 'glib2' 'pipewire')
makedepends=('python3' 'flatpak' 'meson' 'ninja')
source=("https://github.com/flatpak/xdg-desktop-portal/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('5b41a5915c11851493d8c33b9783f147a0a6f419db80ad760e84cd3420fd8c19')

build() {
  mkdir -p build
  cd build

  meson ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    --libexecdir=/usr/lib \
    -Dlibportal=disabled \
    -Dgeoclue=disabled \
    -Dpipewire=enabled
  ninja
}

check() {
  cd build
  ninja test || true
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
}
