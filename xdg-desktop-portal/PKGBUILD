
# 1.x odd numbers are dev, not stable
pkgname=xdg-desktop-portal
pkgver=1.18.2
pkgrel=1
pkgdesc="Portal frontend service for Flatpak and possibly other desktop containment frameworks."
url="https://github.com/flatpak/xdg-desktop-portal"
arch=("x86_64")
license=('LGPL2.1')
depends=('fuse3' 'glib2' 'pipewire')
makedepends=('python3' 'flatpak' 'meson' 'ninja')
source=("https://github.com/flatpak/xdg-desktop-portal/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('dfac239c5476aafd117a9a10131a2f0b142f72106c52fc03859938e00545f440')

build() {
  mkdir -p build
  cd build

  meson setup ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    --libexecdir=/usr/lib \
    -Dlibportal=disabled \
    -Dgeoclue=disabled
  ninja
}

check() {
  cd build
  ninja test || true
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
}
