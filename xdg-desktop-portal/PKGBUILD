
# 1.x odd numbers are dev, not stable
pkgname=xdg-desktop-portal
pkgver=1.18.0
pkgrel=1
pkgdesc="Portal frontend service for Flatpak and possibly other desktop containment frameworks."
url="https://github.com/flatpak/xdg-desktop-portal"
arch=("x86_64")
license=('LGPL2.1')
depends=('fuse3' 'glib2' 'pipewire')
makedepends=('python3' 'flatpak' 'meson' 'ninja')
source=("https://github.com/flatpak/xdg-desktop-portal/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('5ea35a6420a98b598c83355fc19feaea8ba999149641bf84079b0c572b00f5bd')

build() {
  mkdir -p build
  cd build

  meson setup ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    --libexecdir=/usr/lib \
    -Dlibportal=disabled \
    -Dgeoclue=disabled
  ninja
}

check() {
  cd build
  ninja test || true
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
}
