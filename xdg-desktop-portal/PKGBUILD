
# 1.x odd numbers are dev, not stable
pkgname=xdg-desktop-portal
pkgver=1.18.4
pkgrel=1
pkgdesc="Portal frontend service for Flatpak and possibly other desktop containment frameworks."
url="https://github.com/flatpak/xdg-desktop-portal"
arch=("x86_64")
license=('LGPL2.1')
depends=('fuse3' 'glib2' 'pipewire')
makedepends=('python3' 'flatpak' 'meson' 'ninja')
source=("https://github.com/flatpak/xdg-desktop-portal/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('b858aa1e74e80c862790dbb912906e6eab8b1e4db9339cd759473af62b461e65')

build() {
  mkdir -p build
  cd build

  meson setup ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    --libexecdir=/usr/lib \
    -Dlibportal=disabled \
    -Dgeoclue=disabled
  ninja
}

check() {
  cd build
  ninja test || true
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
}
