
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=kalzium
pkgver=${_kdever}
pkgrel=1
pkgdesc="Periodic Table of Elements"
url="https://apps.kde.org/en/kalzium"
arch=('x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('avogadrolibs' 'eigen3' 'ocaml' 'openbabel' 'karchive6' 'kconfig6' 'kcoreaddons6' 'kconfigwidgets6'
         'ki18n6' 'kitemviews6' 'kio6' 'kplotting6' 'kunitconversion6' 'kwidgetsaddons6' 'ktextwidgets6'
         'kxmlgui6' 'qt6-5compat' 'qt6-scxml')
makedepends=('extra-cmake-modules' 'kdoctools6' 'facile')
groups=('kde' 'kde-edu' 'kde-uninstall' 'education')
source=("$_mirror/${pkgname}-${_kdever}.tar.xz"
        "https://invent.kde.org/arojas/kalzium/-/commit/557d9bc96636f413430d0789cbf775915fc0dc45.diff")
md5sums=(`grep ${pkgname}-${_kdever}.tar.xz ../kde-sc.md5 | cut -d" " -f1`
         '819b99f84e57e298caf093c79da7d2b7')

prepare() {
  cd ${pkgname}-${pkgver}

  patch -p1 -i ${srcdir}/557d9bc96636f413430d0789cbf775915fc0dc45.diff
}

build() {  
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DBUILD_TESTING=OFF \
    -DQT_MAJOR_VERSION=6
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
