
pkgname=spun
pkgver=0.4
pkgrel=3
pkgdesc='A simple pacman update notifier, using notify-send.'
arch=('x86_64')
url='http://github.com/george2/spun/'
license=('none')
conflicts=('spun-live')
depends=('libnotify' 'bash' 'fakeroot')
optdepends=('kde-baseapps: for the kdialog configuration gui')
source=('https://raw.github.com/george2/spun/master/spun'
        'https://raw.github.com/george2/spun/master/spun.conf'
        'https://raw.github.com/george2/spun/master/spun.desktop')

_locales=( ca de el es fr it nl_BE nl_NL pl pt_BR )

for lc in ${_locales[@]}; do
	source+=( "https://raw.github.com/george2/spun/master/lang/$lc.po" )
done

md5sums=('485f63d5569afca73799b45eb12ba06f'
         '5bc3b1bcb6661ac9a4c7db3d0d1337cc'
         'ac9e112d6280bb384fb76cd4ed85dd5d'
         '11bd220926809795aeb294b326e6df19'
         '8f5d46a5aea7766c5e31f943755a4f07'
         'dcdeeea3c911d625c89b3337afc48577'
         '8baacf15a141bf8acc73b6d930715692'
         '96389923be0d00129cf8a66f4af38477'
         '58cb0d064ff52d3cc8e14a7f714b6f52'
         '9397841c7c0674d4ceaae131e2ef540c'
         '9397841c7c0674d4ceaae131e2ef540c'
         '779ec83392093ca671f6c1a33f0ba791'
         '9cead3815e2b3676ef803ce187c6d679')

package() {
	install -Dm755 spun "${pkgdir}/usr/bin/spun"
	install -Dm644 spun.conf "${pkgdir}/etc/spun.conf"
	install -Dm755 spun.desktop "${pkgdir}/usr/share/autostart/spun.desktop"

	# localization
	for lc in ${_locales[@]}; do
		install -dm755 "${pkgdir}/usr/share/locale/$lc/LC_MESSAGES/"
		msgfmt -o "${pkgdir}/usr/share/locale/$lc/LC_MESSAGES/spun.mo" \
			"$lc.po"
	done
}
