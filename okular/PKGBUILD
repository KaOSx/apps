
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=okular
arch=('x86_64')
pkgver=${_kdever}
pkgrel=2
pkgdesc="Document viewer that can view and annotate various formats, including PDF, Postscript, Comic Book, and image formats."
url="https://okular.kde.org/"
license=('GPL' 'LGPL' 'FDL')
depends=('kactivities' 'karchive' 'kbookmarks' 'kcompletion' 'kconfig' 'kconfigwidgets' 'kcoreaddons'
         'kjs' 'kio' 'khtml' 'kparts' 'kpty' 'threadweaver' 'kiconthemes' 'kwallet' 'kwindowsystem' 'kcrash'
         'phonon-qt5' 'libxxf86vm' 'sane' 'poppler-qt5' 'chmlib' 'djvulibre' 'libspectre' 'ebook-tools'
         'json-c' 'libkexiv2' 'libkdcraw' 'kdegraphics-mobipocket' 'qt5-speech' 'discount' 'purpose')
makedepends=('pkg-config' 'extra-cmake-modules' 'kdoctools' 'qca-qt5')
groups=('office')
source=("$_mirror/${pkgname}-${_kdever}.tar.xz"
        "https://invent.kde.org/kde/okular/-/commit/504eec7688e7139e9d193fc2b3ddafac0f835b6f.diff")
md5sums=(`grep ${pkgname}-${_kdever}.tar.xz ../kde-sc.md5 | cut -d" " -f1`
         'c94e922c31169d77419e23e380d3fba1')

prepare() {
  cd ${pkgname}-${pkgver}

  patch -p1 -i ${srcdir}/504eec7688e7139e9d193fc2b3ddafac0f835b6f.diff
  #sed -i -e 's|ecm_setup_version(0.99.|ecm_setup_version(1.0.|' CMakeLists.txt
}

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF \
    -DKF5KExiv2_DIR=/usr/lib/cmake/KEXIV2
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
  # fix non-working menu/mobile only items in lost & found
  echo "NoDisplay=true" >> ${pkgdir}/usr/share/applications/org.kde.mobile.okular.desktop
  echo "NoDisplay=true" >> ${pkgdir}/usr/share/applications/org.kde.okular.kirigami.desktop
}
