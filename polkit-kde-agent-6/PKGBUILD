
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=polkit-kde-agent-6
_pkgname=polkit-kde-agent-1
pkgver=${_plasmaver}
pkgrel=1
pkgdesc='Daemon providing a polkit authentication UI for Plasma.'
arch=('x86_64')
url='https://invent.kde.org/plasma/polkit-kde-agent-1'
license=('LGPL')
depends=('ki18n6' 'kwindowsystem6' 'kdbusaddons6' 'kwidgetsaddons6' 'kcoreaddons6' 'kcrash6'
         'kiconthemes6' 'polkit-qt6')
makedepends=('extra-cmake-modules' 'kdoctools6')
groups=('kdeplasma6')
source=("https://download.kde.org/stable/plasma/${pkgver}/${_pkgname}-${pkgver}.tar.xz")
md5sums=(`grep ${_pkgname}-${_plasmaver}.tar.xz ../kf5.md5 | cut -d" " -f1`)

build() {
  cmake -B build -S ${_pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_WITH_QT6=ON \
    -DEXCLUDE_DEPRECATED_BEFORE_AND_AT=${_kf5ver} \
    -DQT_MAJOR_VERSION=6
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
