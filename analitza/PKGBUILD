
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=analitza
pkgver=${_kdever}
pkgrel=1
pkgdesc="Library that lets you add mathematical features to your program"
url="https://invent.kde.org/education/analitza"
arch=('x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('eigen3'  'qt5-svg')
makedepends=('extra-cmake-modules' 'kdoctools' 'qt5-tools')
#groups=('kde' 'kdeedu' 'kde-uninstall' 'education')
source=("$_mirror/${pkgname}-${_kdever}.tar.xz")
md5sums=(`grep ${pkgname}-${_kdever}.tar.xz ../kde-sc.md5 | cut -d" " -f1`)

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF 
  make
}

package() {
  cd build

  make DESTDIR=${pkgdir} install
}
