pkgname=zed
pkgver=0.153.6
pkgrel=1
pkgdesc='High-performance, multiplayer code editor from the creators of Atom and Tree-sitter'
arch=('x86_64')
url='https://zed.dev'
license=('GPL-3.0' 'AGPL' 'Apache')
depends=('alsa-lib' 'fontconfig' 'libgit2' 'libxcb' 'libxkbcommon' 'openssl' 'sqlite'
         'vulkan-tools' 'wayland' 'zlib')
makedepends=('rust' 'clang' 'vulkan-headers' 'vulkan-validationlayers')
source=("https://github.com/zed-industries/zed/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz"
		'zed.sh')
sha256sums=('17ce3e750011e32e33c232b6e58fcf032d18d19567d3b54336f6a219542ca9f2'
            'bdd83dc65df9aa90cd87a74716e4fc9f856249b20556da792af714fc6ca81fd6')

prepare() {
	cd ${pkgname}-${pkgver}
	cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
    # copy setting from script/bundle-linux for .desktop
	export DO_STARTUP_NOTIFY="true"
	export APP_NAME="Zed"
	export APP_ICON="zed"
	export APP_CLI="zed"
	export APP_ARGS="%U"
	export APP_ID="zed"
	envsubst < "crates/zed/resources/zed.desktop.in" > zed.desktop
	./script/generate-licenses
}

build() {
	cd ${pkgname}-${pkgver} 
	RUSTFLAGS+=" --remap-path-prefix $PWD=/"

	export ZED_BUNDLE="true"
	export ZED_UPDATE_EXPLANATION='Please use pacman to update zed'
	# https://github.com/zed-industries/zed/issues/14859
	export RELEASE_VERSION="${pkgver}"

	cargo build --release --frozen --package zed --package cli
}

package() {
	cd ${pkgname}-${pkgver}

	install -Dm0755 target/release/cli ${pkgdir}/usr/bin/zed
	install -Dm0755 target/release/zed ${pkgdir}/usr/lib/${pkgname}/zed-editor
    # https://github.com/zed-industries/zed/issues/12707#issuecomment-2168742761
	install -Dm0644 zed.desktop ${pkgdir}/usr/share/applications/dev.zed.Zed.desktop
	install -Dm0644 crates/zed/resources/app-icon.png ${pkgdir}/usr/share/icons/${pkgname}.png
	# set env to use native window decoration
	install -Dm0755 ../../zed.sh ${pkgdir}/etc/xdg/plasma-workspace/env/zed.sh

	install -Dm0644 assets/licenses.md ${pkgdir}/usr/share/licenses/${pkgname}/licenses.md
}
