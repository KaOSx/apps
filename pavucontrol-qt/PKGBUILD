 
pkgname=pavucontrol-qt
pkgver=1.4.0
pkgrel=1
pkgdesc="Qt port of volume control pavucontrol of sound server PulseAudio."
arch=("x86_64")
url="https://github.com/lxqt/pavucontrol-qt"
license=('GPL2')
depends=('pulseaudio' 'qt5-base')
makedepends=('lxqt-build-tools' 'qt5-tools')
source=("https://github.com/lxqt/pavucontrol-qt/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('0d7ced3b9d215bcfae8fd5df5429aa07b0517984925cfa1cd020a9505749a994')

build() {
    cmake -B build -S ${pkgname}-${pkgver} \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib
    cmake --build build
}

package() {

    DESTDIR=${pkgdir} cmake --install build
}
