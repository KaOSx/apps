
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=kio6
_pkgname=kio
pkgver=${_kf6ver}
_commit=858d79539d04e390d793508b6e8c0a55317da80d
pkgrel=3
pkgdesc='Framework that implements almost all the file management functions you will ever need'
arch=('x86_64')
url='https://invent.kde.org/frameworks/kio'
license=('LGPL')
depends=('karchive6' 'kconfig6' 'kcoreaddons6' 'kcrash6' 'kdbusaddons6' 'ki18n6' 'kservice6' 'solid6'
         'kbookmarks6' 'kcompletion6' 'kconfigwidgets6' 'kguiaddons6' 'kiconthemes6' 'kitemviews6' 'kjobwidgets6'
         'kwidgetsaddons6' 'kwindowsystem6' 'kauth6' 'kwallet6' 'knotifications6' 'kded6'
         'ktextwidgets6' 'krb5' 'qt6-5compat' 'switcheroo-control')
makedepends=('extra-cmake-modules' 'kdoctools6' 'qt6-tools')
groups=('kf6')
source=("https://download.kde.org/stable/frameworks/6.0/${_pkgname}-${pkgver}.tar.xz"
        "https://invent.kde.org/frameworks/kio/-/commit/6e7775d315f389df0a440ed62b842ce83dc9a27e.diff")
md5sums=(`grep ${_pkgname}-${_kf6ver}.tar.xz ../kf6.md5 | cut -d" " -f1`
         'b576ccd7eead4b727085bb5fb1d35a29')

prepare() {
  cd ${_pkgname}-${pkgver}

  patch -p1 -i ${srcdir}/6e7775d315f389df0a440ed62b842ce83dc9a27e.diff
}

build() {
  cmake -B build -S ${_pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
