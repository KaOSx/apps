
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=messagelib
pkgver=${_kdever}
pkgrel=2
pkgdesc="All libs for messages"
arch=('x86_64')
url='https://github.com/KDE/messagelib'
license=('LGPL')
depends=('akonadi' 'ki18n' 'kmime' 'akonadi-mime' 'akonadi-notes' 'kdepim-apps-libs' 'libkdepim' 'karchive' 
         'kldap' 'kpimtextedit' 'kmailtransport' 'kidentitymanagement' 'kiconthemes'
         'kcompletion' 'kcodecs' 'kconfig' 'kxmlgui' 'akonadi-search' 'kcontacts' 'kmbox'  
         'libgravatar' 'grantleetheme' 'libkleo' 'pimcommon' 'grantlee' 'qtwebengine' 'qca-qt5')
makedepends=('extra-cmake-modules' 'kdoctools' 'boost')
source=($_mirror/${pkgname}-${_kdever}.tar.xz
        "https://invent.kde.org/pim/messagelib/-/commit/1f2548805df60707ffba2bba27d35d441232d140.diff")
md5sums=(`grep ${pkgname}-${_kdever}.tar.xz ../kde-sc.md5 | cut -d" " -f1`
         'e96a28b4dec084ad442b872bf7bb4ced')

prepare() {
  cd $pkgname-${pkgver}
  #sed -i -e 's|set(KMIME_LIB_VERSION "4.81.0")|set(KMIME_LIB_VERSION "4.80.0")|' ${srcdir}/${pkgname}/CMakeLists.txt
  patch -p1 -i ${srcdir}/1f2548805df60707ffba2bba27d35d441232d140.diff
}

build() {
  mkdir -p build
  cd build 
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DKDE_INSTALL_QMLDIR=/usr/lib/qt5/qml \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
    -DBUILD_TESTING=OFF 
  make 
}

package() {
  cd build
  
  make DESTDIR=${pkgdir} install
}
