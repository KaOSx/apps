
pkgname=spun-live
pkgver=0.3
pkgrel=1
pkgdesc='A simple pacman update notifier, using notify-send. Live ISO version, with pacman help.'
arch=('x86_64')
url='http://github.com/george2/spun/'
screenshot='http://github.com/george2/spun/raw/master/spun-live.png'
license=('none')
conflicts=('spun')
depends=('libnotify' 'bash')
optdepends=('kde-baseapps: for the kdialog configuration gui')
install=spun.install
source=('https://raw.github.com/george2/spun/master/spun-live'
        'https://raw.github.com/george2/spun/master/spun.conf'
        'https://raw.github.com/george2/spun/master/spun.desktop')

_locales=( ca de el es fr it nl_BE nl_NL pl pt_BR )

for lc in ${_locales[@]}; do
	source+=( "https://raw.github.com/george2/spun/master/lang/$lc.po" )
done

md5sums=('1078edf52996e72772e453efcc83b903'
         '1a1ac3a55999a35798e01945f3827b1e'
         'ac9e112d6280bb384fb76cd4ed85dd5d'
         '11bd220926809795aeb294b326e6df19'
         '8f5d46a5aea7766c5e31f943755a4f07'
         'dcdeeea3c911d625c89b3337afc48577'
         '8baacf15a141bf8acc73b6d930715692'
         '96389923be0d00129cf8a66f4af38477'
         '58cb0d064ff52d3cc8e14a7f714b6f52'
         '9397841c7c0674d4ceaae131e2ef540c'
         '9397841c7c0674d4ceaae131e2ef540c'
         '779ec83392093ca671f6c1a33f0ba791'
         '9cead3815e2b3676ef803ce187c6d679')

package() {
	install -Dm755 spun-live "${pkgdir}/usr/bin/spun"
	install -Dm644 spun.conf "${pkgdir}/etc/spun.conf"
	install -Dm755 spun.desktop "${pkgdir}/usr/share/autostart/spun.desktop"

	# localization
	for lc in ${_locales[@]}; do
		install -dm755 "${pkgdir}/usr/share/locale/$lc/LC_MESSAGES/"
		msgfmt -o "${pkgdir}/usr/share/locale/$lc/LC_MESSAGES/spun.mo" \
			"$lc.po"
	done
}
