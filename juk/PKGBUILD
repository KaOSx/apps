
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=juk
pkgver=${_kdever}
pkgrel=1
pkgdesc='Audio jukebox app, supporting collections of MP3, Ogg Vorbis and FLAC audio files.'
url='https://apps.kde.org/juk/'
arch=('x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kcoreaddons6' 'kcompletion6' 'kconfig6' 'kcrash6' 'kglobalaccel6' 'ki18n6'
         'kiconthemes6' 'kdbusaddons6' 'kio6' 'kjobwidgets6' 'knotifications6'
         'ktextwidgets6' 'kxmlgui6' 'kwallet6' 'kwidgetsaddons6' 'kwindowsystem6'
         'kstatusnotifieritem' 'qt6-svg' 'phonon-qt6' 'taglib')
makedepends=('extra-cmake-modules' 'kdoctools6')
groups=('kde' 'kde-multimedia' 'kde-uninstall')
source=("$_mirror/${pkgname}-${_kdever}.tar.xz")
md5sums=(`grep ${pkgname}-${_kdever}.tar.xz ../kde-sc.md5 | cut -d" " -f1`)

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DHAVE_TUNEPIMP=0 \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}

