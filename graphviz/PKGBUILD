
pkgname=graphviz
pkgver=2.47.3
_pkgver=2.47.3
pkgrel=1
pkgdesc="Open source graph visualization software"
arch=('x86_64')
url="https://www.graphviz.org/"
license=('CPL')
depends=('gd' 'librsvg' 'libxaw' 'ghostscript' 'pango' 'libtool' 'perl' 'python3' 'tcl' 'qt5-base')
makedepends=('swig' 'guile' 'groff')
options=('!libtool')
source=("https://gitlab.com/graphviz/graphviz/-/archive/${_pkgver}/graphviz-${_pkgver}.tar.bz2")
#source=("https://www2.graphviz.org/Packages/stable/portable_source/graphviz-${_pkgver}.tar.gz")
install='graphviz.install'
md5sums=('fd25f89e868de6ddf456d117b44eb0d1')

build() {
    cd graphviz-${_pkgver}
    #sed -i '/LIBPOSTFIX="64"/d' configure
    export LIBPOSTFIX=/

    ./autogen.sh
    ./configure --prefix=/usr \
        --enable-python3=yes \
        --disable-python \
        --without-included-ltdl \
        --disable-ltdl-install
    make
}

package() {
    cd graphviz-${_pkgver}
    make DESTDIR=${pkgdir} install

    install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/graphviz/LICENSE
}
