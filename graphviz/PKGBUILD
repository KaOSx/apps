
pkgname=graphviz
pkgver=12.1.0
_pkgver=12.1.0
pkgrel=1
pkgdesc="Open source graph visualization software"
arch=('x86_64')
url="https://www.graphviz.org/"
license=('CPL')
depends=('gd' 'librsvg' 'libxaw' 'ghostscript' 'pango' 'libtool' 'perl' 'python3' 'tcl' 'qt6-base')
makedepends=('swig' 'guile' 'groff')
options=('!libtool')
#source=("https://gitlab.com/graphviz/graphviz/-/archive/${_pkgver}/graphviz-${_pkgver}.tar.bz2")
source=("https://gitlab.com/api/v4/projects/4207231/packages/generic/${pkgname}-releases/${_pkgver}/${pkgname}-${_pkgver}.tar.xz")
install='graphviz.install'
sha256sums=('fa174c57b11f3ca48f096cf3d4dafa695bcc30a6ffea61804efe3f91ed9bc7ba')

build() {
    cd ${pkgname}-${_pkgver}
    sed -i -e 's|qmake6|qmake-qt6|' configure
    export LIBPOSTFIX=/

    #./autogen.sh
    ./configure --prefix=/usr \
        --enable-python3=yes \
        --disable-python \
        --without-included-ltdl \
        --disable-ltdl-install
    make
}

package() {
    cd ${pkgname}-${_pkgver}
    make DESTDIR=${pkgdir} install

    install -Dm644 COPYING ${pkgdir}/usr/share/licenses/graphviz/COPYING
}
