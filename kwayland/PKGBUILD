
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=kwayland
pkgver=${_kf5ver}
pkgrel=3
pkgdesc="KWayland provides a Qt-style Client and Server library wrapper for the Wayland libraries"
arch=('x86_64')
url="https://invent.kde.org/frameworks/kwayland"
license=('LGPLv2+')
depends=('qt5-wayland' 'wayland' 'plasma-wayland-protocols')
makedepends=('extra-cmake-modules')
groups=('kf5')
#source=("git://anongit.kde.org/kwayland.git")
source=("https://download.kde.org/stable/frameworks/5.98/${pkgname}-${pkgver}.tar.xz"
        "https://invent.kde.org/frameworks/kwayland/-/commit/d02188ad1f6222215adcf842f4c9806ba9e62ccb.diff"
        "https://invent.kde.org/frameworks/kwayland/-/commit/a59a303291829747fcb5cdd2d8dac5514772672c.diff")
md5sums=(`grep ${pkgname}-$_kf5ver.tar.xz ../kf5.md5 | cut -d" " -f1`
         'cb0205df5464439977b4dbfc6e226085'
         '869d7388dc17a510c5536360b8c8b4f8')

prepare() {
  cd ${pkgname}-${pkgver}

  patch -p1 -i ${srcdir}/d02188ad1f6222215adcf842f4c9806ba9e62ccb.diff
  patch -p1 -i ${srcdir}/a59a303291829747fcb5cdd2d8dac5514772672c.diff
}

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_LIBEXECDIR=lib
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
