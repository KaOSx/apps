
pkgname=sqlcipher
pkgver=4.6.1
pkgrel=2
pkgdesc="SQLite extension that provides transparent 256-bit AES encryption of database files"
arch=('x86_64')
url="https://www.zetetic.net/sqlcipher/"
license=('BSD')
depends=('openssl' 'sqlite')
makedepends=('tcl')
source=("https://github.com/sqlcipher/sqlcipher/archive/v${pkgver}.tar.gz")
md5sums=('2651f450cc41fdb9d853770da0f2d96a')

build() {
  cd ${pkgname}-${pkgver}
  export CFLAGS="$CFLAGS -DSQLITE_HAS_CODEC -DSQLITE_ENABLE_COLUMN_METADATA"
  export LDFLAGS="-lcrypto" 

    ./configure --prefix=/usr \
      --enable-tempstore=yes \
      --enable-fts3 \
      --enable-fts4 \
      --enable-fts5 \
      --enable-geopoly \
      --enable-memsys5 \
      --enable-rtree \
      --enable-session
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  install -D -m 0644 LICENSE.md ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.md
}
