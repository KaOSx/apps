
pkgname=polkit
pkgver=124
pkgrel=1
pkgdesc="Application development toolkit for controlling system-wide privileges"
arch=('x86_64')
license=('LGPL')
url="https://gitlab.freedesktop.org/polkit/polkit/"
depends=('glib2' 'pam' 'expat' 'systemd' 'duktape')
makedepends=('intltool' 'gobject-introspection' 'docbook-xsl' 'meson' 'ninja')
provides=('consolekit')
options=('!libtool')
install=polkit.install
source=("https://gitlab.freedesktop.org/polkit/polkit/-/archive/${pkgver}/${pkgname}-${pkgver}.tar.bz2"
        'polkit.pam'
        "https://gitlab.freedesktop.org/polkit/polkit/-/commit/22193ca2cc0f4e023d331cc1391f55143ef9cade.diff")
md5sums=('5cb95118e21d0cf2348069847c3879e8'
         '6564f95878297b954f0572bc1610dd15'
         'fa903fbcce53e93271f42a1f5babf373')

prepare() {
  cd ${pkgname}-${pkgver}
  # keep pam files in /etc
  patch -R -p1 -i ${srcdir}/22193ca2cc0f4e023d331cc1391f55143ef9cade.diff
}

build() {
  mkdir -p build
  cd build

  meson setup ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/polkit-1 \
    -Dsession_tracking=libsystemd-login \
    -Dos_type=redhat \
    -Dexamples=true \
    -Dman=true \
    -Djs_engine=duktape
  ninja
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install

  chown 102 ${pkgdir}/etc/polkit-1/rules.d
  chown 102 ${pkgdir}/usr/share/polkit-1/rules.d

  install -m644 ${srcdir}/polkit.pam ${pkgdir}/etc/pam.d/polkit-1 
}
