
pkgname=polkit
pkgver=0.120
pkgrel=3
pkgdesc="Application development toolkit for controlling system-wide privileges"
arch=('x86_64')
license=('LGPL')
url="https://gitlab.freedesktop.org/polkit/polkit/"
depends=('glib2' 'pam' 'expat' 'systemd' 'js')
makedepends=('intltool' 'gobject-introspection' 'docbook-xsl')
provides=('consolekit')
options=('!libtool')
install=polkit.install
source=("https://www.freedesktop.org/software/polkit/releases/${pkgname}-${pkgver}.tar.gz"
        'polkit.pam'
        "https://gitlab.freedesktop.org/polkit/polkit/-/commit/a2bf5c9c83b6ae46cbd5c779d3055bff81ded683.diff"
        "https://src.fedoraproject.org/rpms/polkit/raw/0a203bd46a1e2ec8cc4b3626840e2ea9d0d13a9a/f/CVE-2021-4115.patch")
md5sums=('a6efe21d021fafe4191ee30331ef801c'
         '6564f95878297b954f0572bc1610dd15'
         'e9355c69556b48415b4b1dc810944950'
         '3841d88798ac15a6a372884dc8d34c80')

build() {
  cd ${pkgname}-${pkgver}
  # CVE-2021-4034
  patch -p1 -i ${srcdir}/a2bf5c9c83b6ae46cbd5c779d3055bff81ded683.diff
  # CVE-2021-4115
  patch -p1 -i ${srcdir}/CVE-2021-4115.patch

  ./configure --prefix=/usr \
      --sysconfdir=/etc \
      --localstatedir=/var \
      --libexecdir=/usr/lib/polkit-1 \
      --enable-libsystemd-login=yes \
      --disable-static \
      --with-os-type=redhat
  make 
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  chown 102 ${pkgdir}/etc/polkit-1/rules.d
  chown 102 ${pkgdir}/usr/share/polkit-1/rules.d

  install -m644 ${srcdir}/polkit.pam ${pkgdir}/etc/pam.d/polkit-1 
}
