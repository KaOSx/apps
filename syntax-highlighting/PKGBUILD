
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=syntax-highlighting
pkgver=${_kf5ver}
pkgrel=2
pkgdesc='Syntax highlighting Engine for Structured Text and Code.'
arch=('x86_64')
url='https://github.com/KDE/syntax-highlighting'
license=('LGPL')
depends=('qt5-xmlpatterns' 'perl')
makedepends=('extra-cmake-modules' 'qt5-tools')
groups=('kf5')
source=("https://download.kde.org/stable/frameworks/5.56/${pkgname}-${pkgver}.tar.xz")
md5sums=(`grep ${pkgname}-$_kf5ver.tar.xz ../kf5.md5 | cut -d" " -f1`)

prepare () {
  cd ${pkgname}-${pkgver}
  # syntax highlighting for PKGBUILD files
  sed -i -e 's|.bash_login;.profile|.bash_login;.profile;PKGBUILD|' data/syntax/bash.xml
}

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build

  make DESTDIR=${pkgdir} install
}
