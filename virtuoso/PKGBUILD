#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org

pkgname=virtuoso
pkgver=6.1.6
pkgrel=1
arch=('x86_64')
pkgdesc='A scalable cross-platform server that combines SQL/RDF/XML Data Management with Web Application Server and Web Services Platform functionality'
url='http://virtuoso.openlinksw.com/wiki/main/Main/'
license=('GPL')
depends=('libldap')
makedepends=('bison' 'flex' 'gperf' 'gawk' 'm4' 'openssl' 'zlib' 'python2' 'unixodbc' 'libiodbc')
options=('!libtool')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-opensource-${pkgver}.tar.gz")
md5sums=('3991149c570f3738c3e819ce603e487f')

build() {
  cd ${srcdir}/virtuoso-opensource-${pkgver}

  CFLAGS="$CFLAGS -I/usr/include/libiodbc"
  CXXFLAGS="$CXXFLAGS -I/usr/include/libiodbc/"
  LDFLAGS="$LDFLAGS -L/usr/include/libiodbc/"

  ./autogen.sh
  ./configure --without-internal-zlib \
    --prefix=/usr \
    --localstatedir=/var \
    --sysconfdir=/etc \
    --disable-rendezvous \
    --disable-hslookup \
    --disable-all-vads \
    --disable-static \
    --enable-python \
    --with-iodbc=/usr/include/libiodbc/
  make
}

package() {
  # install server
  cd ${srcdir}/virtuoso-opensource-${pkgver}/binsrc/virtuoso
  make DESTDIR=${pkgdir} install

  # install driver
  cd ${srcdir}/virtuoso-opensource-${pkgver}/binsrc/driver
  make DESTDIR=${pkgdir} install

  # Install some useful tools; rename to avoid conflicts
  for bin in isql isqlw; do
    install -Dm755 ${srcdir}/virtuoso-opensource-${pkgver}/binsrc/tests/$bin \
      ${pkgdir}/usr/bin/$bin-vt
  done
}
