
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=ktp-auth-handler
pkgver=${_kdever}
pkgrel=2
pkgdesc="Provide UI/KWallet Integration for passwords and ssl errors on account connect"
arch=('x86_64')
url="https://projects.kde.org/projects/extragear/network/telepathy/ktp-auth-handler/"
license=('GPL')
depends=('kwallet' 'kwidgetsaddons' 'ki18n' 'kio' 'ktp-common-internals' 
         'kaccounts-integration' 'qca-qt5' 'signon-ui' 'signon-oauth2' 'signon-kwallet-extension')
makedepends=('extra-cmake-modules' 'kdoctools' 'git')
conflicts=('kf5-ktp-auth-handler')
replaces=('kf5-ktp-auth-handler')
groups=('kde-telepathy')
source=("$_mirror/${pkgname}-$_kdever.tar.xz")
md5sums=(`grep ${pkgname}-$_kdever.tar.xz ../kde-sc.md5 | cut -d" " -f1`)

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_LIBEXECDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_QMLDIR=/usr/lib/qt5/qml \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
    -DBUILD_TESTING=OFF 
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
