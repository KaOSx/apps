
pkgname=qtcreator
pkgver=4.7.2
_pkgver=4.7
__pkgver=4.7.2
pkgrel=2
pkgdesc="Lightweight, cross-platform integrated development environment."
arch=('x86_64')
url="http://qt.digia.com/"
license=('LGPL')
depends=('qt5-tools' 'qt5-quickcontrols' 'qt5-script' 'qtwebengine' 'clang')
makedepends=('git')
optdepends=('qt5-docs: for the integrated Qt documentation'
            'gdb: for the debugger'
            'cmake: for cmake project suppport'
            'git: for git support'
            'mercurial: for mercurial support'
            'bzr: for bazaar support')
groups=('programming')
source=("http://download.qt-project.org/official_releases/${pkgname}/${_pkgver}/${__pkgver}/qt-creator-opensource-src-${__pkgver}.tar.xz"
        'qtcreator.desktop')
md5sums=('ef5eaaf96fc034f116c3b0d0170f56a8'
         '5162b73dd63f457bdb02bd9c8b1cc66b')

prepare() {
  cd qt-creator-opensource-src-${__pkgver}

  sed -i 's|$$IDE_OUTPUT_PATH/libexec/qtcreator|$$IDE_OUTPUT_PATH/lib/qtcreator|' qtcreator.pri
  sed -i 's|$$QTC_PREFIX/libexec/qtcreator|$$QTC_PREFIX/lib/qtcreator|' qtcreator.pri
}         

build() {
  mkdir -p build
  cd build

  /usr/lib/qt5/bin/qmake LLVM_INSTALL_DIR=/usr QMAKE_CFLAGS_ISYSTEM=-I ${srcdir}/qt-creator-opensource-src-${__pkgver}/qtcreator.pro
  make 
  make docs -j1
}

package() {
  cd build

  make INSTALL_ROOT=${pkgdir}/usr install
  make INSTALL_ROOT=${pkgdir}/usr/ install_docs

  #install -Dm644 ${srcdir}/qtcreator.desktop \
  #  ${pkgdir}/usr/share/applications/qtcreator.desktop
  install -Dm644 ${srcdir}/qt-creator-opensource-src-${__pkgver}/LICENSE.GPL3-EXCEPT \
    ${pkgdir}/usr/share/licenses/qtcreator/LICENSE.GPL3-EXCEPT
}
