
pkgname=qtcreator
_pkgname=qt-creator-opensource-src
pkgver=11.0.3
_pkgver=11.0
__pkgver=11.0.3
pkgrel=3
pkgdesc="Lightweight, cross-platform integrated development environment."
arch=('x86_64')
url="https://www.qt.io/development-tools"
license=('GPL3')
depends=('qt6-tools' 'qt6-quick3d' 'qt6-svg' 'qt6-serialport' 'qt6-webengine'
         'qt6-translations' 'qt6-5compat' 'llvm' 'clang' 'yaml-cpp' 'syntax-highlighting'
         'clazy' 'gdb' 'git' 'cmake')
optdepends=('mercurial: for mercurial support')
groups=('programming')
source=("https://download.qt.io/official_releases/${pkgname}/${_pkgver}/${__pkgver}/${_pkgname}-${__pkgver}.tar.xz"
        'qtcreator.desktop'
        "https://github.com/qt-creator/qt-creator/commit/170f9acfb41704b68e2ba98690fd6d5e98addd85.diff"
        "https://github.com/qt-creator/qt-creator/commit/6c9d4a60e00cd00712d739746d9ded34876901a9.diff")
md5sums=('68d78bc58aee5f9dcc3775f7234259cb'
         '5162b73dd63f457bdb02bd9c8b1cc66b'
         '0608e38a4aae9ea23b683c33652797fc'
         '061ee2095f554460bdcd0ec7e1522c5c')

prepare() {
  cd ${_pkgname}-${__pkgver}

  sed -e 's|libexec/qtcreator|lib/qtcreator|' -i cmake/QtCreatorAPIInternal.cmake
  # yaml-cpp 0.8.0 needed
  #patch -p1 -i ${srcdir}/170f9acfb41704b68e2ba98690fd6d5e98addd85.diff
  #patch -p1 -i ${srcdir}/6c9d4a60e00cd00712d739746d9ded34876901a9.diff
}

build() {
  cmake -B build -S ${_pkgname}-${__pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DIDE_LIBEXEC_PATH=lib \
    -DBUILD_WITH_PCH=OFF \
    -DENABLE_SVG_SUPPORT=ON \
    -DWITH_DOCS=ON \
    -DBUILD_DEVELOPER_DOCS=ON
  cmake --build build
  cmake --build build --target docs

  #/usr/lib/qt5/bin/qmake LLVM_INSTALL_DIR=/usr KSYNTAXHIGHLIGHTING_LIB_DIR=/usr/lib QMAKE_CFLAGS_ISYSTEM=-I ${srcdir}/qt-#creator-opensource-src-${__pkgver}/qtcreator.pro
  #make 
  #make docs -j1
}

package() {
  DESTDIR=${pkgdir} cmake --install build

  cp -r build/share/doc ${pkgdir}/usr/share
  install -Dm644 ${_pkgname}-${__pkgver}/LICENSE.GPL3-EXCEPT \
    ${pkgdir}/usr/share/licenses/qtcreator/LICENSE.GPL3-EXCEPT
}
