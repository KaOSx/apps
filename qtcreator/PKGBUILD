
pkgname=qtcreator
_pkgname=qt-creator-opensource-src
pkgver=11.0.1
_pkgver=11.0
__pkgver=11.0.1
pkgrel=2
pkgdesc="Lightweight, cross-platform integrated development environment."
arch=('x86_64')
url="https://www.qt.io/development-tools"
license=('GPL3')
depends=('qt6-tools' 'qt6-quick3d' 'qt6-svg' 'qt6-serialport' 'qt6-webengine'
         'qt6-translations' 'qt6-5compat' 'llvm' 'clang' 'yaml-cpp' 'syntax-highlighting'
         'clazy' 'gdb' 'git' 'cmake')
optdepends=('mercurial: for mercurial support')
groups=('programming')
source=("https://download.qt.io/official_releases/${pkgname}/${_pkgver}/${__pkgver}/${_pkgname}-${__pkgver}.tar.xz"
        'qtcreator.desktop'
        'clang15.diff')
md5sums=('0d0f30734fb2c3245423cb9fba0ba7c2'
         '5162b73dd63f457bdb02bd9c8b1cc66b'
         'bd768440fb33f478173b95b005993833')

prepare() {
  cd ${_pkgname}-${__pkgver}

  sed -e 's|libexec/qtcreator|lib/qtcreator|' -i cmake/QtCreatorAPIInternal.cmake
  # LLVM 15 needed fix
  #patch -p1 -i ${srcdir}/clang15.diff
}

build() {
  cmake -B build -S ${_pkgname}-${__pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DIDE_LIBEXEC_PATH=lib \
    -DBUILD_WITH_PCH=OFF \
    -DENABLE_SVG_SUPPORT=ON \
    -DWITH_DOCS=ON \
    -DBUILD_DEVELOPER_DOCS=ON
  cmake --build build
  cmake --build build --target docs

  #/usr/lib/qt5/bin/qmake LLVM_INSTALL_DIR=/usr KSYNTAXHIGHLIGHTING_LIB_DIR=/usr/lib QMAKE_CFLAGS_ISYSTEM=-I ${srcdir}/qt-#creator-opensource-src-${__pkgver}/qtcreator.pro
  #make 
  #make docs -j1
}

package() {
  DESTDIR=${pkgdir} cmake --install build

  cp -r build/share/doc ${pkgdir}/usr/share
  install -Dm644 ${_pkgname}-${__pkgver}/LICENSE.GPL3-EXCEPT \
    ${pkgdir}/usr/share/licenses/qtcreator/LICENSE.GPL3-EXCEPT
}
