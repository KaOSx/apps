
pkgname=qtcreator
_pkgname=qt-creator-opensource-src
pkgver=5.0.3
_pkgver=5.0
__pkgver=5.0.3
pkgrel=1
pkgdesc="Lightweight, cross-platform integrated development environment."
arch=('x86_64')
url="https://www.qt.io/development-tools"
license=('GPL3')
depends=('qt5-tools' 'qt5-quickcontrols' 'qt5-quickcontrols2' 'qt5-quick3d' 'qt5-svg' 'qt5-script'
         'qt5-serialport' 'qt5-quick3d' 'qtwebengine' 'llvm' 'clang' 'yaml-cpp' 'clang-tools-extra'
         'syntax-highlighting' 'clazy')
makedepends=('cmake')
optdepends=('qt5-doc: for the integrated Qt documentation'
            'gdb: for the debugger'
            'cmake: for cmake project suppport'
            'git: for git support'
            'mercurial: for mercurial support')
groups=('programming')
source=("https://download.qt.io/official_releases/${pkgname}/${_pkgver}/${__pkgver}/${_pkgname}-${__pkgver}.tar.xz"
        'qtcreator.desktop')
md5sums=('94fad14bd0ecd5e9388899a984f851c0'
         '5162b73dd63f457bdb02bd9c8b1cc66b')

prepare() {
  cd ${_pkgname}-${__pkgver}

  sed -e 's|libexec/qtcreator|lib/qtcreator|' -i cmake/QtCreatorAPIInternal.cmake
  #sed -i 's|$$IDE_OUTPUT_PATH/libexec/qtcreator|$$IDE_OUTPUT_PATH/lib/qtcreator|' qtcreator.pri
  #sed -i 's|$$QTC_PREFIX/libexec/qtcreator|$$QTC_PREFIX/lib/qtcreator|' qtcreator.pri
  #sed -i 's|$$IDE_BUILD_TREE/libexec/qtcreator|$$IDE_BUILD_TREE/lib/qtcreator|' src/tools/tools.pro
  #sed -i 's|$$QTC_PREFIX/libexec/qtcreator|$$QTC_PREFIX/lib/qtcreator|' src/tools/tools.pro
}         

build() {
  cmake -B build -S ${_pkgname}-${__pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DIDE_LIBEXEC_PATH=lib \
    -DBUILD_WITH_PCH=OFF \
    -DENABLE_SVG_SUPPORT=ON \
    -DWITH_DOCS=ON \
    -DBUILD_DEVELOPER_DOCS=ON
  cmake --build build
  cmake --build build --target docs

  #/usr/lib/qt5/bin/qmake LLVM_INSTALL_DIR=/usr KSYNTAXHIGHLIGHTING_LIB_DIR=/usr/lib QMAKE_CFLAGS_ISYSTEM=-I ${srcdir}/qt-#creator-opensource-src-${__pkgver}/qtcreator.pro
  #make 
  #make docs -j1
}

package() {
  DESTDIR=${pkgdir} cmake --install build

  cp -r build/share/doc ${pkgdir}/usr/share
  install -Dm644 ${_pkgname}-${__pkgver}/LICENSE.GPL3-EXCEPT \
    ${pkgdir}/usr/share/licenses/qtcreator/LICENSE.GPL3-EXCEPT
}
