
pkgname=qtcreator
_pkgname=qt-creator-opensource-src
pkgver=4.14.1
_pkgver=4.14
__pkgver=4.14.1
pkgrel=1
pkgdesc="Lightweight, cross-platform integrated development environment."
arch=('x86_64')
url="https://www.qt.io/development-tools"
license=('LGPL')
depends=('qt5-tools' 'qt5-quickcontrols' 'qt5-quickcontrols2' 'qt5-quick3d' 'qt5-svg' 'qt5-script' 'qt5-serialport'
         'qtwebengine' 'llvm' 'clang' 'yaml-cpp' 'clang-tools-extra' 'syntax-highlighting')
makedepends=('cmake')
optdepends=('qt5-doc: for the integrated Qt documentation'
            'gdb: for the debugger'
            'cmake: for cmake project suppport'
            'git: for git support'
            'mercurial: for mercurial support')
groups=('programming')
source=("https://download.qt.io/official_releases/${pkgname}/${_pkgver}/.${__pkgver}/${_pkgname}-${__pkgver}.tar.xz"
        'qtcreator.desktop')
md5sums=('1882e6458e3c9291fdf25d2fad4be020'
         '5162b73dd63f457bdb02bd9c8b1cc66b')

prepare() {
  cd ${_pkgname}-${__pkgver}

  sed -e 's|libexec/qtcreator|lib/qtcreator|' -i cmake/QtCreatorAPIInternal.cmake
  #sed -i 's|$$IDE_OUTPUT_PATH/libexec/qtcreator|$$IDE_OUTPUT_PATH/lib/qtcreator|' qtcreator.pri
  #sed -i 's|$$QTC_PREFIX/libexec/qtcreator|$$QTC_PREFIX/lib/qtcreator|' qtcreator.pri
  #sed -i 's|$$IDE_BUILD_TREE/libexec/qtcreator|$$IDE_BUILD_TREE/lib/qtcreator|' src/tools/tools.pro
  #sed -i 's|$$QTC_PREFIX/libexec/qtcreator|$$QTC_PREFIX/lib/qtcreator|' src/tools/tools.pro
}         

build() {
  cmake -B build -S ${_pkgname}-${__pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DIDE_LIBEXEC_PATH=lib \
    -DBUILD_WITH_PCH=OFF \
    -DENABLE_SVG_SUPPORT=ON \
    -DWITH_DOCS=ON
  cmake --build build

  #/usr/lib/qt5/bin/qmake LLVM_INSTALL_DIR=/usr KSYNTAXHIGHLIGHTING_LIB_DIR=/usr/lib QMAKE_CFLAGS_ISYSTEM=-I ${srcdir}/qt-#creator-opensource-src-${__pkgver}/qtcreator.pro
  #make 
  #make docs -j1
}

package() {
  DESTDIR=${pkgdir} cmake --install build

  #install -Dm644 ${srcdir}/qtcreator.desktop \
  #  ${pkgdir}/usr/share/applications/qtcreator.desktop
  install -Dm644 ${_pkgname}-${__pkgver}/LICENSE.GPL3-EXCEPT \
    ${pkgdir}/usr/share/licenses/qtcreator/LICENSE.GPL3-EXCEPT
}
