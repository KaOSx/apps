
pkgname=nx-firewall
arch=('x86_64')
pkgver=0.1
_commit=c2458b031b6f983466875d2576824e9fe8499517
pkgrel=1
pkgdesc="Firewall tool for Linux uses the Qt toolkit."
url="https://nxos.org/#nomad-firewall"
license=('GPL')
depends=('kcoreaddons' 'kcmutils' 'ki18n' 'plasma-framework' 'kdelibs4support' 'kdeclarative'
         'kauth' 'kconfig' 'kio' 'knewstuff' 'kconfigwidgets' 'python3' 'ufw')
makedepends=('extra-cmake-modules' 'kdoctools')
groups=('utils')
source=("https://github.com/nomad-desktop/nomad-firewall/archive/${_commit}.zip"
        'ufw_enable.diff')
md5sums=('bee6e7bca8d473a9c0319e486bb3b545'
         'e7e5705e7bbf4e0d13540b30b4fb57f7')

prepare() {
  cd ${pkgname}-${_commit}
  
  patch -p1 -i ${srcdir}/ufw_enable.diff
  #sed -i 's|add_subdirectory(netstat)|#add_subdirectory(netstat)|' plugins/CMakeLists.txt
}

build() {  
  mkdir -p build
  cd build

  cmake ../${pkgname}-${_commit} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_C_FLAGS="-Wno-error=suggest-override" \
    -DCMAKE_CXX_FLAGS="-Wno-error=suggest-override" \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_LIBEXECDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF 
  make -i
}

package() {
  cd build

  make DESTDIR=${pkgdir} install
}
