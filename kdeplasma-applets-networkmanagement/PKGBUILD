
pkgname=('kdeplasma-applets-networkmanagement')       
arch=('x86_64')
pkgver=0.9.0.8
pkgrel=1
pkgdesc="KDE SC networkmanager plasmoid"
url="http://plasma.kde.org"
license=('GPL')
depends=('kde-workspace' 'networkmanager>=0.9')
makedepends=('cmake' 'python2' 'automoc4' 'mobile-broadband-provider-info'
             'openconnect')
optdepends=('mobile-broadband-provider-info: Database of mobile broadband service providers'
            'openconnect: Cisco AnyConnect compatible VPN client')
install=${pkgname}.install
source=("ftp://ftp.kde.org/pub/kde/unstable/networkmanagement/${pkgver}/src/networkmanagement-${pkgver}.tar.bz2")
md5sums=('ec80ff922257d34a942cb5f284d03ce6')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  
  cmake ../networkmanagement-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd "${srcdir}"/build
  
  make DESTDIR="${pkgdir}" install
}
