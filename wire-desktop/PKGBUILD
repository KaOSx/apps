
pkgname=wire-desktop
pkgver=3.8.2894
pkgrel=1
pkgdesc="Cross platform desktop app, wrapping the wire-webapp. Based on Electron"
arch=('x86_64')
url="https://wire.com/en/"
license=('GPL3')
depends=('alsa-lib' 'gtk3' 'libxtst' 'nss')
makedepends=('nodejs' 'yarn')
source=("https://github.com/wireapp/wire-desktop/archive/linux/${pkgver}.tar.gz"
        'wire-desktop.desktop')
md5sums=('5c9e8848ac862b432e74ea8af16ee101'
         '32a998ab2d5cec93ab975e5aff4ad223')

build() {
  cd ${pkgname}-linux-${pkgver}
  
  yarn
  yarn build:ts
  npx grunt 'linux-other'
}

package() {
  cd ${pkgname}-linux-${pkgver}
  
  install -d ${pkgdir}/usr/lib/${pkgname}
  install -d ${pkgdir}/usr/bin
  
  cp -a wrap/dist/linux-unpacked/* ${pkgdir}/usr/lib/${pkgname}
  ln -s /usr/lib/${pkgname}/${pkgname} ${pkgdir}/usr/bin/${pkgname}

  install -Dm644 ../${pkgname}.desktop ${pkgdir}/usr/share/applications/${pkgname}.desktop
  install -Dm644 resources/icons/256x256.png ${pkgdir}/usr/share/pixmaps/${pkgname}.png
  install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
