# Maintainer: abveritas@chakra-project.org

pkgname=qt-mobility
pkgver=1.2.0
pkgrel=3
pkgdesc="Qt APIs for mobile device functionality"
arch=('x86_64')
url="http://qt.nokia.com"
license=('LGPL')
depends=('qt' 'bluez' 'gstreamer0.10-base' 'gstreamer0.10-bad' 'networkmanager'
         'systemd' 'pulseaudio')
makedepends=('perl' 'mesa')
optdepends=('tracker: Document Gallery')
source=("http://pkgs.fedoraproject.org/repo/pkgs/qt-mobility/qt-mobility-opensource-src-1.2.0.tar.gz/ea5db5a8d3dd4709c2926dceda646bd8/qt-mobility-opensource-src-1.2.0.tar.gz"
        #"http://get.qt.nokia.com/qt/add-ons/$pkgname-opensource-src-$pkgver.tar.gz"
        use_isabspath.patch
        gcc-4.7_unistd.patch
        no_rpath.patch
        fix_translations_install_path.patch
        QTMOBILITY-790_subdirs_for_serviceframework-b90cee3.patch
        Fix_metaobjectbuilder_build_errors_4.8_and_4.7-f102053.patch)

md5sums=('ea5db5a8d3dd4709c2926dceda646bd8'
         'c7051d3e8c016b26ead62818e203b201'
         '5ff7e6e3c53615b7f67dceaf927be406'
         '98489d85f6372af60aadac6a621069c5'
         '7e02a52caef126aa7e6d9559dd4c2e4d'
         '0f958da15a65464cdf0d2d7e8afe563a'
         'dcba6a085c4b1c2804ecf1a2df5cd35d')


build() {
    cd "$srcdir/${pkgname}-opensource-src-${pkgver}"

    patch -p1 -i ../use_isabspath.patch
    patch -p1 -i ../gcc-4.7_unistd.patch
    patch -p1 -i ../no_rpath.patch
    patch -p1 -i ../fix_translations_install_path.patch
    patch -p1 -i ../QTMOBILITY-790_subdirs_for_serviceframework-b90cee3.patch
    patch -p1 -i ../Fix_metaobjectbuilder_build_errors_4.8_and_4.7-f102053.patch

    ./configure -prefix /usr \
                -plugindir /usr/lib/qt/plugins \
                -examples \
                -release \
                -silent

    make
}

package() {
    cd "$srcdir/${pkgname}-opensource-src-${pkgver}"

    make INSTALL_ROOT="$pkgdir/" install

    # Install docs
    install -d ${pkgdir}/usr/share/doc/qt
    cp -r doc/qch ${pkgdir}/usr/share/doc/qt/
    install -d ${pkgdir}/usr/share/doc/qt/html/qtmobility
    cp -a doc/html/* ${pkgdir}/usr/share/doc/qt/html/qtmobility
}
