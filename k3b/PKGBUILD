
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=k3b
pkgver=${_kdever}
pkgrel=1
epoch=1
pkgdesc="Feature-rich and easy to handle CD burning application"
arch=('x86_64')
url="http://k3b.sourceforge.net/"
depends=('karchive' 'kconfig' 'ki18n' 'kcmutils' 'kcoreaddons' 'knotifyconfig' 'solid'
         'kfilemetadata' 'kiconthemes' 'kjobwidgets' 'kio' 'knotifications' 'knewstuff' 
         'kservice' 'kwidgetsaddons' 'kxmlgui' 'libkcddb5' 'libsamplerate' 
         'libmad' 'ffmpeg' 'taglib' 'libmpcdec' 'libdvdread' 'cdrdao' 'cdrtools' 
         'shared-mime-info' 'libxft' 'libburn')
makedepends=('extra-cmake-modules' 'kdoctools' 'pkg-config' 'dvd+rw-tools')
optdepends=('dvd+rw-tools: for dvd burning support'
            'emovix:       for bootable multimedia cd/dvd support'
            'cdparanoia:   for cd ripping support')
license=('GPL')
groups=('multimedia')
conflicts=('kf5-k3b')
replaces=('kf5-k3b')
options=('!libtool')
source=($_mirror/${pkgname}-${_kdever}.tar.xz)
md5sums=(`grep ${pkgname}-${_kdever}.tar.xz ../kde-sc.md5 | cut -d" " -f1`)

prepare() {
  cd ${pkgname}-${pkgver}
  #patch -p1 -i ${srcdir}/k3b-fix-mp3decoder.diff
  #sed -i -e 's|set(TAGLIB_MIN_VERSION "1.4")|set(TAGLIB_MIN_VERSION "1.10")|' cmake/modules/FindTaglib.cmake 
}

build() {
  mkdir -p build
  cd build

  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_QMLDIR=/usr/lib/qt5/qml \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF 
  make
}

package() {
  cd build

  make DESTDIR=${pkgdir} install
}

