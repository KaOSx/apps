
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=plasma-integration
pkgver=${_plasmaver}
pkgrel=3
pkgdesc='Qt Platform Theme integration plugins for Plasma Workspaces.'
arch=('x86_64')
url='https://invent.kde.org/plasma/plasma-integration'
license=('LGPL')
depends=('kconfig' 'kconfigwidgets' 'ki18n' 'kiconthemes' 'kio' 'knotifications' 'kwidgetsaddons'
         'kwayland' 'noto-fonts' 'breeze' 'ttf-hack' 'qt5-quickcontrols2' 'qqc2-desktop-style')
makedepends=('extra-cmake-modules')
groups=('kdeplasma5')
source=("https://download.kde.org/stable/plasma/${pkgver}/${pkgname}-${pkgver}.tar.xz"
        "https://invent.kde.org/plasma/plasma-integration/-/commit/52bd3c9cc2a78e2ba8d87e09e01706d309720a6f.diff")
md5sums=(`grep ${pkgname}-${_plasmaver}.tar.xz ../kf5.md5 | cut -d" " -f1`
         '66d92e94c294fb123b5dfecc90472369')

prepare () {
  cd ${pkgname}-${pkgver}

  patch -p1 -i ${srcdir}/52bd3c9cc2a78e2ba8d87e09e01706d309720a6f.diff
}

build () {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
    -DBUILD_TESTING=OFF 
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}

