
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=knewstuff
pkgver=${_kf5ver}
pkgrel=2
pkgdesc='KNewStuff'
arch=('x86_64')
url='https://invent.kde.org/frameworks/knewstuff'
license=('LGPL')
depends=('kf5-attica' 'kconfigwidgets' 'kio' 'kirigami' 'kpackage')
makedepends=('extra-cmake-modules')
groups=('kf5')
source=("https://download.kde.org/unstable/frameworks/${pkgver}/${pkgname}-${pkgver}.tar.xz"
        "https://invent.kde.org/frameworks/knewstuff/-/commit/693a2ea3926400b1482888a2df2c532852b8f971.diff")
md5sums=(`grep ${pkgname}-${_kf5ver}.tar.xz ../kf5.md5 | cut -d" " -f1`
         '07fcfc0f5e36c31532060c177cd6beb0')

prepare() {
  cd ${pkgname}-${pkgver}
  
  patch -p1 -i ${srcdir}/693a2ea3926400b1482888a2df2c532852b8f971.diff
}

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=/usr/lib \
    -DECM_MKSPECS_INSTALL_DIR=/usr/lib/qt5/mkspecs/modules \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
