
pkgname=wine
pkgver=4.17
_pkgver=4.17
pkgrel=1
pkgdesc="Compatibility layer capable of running Windows applications on several POSIX-compliant operating systems."
url="https://www.winehq.com"
arch=('x86_64')
license=('LGPL')
depends=('fontconfig' 'glu' 'libxcursor' 'libxrandr' 'libxdamage' 'libxxf86dga' 'alsa-lib' 
         'desktop-file-utils' 'giflib' 'libpng' 'libxml2' 'libldap' 'lcms2' 'openal' 'jack'
         'cups' 'gnutls' 'v4l-utils' 'libjpeg-turbo' 'mpg123' 'libxinerama' 'libxslt'
         'ncurses' 'libcl' 'libxcomposite' 'alsa-plugins' 'samba' 'gst-plugins-base' 'pulseaudio'
         'gsm' 'sane' 'libgphoto2' 'opencl-headers')
makedepends=('autoconf' 'perl' 'fontforge' 'flex' 'libxpm' 'libxmu')
install=wine.install
groups=('system')
source=("https://dl.winehq.org/wine/source/4.x/${pkgname}-${_pkgver}.tar.xz")
sha1sums=('d8584671dfe40716b0efdb8d46dba1216546e364')

build() {
    mkdir -p build
    cd build

    ../${pkgname}-${_pkgver}/configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --libdir=/usr/lib \
        --with-x \
        --with-gstreamer \
        --enable-win64

    make
}

package() {
    cd build
    make prefix=${pkgdir}/usr libdir=${pkgdir}/usr/lib dlldir=${pkgdir}/usr/lib/wine install
    
    sed -i -e 's|wine start /unix %f|wine64 start /unix %f|' ${pkgdir}/usr/share/applications/wine.desktop
}


