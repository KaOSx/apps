
pkgname=wine
pkgver=9.14
_pkgver=9.x
__pkgver=9.14
pkgrel=1
pkgdesc="Compatibility layer capable of running Windows applications on several POSIX-compliant operating systems."
url="https://www.winehq.org"
arch=('x86_64')
license=('LGPL')
depends=('fontconfig' 'libxcursor' 'libxrandr' 'alsa-lib' 'desktop-file-utils' 'giflib'
         'jack' 'cups' 'gnutls' 'v4l-utils' 'libxinerama' 'ncurses' 'libxcomposite'
         'alsa-plugins' 'samba' 'gst-plugins-base' 'pulseaudio' 'sane'
         'libgphoto2' 'opencl-headers' 'vulkan-validationlayers' 'libxkbcommon' 'wayland')
makedepends=('autoconf' 'perl' 'fontforge' 'flex' 'libxpm')
install=wine.install
groups=('system')
source=("https://dl.winehq.org/wine/source/${_pkgver}/${pkgname}-${__pkgver}.tar.xz")
sha512sums=('f242d4fa55db2d68813a8516c16f5404000e0da941c5e4796fea837625a67df16d31a60b7a4f5769662f0709086dc1d952e8d63cb16c493c805eff9a4264b117')

build() {
    mkdir -p build
    cd build

    ../${pkgname}-${__pkgver}/configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --libdir=/usr/lib \
        --with-x \
        --with-gstreamer \
        --enable-win64 \
        --with-wayland
    make
}

package() {
    cd build
    make prefix=${pkgdir}/usr libdir=${pkgdir}/usr/lib dlldir=${pkgdir}/usr/lib/wine install

    sed -i -e 's|wine start /unix %f|wine64 start /unix %f|' ${pkgdir}/usr/share/applications/wine.desktop
}


