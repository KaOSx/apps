
pkgname=wine
pkgver=8.9
_pkgver=8.x
__pkgver=8.9
pkgrel=1
pkgdesc="Compatibility layer capable of running Windows applications on several POSIX-compliant operating systems."
url="https://www.winehq.org"
arch=('x86_64')
license=('LGPL')
depends=('fontconfig' 'libxcursor' 'libxrandr' 'alsa-lib' 'desktop-file-utils' 'giflib'
         'jack' 'cups' 'gnutls' 'v4l-utils' 'libxinerama' 'ncurses' 'libxcomposite'
         'alsa-plugins' 'samba' 'gst-plugins-base' 'pulseaudio' 'sane'
         'libgphoto2' 'opencl-headers' 'vulkan-validationlayers')
makedepends=('autoconf' 'perl' 'fontforge' 'flex' 'libxpm')
install=wine.install
groups=('system')
source=("https://dl.winehq.org/wine/source/${_pkgver}/${pkgname}-${__pkgver}.tar.xz")
sha512sums=('4de85654d2d5c9d48fa2a748eb9bee4477f0ba7fbd2c5dd40ea69662d8f6dfbb6432d1ec9109233d24608dc9554bad525c1e1ecdc86a5d00d133772810b12261')

build() {
    mkdir -p build
    cd build

    ../${pkgname}-${__pkgver}/configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --libdir=/usr/lib \
        --with-x \
        --with-gstreamer \
        --enable-win64
    make
}

package() {
    cd build
    make prefix=${pkgdir}/usr libdir=${pkgdir}/usr/lib dlldir=${pkgdir}/usr/lib/wine install

    sed -i -e 's|wine start /unix %f|wine64 start /unix %f|' ${pkgdir}/usr/share/applications/wine.desktop
}


