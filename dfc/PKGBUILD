
pkgname=dfc
pkgver=3.1.1
pkgrel=2
pkgdesc="Display file system space usage using graphs and colors"
arch=('x86_64')
url="https://projects.gw-computing.net/projects/dfc"
license=('BSD')
depends=('glibc')
makedepends=('cmake' 'gettext')
source=("https://projects.gw-computing.net/attachments/download/615/${pkgname}-${pkgver}.tar.gz")
install='dfc.install'
md5sums=('26fd905a07078332d98c2806cdd0fc0e')
groups=('color')

build() {
  cd ${pkgname}-${pkgver}

  cmake . -DPREFIX=/usr -DSYSCONFDIR=/etc -DCMAKE_BUILD_TYPE=RELEASE
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

