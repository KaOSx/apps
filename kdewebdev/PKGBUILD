#
# KDE SC Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

#
# package info
#
pkgbase=('kdewebdev')
pkgname=('kdewebdev-kfilereplace'
         'kdewebdev-kimagemapeditor'
         'kdewebdev-kommander'
         'kdewebdev-klinkstatus')

arch=('i686' 'x86_64')
pkgver=${_kdever}
pkgrel=2

pkgdesc="split package"
url="http://www.kde.org/"
license=('GPL' 'LGPL' 'FDL')

options=('docs' '!splithdr' 'splitdbg' 'log')

makedepends=('pkgconfig' 'cmake' 'automoc4' 'ruby' "kdepimlibs>=${_kdever}"
             "kde-runtime>=${_kdever}" 'tidyhtml' "kdepim-runtime>=${_kdever}")

source=($_mirror/${pkgbase}-$_kdever.tar.xz)
md5sums=(`grep ${pkgbase}-$_kdever.tar.xz ../kde-sc.md5 | cut -d" " -f1`)



#
# build function
#
build() {
	cd ${srcdir}/${pkgbase}-${pkgver}

	msg "starting build ..."
	cmake . -DCMAKE_BUILD_TYPE=${_build_type} \
		-DCMAKE_INSTALL_PREFIX=${_installprefix} \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed'

	make
}



#
# split-install functions
#
package_kdewebdev-kfilereplace() {
	pkgdesc="Utility that replace some strings with others in a lot of files in an only operation"
	depends=("kde-runtime>=${_kdever}" "kdelibs>=${_kdever}")
	groups=("kde" "${pkgbase}" "kde-uninstall")
	categories=('utils')
	conflicts=('kdewebdev-common' Â'kdewebdev-doc')
	install=${pkgbase}.install

	splitdirs="kfilereplace doc/kfilereplace"
		for i in ${splitdirs} ; do
			cd ${srcdir}/${pkgbase}-${pkgver}/${i}
			make DESTDIR=${pkgdir} install || return 1
		done
}

package_kdewebdev-kimagemapeditor() {
	pkgdesc="Image map editor"
	depends=("kde-runtime>=${_kdever}" "kdelibs>=${_kdever}")
        conflicts=('kdewebdev-common' 'kdewebdev-doc')
	groups=("kde" "${pkgbase}" "kde-uninstall")
	categories=('graphics')
	install=${pkgbase}.install

	splitdirs="kimagemapeditor doc/kimagemapeditor"
		for i in ${splitdirs} ; do
			cd ${srcdir}/${pkgbase}-${pkgver}/${i}
			make DESTDIR=${pkgdir} install || return 1
		done
}

package_kdewebdev-kommander() {
	pkgdesc="Kommander, the visual application builder"
	depends=("kde-runtime>=${_kdever}" "kdelibs>=${_kdever}")
        conflicts=('kdewebdev-common' 'kdewebdev-doc')
	groups=("kde" "${pkgbase}" "kde-uninstall")
	categories=('programming')
	install=${pkgbase}.install

	splitdirs="kommander" # FIXME: doc/kommander
		for i in ${splitdirs} ; do
			cd ${srcdir}/${pkgbase}-${pkgver}/${i}
			make DESTDIR=${pkgdir} install || return 1
		done
}

package_kdewebdev-klinkstatus() {
	pkgdesc="A Link checker utility"
	depends=("kde-runtime>=${_kdever}" "kdelibs>=${_kdever}" 'tidyhtml' "kdepimlibs>=${_kdever}") # "kdepim-runtime>=${_kdever}"
        conflicts=('kdewebdev-common' 'kdewebdev-doc')
	groups=("kde" "${pkgbase}" "kde-uninstall")
	categories=('utils')
	install=${pkgbase}.install

	splitdirs="klinkstatus doc/klinkstatus"
		for i in ${splitdirs} ; do
			cd ${srcdir}/${pkgbase}-${pkgver}/${i}
			make DESTDIR=${pkgdir} install || return 1
		done
}
