
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=falkon6
_pkgname=falkon
pkgver=${_kdever}
pkgrel=1
arch=('x86_64')
pkgdesc="Open source browser based on QtWebengine, written in Qt Framework"
url="https://www.falkon.org/"
license=('GPL3')
depends=('qt6-webengine' 'qt6-webchannel' 'qt6-5compat' 'ki18n6' 'kwallet6' 'kio6'
         'kcrash6' 'kcoreaddons6' 'purpose6')
makedepends=('extra-cmake-modules6' 'qt6-tools')
#groups=('network-web')
#source=("${_mirror}/${pkgname}-${_kdever}.tar.xz"
source=("https://invent.kde.org/carewolf/falkon/-/archive/qt6/falkon-qt6.tar.bz2"
        'speeddial.diff'
        'preferences.diff'
        'browserwindow.diff'
        'CMakeLists.diff')
#md5sums=(`grep ${_pkgname}-${_kdever}.tar.xz ../kde-sc.md5 | cut -d" " -f1`
md5sums=('83301646cfb1d6865e01e71fdd1ca55a'
         '2f1cec281364a60aac4b1d7f03f9902f'
         '2addb3a5e484bcfbb19f702d2e86d2fd'
         'cafc9d1284e700ad615265bf39a2bafb'
         '3c008ce76f095cc5237cdd1638ccee13')

prepare() {
  cd ${_pkgname}-qt6

  #patch -R -p1 -i ${srcdir}/revert.diff
  #patch -p1 -i ${srcdir}/speeddial.diff
  patch -p0 -i ${srcdir}/preferences.diff
  patch -p0 -i ${srcdir}/browserwindow.diff
  patch -p1 -i ${srcdir}/CMakeLists.diff
  sed -e "s|</opt/kde6/include/KF6/KArchive/KZip>|</usr/include/KF6/KArchive/KZip>|" -i src/lib/plugins/ocssupport.cpp
  sed -e "s|<PurposeWidgets/Menu>|</usr/include/KF6/PurposeWidgets/Purpose/Menu>|" -i src/plugins/KDEFrameworksIntegration/kdeframeworksintegrationplugin.h
  sed -e "s|#include <KIO/AccessManager>||" -i src/plugins/KDEFrameworksIntegration/kioschemehandler.cpp
}

build() {
  cmake -B build -S ${_pkgname}-qt6 \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE_INSTALL_LIBDIR=lib \
    -DENABLE_KDE_FRAMEWORKS_INTEGRATION_PLUGIN=TRUE \
    -DQT_MAJOR_VERSION=6 \
    -DNO_X11=OFF
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build

  install -Dm644 ${_pkgname}-qt6/linux/completion/_falkon ${pkgdir}/usr/share/zsh/site-functions/_falkon
}

