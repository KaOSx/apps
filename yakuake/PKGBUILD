
pkgname=yakuake
pkgver=2.9.9
pkgrel=2
pkgdesc="A KDE konsole application with the look and feel of that in the Quake engine"
arch=('x86_64')
url="http://yakuake.kde.org"
license=('GPL')
depends=('kdebase-konsole')
makedepends=('cmake' 'automoc4')
categories=('system')
screenshots=('http://kde-apps.org/CONTENT/content-pre1/29153-1.png')
install="${pkgname}.install"
source=("http://download.kde.org/stable/${pkgname}/${pkgver}/src/${pkgname}-${pkgver}.tar.xz")
md5sums=('ef97612710b28d62e43a58bed49cbead')

build() {
  cd ${srcdir}
  
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd ${srcdir}/build
  
  make DESTDIR=${pkgdir} install
}
