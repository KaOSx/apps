
pkgname=kstars
pkgver=3.6.4
_commit=cf5db0400263577fb4805fad64fd5ad67c3b3bec
epoch=1
pkgrel=1
pkgdesc="Open source, cross-platform Astronomy Software by KDE"
url="https://edu.kde.org/kstars/"
arch=('x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kauth' 'kconfig' 'kcrash' 'kwidgetsaddons' 'knewstuff' 'kio' 'ki18n' 'kxmlgui' 'kplotting' 
         'knotifications' 'knotifyconfig' 'cfitsio' 'wcslib' 'xplanet' 'qt5-multimedia' 'libindi' 
         'libraw' 'qt5-websockets' 'qtkeychain' 'gsl' 'qt5-datavis3d' 'stellarsolver')
makedepends=('extra-cmake-modules' 'kdoctools' 'eigen3')
groups=('kde' 'kde-edu' 'kde-uninstall' 'education')
source=("https://download.kde.org/stable/kstars/${pkgname}-${pkgver}.tar.xz")
#source=("https://invent.kde.org/education/kstars/-/archive/${_commit}/kstars-${_commit}.tar.bz2")
sha256sums=('f4c26a25581265507394bbf4f19ea2f323b8615d5ec6c0f9fb22c98ea2060f6d')

prepare() {
  cd ${pkgname}-${pkgver}
  #sed -i -e 's|add_subdirectory(doc)|#add_subdirectory(doc)|' ${srcdir}/${pkgname}/CMakeLists.txt
  #patch -p1 -i $srcdir/isnan.diff
}

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_QMLDIR=/usr/lib/qt5/qml \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
    -DBUILD_TESTING=OFF 
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
