 
pkgname=ostree
_pkgname=libostree
pkgver=2022.6
_pkgver=2022.6
pkgrel=1
pkgdesc="Suite of command line tools that combines a git-like model for committing and downloading bootable filesystem trees"
url="https://github.com/ostreedev/ostree"
arch=('x86_64')
license=('GPL')
depends=('glib2' 'gpgme' 'fuse3' 'libarchive' 'curl' 'mkinitcpio' 'util-linux' 'xz' 'zlib' 'openssl'
         'libsodium' 'dracut')
makedepends=('e2fsprogs' 'libxslt' 'python3')
source=("https://github.com/ostreedev/ostree/releases/download/v${_pkgver}/${_pkgname}-${pkgver}.tar.xz"
        'ostree-mkinitcpio.conf'
        "https://github.com/ostreedev/ostree/commit/4fd0d6b4c1566a75cb234a41e895a603c19448d4.diff")
md5sums=('25c87b942fa7007df711a5968023b3e5'
         '5b424fa5c03398f9ab3a60fd669a768a'
         '6473a024330a2b0b165331f4c9cdcd6b')

prepare() {
  cd ${_pkgname}-${_pkgver}

  cp ${srcdir}/ostree-mkinitcpio.conf src/boot/mkinitcpio/ostree-mkinitcpio.conf
  # https://forum.kaosx.us/d/2942-discover-wont-start-after-update-to-5245
  #patch -p1 -R -i $srcdir/4fd0d6b4c1566a75cb234a41e895a603c19448d4.diff
}

build() {
  cd ${_pkgname}-${_pkgver}

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib \
    --with-dracut \
    --with-mkinitcpio \
    --with-builtin-grub2-mkconfig \
    --with-openssl \
    --with-curl \
    --with-ed25519-libsodium \
    --without-soup

  make
}

package() {
  cd ${_pkgname}-${_pkgver}

  make DESTDIR=${pkgdir} install
}
