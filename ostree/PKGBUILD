 
pkgname=ostree
_pkgname=libostree
pkgver=2018.9.1
_pkgver=2018.9
pkgrel=1
pkgdesc="Suite of command line tools that combines a git-like model for committing and downloading bootable filesystem trees"
url="https://github.com/ostreedev/ostree"
arch=('x86_64')
license=('GPL')
depends=('glib2' 'gpgme' 'fuse' 'libarchive' 'libsoup' 'mkinitcpio' 'util-linux' 'xz' 'zlib' 'openssl')
makedepends=('e2fsprogs' 'libxslt' 'python3')
source=("https://github.com/ostreedev/ostree/releases/download/v${_pkgver}/${_pkgname}-${pkgver}.tar.xz"
        'ostree-mkinitcpio.conf')
md5sums=('96b5cbb53fc2d68a75c040a6d2fdbf1e'
         '5b424fa5c03398f9ab3a60fd669a768a')

prepare() {
  cd ${_pkgname}-${_pkgver}
  
  cp ${srcdir}/ostree-mkinitcpio.conf src/boot/mkinitcpio/ostree-mkinitcpio.conf
}

build() {
  cd ${_pkgname}-${_pkgver}

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib \
    --with-mkinitcpio \
    --with-builtin-grub2-mkconfig \
    --with-openssl

  make
}

package() {
  cd ${_pkgname}-${_pkgver}
  
  make DESTDIR=${pkgdir} install
}
