 
pkgname=ostree
_pkgname=libostree
pkgver=2018.5
pkgrel=1
pkgdesc="Suite of command line tools that combines a git-like model for committing and downloading bootable filesystem trees"
url="https://github.com/ostreedev/ostree"
arch=('x86_64')
license=('GPL')
depends=('glib2' 'gpgme' 'fuse' 'libarchive' 'libsoup' 'mkinitcpio' 'util-linux' 'xz' 'zlib' 'openssl')
makedepends=('e2fsprogs' 'libxslt' 'python3')
source=("https://github.com/ostreedev/ostree/releases/download/v${pkgver}/${_pkgname}-${pkgver}.tar.xz"
        'ostree-mkinitcpio.conf')
md5sums=('bebd5962e583b402a4a6902cf6cfbe48'
         '5b424fa5c03398f9ab3a60fd669a768a')

prepare() {
  cd ${_pkgname}-${pkgver}
  
  cp ${srcdir}/ostree-mkinitcpio.conf src/boot/mkinitcpio/ostree-mkinitcpio.conf
}

build() {
  cd ${_pkgname}-${pkgver}

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib \
    --with-mkinitcpio \
    --with-builtin-grub2-mkconfig \
    --with-openssl

  make
}

package() {
  cd ${_pkgname}-${pkgver}
  
  make DESTDIR=${pkgdir} install
}
