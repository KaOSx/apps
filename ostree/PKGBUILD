 
pkgname=ostree
_pkgname=libostree
pkgver=2022.1
_pkgver=2022.1
pkgrel=2
pkgdesc="Suite of command line tools that combines a git-like model for committing and downloading bootable filesystem trees"
url="https://github.com/ostreedev/ostree"
arch=('x86_64')
license=('GPL')
depends=('glib2' 'gpgme' 'fuse' 'libarchive' 'curl' 'mkinitcpio' 'util-linux' 'xz' 'zlib' 'openssl'
         'libsodium' 'dracut')
makedepends=('e2fsprogs' 'libxslt' 'python3')
source=("https://github.com/ostreedev/ostree/releases/download/v${_pkgver}/${_pkgname}-${pkgver}.tar.xz"
        'ostree-mkinitcpio.conf'
        "https://github.com/ostreedev/ostree/commit/522d31b2d4604026f1c0a442887ca9d4b319e9cc.diff")
md5sums=('f41caf46de194432906d22eeb4ab383f'
         '5b424fa5c03398f9ab3a60fd669a768a'
         '073c8ab33717a8070ab4868eaac906f9')

prepare() {
  cd ${_pkgname}-${_pkgver}
  
  cp ${srcdir}/ostree-mkinitcpio.conf src/boot/mkinitcpio/ostree-mkinitcpio.conf
  # https://bugs.kde.org/show_bug.cgi?id=411657
  #patch -p1 -i $srcdir/522d31b2d4604026f1c0a442887ca9d4b319e9cc.diff
}

build() {
  cd ${_pkgname}-${_pkgver}

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib \
    --with-dracut \
    --with-mkinitcpio \
    --with-builtin-grub2-mkconfig \
    --with-openssl \
    --with-curl \
    --with-ed25519-libsodium \
    --without-soup

  make
}

package() {
  cd ${_pkgname}-${_pkgver}
  
  make DESTDIR=${pkgdir} install
}
