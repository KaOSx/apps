
pkgname=gimp
pkgver=2.99.14
_pkgver=2.99
pkgrel=2
pkgdesc="GNU Image Manipulation Program"
arch=('x86_64')
url="https://www.gimp.org/"
license=('GPL3' 'LGPL3')
depends=('poppler' 'lcms2' 'libxpm' 'librsvg' 'libmng' 'libgudev' 'dbus-glib' 'libexif' 
         'gegl' 'desktop-file-utils' 'hicolor-icon-theme' 'gexiv2' 'libmypaint' 'alsa-lib'
         'curl' 'glib-networking' 'mypaint-brushes' 'ghostscript' 'aalib' 'libunwind' 'python3'
         'appstream-glib' 'gtk3' 'jpeg-xl')
makedepends=('intltool' 'iso-codes' 'meson' 'ninja' 'python3-gobject3')
options=('!libtool' '!makeflags')
source=("https://download.gimp.org/pub/gimp/v${_pkgver}/gimp-${pkgver}.tar.xz"
#source=("https://github.com/GNOME/gimp/archive/${_commit}.zip"
        'linux.gpl'
        'Adwaita.tar.gz')
sha256sums=('313a205475d1ff03c5c4d9602f09f5c975ba6c1c79d8843e2396f9fe2abdf7a8'
            '1003bbf5fc292d0d63be44562f46506f7b2ca5729770da9d38d3bb2e8a2f36b3'
            '4e23495f95c9716061b7daa31135d0a964ae0950e118d33b65d67aea988bea58')

prepare() {
  cd ${pkgname}-${pkgver}

  sed -i -e 's|mypaint-brushes-1.0|mypaint-brushes-2.0|' meson.build
}

build() {
  mkdir -p build
  cd build

  meson ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    --libexecdir=/usr/lib \
    -Djavascript=false \
    -Dlua=false \
    -Dbug-report-url=https://kaosx.us/bugs/
  ninja
}

package() {
  cd build
  DESTDIR=${pkgdir} ninja install

  install -D -m644 ${srcdir}/linux.gpl ${pkgdir}/usr/share/gimp/2.99/palettes/Linux.gpl

  ln -sf gimp-console-${pkgver}.1.gz ${pkgdir}/usr/share/man/man1/gimp-console.1.gz
  ln -sf gimprc-${pkgver}.5.gz ${pkgdir}/usr/share/man/man5/gimprc.5.gz
  ln -sf gimptool-2.0.1.gz ${pkgdir}/usr/share/man/man1/gimptool.1.gz
  ln -s gimp-${pkgver}.1.gz ${pkgdir}/usr/share/man/man1/gimp.1.gz
  # workaround to stop gimp from crashing on image-missing.png
  mkdir -p ${pkgdir}/usr/share/icons/Adwaita/48x48/status
  install -D -m644 ${srcdir}/Adwaita/index.theme ${pkgdir}/usr/share/icons/Adwaita/
  install -D -m644 ${srcdir}/Adwaita/48x48/status/image-missing.png ${pkgdir}/usr/share/icons/Adwaita/48x48/status/
}
