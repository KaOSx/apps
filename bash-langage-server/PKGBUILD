
pkgname=bash-language-server
pkgver=5.4.2
pkgrel=1
pkgdesc="Bash language server implementation based on Tree Sitter and its grammar for Bash with explainshell integration."
arch=("x86_64")
url="https://github.com/bash-lsp/bash-language-server"
license=('MIT')
depends=('nodejs')
makedepends=('pnpm')
source=("https://github.com/bash-lsp/bash-language-server/archive/refs/tags/server-${pkgver}.tar.gz")
md5sums=('d8a5f94961662a6baa0188e19b75428d')

build() {
    cd ${pkgname}-server-${pkgver}

    pnpm install --frozen-lockfile
    pnpm run compile
}

package() {
    cd ${pkgname}-server-${pkgver}/server

    install -d ${pkgdir}/usr/lib/node_modules/${pkgname}
    install -d ${pkgdir}/usr/bin

    # running npm on this dir requires husky
    rm -r node_modules
    npm install --omit=dev

    cp -a * ${pkgdir}/usr/lib/node_modules/${pkgname}/
    ln -s /usr/lib/node_modules/${pkgname}/out/cli.js $pkgdir/usr/bin/${pkgname}
    chmod +x ${pkgdir}/usr/lib/node_modules/${pkgname}/out/cli.js

    install -Dm644 ../LICENSE -t ${pkgdir}/usr/share/licenses/${pkgname}/
}
