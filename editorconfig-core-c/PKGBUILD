 
pkgname=editorconfig-core-c
pkgver=0.12.8
pkgrel=1
pkgdesc='EditorConfig makes it easy to maintain the correct coding style when switching between different text editors and between different projects.'
arch=('x86_64')
url='https://editorconfig.org/'
license=('BSD')
depends=('pcre2')
makedepends=('cmake' 'doxygen')
groups=('programming')
source=("https://github.com/editorconfig/editorconfig-core-c/archive/v${pkgver}.tar.gz")
sha256sums=('508f7633416a2ce3c05104ea7daac61c4953803c9935cca6e059086cfa67ee63')

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_DOCDIR=/usr/share/man
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
  install -Dm644 ${pkgname}-${pkgver}/LICENSE -t ${pkgdir}/usr/share/licenses/${pkgname}
}
