
pkgname=octopi
pkgver=0.10.81
pkgrel=1
_commit=3b9407250283dd7bf0e32f4f25288435b1863136
pkgdesc="This is Octopi, a powerful Pacman frontend using Qt libs"
url="https://tintaescura.com/projects/octopi/"
arch=('x86_64')
license=('GPL2')
depends=('pacman' 'pacman-contrib' 'pkgfile' 'knotifications' 'alpm_octopi_utils' 'qtermwidget' 'sudo')
makedepends=('qt5-tools' 'cmake')
groups=('system')
#install=octopi.install
source=("https://github.com/aarnt/octopi/archive/${_commit}.zip"
        "images3.tar.xz"
        'folder_tree.png'
        'searchlineedit.diff')
md5sums=('e95e45f6eb73dcea6ace1ecc0c86acb7'
         '09decb09a2fa58f2211ff3d5e0350ff0'
         'eff9d31629ad8de08c9198524f798bd3'
         '79be301d683ccd8572db0571f868298c')

prepare() {
   cd ${pkgname}-${_commit}/
   rm -r resources/images
   mv -iv ${srcdir}/images/ resources/
   cp resources/images/octopi_green.png resources/images/octopi.png
   cp ${srcdir}/folder_tree.png resources/images/folder_tree.png

   # enable the kstatus switch
   sed -e "s|#KSTATUS|KSTATUS|" -i notifier/octopi-notifier.pro
   sed -e "s|gui network|gui network dbus|" -i notifier/octopi-notifier.pro
   
   sed -e "s|pacman -U --force|pacman -U|" -i src/pacmanexec.cpp
   
   # show correct text for SysInfo window
   sed -e "s|mhwd -li -d|installation.log|" -i src/strconstants.cpp
   sed -e "s|head --bytes=256K /var/log/pacman.log|/var/log/pacman.log|" -i src/strconstants.cpp
}
         
build() {
  cmake -B build -S ${pkgname}-${_commit} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
