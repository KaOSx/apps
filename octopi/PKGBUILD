
pkgname=octopi
pkgver=0.9.0.98
pkgrel=2
_commit=e187d54d0b63cb7fbbd518d028038148db2d5696
pkgdesc="This is Octopi, a powerful Pacman frontend using Qt libs"
url="https://octopiproject.wordpress.com/"
arch=('x86_64')
license=('GPL2')
depends=('pacman' 'pacman-contrib' 'pkgfile' 'knotifications' 'alpm_octopi_utils' 'qtermwidget' 'sudo')
makedepends=('qt5-tools')
groups=('system')
#install=octopi.install
source=("https://github.com/aarnt/octopi/archive/${_commit}.zip"
        "images3.tar.xz"
        'folder_tree.png'
        'xterm.diff')
md5sums=('c2d2d3f450ada1ace9dd5276d5f48381'
         '09decb09a2fa58f2211ff3d5e0350ff0'
         'eff9d31629ad8de08c9198524f798bd3'
         '3231f1cdccda4e97a823c05765bac876')

prepare() {
   cd ${pkgname}-${_commit}/
   rm -r resources/images
   mv -iv ${srcdir}/images/ resources/
   cp resources/images/octopi_green.png resources/images/octopi.png
   cp ${srcdir}/folder_tree.png resources/images/folder_tree.png

   # enable the kstatus switch
   sed -e "s|#KSTATUS|KSTATUS|" -i notifier/octopi-notifier.pro
   sed -e "s|gui network|gui network dbus|" -i notifier/octopi-notifier.pro
   
   sed -e "s|pacman -U --force|pacman -U|" -i src/pacmanexec.cpp
   
   # show correct text for SysInfo window
   sed -e "s|mhwd -li -d|installation.log|" -i src/strconstants.cpp
   sed -e "s|head --bytes=256K /var/log/pacman.log|/var/log/pacman.log|" -i src/strconstants.cpp
   # different color for search not found, Kvantum needed
   sed -e "s|QPalette::Text, Qt::white|QPalette::Text, Qt::darkRed|" -i src/searchlineedit.cpp
   
   
   #patch -R -p1 -i ${srcdir}/a9d9eb30d37e97369cfc64204fe0bde9e0e29918.diff
}
         
build() {
   cd ${pkgname}-${_commit}
   
   /usr/lib/qt5/bin/qmake octopi.pro
   make
   
   cd sudo
   /usr/lib/qt5/bin/qmake octopi-sudo.pro
   make
   cd ..
   
   cd helper
   /usr/lib/qt5/bin/qmake octopi-helper.pro
   make
   cd ..
   
   cd notifier
   /usr/lib/qt5/bin/qmake octopi-notifier.pro
   make
   cd ..
   
   cd cachecleaner
   /usr/lib/qt5/bin/qmake octopi-cachecleaner.pro
   make
}

package() {
   cd ${pkgname}-${_commit}
   make INSTALL_ROOT=${pkgdir} install
   
   cd sudo
   make INSTALL_ROOT=${pkgdir} install
   cd ..
   
   cd helper
   make INSTALL_ROOT=${pkgdir} install
   cd ..
   
   cd notifier
   make INSTALL_ROOT=${pkgdir} install
   cd ..
   
   cd cachecleaner
   make INSTALL_ROOT=${pkgdir} install
}
