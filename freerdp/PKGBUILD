
pkgname=freerdp
_pkgname=FreeRDP
pkgver=2.0.0rc4
_pkgver=2.0.0-rc4
_commit=f8c9f438436a4492e37011396148d804f7e3651f
pkgrel=2
pkgdesc="Free RDP client"
arch=('x86_64')
url="https://www.freerdp.com/"
license=('GPL')
depends=('openssl' 'libxcursor' 'libcups' 'alsa-lib' 'libxext' 'libxdamage'
        'ffmpeg' 'libxkbfile' 'libxinerama' 'libxv' 'pulseaudio' 'libxrender' 
        'libxfixes' 'gst-plugins-base' 'libxkbcommon' 'libxrandr') 
makedepends=('krb5' 'cmake' 'damageproto' 'xmlto' 'docbook-xsl')
#source=("https://github.com/FreeRDP/FreeRDP/archive/${_commit}.zip")
source=("https://pub.freerdp.com/releases/${pkgname}-${_pkgver}.tar.gz")
sha256sums=('bd1cd5579765b2d6b444508cafc7b3669c68ff2246941a081e771f744b150f6a')

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${_pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DWITH_WAYLAND=ON \
    -DWITH_PULSE=ON \
    -DWITH_CUPS=ON
  make
}

package() {
  cd build
  
  make DESTDIR=$pkgdir install
}
