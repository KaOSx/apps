
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=calendarsupport
pkgver=${_kdever}
pkgrel=1
pkgdesc="This lib provides calendar support."
arch=('x86_64')
url='https://projects.kde.org/projects/kde/pim/calendersupport'
license=('LGPL')
depends=('akonadi-mime' 'ki18n' 'kmime' 'kcodecs' 'kcalutils' 'kcalcore' 'kidentitymanagement'
         'kholidays' 'akonadi-calendar' 'pimcommon' 'kdepim-apps-libs' 'ktextwidgets')
makedepends=('extra-cmake-modules' 'kdoctools' 'boost' 'qt5-tools')
groups=('kde' 'kdepim' 'kde-uninstall')
source=("http://download.kde.org/stable/applications/${_kdever}/src/${pkgname}-${_kdever}.tar.xz")
md5sums=(`grep ${pkgname}-${_kdever}.tar.xz ../kde-sc.md5 | cut -d" " -f1`)

build() {
  mkdir build
  cd build 
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DKDE_INSTALL_QMLDIR=/usr/lib/qt5/qml \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
    -DBUILD_TESTING=OFF 
  make 
}

package() {
  cd build
  
  make DESTDIR=${pkgdir} install
}
