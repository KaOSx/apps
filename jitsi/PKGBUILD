
pkgname=jitsi
pkgver=2.10.5550
pkgrel=3
pkgdesc="An audio/video SIP VoIP phone and instant messenger written in Java (formerly SIP-Communicator)"
arch=('x86_64')
url="https://jitsi.org"
license=('LGPL')
depends=('java-runtime' 'gtk2')
makedepends=('apache-ant' 'java-environment')
options=('!strip' '!emptydirs' 'zipman' '!libtool' 'docs')
#source=("https://github.com/jitsi/jitsi/archive/Jitsi-2.10.tar.gz")
source=("https://download.jitsi.org/jitsi/src/jitsi-src-${pkgver}.zip"
        'jitsi.desktop'
        'jitsi.sh')
md5sums=('dc512fbf49a6056099917ff9ae8f3315'
         'aad7cf1fb18ff5d7c964834ecc38aed8'
         'ab1953a6442b685c7acb3c79b630a495')

build()
{
  cd ${pkgname}
  # append the build revision to the jitsi version
  sed -i "s/0\.build\.by\.SVN/build.${pkgver}/" src/net/java/sip/communicator/impl/version/NightlyBuildID.java
  
  . /etc/profile.d/apache-ant.sh
  ant rebuild
}

package() {
  cd ${pkgname}
  
  find lib/ lib/bundle/ -maxdepth 1 -type f \
    -exec install -Dm644 {} ${pkgdir}/usr/lib/${pkgname}/{} \;
  
  shopt -sq extglob
  
  find lib/native/linux$(sed 's/_/-/g' <<<${CARCH/#*(i?86|x86)/})/ -maxdepth 1 -type f \
    -execdir install -Dm644 {} ${pkgdir}/usr/lib/${pkgname}/lib/native/{} \;
    
  find sc-bundles/{,os-specific/linux/} -maxdepth 1 -type f \
    -execdir install -Dm644 {} ${pkgdir}/usr/lib/${pkgname}/sc-bundles/{} \;
    
  install -Dm755 ${srcdir}/${pkgname}.sh ${pkgdir}/usr/bin/${pkgname}
  install -Dm644 ${srcdir}/${pkgname}.desktop ${pkgdir}/usr/share/applications/${pkgname}.desktop

  cd "resources/install/debian/"
  for _file in *.{svg,xpm}; do
    install -Dm644 ${_file} ${pkgdir}/usr/share/pixmaps/${_file}
  done
}
