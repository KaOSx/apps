 
pkgname=qcoro
pkgver=0.6.0
pkgrel=1
pkgdesc="Library that provides set of tools to make use of the C++20 coroutines in connection with certain asynchronous Qt actions."
arch=('x86_64')
url="https://qcoro.dvratil.cz/"
license=('MIT')
depends=('qt5-websockets')
makedepends=('cmake' 'clang')
source=("https://github.com/danvratil/qcoro/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('26d6ea1103c51b895e93d27b59bee394f80db8fd9ca21e7c0056583b1938906d')

build() {
  export CC=/usr/bin/clang
  export CXX=/usr/bin/clang++

  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_SHARED_LIBS=ON \
    -DUSE_QT_VERSION=5
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
  install -D -m644 ${pkgname}-${pkgver}/LICENSES/MIT.txt ${pkgdir}/usr/share/licenses/${pkgname}/MIT.txt
}
