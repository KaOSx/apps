 
pkgname=qcoro
pkgver=0.4.0
pkgrel=1
pkgdesc="Library that provides set of tools to make use of the C++20 coroutines in connection with certain asynchronous Qt actions."
arch=('x86_64')
url="https://qcoro.dvratil.cz/"
license=('MIT')
depends=('qt5-base')
makedepends=('cmake')
source=("https://github.com/danvratil/qcoro/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('0e68b3f0ce7bf521ffbdd731464d2d60d8d7a39a749b551ed26855a1707d86d1')

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_SHARED_LIBS=ON \
    -DUSE_QT_VERSION=5
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
  install -D -m644 ${pkgname}-${pkgver}/LICENSES/MIT.txt ${pkgdir}/usr/share/licenses/${pkgname}/MIT.txt
}
