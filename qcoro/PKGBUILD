 
pkgname=qcoro
pkgver=0.8.0
pkgrel=1
pkgdesc="Library that provides set of tools to make use of the C++20 coroutines in connection with certain asynchronous Qt actions."
arch=('x86_64')
url="https://qcoro.dvratil.cz/"
license=('MIT')
depends=('qt5-websockets')
makedepends=('cmake')
source=("https://github.com/danvratil/qcoro/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('28f90d817f42352084242f7a1ff090c5cff92359e4dbecea8f04d39ebcf630f8')

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_SHARED_LIBS=ON \
    -DUSE_QT_VERSION=5
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
  install -D -m644 ${pkgname}-${pkgver}/LICENSES/MIT.txt ${pkgdir}/usr/share/licenses/${pkgname}/MIT.txt
}
