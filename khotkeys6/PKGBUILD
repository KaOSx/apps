
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=khotkeys6
_pkgname=khotkeys
pkgver=${_plasmaver}
pkgrel=2
pkgdesc='Trigger actions when certain keys are pressed.'
arch=('x86_64')
url='https://invent.kde.org/plasma/khotkeys'
license=('LGPL')
depends=('kdbusaddons6' 'kglobalaccel6' 'ki18n6' 'kcmutils6' 'kio6' 'ktextwidgets6' 'kxmlgui6'
         'plasma-framework6' 'plasma-workspace6')
makedepends=('extra-cmake-modules6' 'kdoctools6')
groups=('kdeplasma6')
#source=("https://download.kde.org/stable/plasma/${pkgver}/${_pkgname}-${pkgver}.tar.xz"
#        'dbus_action.diff')
#md5sums=(`grep ${_pkgname}-${_plasmaver}.tar.xz ../kf5.md5 | cut -d" " -f1`
#         'b72371a922acbf0b812d92cd509595a9')
source=("git+https://invent.kde.org/plasma/${_pkgname}.git"
        'dbus_action.diff')
md5sums=('SKIP'
         'b8b776b8258f44a0b2977f240980dd76')

prepare() {
  cd ${_pkgname}

  patch -p1 -i ${srcdir}/dbus_action.diff
  sed -i 's|qdbusviewer|qdbusviewer-qt6|' kcm_hotkeys/actions/dbus_action_widget.cpp
}

build() {
  cmake -B build -S ${_pkgname} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_LIBEXECDIR=/usr/lib \
    -DBUILD_TESTING=OFF \
    -DQT_MAJOR_VERSION=6
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
