
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=khotkeys6
_pkgname=khotkeys
pkgver=${_plasmaver}
pkgrel=1
pkgdesc='Trigger actions when certain keys are pressed.'
arch=('x86_64')
url='https://invent.kde.org/plasma/khotkeys'
license=('LGPL')
depends=('kdbusaddons6' 'kglobalaccel6' 'ki18n6' 'kcmutils6' 'kio6' 'ktextwidgets6' 'kxmlgui6'
         'plasma-framework6' 'plasma-workspace6')
makedepends=('extra-cmake-modules' 'kdoctools6')
groups=('kdeplasma6')
source=("https://download.kde.org/stable/plasma/${pkgver}/${_pkgname}-${pkgver}.tar.xz"
        'dbus_action.diff')
md5sums=(`grep ${_pkgname}-${_plasmaver}.tar.xz ../kf5.md5 | cut -d" " -f1`
         'b72371a922acbf0b812d92cd509595a9')

prepare() {
  cd ${_pkgname}-${pkgver}
  
  patch -p1 -i ${srcdir}/dbus_action.diff
  sed -i 's|qdbusviewer|qdbusviewer-qt6|' kcm_hotkeys/actions/dbus_action_widget.cpp
}

build() {
  cmake -B build -S ${_pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_LIBEXECDIR=/usr/lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_WITH_QT6=ON \
    -DEXCLUDE_DEPRECATED_BEFORE_AND_AT=${_kf5ver} \
    -DQT_MAJOR_VERSION=6
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
