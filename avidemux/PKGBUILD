
pkgname=avidemux
pkgver=2.8.1
_commit=ed7aa6bdbffc43a2cd198130a353b088abd02aa0
pkgrel=5
pkgdesc="Video editor designed for simple cutting, filtering and encoding tasks using a variety of codecs."
arch=('x86_64')
url="http://fixounet.free.fr/avidemux/"
license=('Public Domain')
depends=('qt5-base' 'flac' 'libva' 'libxv' 'desktop-file-utils' 'x264' 'pulseaudio' 'jack'
         'lame' 'sqlite' 'libvpx' 'x265' 'faac' 'faad2' 'fribidi' 'glu' 'libdca' 'opencore-amr'
         'xvidcore')
makedepends=('cmake' 'yasm' 'pkg-config' 'qt5-tools')
options=('!makeflags')
source=("https://downloads.sourceforge.net/avidemux/avidemux_${pkgver}.tar.gz"
#source=("https://github.com/mean00/avidemux2/archive/${_commit}.zip"
        'bootStrap.diff'
        "https://github.com/mean00/avidemux2/commit/4fc46b6415373f539a8cc7974258e03566f88a30.diff")
md5sums=('fee41934cba460ec0fde2a453d5a6307'
         '4a6a91ac7d345e6c19af0c6674f3659c'
         '0f205b38fb2c4ea0c6eeab8ffa59c5d2')

prepare() {
    cd ${pkgname}_${pkgver}

    patch -p1 -i ${srcdir}/bootStrap.diff
    # binutils 2.42 needed
    patch -p1 -i ${srcdir}/4fc46b6415373f539a8cc7974258e03566f88a30.diff
    # Don't hide the build output
    sed 's|$PARAL >& /tmp/log$BUILDDIR|$PARAL |g' -i bootStrap.bash
    # set to Qt6
    #sed 's|avidemux3_qt5|avidemux3_qt6|g' -i avidemux/qt4/xdg_data/org.avidemux.Avidemux.desktop
}

build() {
    cd ${pkgname}_${pkgver}
    bash bootStrap.bash --with-core \
        --with-plugins
        #--enable-qt6
}

package() {
    cd ${pkgname}_${pkgver}
    cp -r install/usr/ ${pkgdir}/

    install -Dm644 ${pkgname}_icon.png ${pkgdir}/usr/share/pixmaps/${pkgname}.png
}
