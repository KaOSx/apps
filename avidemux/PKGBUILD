
pkgname=avidemux
pkgver=2.7.3
_commit=ed7aa6bdbffc43a2cd198130a353b088abd02aa0
pkgrel=1
pkgdesc="Video editor designed for simple cutting, filtering and encoding tasks using a variety of codecs."
arch=('x86_64')
url="http://fixounet.free.fr/avidemux/"
license=('Public Domain')
depends=('qt5-script' 'flac' 'libva' 'libxv' 'desktop-file-utils' 'x264' 'pulseaudio' 'jack'
         'lame' 'sqlite' 'libvpx' 'x265' 'faac' 'faad2' 'fribidi' 'glu' 'libdca' 'opencore-amr'
         'xvidcore')
makedepends=('cmake' 'yasm' 'pkg-config' 'qt5-tools')
options=('!makeflags')
source=("https://downloads.sourceforge.net/avidemux/avidemux_${pkgver}.tar.gz"
#source=("https://github.com/mean00/avidemux2/archive/${_commit}.zip"
        'bootStrap.diff')
md5sums=('f01d2cfeda010b56ce0f1fb111201463'
         'fb9c8caa824dd013cedcabe90aab37f9')

prepare() {
    cd ${pkgname}_${pkgver}
    # try to disable vdpau, build fails on vdpau, workaround, remove libvdpau from chroot
    #sed 's|SET(USE_VDPAU 1)|#SET(USE_VDPAU 1)|g' -i cmake/admCheckVDPAU.cmake
    patch -p1 -i $srcdir/bootStrap.diff
}

build() {
    cd ${pkgname}_${pkgver}
    bash bootStrap.bash --with-core \
        --with-plugins 
}

package() {
    cd ${pkgname}_${pkgver}
    cp -r install/usr/ ${pkgdir}/

    install -Dm644 ${pkgname}_icon.png ${pkgdir}/usr/share/pixmaps/${pkgname}.png
}
