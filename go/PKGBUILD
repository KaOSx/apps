
pkgname=go
pkgver=1.10.3
pkgrel=1
pkgdesc='Google Go compiler and tools (release version)'
arch=('x86_64')
url='http://golang.org/'
license=('BSD')
depends=('perl' 'gawk')
makedepends=('inetutils' 'go')
options=('!strip' 'staticlibs')
install="${pkgname}.install"
# check for versions: http://golang.org/dl/
source=("https://storage.googleapis.com/golang/${pkgname}${pkgver}.linux-amd64.tar.gz")
sha256sums=('fa1b0e45d3b647c252f51f5e1204aba049cde4af177ef9f2181f43004f901035')

build() {
  cd go/src

  export GOROOT="${srcdir}/${pkgname}"
  export GOBIN="$GOROOT/bin"
  export GOPATH="${srcdir}/"
  export GOROOT_FINAL=/usr/lib/go
  export GOROOT_BOOTSTRAP="$GOROOT_FINAL"
  export GOOS=linux
  
  bash make.bash
  
  export GOARCH=amd64

  bash make.bash --no-clean

  GOOS=linux

  $GOROOT/bin/go get -d golang.org/x/tools/cmd/godoc
  $GOROOT/bin/go build -o ${srcdir}/godoc golang.org/x/tools/cmd/godoc
  for tool in benchcmp bundle callgraph cover digraph eg fiximports goimports gorename html2article present ssadump stress stringer tip; do
    $GOROOT/bin/go get -d golang.org/x/tools/cmd/${tool}
    $GOROOT/bin/go build -o $GOROOT/pkg/tool/${GOOS}_amd64/${tool} golang.org/x/tools/cmd/${tool}
  done
}

check() {
  cd go

  export GOOS=linux
  export GOARCH=amd64
  export GOROOT="${srcdir}/${pkgname}"
  export GOBIN="$GOROOT/bin"
  export GOCACHE=off
  export PATH="${srcdir}/${pkgname}-$pkgver/bin:$PATH"

  cd src && bash run.bash --no-rebuild || true
}

package() {
  cd go

  export GOROOT="${srcdir}/${pkgname}"
  export GOBIN="$GOROOT/bin"

  install -Dm755 "${srcdir}/godoc" "${pkgdir}/usr/bin/godoc"

  install -Dm644 LICENSE \
    "${pkgdir}/usr/share/licenses/go/LICENSE"

  mkdir -p \
    "${pkgdir}/"{etc/profile.d,usr/{share/go,lib/go,lib/go/src,lib/go/site/src}}

  cp -r doc misc -t "${pkgdir}/usr/share/go"
  ln -s /usr/share/go/doc "${pkgdir}/usr/lib/go/doc"
  cp -a bin "${pkgdir}/usr"
  cp -a pkg "${pkgdir}/usr/lib/go"
  cp -a "$GOROOT/src" "${pkgdir}/usr/lib/go/"
  cp -a "$GOROOT/src/cmd" "${pkgdir}/usr/lib/go/src/cmd"
  cp -a "$GOROOT/lib" "${pkgdir}/usr/lib/go/"

  install -Dm644 src/Make.* "${pkgdir}/usr/lib/go/src"

  find "${pkgdir}/usr/lib/go/src/" -type f -name '*.[ao]' -delete
  find "${pkgdir}" -type f -name sql.go -exec chmod -x {} \;
  find "${pkgdir}/usr/lib/go/src" -type f -executable -delete

  ln -sf /usr/bin "${pkgdir}/usr/lib/go/bin"

  install -Dm755 src/make.bash "${pkgdir}/usr/lib/go/src/make.bash"
  install -Dm755 src/run.bash "${pkgdir}/usr/lib/go/src/run.bash"
  cp -r misc/ "${pkgdir}/usr/lib/go/"

  install -Dm644 favicon.ico "${pkgdir}/usr/lib/go/favicon.ico"

  rm -f "${pkgdir}/usr/share/go/doc/articles/wiki/get.bin"

  install -Dm644 VERSION "${pkgdir}/usr/lib/go/VERSION"

  find "${pkgdir}/usr/"{lib/go/pkg,bin} -type f -exec touch '{}' +
}
