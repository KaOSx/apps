
pkgname=krusader
pkgver=2.8.0
pkgrel=1
epoch=1
pkgdesc="An alternative file manager for KDE resembling Midnight Commander"
url="https://krusader.org/"
arch=('x86_64')
license=('GPL')
depends=('karchive' 'kbookmarks' 'kcodecs' 'kcompletion' 'kconfig' 'ki18n' 'kiconthemes' 'kitemviews'
         'kio' 'knotifications' 'kparts' 'solid' 'ktextwidgets' 'kwallet' 'kwidgetsaddons'
         'kwindowsystem' 'kxmlgui' 'acl' 'ark' 'keditbookmarks' 'konsole' 'kate')
makedepends=('extra-cmake-modules' 'kdoctools' 'docbook-xsl')
groups=('kde-admin' 'kde-system')
source=("https://download.kde.org/stable/krusader/${pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('8e4cf05a9318b0bc1b0941811b988b2f2bb0c04a0d1e37998212a9190cf2c29a')

prepare() {
  cd ${pkgname}-${pkgver}

  #patch -p1 -i "$srcdir/kde_bug309119.diff" 
}

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
    -DKDESU_PATH=/usr/lib/kf5/kdesu
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
