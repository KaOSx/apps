
pkgname=mellowplayer
_pkgname=MellowPlayer
pkgver=3.5.6
_commit=0f919a1
pkgrel=1
pkgdesc="Open source and cross-platform desktop application that runs web-based music streaming 
         services in its own window and provides integration with your desktop, specifically crafted for KaOS"
url='https://colinduquesnoy.gitlab.io/MellowPlayer/'
license=('GPL')
arch=('x86_64')
depends=('kglobalaccel' 'knotifications' 'qtwebengine' 'pepper-flash' 'widevine'
         'qt5-quickcontrols2' 'qt5-quickcontrols' 'libnotify')
makedepends=('qt5-tools' 'cmake')
#source=("https://github.com/ColinDuquesnoy/MellowPlayer/tarball/qbs/MellowPlayer-${pkgver}.tar.gz")
source=("https://gitlab.com/ColinDuquesnoy/MellowPlayer/-/archive/${pkgver}/${_pkgname}-${pkgver}.tar.gz")
md5sums=('0ea76ba0cb79a16071f918ced0d8c1db')

prepare() {
  cd ${_pkgname}-${pkgver}

  sed -i 's|Exec=MellowPlayer|Exec=MellowPlayer --ppapi-widevine-path=/usr/lib/chromium/libwidevinecdmadapter.so|' src/main/share/applications/mellowplayer.desktop
}


build() {
  mkdir -p build
  cd build
  
  cmake ../${_pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTS=OFF \
    -Wno-dev \
    -DDEFAULT_THEME="Midna"
  make 
}

package() {
  cd build

  make DESTDIR=${pkgdir} install
}
