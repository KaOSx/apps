
# only build local, server for unknown reasons does not follow perl install dir
pkgname=image-exiftool
_pkgname=Image-ExifTool
pkgver=12.37
pkgrel=1
pkgdesc="Platform-independent Perl library plus a command-line application for reading, writing and editing meta information in a wide variety of files."
arch=('x86_64')
url="https://exiftool.org/"
license=('GPL')
# https://github.com/exiftool/exiftool/blob/master/README for recommended depends
depends=('perl-archive-zip')
options=('!emptydirs')
source=("https://exiftool.org/${_pkgname}-$pkgver.tar.gz")
sha1sums=('ecb4e89cffee51208f5bcea6903ed446299ab1d3')

build() {
  cd ${_pkgname}-${pkgver}

  perl Makefile.PL INSTALLDIRS=vendor 
  make
}

check() {
  cd ${_pkgname}-${pkgver}

  make test
}

package() {
  cd ${_pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install
}
