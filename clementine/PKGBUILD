
pkgname=clementine
_pkgname=Clementine
pkgver=1.3.93
_pkgver=1.4.0rc1
pkgrel=5
_commit=d20c2244a82b65f1d2c444091649e28834a63341
pkgdesc="Modern music player and library organiser that takes advantage of Qt."
url="http://www.clementine-player.org/"
license=('GPL')
arch=('x86_64')
depends=('gstreamer' 'taglib' 'glew' 'liblastfm' 'libmtp' 'libplist'
         'hicolor-icon-theme' 'qt5-x11extras' 'libimobiledevice' 'protobuf'
         'libcdio' 'libcdio-paranoia' 'qca-qt5' 'chromaprint' 'sparsehash' 
         'gst-plugins-bad' 'gst-plugins-good' 'gst-plugins-ugly' 'pulseaudio' 
         'crypto++' 'libmygpo-qt')
makedepends=('cmake' 'boost' 'mesa' 'qt5-tools')
groups=('multimedia')
source=("https://github.com/clementine-player/Clementine/releases/download/${_pkgver}/${pkgname}-${_pkgver}.tar.xz"
#source=("https://github.com/clementine-player/Clementine/archive/${_commit}.zip"
        'qt5.15.diff')
md5sums=('c58d09300081854485749cb8c8b18d61'
         '6d3ca42c0edb6ca32831aa2be518abc1')

prepare() {
  cd ${pkgname}-${_pkgver}

  sed -i 's|Exec=clementine %U|Exec=clementine|g' ${srcdir}/${pkgname}-${_pkgver}/dist/clementine.desktop
  patch -p1 -i ${srcdir}/qt5.15.diff
}

build() {  
  mkdir -p build 
  cd build
  
  cmake ../${pkgname}-${_pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DENABLE_BREAKPAD=OFF \
    -DENABLE_SPOTIFY_BLOB=OFF \
    -DUSE_SYSTEM_TAGLIB=ON \
    -DENABLE_VISUALISATIONS=OFF
  make
}

package() {
  cd build
  
  make install DESTDIR=${pkgdir}
}

