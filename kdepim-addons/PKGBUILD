
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=kdepim-addons
pkgver=${_kdever}
pkgrel=1
arch=('x86_64')
pkgdesc="Addons for kdepim apps (as kmail addons/ kmail theme/ kaddressbook addons etc.)"
url="https://github.com/KDE/kdepim-addons"
license=('GPL' 'LGPL' 'FDL')
depends=('libkgapi' 'qtwebengine' 'boost-libs'
         #frameworks
         'ki18n' 'khtml' 'kconfig' 'kdbusaddons' 'kxmlgui' 'kdeclarative' 
         #kdepimlibs
         'mailcommon' 'kdepim-apps-libs' 'libkleo' 'grantleetheme'
         'pimcommon' 'libkdepim' 'incidenceeditor' 'ktnef' 'akonadi-import-wizard'
         'messagelib' 'calendarsupport' 'eventviews' 'akonadi' 'libgravatar' 'libksieve'
         'kpkpass' 'kitinerary' 'kontactinterface')
makedepends=('extra-cmake-modules' 'kdoctools' 'clang' 'boost')
groups=('kde' 'kdepim' 'kde-uninstall')
source=($_mirror/${pkgname}-${_kdever}.tar.xz)
md5sums=(`grep ${pkgname}-${_kdever}.tar.xz ../kde-sc.md5 | cut -d" " -f1`)

prepare() {
  cd $pkgname-${pkgver}
  sed -i -e 's|setRecursiveFiltering(true)|setRecursiveFilteringEnabled(true);|' sieveeditor/imapfoldercompletion/selectimapfolderwidget.cpp
  # fix license issue
  #patch -p1 -i ${srcdir}/ad9b246409bf9fbea70f57a69b4cfef1ca45c2b3.diff
}

build() {
  mkdir build
  
  export CC=/usr/bin/clang
  export CXX=/usr/bin/clang++
  
  cd build 
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_LIBEXECDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_QMLDIR=/usr/lib/qt5/qml \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=FALSE \
    -DKDEPIMADDONS_BUILD_EXAMPLES=TRUE
  make 
}

package() {
  cd build
  
  make DESTDIR=${pkgdir} install
}
