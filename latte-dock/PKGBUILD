
pkgname=latte-dock
pkgver=0.8.4
pkgrel=1
pkgdesc="Dock based on plasma frameworks that provides an elegant and intuitive experience for your tasks and plasmoids"
arch=('x86_64')
url="https://store.kde.org/p/1169519/"
license=('GPL')
depends=('plasma-framework' 'kactivities' 'kcoreaddons' 'kdbusaddons' 'kdeclarative' 'kwayland'
         'kpackage' 'kxmlgui' 'kiconthemes' 'ki18n' 'karchive' 'knotifications' 'kglobalaccel'
         'kwindowsystem' 'kcrash' 'knewstuff' 'qt5-x11extras')
makedepends=('extra-cmake-modules' 'kdoctools')
groups=('kdeplasma-addons')
conflicts=('now-dock' 'nowdock-panel')
replaces=('now-dock' 'nowdock-panel')
source=("https://download.kde.org/stable/latte-dock/${pkgname}-${pkgver}.tar.xz")
sha256sums=('c32b090c3bb71afbd2477fe405f51270111b135cc15534d428ad7ae527eba27e')

build() {
  cd ${pkgname}-${pkgver}
  mkdir -p build
  cd build 
  
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_QMLDIR=/usr/lib/qt5/qml \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins 
  make
}

package() {
  cd ${pkgname}-${pkgver}/build
  make DESTDIR=${pkgdir} install
  
  install -D -m644 ../COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}

