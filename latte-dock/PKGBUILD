
pkgname=latte-dock
pkgver=0.10.1
_commit=cf1a008791c573eaf00d367303c39c77f7c16af0
pkgrel=1
pkgdesc="Dock based on plasma frameworks that provides an elegant and intuitive experience for your tasks and plasmoids"
arch=('x86_64')
url="https://apps.kde.org/latte-dock/"
license=('GPL')
depends=('plasma-framework' 'kactivities' 'kcoreaddons' 'kdbusaddons' 'kdeclarative' 'kwayland'
         'kpackage' 'kxmlgui' 'kiconthemes' 'ki18n' 'karchive' 'knotifications' 'kglobalaccel'
         'kirigami' 'kwindowsystem' 'kcrash' 'kio' 'knewstuff' 'qt5-x11extras')
makedepends=('extra-cmake-modules' 'kdoctools')
groups=('kdeplasma-addons')
source=("https://download.kde.org/stable/latte-dock/${pkgname}-${pkgver}.tar.xz")
#source=("https://invent.kde.org/plasma/latte-dock/-/archive/${_commit}/latte-dock-${_commit}.tar.bz2")
sha256sums=('63f59a6d5ca91c31a115d24c907b3e90761199bb7e948c4123da85f9b0e38d0d')

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_QMLDIR=/usr/lib/qt5/qml \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins 
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}

