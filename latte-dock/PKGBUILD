
pkgname=latte-dock
pkgver=0.9.8
pkgrel=1
pkgdesc="Dock based on plasma frameworks that provides an elegant and intuitive experience for your tasks and plasmoids"
arch=('x86_64')
url="https://kde.org/applications/utilities/org.kde.latte-dock"
license=('GPL')
depends=('plasma-framework' 'kactivities' 'kcoreaddons' 'kdbusaddons' 'kdeclarative' 'kwayland'
         'kpackage' 'kxmlgui' 'kiconthemes' 'ki18n' 'karchive' 'knotifications' 'kglobalaccel'
         'kwindowsystem' 'kcrash' 'knewstuff' 'qt5-x11extras')
makedepends=('extra-cmake-modules' 'kdoctools')
groups=('kdeplasma-addons')
conflicts=('now-dock' 'nowdock-panel')
replaces=('now-dock' 'nowdock-panel')
source=("https://download.kde.org/stable/latte-dock/${pkgname}-${pkgver}.tar.xz")
sha256sums=('deeb3238b3c97766f58aab13b64136e72037951578dd828c6bf25f0d1c323c89')

build() {
  cd ${pkgname}-${pkgver}
  mkdir -p build
  cd build 
  
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_QMLDIR=/usr/lib/qt5/qml \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins 
  make
}

package() {
  cd ${pkgname}-${pkgver}/build
  make DESTDIR=${pkgdir} install
  
  install -D -m644 ../COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}

