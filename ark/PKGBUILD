
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=ark
arch=('x86_64')
pkgver=${_kdever}
pkgrel=1
pkgdesc="Graphical file compression/decompression utility with support for multiple formats, including tar, gzip, bzip2, rar and zip, as well as CD-ROM images"
url="https://apps.kde.org/ark/"
license=('GPL' 'LGPL' 'FDL')
depends=('karchive' 'kconfig' 'kcrash' 'kdbusaddons' 'ki18n' 'kpty' 'kparts' 'kiconthemes'
         'kio' 'kservice' 'kwidgetsaddons' 'kitemmodels'
         'libarchive' 'p7zip' 'zip' 'unzip' 'unrar' 'libzip' 'lzop')
makedepends=('extra-cmake-modules' 'kdoctools')
groups=('kde' 'kde-utils' 'kde-uninstall' 'utils')
source=($_mirror/${pkgname}-$_kdever.tar.xz
        "https://invent.kde.org/utilities/ark/-/commit/9bcbcb056c43abef88540c54f25bc6c1a78c7c0e.diff")
md5sums=(`grep ${pkgname}-$_kdever.tar.xz ../kde-sc.md5 | cut -d" " -f1`
         '4daea61f1e0ce97c511bd0d0528325f6')

prepare() {
  cd ${pkgname}-${pkgver}

  patch -p1 -i ${srcdir}/9bcbcb056c43abef88540c54f25bc6c1a78c7c0e.diff

  #sed -i 's|application/x-rar|application/vnd.rar|' plugins/clirarplugin/CMakeLists.txt
}

build() {  
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF 
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
