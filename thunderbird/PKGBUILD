
pkgname=thunderbird
pkgver=128.2.1
pkgrel=1
pkgdesc="Standalone Mail/News reader"
arch=('x86_64')
license=('MPL' 'GPL' 'LGPL')
url="https://thunderbird.net"
depends=('alsa-lib' 'cairo' 'dbus-glib' 'desktop-file-utils' 'fontconfig' 'freetype2'
         'gtk3' 'hicolor-icon-theme' 'hunspell' 'libevent' 'libjpeg' 'libmng' 'libpng'
         'libvpx' 'libxt' 'nspr' 'nss' 'shared-mime-info' 'sqlite'
         'startup-notification' 'libcanberra')
optdepends=('kde-gtk-config: gtk integration settings for Plasma')
groups=('network-email')
source=("https://ftp.mozilla.org/pub/thunderbird/releases/${pkgver}esr/linux-x86_64/en-US/thunderbird-${pkgver}esr.tar.bz2"
        'thunderbird.desktop'
        'vendor.js')
sha256sums=('40b08f916f96ca267ddec8cb748bd2fe130c31ef49058c26645557f7c88f6fbd'
            '698726e3bd119fdcd31de5a54d8747211aa4aaf73ff192722ce1b2b8c6e5b979'
            'd283af3d8b65c3ad73fa831b6aa904ed6bc4487976d9906d4da6ad9953cfb522')

package() {
  install -d ${pkgdir}/{usr/bin,opt}

  cp -r thunderbird/ ${pkgdir}/opt/${pkgname}-${pkgver}/
  cp vendor.js ${pkgdir}/opt/${pkgname}-${pkgver}/defaults/pref/
  
  ln -s /opt/${pkgname}-${pkgver}/thunderbird ${pkgdir}/usr/bin/thunderbird
  
  for i in 16x16 22x22 24x24 32x32 48x48 64x64 128x128; do
      install -Dm644 thunderbird/chrome/icons/default/default${i/x*/}.png ${pkgdir}/usr/share/icons/hicolor/$i/apps/thunderbird.png
  done

  install -Dm644 thunderbird.desktop ${pkgdir}/usr/share/applications/thunderbird.desktop

  rm -rf ${pkgdir}/opt/${pkgname}-${pkgver}/dictionaries/
  ln -sf /usr/share/hunspell/ ${pkgdir}/opt/${pkgname}-${pkgver}/dictionaries
  ln -sf /usr/share/hyphen/ ${pkgdir}/opt/${pkgname}-${pkgver}/hyphenation
}
