
pkgname=thunderbird
pkgver=78.5.1
pkgrel=1
pkgdesc="Standalone Mail/News reader"
arch=('x86_64')
license=('MPL' 'GPL' 'LGPL')
url="https://www.mozilla.org/thunderbird"
depends=('alsa-lib' 'cairo' 'dbus-glib' 'desktop-file-utils' 'fontconfig' 'freetype2'
         'gtk3' 'hicolor-icon-theme' 'hunspell' 'libevent' 'libjpeg' 'libmng' 'libpng'
         'libvpx' 'libxt' 'mozilla-common' 'nspr' 'nss' 'shared-mime-info' 'sqlite'
         'startup-notification' 'libcanberra')
optdepends=('kde-gtk-config: gtk integration settings for Plasma')
groups=('network-email')
source=("https://ftp.mozilla.org/pub/mozilla.org/thunderbird/releases/${pkgver}/linux-x86_64/en-US/thunderbird-${pkgver}.tar.bz2"
        'thunderbird.desktop'
        'vendor.js')
sha256sums=('4363d8fd759ac4f4783eeb76726c8cf22e5afb3c171f26fe5cca5ab194ab6959'
            '8d0b499b3009934f66fd10975acee985fe64a8c0447918b309b8c5e6a5aa4372'
            'd283af3d8b65c3ad73fa831b6aa904ed6bc4487976d9906d4da6ad9953cfb522')

package() {
  install -d ${pkgdir}/{usr/bin,opt}

  cp -r thunderbird/ ${pkgdir}/opt/${pkgname}-${pkgver}/
  cp vendor.js ${pkgdir}/opt/${pkgname}-${pkgver}/defaults/pref/
  
  ln -s /opt/${pkgname}-${pkgver}/thunderbird ${pkgdir}/usr/bin/thunderbird
  
  for i in 16x16 22x22 24x24 32x32 48x48 64x64 128x128; do
      install -Dm644 thunderbird/chrome/icons/default/default${i/x*/}.png ${pkgdir}/usr/share/icons/hicolor/$i/apps/thunderbird.png
  done
  
  install -Dm644 thunderbird.desktop ${pkgdir}/usr/share/applications/thunderbird.desktop
  
  rm -rf ${pkgdir}/opt/${pkgname}-${pkgver}/dictionaries/
  ln -sf /usr/share/hunspell/ ${pkgdir}/opt/${pkgname}-${pkgver}/dictionaries
  ln -sf /usr/share/hyphen/ ${pkgdir}/opt/${pkgname}-${pkgver}/hyphenation
}
