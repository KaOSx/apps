
pkgbase=transmission
pkgname=('transmission-cli' 'transmission-qt')
pkgver=3.00
pkgrel=1
arch=('x86_64')
url="http://www.transmissionbt.com/"
license=('GNU GPLv2')
makedepends=('intltool' 'curl' 'pkgconfig' 'desktop-file-utils' 'qt5-declarative' 'libevent'
             'gettext' 'cmake' 'qt5-tools' 'miniupnpc')
source=("https://github.com/transmission/transmission-releases/raw/master/${pkgbase}-${pkgver}.tar.xz"
#source=("https://build.transmissionbt.com/job/trunk-linux/lastBuild/artifact/transmission-trunk-r14679.tar.xz"
        'transmission.systemd'
        'transmission.tmpfiles'
        'dbushelper.diff'
        'transmission.svg'
        'transmission-tray.svg'
        'transmission-tray-dark.svg')
md5sums=('a23a32672b83c89b9b61e90408f53d98'
         '5c289c8901221a94be74665368ab5c2c'
         '23f2320361ad54373c3a4551ef634fe8'
         '796d52f26433300767ce16f8c92eebfb'
         '019c6e9dbdcdb2704a266874d02975c0'
         'dbbc3c6ce0707808895778bd81e36707'
         'c4d8952b3280d99d909ccfbd48940a0a')
        
prepare () {
  cd ${pkgbase}-${pkgver}

  #patch -p1 -i $srcdir/dbushelper.diff
}  

build() {
  mkdir -p build
  cd build

  cmake ../${pkgbase}-${pkgver} \
        -DENABLE_TESTS=OFF \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DENABLE_GTK=OFF \
        -DENABLE_QT=ON \
        -DENABLE_CLI=ON \
        -DUSE_QT5=ON \
        -DINSTALL_LIB=OFF \
        -DWITH_CRYPTO="openssl" \
        -DWITH_INOTIFY=ON
     
      make
}

package_transmission-cli() {
  pkgdesc="Fast, easy, and free BitTorrent client (CLI tools, daemon and web client)"
  depends=('curl' 'libevent')
  groups=('network')

  cd build

  for dir in daemon cli utils
  do
      make -C ${dir} DESTDIR=${pkgdir} install
  done

  install -D -m644 ${srcdir}/transmission.systemd ${pkgdir}/usr/lib/systemd/system/transmission.service
  install -D -m644 ${srcdir}/transmission.tmpfiles ${pkgdir}/usr/lib/tmpfiles.d/transmission.conf
  install -D -m644 ${srcdir}/${pkgbase}-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/transmission-cli/COPYING
}

package_transmission-qt() {
  pkgdesc="Fast, easy, and free BitTorrent client (Qt GUI)"
  depends=('curl' 'qt5-declarative' 'libevent' 'miniupnpc')
  optdepends=('transmission-cli: daemon support')
  groups=('network')
  install=transmission-qt.install

  cd build/qt
  make DESTDIR=${pkgdir} install
  
  cd ../..

  install -D -m644 ${pkgbase}-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/transmission-qt/COPYING
  install -D -m644 ../transmission.svg ${pkgdir}/usr/share/pixmaps/transmission.svg
  install -D -m644 ../transmission-tray.svg ${pkgdir}/usr/share/pixmaps/transmission-tray.svg
  install -D -m644 ../transmission-tray-dark.svg ${pkgdir}/usr/share/icons/breeze-dark/apps/48/transmission-tray.svg
}
