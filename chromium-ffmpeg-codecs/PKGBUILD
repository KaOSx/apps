 
pkgname=chromium-ffmpeg-codecs
pkgver=103.0.5060.114
pkgrel=1
pkgdesc="Extra ffmpeg codecs from the Chromium Browser for use in other browsers"
arch=('x86_64')
url="https://ffmpeg.org/"
license=('LGPL2.1')
depends=('gtk3' 'libexif' 'pulseaudio' 'libxss' 'gdk-pixbuf2')
makedepends=('ninja' 'nss' 'pciutils' 'python3' 'xdg-utils' 'mesa' 'gn'
             'clang' 'libva' 'nodejs')
options=('!strip')
groups=('network-web' 'multimedia')
source=("https://commondatastorage.googleapis.com/chromium-browser-official/chromium-${pkgver}.tar.xz"
        'glibc2.34.patch'
        'ffmpeg_clang.patch')
        #"https://gitweb.gentoo.org/repo/gentoo.git/plain/www-client/chromium/files/chromium-FORTIFY_SOURCE-r2.patch"
        #"https://gitweb.gentoo.org/repo/gentoo.git/plain/www-client/chromium/files/chromium-memcpy-r0.patch"
        #"https://raw.githubusercontent.com/gentoo/gentoo/master/www-client/chromium/files/chromium-clang-r2.patch")
md5sums=('6bb5e3b0e627338a29a204794bd381dd'
         'd4c8aed09b852568a58591933b7b0c96'
         '767e3e82f89de949909eb990204185cd')

build() {
  cd chromium-${pkgver}
  #touch chrome/test/data/webui/i18n_process_css_test.html
  #sed -i "s|base/callback_internal.cc|base/callback_helpers.cc|g" tools/gn/bootstrap/bootstrap.py
  # from https://github.com/gentoo/gentoo/tree/master/www-client/chromium/files
  #patch -p1 -i ${srcdir}/ffmpeg_clang.patch
  #patch -p1 -i ${srcdir}/chromium-FORTIFY_SOURCE-r2.patch
  #patch -p1 -i ${srcdir}/glibc2.34.patch

  export CC="/usr/bin/clang"
  export CXX="/usr/bin/clang++"

  #sed -i '1s|python$|&2|' third_party/dom_distiller_js/protoc_plugins/*.py

  python3 tools/clang/scripts/update.py

  mkdir -p third_party/node/linux/node-linux-x64/bin/
  ln -sf /usr/bin/node third_party/node/linux/node-linux-x64/bin/node

  local _flags=("ffmpeg_branding=\"ChromeOS\""
                'proprietary_codecs=true'
                'enable_platform_hevc=true'
                'enable_platform_ac3_eac3_audio=true'
                'enable_platform_mpeg_h_audio=true'
                'enable_platform_dolby_vision=true'
                'enable_mse_mpeg2ts_stream_parser=true'
                'enable_wayland_server=false'
                'use_gnome_keyring=false'
                'use_kerberos=false'
                'use_cups=false'
                'use_sysroot=false'
                'use_gold=false'
                'linux_use_bundled_binutils=false'
                'use_allocator="none"'
                'is_clang=true'
                'clang_use_chrome_plugins=true'
                'fatal_linker_warnings=false'
                'treat_warnings_as_errors=false'
                'enable_nacl=false' 
                'enable_nacl_nonsfi=false'
                'is_component_build=true'
                'is_debug=false'
                'symbol_level=0'
                'use_custom_libcxx=true')

  gn gen out/Release -v --args="${_flags[*]}" --script-executable=/usr/bin/python3

  ninja -C out/Release -v media/ffmpeg
}

package() {
  cd chromium-${pkgver}

  install -Dm644 out/Release/libffmpeg.so ${pkgdir}/usr/lib/chromium/libs/libffmpeg.so
}
