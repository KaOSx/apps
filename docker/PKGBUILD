pkgname=docker
pkgver=19.03.14
_pkgver=19.03.14
# add commit associated with the release tag
# build runc & containerd from https://github.com/moby/moby/blob/master/hack/dockerfile/install/containerd.installer &
# https://github.com/moby/moby/blob/master/hack/dockerfile/install/runc.installer & https://github.com/moby/moby/blob/master/hack/dockerfile/install/proxy.installer for docker-proxy
# all at https://github.com/docker/docker-ce/tree/master/components/engine/hack/dockerfile/install --branch
DOCKER_GITCOMMIT=5eb3275
pkgrel=1
pkgdesc='Pack, ship and run any application as a lightweight container'
arch=('x86_64')
url='https://www.docker.com/'
license=('Apache')
depends=('bridge-utils' 'iproute2' 'device-mapper' 'sqlite' 'systemd' 'containerd' 'docker-proxy')
makedepends=('git' 'go' 'go-md2man')
options=('!strip')
install=$pkgname.install
source=("https://github.com/docker/docker-ce/archive/v${_pkgver}.tar.gz"
        'docker.sysusers'
        "https://github.com/docker/docker/commit/6f89a8ee1ba6b598dc1f76e829d6db6f830cdb8e.patch")
md5sums=('f932ff952a0411223a90baaf5b829883'
         '4324edeb9adc210a2c22f44eb4cb8a74'
         'c7a8981a18cf7d67276cb0ab058173c0')

prepare() {
  mkdir -p ${srcdir}/go/src/github.com/docker
  ln -s ${srcdir}/docker-ce-${_pkgver}/components/cli ${srcdir}/go/src/github.com/docker/
  
  export GOPATH=${srcdir}/go
}
         
build() {
  cd docker-ce-${_pkgver}/components/engine
  export AUTO_GOPATH=1
  export DOCKER_GITCOMMIT
  
  hack/make.sh dynbinary
  ../cli/man/md2man-all.sh
  
  cd ../cli
  export DOCKER_GITCOMMIT
  
  LDFLAGS="" make dynbinary # manpages
}

package() {
  cd docker-ce-${_pkgver}/components
  install -Dm755 cli/build/docker-linux-amd64 ${pkgdir}/usr/bin/docker
  
  install -Dm644 cli/contrib/completion/bash/docker ${pkgdir}/usr/share/bash-completion/completions/docker
  install -Dm644 cli/contrib/completion/zsh/_docker ${pkgdir}/usr/share/zsh/site-functions/_docker
  install -Dm644 cli/contrib/completion/fish/docker.fish ${pkgdir}/usr/share/fish/vendor_completions.d/docker.fish
  
  install -Dm644 packaging/systemd/docker.service ${pkgdir}/usr/lib/systemd/system/docker.service
  install -Dm644 packaging/systemd/docker.socket ${pkgdir}/usr/lib/systemd/system/docker.socket
  
  cd engine
  install -Dm755 bundles/dynbinary-daemon/dockerd-dev ${pkgdir}/usr/bin/dockerd

  install -Dm644 contrib/syntax/vim/syntax/dockerfile.vim ${pkgdir}/usr/share/vim/vimfiles/syntax/dockerfile.vim
  install -Dm644 contrib/syntax/vim/ftdetect/dockerfile.vim ${pkgdir}/usr/share/vim/vimfiles/ftdetect/dockerfile.vim
  
  install -Dm644 ${srcdir}/${pkgname}.sysusers ${pkgdir}/usr/lib/sysusers.d/$pkgname.conf
  
  # symlink runc & containerd binariers https://github.com/KaOSx/apps/issues/54
  ln -s runc ${pkgdir}/usr/bin/docker-runc
  ln -s containerd ${pkgdir}/usr/bin/docker-containerd
  ln -s containerd-shim ${pkgdir}/usr/bin/docker-containerd-shim
  ln -s ctr ${pkgdir}/usr/bin/docker-containerd-ctr

  install -dm755 ${pkgdir}/usr/share/man
  mv ../cli/man/man* ${pkgdir}/usr/share/man
}

