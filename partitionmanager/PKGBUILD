
pkgname=partitionmanager
pkgver=4.1.0
_commit=771c0ceaa2bfd5634f65e6e390733cec557e90de
pkgrel=1
epoch=1
pkgdesc="Easily manage disks, partitions and file systems on your KDE Desktop"
arch=('x86_64')
url="https://www.kde.org/applications/system/kdepartitionmanager"
license=('GPL2')
depends=('kconfig' 'kconfigwidgets' 'kcoreaddons' 'kcrash' 'kdbusaddons' 'ki18n' 'kiconthemes'
         'kjobwidgets' 'kio' 'kxmlgui' 'kwidgetsaddons' 'parted' 'libatasmart' 'udisks2' 'kpmcore'
         'ntfs-3g' 'dosfstools')
makedepends=('extra-cmake-modules' 'pkgconfig' 'kdoctools')
groups=('system')
options=('debug')	
source=("https://download.kde.org/stable/partitionmanager/${pkgver}/src/partitionmanager-${pkgver}.tar.xz")
#source=("https://github.com/KDE/partitionmanager/archive/${_commit}.zip")
sha256sums=('d61e20237c854cd3622b19cec5939c51bf4f350741106fdb44fc5a82155735b4')

prepare() {
  cd ${pkgname}-${pkgver}
  # filsystem code from src/fs/filesystem.h, set as default
  #sed -i -e 's/FileSystem::Ext3/FileSystem::Xfs/' ${srcdir}/${pkgname}-${pkgver}/src/config.kcfg
  sed -i -e 's/FileSystem::Ext4/FileSystem::Xfs/' src/partitionmanager.kcfg
}

build() {
  mkdir -p build
  cd build

  cmake ../${pkgname}-${pkgver} \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_QMLDIR=/usr/lib/qt5/qml \
        -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
        -DBUILD_TESTING=OFF 

  make 
}

package() {
  cd build
  
  make DESTDIR=${pkgdir} install 
}

