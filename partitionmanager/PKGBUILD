
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=partitionmanager
pkgver=${_kdever}
epoch=1
pkgrel=1
pkgdesc="Easily manage disks, partitions and file systems on your KDE Desktop"
arch=('x86_64')
url="https://apps.kde.org/en/partitionmanager"
license=('GPL2')
depends=('kconfig6' 'kconfigwidgets6' 'kcoreaddons6' 'kcrash6' 'kdbusaddons6' 'ki18n6'
         'kjobwidgets6' 'kio6' 'kxmlgui6' 'kwidgetsaddons6' 'kwindowsystem6' 'parted'
         'libatasmart' 'udisks2' 'kpmcore' 'ntfs-3g' 'dosfstools')
makedepends=('extra-cmake-modules' 'kdoctools6')
conflicts=('partitionmanager6')
replaces=('partitionmanager6')
groups=('system')
options=('debug')	
source=($_mirror/${pkgname}-${_kdever}.tar.xz
        "https://invent.kde.org/system/partitionmanager/-/commit/aecf012c2bf08c3ba5677fc0aaccc2b67317e0de.diff")
md5sums=(`grep ${pkgname}-${_kdever}.tar.xz ../kde-sc.md5 | cut -d" " -f1`
         '57c73e88b6cd38ac3b37571407625b88')

prepare() {
  cd ${pkgname}-${pkgver}
  # filsystem code from src/fs/filesystem.h, set as default
  sed -i -e 's/FileSystem::Type::Btrfs/FileSystem::Type::Xfs/' src/partitionmanager.kcfg
  # bcachefs support
  patch -p1 -i ${srcdir}/aecf012c2bf08c3ba5677fc0aaccc2b67317e0de.diff
}

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DKDE_INSTALL_LIBDIR=lib \
        -DBUILD_TESTING=OFF \
        -DQT_MAJOR_VERSION=6
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}

