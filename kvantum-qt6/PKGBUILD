
pkgname=kvantum-qt6
_pkgname=Kvantum
pkgver=1.0.10
pkgrel=2
pkgdesc="SVG-based Qt6 theme engine plus a config tool and extra themes"
arch=('x86_64')
url="https://github.com/tsujan/Kvantum"
license=('GPL3')
depends=('qt6-svg' 'libxext')
makedepends=('cmake' 'qt6-tools')
source=("https://github.com/tsujan/Kvantum/releases/download/V${pkgver}/${_pkgname}-${pkgver}.tar.xz")
#source=("https://github.com/tsujan/Kvantum/archive/${_commit}.zip")
md5sums=('3746a454e30ddb5ecf5cef6d4c124bc5')

prepare() {
    cd ${_pkgname}-${pkgver}/${_pkgname}

    sed -e "s|if(ENABLE_QT5)|if(NOT ENABLE_QT5)|" -i CMakeLists.txt
}

build() {
    cmake -B build -S ${_pkgname}-${pkgver}/${_pkgname} \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DENABLE_QT5=OFF
    cmake --build build
}

package() {
    DESTDIR=${pkgdir} cmake --install build

    install -Dm644 ${_pkgname}-${pkgver}/${_pkgname}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
} 
