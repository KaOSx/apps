
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=kmail-account-wizard
pkgver=${_kdever}
pkgrel=1
pkgdesc="Application which assists you with the configuration of accounts in KMail."
arch=('x86_64')
url='https://github.com/KDE/kmail-account-wizard'
license=('LGPL')
depends=('kwallet' 'kcmutils' 'knotifyconfig' 'kconfig' 'kservice' 'kdbusaddons' 'ktexteditor' 'kcodecs' 
         'kcrash' 'knotifications' 'kross' 'knewstuff'
         'kpimtextedit' 'akonadi' 'kcontacts' 'akonadi-contacts' 'kidentitymanagement' 'kldap'
         'kmailtransport' 'mailcommon' 'messagelib' 'pimcommon' 'libkdepim' 'libkleo')
makedepends=('extra-cmake-modules' 'kdoctools' 'boost' 'qt5-tools')
groups=('kdepim' 'network-email')
source=($_mirror/${pkgname}-${_kdever}.tar.xz)
md5sums=(`grep ${pkgname}-${_kdever}.tar.xz ../kde-sc.md5 | cut -d" " -f1`)

build() {
  mkdir -p build
  cd build 
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DKDE_INSTALL_QMLDIR=/usr/lib/qt5/qml \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
    -DBUILD_TESTING=OFF 
  make 
}

package() {
  cd build
  
  make DESTDIR=${pkgdir} install
}
