
pkgname=bibletime
pkgver=3.0.3
pkgrel=1
pkgdesc='Bible study program that uses the Sword Engine'
arch=('x86_64')
url='http://bibletime.info/'
license=('GPL2')
depends=('clucene' 'sword' 'qt5-declarative' 'qt5-svg')
makedepends=('cmake' 'qt5-tools')
source=("https://github.com/bibletime/bibletime/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('80e5e4ccb69a95201036442dd3f13dffcfcb0f001be1222ce7eefcb30fcc2692')

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_DOCS=OFF \
    -DBUILD_HANDBOOK_PDF=OFF \
    -DBUILD_HANDBOOK_HTML=OFF \
    -DBUILD_HOWTO_HTML=OFF \
    -DBUILD_HOWTO_PDF=OFF
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
