
pkgname=bibletime
pkgver=3.0.3
_commit=ad8506b5acdc2075dceacc6f888c1475071377a3
pkgrel=3
pkgdesc='Bible study program that uses the Sword Engine'
arch=('x86_64')
url='http://bibletime.info/'
license=('GPL2')
depends=('clucene' 'sword' 'qt6-declarative' 'qt6-svg')
makedepends=('cmake' 'qt6-tools')
#source=("https://github.com/bibletime/bibletime/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.xz")
source=("https://github.com/bibletime/bibletime/archive/${_commit}.zip")
sha256sums=('ff2ffb1394130c17bfb70a3daf0614924893dc703c80e4c2e9813d919e80739e')

prepare() {
  cd  ${pkgname}-${_commit}

  sed -i "s|/usr/lib/qt5/bin/|/usr/lib/qt6/bin/|g" cmake/BTUITranslation.cmake
}

build() {
  cmake -B build -S ${pkgname}-${_commit} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_DOCS=OFF \
    -DBUILD_HANDBOOK_PDF=OFF \
    -DBUILD_HANDBOOK_HTML=OFF \
    -DBUILD_HOWTO_HTML=OFF \
    -DBUILD_HOWTO_PDF=OFF \
    -DUSE_QT6=ON
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
  # empty doc directory needed since 3.0.3, see https://github.com/bibletime/bibletime/issues/392
  mkdir -p ${pkgdir}/usr/share/doc/bibletime
}
