
pkgname=ardour
_pkgname=Ardour
pkgver=6.5.0
pkgrel=1
pkgdesc="A multichannel hard disk recorder and digital audio workstation"
arch=('x86_64')
url="https://ardour.org/"
license=('GPL')
depends=('atkmm' 'aubio' 'cairomm' 'gdk-pixbuf2' 'gtkmm' 'libx11' 'libxml2' 'pangomm' 'qjackctl' 'taglib' 'fftw' 'flac' 
         'graphviz' 'harfbuzz' 'liblo' 'liblrdf' 'libogg' 'pulseaudio' 'libsamplerate' 'libsndfile' 'lilv' 'lv2'
         'pango' 'readline' 'rubberband' 'serd' 'sord' 'sratom' 'suil')
makedepends=('boost' 'cppunit' 'itstool' 'libusb')
optdepends=('kde-gtk-config: gtk integration settings for Plasma')
options=('!strip')
source=("https://community.ardour.org/srctar"
        'ardour.desktop')
md5sums=('ab33dce17ad186a191929571e83679ce'
         '27b85c4048094607d2cff4eb36a578d5')

build() {
  cd ${_pkgname}-${pkgver}
  
  ./waf configure --prefix=/usr \
    --configdir=/etc \
    --with-backends="alsa,dummy,jack,pulseaudio" \
    --libjack=weak \
    --nls \
    --optimize \
    --cxx11 \
    --freedesktop \
    --ptformat \
    --lxvst \
    --no-phone-home
  ./waf build -v
}

package() {
  cd ${_pkgname}-${pkgver}

  ./waf i18n --destdir=${pkgdir}
  ./waf install --destdir=${pkgdir}

  install -Dm644 build/gtk2_ardour/ardour6.desktop ${pkgdir}/usr/share/applications/${pkgname}6.desktop
  install -vDm 644 gtk2_ardour/resources/Ardour-icon_48px.png ${pkgdir}/usr/share/icons/hicolor/48x48/apps/${pkgname}6.png
}
