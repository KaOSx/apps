
pkgname=ardour
_pkgname=Ardour
pkgver=6.6.0
pkgrel=2
pkgdesc="A multichannel hard disk recorder and digital audio workstation"
arch=('x86_64')
url="https://ardour.org/"
license=('GPL')
depends=('atkmm' 'aubio' 'cairomm' 'gdk-pixbuf2' 'gtkmm' 'libx11' 'libxml2' 'pangomm' 'qjackctl' 'taglib' 'fftw' 'flac' 
         'graphviz' 'harfbuzz' 'liblo' 'liblrdf' 'libogg' 'pulseaudio' 'libsamplerate' 'libsndfile' 'lilv' 'lv2'
         'pango' 'readline' 'rubberband' 'serd' 'sord' 'sratom' 'suil')
makedepends=('boost' 'cppunit' 'itstool' 'libusb')
optdepends=('kde-gtk-config: gtk integration settings for Plasma')
options=('!strip')
source=("https://community.ardour.org/srctar"
        'ardour.desktop'
        "https://github.com/Ardour/ardour/commit/cc7b8b1bc5fb19e0ec5e476741c55627d4b62ba9.diff"
        "https://github.com/Ardour/ardour/commit/8b4edaa9506dc945cfbd8ed9869fd9b384a513d7.diff"
        "https://github.com/Ardour/ardour/commit/ea01ec5988185228c41b7799b45c5051f0b80d15.diff")
md5sums=('c5a2d0f5cb54f9fb26023afa2a66abe7'
         '27b85c4048094607d2cff4eb36a578d5'
         '97b3c03e76822d6e160d3662f497bf9d'
         '01a089abf27ed5e4aa3a05d204ddac00'
         '1c26611d3e6457704c18098e2f1b17d9')

build() {
  cd ${_pkgname}-${pkgver}
  # glib2 2.68 needed
  patch -p1 -i ${srcdir}/cc7b8b1bc5fb19e0ec5e476741c55627d4b62ba9.diff
  patch -p1 -i ${srcdir}/8b4edaa9506dc945cfbd8ed9869fd9b384a513d7.diff
  patch -p1 -i ${srcdir}/ea01ec5988185228c41b7799b45c5051f0b80d15.diff
  
  ./waf configure --prefix=/usr \
    --configdir=/etc \
    --with-backends="alsa,dummy,jack,pulseaudio" \
    --libjack=weak \
    --nls \
    --optimize \
    --cxx11 \
    --freedesktop \
    --ptformat \
    --lxvst \
    --no-phone-home
  ./waf build -v
}

package() {
  cd ${_pkgname}-${pkgver}

  ./waf i18n --destdir=${pkgdir}
  ./waf install --destdir=${pkgdir}

  install -Dm644 build/gtk2_ardour/ardour6.desktop ${pkgdir}/usr/share/applications/${pkgname}6.desktop
  install -vDm 644 gtk2_ardour/resources/Ardour-icon_48px.png ${pkgdir}/usr/share/icons/hicolor/48x48/apps/${pkgname}6.png
}
