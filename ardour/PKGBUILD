
pkgname=ardour
_pkgname=Ardour
pkgver=8.1.0
pkgrel=1
pkgdesc="A multichannel hard disk recorder and digital audio workstation"
arch=('x86_64')
url="https://ardour.org/"
license=('GPL')
depends=('atkmm' 'aubio' 'cairomm' 'gdk-pixbuf2' 'gtkmm' 'libx11' 'libxml2' 'pangomm' 'qjackctl' 'taglib' 'fftw' 'flac' 
         'graphviz' 'harfbuzz' 'liblo' 'liblrdf' 'libogg' 'pulseaudio' 'libsamplerate' 'libsndfile' 'lilv' 'lv2'
         'pango' 'readline' 'rubberband' 'serd' 'sord' 'sratom' 'suil' 'libarchive')
makedepends=('boost' 'cppunit' 'itstool' 'libusb')
optdepends=('kde-gtk-config: gtk integration settings for Plasma')
options=('!strip')
#source=("https://community.ardour.org/srctar"
source=("https://sourceforge.net/projects/kaosx/files/sources/ardour/${_pkgname}-${pkgver}.tar.bz2"
        "https://github.com/Ardour/ardour/commit/cc7b8b1bc5fb19e0ec5e476741c55627d4b62ba9.diff")
md5sums=('278d59f3343057124989acbbf67d7f15'
         'a5f2b506deee5852bb6a92a34e1d8cc4')

build() {
  cd ${_pkgname}-${pkgver}
  # glib2 2.68 needed
  #patch -p1 -i ${srcdir}/cc7b8b1bc5fb19e0ec5e476741c55627d4b62ba9.diff

  ./waf configure --prefix=/usr \
    --configdir=/etc \
    --with-backends="alsa,dummy,jack,pulseaudio" \
    --libjack=weak \
    --optimize \
    --cxx11 \
    --freedesktop \
    --ptformat \
    --no-phone-home
  ./waf build -v
}

package() {
  cd ${_pkgname}-${pkgver}

  ./waf i18n --destdir=${pkgdir}
  ./waf install --destdir=${pkgdir}
}
