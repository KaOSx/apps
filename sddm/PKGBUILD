
pkgname=sddm
pkgver=20201103
_commit=c5e7043291a6d237e33c2f666d9937c93d668594 # 0.19.0
pkgrel=4
pkgdesc="QML based X11 and Wayland display manager"
arch=('x86_64')
url="https://github.com/sddm/sddm"
license=('GPL')
depends=('qt5-declarative' 'libxcb')
makedepends=('extra-cmake-modules' 'qt5-tools' 'python3-docutils')
backup=('etc/sddm.conf'
        'usr/share/sddm/scripts/Xsetup'
        'usr/share/sddm/scripts/Xstop'
        'etc/pam.d/sddm'
        'etc/pam.d/sddm-autologin'
        'etc/pam.d/sddm-greeter')
install=sddm.install
source=("https://github.com/sddm/sddm/archive/${_commit}.zip"
        'sddm.conf'
        "https://github.com/sddm/sddm/commit/994fa67b01ccfac1aaac08572302bbbea7842dc3.diff")
md5sums=('f6a2e2a387c42f9191fc5642f13e8303'
         '93fa7a39377cc02494f652ffd2018554'
         'f541120d2749fbfd8cfcf193a6d1ed3f')
         
prepare() {
  cd $pkgname-${_commit}
  
  patch -R -p1 -i ${srcdir}/994fa67b01ccfac1aaac08572302bbbea7842dc3.diff
  # Pam > 1.5 no longer ships pam_tally
  # https://github.com/sddm/sddm/issues/1313
  sed -i -e 's|pam_tally2.so file=/var/log/tallylog onerr=succeed|pam_faillock.so preauth|' services/sddm-autologin.pam
}

build() {
  mkdir -p build
  cd build

  cmake ../${pkgname}-${_commit} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_LIBEXECDIR=/usr/lib/sddm \
    -DBUILD_MAN_PAGES=ON 
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
  
  install -m 644 -p ${srcdir}/sddm.conf ${pkgdir}/etc/
  install -d -g 136 -o 136 ${pkgdir}/var/lib/sddm
  #install -Dm644 $srcdir/wayland.desktop $pkgdir/usr/share/wayland-sessions/wayland.desktop
  # fix systemd 230 double mounting of /
  #sed -i 's|PrivateTmp=yes|PrivateTmp=no|' ${pkgdir}/usr/lib/systemd/system/sddm.service
}
