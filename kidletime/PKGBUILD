
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=kidletime
pkgver=${_kf5ver}
pkgrel=1
pkgdesc='Reporting of idle time of user and system'
arch=('x86_64')
url='https://invent.kde.org/frameworks/kidletime'
license=('LGPL')
depends=('qt5-x11extras' 'qt5-wayland' 'libxss')
makedepends=('extra-cmake-modules')
groups=('kf5')
source=("https://download.kde.org/unstable/frameworks/${pkgver}/${pkgname}-${pkgver}.tar.xz")
md5sums=(`grep ${pkgname}-${_kf5ver}.tar.xz ../kf5.md5 | cut -d" " -f1`)

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DECM_MKSPECS_INSTALL_DIR=/usr/lib/qt5/mkspecs/modules \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
