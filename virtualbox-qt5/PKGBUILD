
# create vbox-modules tar from the /VirtualBox-${_pkgver}/out/linux.amd64/release/bin/src dir
# rename /src to vbox-kernel-module-src-${_pkgver} and compress to tar.xz
# repeat for guest-modules from /VirtualBox-${_pkgver}/out/linux.amd64/release/bin/additions/src

pkgbase=virtualbox-qt5
pkgname=('virtualbox-qt5' 'virtualbox-guest-utils')
pkgver=6.0.12
_pkgver=6.0.12
pkgrel=1
arch=('x86_64')
url='https://virtualbox.org'
license=('GPL' 'custom')
makedepends=('alsa-lib' 'bin86' 'cdrkit' 'curl' 'device-mapper' 'glu' 'iasl' 'openjdk'
             'libidl2' 'libvncserver' 'libvpx' 'libxcomposite' 'libxcursor' 'libxinerama'
             'libxml2' 'libxmu' 'libxrandr' 'libxslt' 'libxtst' 'linux-headers' 'mesa' 'pulseaudio'
             'python2' 'qt5-tools' 'qt5-x11extras' 'sdl' 'sdl_ttf' 'xf86driproto' 
             'xf86-input-mouse' 'xorg-server' 'nasm' 'yasm' 'opus')
source=("https://download.virtualbox.org/virtualbox/${pkgver}/VirtualBox-${_pkgver}.tar.bz2"
        '60-vboxdrv.rules'
        '60-vboxguest.rules'
        'LocalConfig.kmk'
        'vboxservice.service'
        'driver-path.patch'
        'include-path.patch')
sha256sums=('c8878d1ad867461e6adfb82c22f15bf5d284e214e75f7b2c6bcd81854edeabc3'
            '9c5238183019f9ebc7d92a8582cad232f471eab9d3278786225abc1a1c7bf66e'
            '033c597e0f5285d2ddb0490868e5b6f945f45c7b1b1152a02a9e6fea438b2c95'
            '5a98dc3467bc43fa88c7e84a771455863bf7d60fdbd5b55956299d96c6b36b35'
            '94a808f46909a51b2d0cf2c6e0a6c9dea792034943e6413bf9649a036c921b21'
            'f67674931c30187f867233e3a4ae662f93c9110fbd0bfce50dd9f391f4533bc0'
            '188014070bce4f6fb9b2535bcd64d066119e182e848c521407ea891ab41a995b')

prepare() {
    cd VirtualBox-${pkgver}

    patch -p1 -i ${srcdir}/driver-path.patch
    #patch -p1 -i ${srcdir}/include-path.patch
    
    #sed -i -e 's|/usr/X11R6/bin/xterm|/usr/X11R6/bin/xterm|' src/VBox/Runtime/VBox/RTAssertShouldPanic-vbox.cpp
    sed -i -e 's|X11R6/||' configure
    #sed -i -e 's|CXX_FLAGS=""|CXX_FLAGS="-std=c++11"|' configure
    sed -i -e 's|X11R6/||' Config.kmk
    sed -i -e 's|.*&& check_makeself||' configure
    sed -i -e 's|smc-napa|smcnapa|' src/VBox/Devices/PC/vbox.dsl
    sed -i -e 's|$(filter-out vboxvideo_drv_%,$(DLLS)) vboxvideo_drv_system|$(filter-out vboxvideo_drv_%,$(DLLS))|' src/VBox/Additions/x11/vboxvideo/Makefile.kmk

    cp ${srcdir}/LocalConfig.kmk .

    echo "VBOX_GCC_OPT=$CXXFLAGS" >> LocalConfig.kmk
}

build() {
    cd VirtualBox-${pkgver}

    ./configure \
        --disable-docs \
        --disable-kmods \
        --disable-vmmraw \
        --enable-pulse \
        --enable-vde \
		--enable-vnc \
		--nofatal \
        --enable-qt5

    source ./env.sh
    kmk KBUILD_VERBOSE=2 all
}

package_virtualbox-qt5() {
    pkgdesc='Oracle VM VirtualBox is powerful x86 virtualization for enterprise as well as home use'
    depends=('glibc' 'openssl' 'curl' 'gcc-libs' 'libpng' 'python2' 'sdl'
             'libvpx' 'libxml2' 'procps-ng' 'shared-mime-info' 'zlib'
             'libxcursor' 'libxinerama' 'libx11' 'libxext' 'libxmu' 'libxt' 'xf86-input-mouse'
             'qt5-x11extras' 'virtualbox-modules' 'opus')
    optdepends=('virtualbox-guest-iso: Guest Additions CD image'
                'virtualbox-ext-oracle: for Oracle extensions'
                'net-tools: Host-only or bridged networking support')
    conflicts=('virtualbox')
    replaces=('virtualbox')
    provides=('virtualbox')
    backup=('etc/vbox/vbox.cfg')
    install=virtualbox.install

    source VirtualBox-${pkgver}/env.sh
    cd VirtualBox-${pkgver}/out/linux.amd64/release/bin

    # binaries
    install -dm755 ${pkgdir}/usr/bin
    install -m755 VBox.sh ${pkgdir}/usr/bin/VBox
    for i in VBoxHeadless VBoxManage VBoxSDL VirtualBox VBoxBalloonCtrl; do
        ln -sf VBox ${pkgdir}/usr/bin/$i
        ln -sf VBox ${pkgdir}/usr/bin/${i,,}
    done
    install -m755 VBoxTunctl ${pkgdir}/usr/bin

    # libraries
    install -dm755 ${pkgdir}/usr/lib/virtualbox
    install -m755 *.so ${pkgdir}/usr/lib/virtualbox
    install -m644 *.r0 VBoxEFI*.fd ${pkgdir}/usr/lib/virtualbox
    ## setuid root binaries
    install -m4755 VBoxSDL VirtualBoxVM VBoxHeadless VBoxNetDHCP VBoxNetAdpCtl VBoxNetNAT -t ${pkgdir}/usr/lib/virtualbox
    ## other binaries
    install -m755 VirtualBox VBoxManage VBoxSVC VBoxExtPackHelperApp VBoxXPCOMIPCD VBoxTestOGL VBoxBalloonCtrl -t ${pkgdir}/usr/lib/virtualbox

    # components
    install -dm755 ${pkgdir}/usr/lib/virtualbox/components
    install -m755 components/* -t ${pkgdir}/usr/lib/virtualbox/components

    # extensions packs, disable for now, experimental according to ExtPack.xml
    #install -dm755 ${pkgdir}/usr/lib/virtualbox/ExtensionPacks
    #install -m755 ExtensionPacks/* -t ${pkgdir}/usr/lib/virtualbox/ExtensionPacks

    # languages
    install -dm755 ${pkgdir}/usr/share/virtualbox/nls
    install -m755 nls/*.qm -t ${pkgdir}/usr/share/virtualbox/nls

    # scripts
    install -m755 VBoxCreateUSBNode.sh VBoxSysInfo.sh -t ${pkgdir}/usr/share/virtualbox

    # icons
    install -Dm644 VBox.png ${pkgdir}/usr/share/pixmaps/VBox.png

    pushd icons >/dev/null
    for i in *; do
        install -d ${pkgdir}/usr/share/icons/hicolor/$i/mimetypes
        cp $i/* ${pkgdir}/usr/share/icons/hicolor/$i/mimetypes
    done
    popd >/dev/null

    #desktop
    install -Dm644 virtualbox.desktop ${pkgdir}/usr/share/applications/virtualbox.desktop
    install -Dm644 virtualbox.xml ${pkgdir}/usr/share/mime/packages/virtualbox.xml

    #install configuration files
    install -dm755 ${pkgdir}/etc/vbox
    echo 'INSTALL_DIR=/usr/lib/virtualbox' > ${pkgdir}/etc/vbox/vbox.cfg
    install -dm755 ${pkgdir}/etc/ld.so.conf.d
    echo '/usr/lib/virtualbox' > ${pkgdir}/etc/ld.so.conf.d/virtualbox.conf

    cd ${srcdir}

    install -Dm644 VirtualBox-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/$pkgname/LICENSE

    # systemd
    install -Dm644 60-vboxdrv.rules ${pkgdir}/usr/lib/udev/rules.d/60-vboxdrv.rules
}
    
package_virtualbox-guest-utils() {
    pkgdesc='Oracle VM VirtualBox Guest userspace utilities'
    depends=('glibc' 'pam' 'libx11' 'libxcomposite' 'libxdamage' 'libxext' 'libxfixes' 
             'libxmu' 'libxt' 'xorg-server-utils' 'virtualbox-guest-modules')
    install=virtualbox-guest-utils.install

    source VirtualBox-${pkgver}/env.sh
    
    pushd VirtualBox-${pkgver}/out/linux.amd64/release/bin/additions
    install -d ${pkgdir}/usr/bin
    install -m755 VBoxClient VBoxControl VBoxService mount.vboxsf ${pkgdir}/usr/bin
    install -m755 -D ${srcdir}/VirtualBox-${pkgver}/src/VBox/Additions/x11/Installer/98vboxadd-xclient \
        ${pkgdir}/usr/bin/VBoxClient-all
    install -m755 -D ${srcdir}/VirtualBox-${pkgver}/src/VBox/Additions/x11/Installer/vboxclient.desktop \
        ${pkgdir}/etc/xdg/autostart/vboxclient.desktop
    #install -D vboxvideo_drv_system.so ${pkgdir}/usr/lib/xorg/modules/drivers/vboxvideo.so
    install -d ${pkgdir}/usr/lib/xorg/modules/dri
    install -m755 VBoxOGL*.so ${pkgdir}/usr/lib
    ln -s /usr/lib/VBoxOGL.so ${pkgdir}/usr/lib/xorg/modules/dri/vboxvideo_dri.so
    install -m755 -D pam_vbox.so ${pkgdir}/usr/lib/security/pam_vbox.so
    popd
 
    install -Dm644 60-vboxguest.rules ${pkgdir}/usr/lib/udev/rules.d/60-vboxguest.rules
    install -Dm644 vboxservice.service ${pkgdir}/usr/lib/systemd/system/vboxservice.service

    install -Dm644 VirtualBox-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/$pkgname/LICENSE
}


