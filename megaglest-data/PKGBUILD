
pkgname=megaglest-data
pkgver=3.13.0
pkgrel=3
pkgdesc="Libre software cross platform real-time strategy game. (data files)"
arch=('x86_64')
url="https://github.com/megaglest"
license=('custom:CCBYSA3.0')
makedepends=('cmake' 'xz')
source=("https://github.com/MegaGlest/megaglest-data/releases/download/${pkgver}/megaglest-data-${pkgver}.tar.xz")
md5sums=('f9664a250cab051686367bf534d449d1')

build() {
  cmake -B build -S megaglest-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build

  install -Dm644 megaglest-${pkgver}/docs/README.data-license.txt \
    ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
