
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=baloo
pkgver=${_kf5ver}
#pkgver=5.9.2
pkgrel=2
pkgdesc="A framework for searching and managing metadata"
arch=('x86_64')
url='https://projects.kde.org/projects/kde/kdelibs/baloo'
license=('LGPL')
depends=('xapian-core' 'kfilemetadata' 'ki18n' 'kconfig' 'kidletime' 'kauth' 'kcrash' 'solid' 'kio'
         'kdbusaddons' 'lmdb') 
makedepends=('extra-cmake-modules' 'kdoctools')
conflicts=('kf5-baloo')
replaces=('kf5-baloo')
provides=('kf5-baloo')
backup=('etc/sysctl.d/97-kde-baloo-filewatch-inotify.conf')
source=("http://download.kde.org/stable/frameworks/5.23/baloo-${pkgver}.tar.xz"
        '97-kde-baloo-filewatch-inotify.conf'
        'dolphin_db.diff')
md5sums=(`grep ${pkgname}-${_kf5ver}.tar.xz ../kf5.md5 | cut -d" " -f1`
         '0c9e9337b006f5826ffb85e704f414c1'
         '965e3031d46db546e5c2b3080026b8c6')
groups=('kde-system')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i ${srcdir}/dolphin_db.diff
}
         
build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_LIBEXECDIR=lib
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
  
  install -Dm644 ${srcdir}/97-kde-baloo-filewatch-inotify.conf ${pkgdir}/etc/sysctl.d/97-kde-baloo-filewatch-inotify.conf
}
