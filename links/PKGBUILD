
pkgname=links
pkgver=2.21
pkgrel=1
pkgdesc="A text WWW browser, similar to Lynx"
arch=('x86_64')
url="http://links.twibright.com/"
license=('GPL')
depends=('bzip2' 'xz' 'zlib' 'openssl' 'gpm' 'libx11' 'libtiff' 'libpng')
makedepends=('libtiff' 'libpng' 'libxt')
groups=('network-web')
source=("http://links.twibright.com/download/${pkgname}-${pkgver}.tar.bz2"
        'links.desktop')
md5sums=('b88a46733d6932442ed8a6b751aac6b4'
         '7c47b15aaef51f1de129ff3a2afb7eac')

build() {
  cd ${pkgname}-${pkgver}
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" configure
  (cd intl; ./gen-intl; ./synclang)
  
  ./configure --prefix=/usr \
        --mandir=/usr/share/man \
        --disable-javascript \
        --enable-graphics \
        --with-x \
        --with-fb
  make
  mv links xlinks

  ./configure --prefix=/usr \
         --mandir=/usr/share/man \
         --disable-javascript \
         --disable-graphics \
         --without-x \
         --without-fb
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  install -m755 xlinks ${pkgdir}/usr/bin/xlinks
  ln -s links.1.gz ${pkgdir}/usr/share/man/man1/xlinks.1.gz

  install -D -m644 ${srcdir}/links.desktop ${pkgdir}/usr/share/applications/links.desktop
  install -d ${pkgdir}/usr/share/pixmaps
  install -m644 links_16x16_1.xpm links_16x16_2.xpm links_32x32.xpm ${pkgdir}/usr/share/pixmaps/

  install -d ${pkgdir}/usr/share/doc/links/calibration
  install -m644 doc/links_cal/* ${pkgdir}/usr/share/doc/links/calibration/
}
