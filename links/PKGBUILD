
pkgname=links
pkgver=2.17
pkgrel=2
pkgdesc="A text WWW browser, similar to Lynx"
arch=('x86_64')
url="http://links.twibright.com/"
license=('GPL')
depends=('bzip2' 'xz' 'zlib' 'openssl' 'gpm')
makedepends=('libtiff' 'libpng' 'libxt')
optdepends=('libx11: for using xlinks'
            'libtiff: for using xlinks'
            'libpng: for using xlinks')
source=("http://links.twibright.com/download/${pkgname}-${pkgver}.tar.bz2"
        'links.desktop')
md5sums=('e0cd5582b99a678b5ad3b112e9b42f7e'
         '7c47b15aaef51f1de129ff3a2afb7eac')
groups=('network-web')

build() {
  cd ${pkgname}-${pkgver}
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" configure
  (cd intl; ./gen-intl; ./synclang)
  
  ./configure --prefix=/usr \
        --mandir=/usr/share/man \
        --disable-javascript \
        --enable-graphics \
        --with-x \
        --with-fb
  make
  mv links xlinks

  ./configure --prefix=/usr \
         --mandir=/usr/share/man \
         --disable-javascript \
         --disable-graphics \
         --without-x \
         --without-fb
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  install -m755 xlinks ${pkgdir}/usr/bin/xlinks
  ln -s links.1.gz ${pkgdir}/usr/share/man/man1/xlinks.1.gz

  install -D -m644 ${srcdir}/links.desktop ${pkgdir}/usr/share/applications/links.desktop
  install -d ${pkgdir}/usr/share/pixmaps
  install -m644 links_16x16_1.xpm links_16x16_2.xpm links_32x32.xpm ${pkgdir}/usr/share/pixmaps/

  install -d ${pkgdir}/usr/share/doc/links/calibration
  install -m644 doc/links_cal/* ${pkgdir}/usr/share/doc/links/calibration/
}
