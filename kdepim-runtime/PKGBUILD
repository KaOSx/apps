
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=kdepim-runtime
pkgver=${_kdever}
pkgrel=2
arch=('x86_64')
pkgdesc="KDE Pim Runtime"
url="http://www.kde.org"
license=('GPL' 'LGPL' 'FDL')
depends=('akonadi-mime' 'akonadi-notes' 'akonadi-contacts' 'knotifyconfig' 'knewstuff' 'kcmutils'
         'kdelibs4support' 'kconfig' 'kio' 'kitemmodels' 'kcodecs' 'accountsqt' 'signonqt' 'shared-mime-info' 
         'akonadi' 'kmime' 'kmailtransport' 'kidentitymanagement' 'kcontacts' 'kalarmcal' 'kcalcore' 
         'kcalutils' 'kmbox' 'kimap' 'syndication' 'akonadi-calendar' 'kaccounts-integration' 'pimcommon'
         'kdav' 'libkgapi' 'libkolab' 'qtwebkit-tp' 'qca-qt5' 'qt5-networkauth')
makedepends=('extra-cmake-modules' 'kdoctools' 'boost')
options=('debug')
conflicts=('kf5-kdepim-runtime')
replaces=('kf5-kdepim-runtime')
groups=('kde' 'kdepim' 'kde-uninstall')
source=($_mirror/${pkgname}-${_kdever}.tar.xz)
md5sums=(`grep ${pkgname}-${_kdever}.tar.xz ../kde-sc.md5 | cut -d" " -f1`)

prepare() {
    cd ${pkgname}-${pkgver}

    #patch -p1 -i ${srcdir}/c9ae16363e68d6958db0cd835cb0180b340594b5.diff
    sed -i -e 's|setRecursiveFiltering(true)|setRecursiveFilteringEnabled(true);|' agents/newmailnotifier/newmailnotifierselectcollectionwidget.cpp
}

build() {    
    mkdir -p build
    cd build

    cmake ../${pkgname}-${pkgver} \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_SKIP_RPATH=ON \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_QMLDIR=/usr/lib/qt5/qml \
        -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
        -DBUILD_TESTING=OFF
    make
}

package() {
    cd build

    make DESTDIR=${pkgdir} install
}
