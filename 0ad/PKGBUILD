
pkgname=0ad
pkgver=a23.1
_pkgver=0.0.23b-alpha
pkgrel=11
pkgdesc="Cross-platform, 3D and historically-based real-time strategy game"
arch=('x86_64')
url="https://play0ad.com/"
groups=('games')
license=('GPL2' 'CCPL')
depends=('binutils' 'boost-libs' 'curl' 'enet>=1.3' 'libogg' 'libpng' 'libvorbis' 'libxml2'
         'openal' 'sdl2' 'zlib' 'libgl' '0ad-data' 'glu' 'libxcursor' 'mesa'
         'libjpeg' 'libtiff' 'openexr' 'gloox' 'libsodium')
makedepends=('boost' 'cmake' 'zip' 'python2')
source=("https://releases.wildfiregames.com/${pkgname}-${_pkgver}-unix-build.tar.xz"
        'icu1.68.patch'
        'fcollada.patch')
sha1sums=('3c6bb8924d65d17200e6b9475101e7d04f5db13a'
          'f703537288d133fcd1a0f8a15b73d9ce05b62771'
          'f35aca11e11588cfedf9b15e6735fc8aad9b87e9')

prepare() {
  cd ${pkgname}-${_pkgver}/
  patch -p1 -i $srcdir/icu1.68.patch
  patch -p1 -i $srcdir/fcollada.patch
}

build() {
  cd ${pkgname}-${_pkgver}/build/workspaces

  unset CPPFLAGS 

  ./update-workspaces.sh \
      --without-miniupnpc \
      --bindir=/usr/bin \
      --libdir=/usr/lib/0ad \
      --disable-atlas \
      --datadir=/usr/share/${pkgname}/data

  cd gcc

  make 
}

package() {
  install -d ${pkgdir}/usr/{bin,lib/0ad,share/${pkgname}/data}

  install -Dm755 ${srcdir}/${pkgname}-${_pkgver}/binaries/system/pyrogenesis ${pkgdir}/usr/bin
  install -Dm755 ${srcdir}/${pkgname}-${_pkgver}/binaries/system/*.so* ${pkgdir}/usr/lib/0ad
  
  cp -r ${srcdir}/${pkgname}-${_pkgver}/binaries/data/l10n/ ${pkgdir}/usr/share/${pkgname}/data/

  install -Dm755 ${srcdir}/${pkgname}-${_pkgver}/build/resources/${pkgname}.sh ${pkgdir}/usr/bin/${pkgname}
  install -Dm644 ${srcdir}/$pkgname-$_pkgver/build/resources/${pkgname}.desktop ${pkgdir}/usr/share/applications/${pkgname}.desktop
  install -Dm644 ${srcdir}/$pkgname-$_pkgver/build/resources/${pkgname}.png ${pkgdir}/usr/share/pixmaps/${pkgname}.png
}
