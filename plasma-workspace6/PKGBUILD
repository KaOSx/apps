
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=plasma-workspace6
_pkgname=plasma-workspace
pkgver=${_plasmaver}
pkgrel=3
pkgdesc='Various components needed to run a Plasma-based environment.'
arch=('x86_64')
url='https://invent.kde.org/plasma/plasma-workspace'
license=('LGPL')
depends=('kauth6' 'kparts6' 'libplasma' 'krunner6' 'knotifications6' 'knotifyconfig6' 'knewstuff6'
         'kwallet6' 'kidletime6' 'kdeclarative6' 'ki18n6' 'kcmutils6' 'ktextwidgets6' 'kcrash6'
         'kglobalaccel6' 'kdbusaddons6' 'kwayland6' 'kcoreaddons6' 'kpeople6' 'plasma-activities-stats'
         'kactivities6' 'kio6' 'prison6' 'kpackage6' 'kguiaddons6' 'karchive6' 'kitemmodels6' 'kiconthemes6' 'kunitconversion6' 'ktexteditor6' 'kded6' 'kpipewire6' 'networkmanager-qt6' 'kirigami-addons6' 'kquickcharts6'
         'kuserfeedback6' 'libksysguard6' 'baloo6' 'kwin6' 'libkscreen6' 'kscreenlocker6' 'layer-shell-qt6'
         'kholidays6' 'breeze6' 'attica' 'frameworkintegration6' 'milou6' 'kdesu6'
         'libqalculate' 'phonon-qt6' 'qt6-wayland' 'qt6-tools' 'plasma-wayland-protocols' 'icu'
         'glu' 'libxkbfile' 'libxcursor' 'lm_sensors' 'libraw1394' 'libusb' 'pciutils' 'libbsd' 'libxrandr'
         'gpsd' 'iso-codes' 'oxygen-icons5' 'sddm-kcm6' 'kactivitymanagerd6' 'drkonqi6' 'plasma5support'
         'kio-extras6' 'libkexiv6' 'khelpcenter6' 'accountsservice' 'kio-fuse') # 'libdbusmenu-qt' 
makedepends=('extra-cmake-modules' 'kdoctools6' 'boost') 
groups=('kdeplasma6')
#options=('debug')
source=("https://download.kde.org/stable/plasma/${pkgver}/${_pkgname}-${pkgver}.tar.xz"
        'shellcorona.diff'
        'kicker_qt6.diff')
md5sums=(`grep ${_pkgname}-${_plasmaver}.tar.xz ../kf6.md5 | cut -d" " -f1`
         '1913544f6cbf179033c3a1fdceac1b7e'
         'c12de12d61f8c65c998d971cb83b74d5')

prepare () {
  cd ${_pkgname}-${pkgver}

  patch -p1 -i ${srcdir}/shellcorona.diff
  #sed -i 's|<default>@KDE_INSTALL_FULL_WALLPAPERDIR@/Next/</default>|<default>@KDE_INSTALL_FULL_WALLPAPERDIR@/midna/</default>|' wallpapers/image/main.xml.in
}

build() {
  cmake -B build -S ${_pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_LIBEXECDIR=/usr/lib \
    -DBUILD_TESTING=OFF \
    -DQT_MAJOR_VERSION=6
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
