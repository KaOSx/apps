
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=kmplot
pkgver=${_kdever}
pkgrel=1
pkgdesc="Mathematical Function Plotter"
url="http://kde.org/applications/education/kmplot/"
arch=('x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kguiaddons' 'ki18n' 'kdelibs4support' 'kparts' 'kwidgetsaddons')
makedepends=('extra-cmake-modules' 'kdoctools')
conflicts=('kdeedu-kmplot')
replaces=('kdeedu-kmplot')
groups=('kde' 'kdeedu' 'kde-uninstall' 'education')
install=${pkgname}.install
source=("$_mirror/${pkgname}-$_kdever.tar.xz")
md5sums=(`grep ${pkgname}-$_kdever.tar.xz ../kde-sc.md5 | cut -d" " -f1`)

prepare() {
   cd $pkgname-${pkgver}
   sed -i -e 's|if ( isnan(x) )|if ( std::isnan(x) )|' kmplot/view.cpp
   sed -i -e 's|if ( isnan(y) )|if ( std::isnan(y) )|' kmplot/view.cpp
   sed -i -e 's|if ( isinf(x)|if ( std::isinf(x)|' kmplot/view.cpp
   sed -i -e 's|if ( isinf(y)|if ( std::isinf(y)|' kmplot/view.cpp
}

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DPLUGIN_INSTALL_DIR=/usr/lib/qt5/plugins \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF 
  make
}

package() {
  cd build

  make DESTDIR=${pkgdir} install
}
