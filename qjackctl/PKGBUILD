
pkgname=qjackctl
pkgver=1.0.3
pkgrel=1
pkgdesc="Qt front-end for the JACK low-latency audio server"
url="https://qjackctl.sourceforge.io/"
arch=('x86_64')
license=('GPL')
depends=('jack' 'qt6-base' 'portaudio' 'qt6-svg')
makedepends=('cmake' 'qt6-tools')
options=('!makeflags')
groups=('multimedia')
source=("https://www.rncbc.org/archive/${pkgname}-${pkgver}.tar.gz")
md5sums=('10552889c7cbe9db4b524e45d481b68a')

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCONFIG_XUNIQUE=ON \
    -DCONFIG_QT6=ON
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
