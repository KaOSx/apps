
pkgname=kmymoney
pkgver=5.0.5
pkgrel=1
pkgdesc="An finance manager for KDE with HBCI support"
arch=('x86_64')
url="http://www.kmymoney.org/"
license=('GPL')
depends=('karchive' 'kcoreaddons' 'kconfig' 'kconfigwidgets' 'ki18n' 'kcompletion' 'kcmutils' 'kitemmodels' 'kitemviews'
         'kservice' 'kwallet' 'kiconthemes' 'kxmlgui' 'ktextwidgets' 'knotifications' 'kio' 'kholidays' 
         'gpgme' 'kcontacts' 'akonadi-contacts' 'akonadi' 'kidentitymanagement' 'kross'
         'shared-mime-info' 'boost-libs' 'pulseaudio' 'libofx' 'libical' 'alkimia' 'kdiagram'
         'kactivities' 'aqbanking' 'kdewebkit' 'sqlcipher')
makedepends=('extra-cmake-modules' 'kdoctools' 'docbook-xsl' 'boost')  
groups=('office')
source=("https://download.kde.org/stable/kmymoney/${pkgver}/src/${pkgname}-${pkgver}.tar.xz")
sha256sums=('e7245d14097ea21f971fae45627a20b15ed9cc3d3e7c7e797cc24e730cd1f0c1')

build() {
  mkdir -p build
  cd build

  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_QMLDIR=/usr/lib/qt5/qml \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF \
    -DENABLE_SQLCIPHER=ON \
    -DENABLE_WEBENGINE=OFF # build fails when using qtwebengine looking for KWebView
  make
}

package() {
  cd build

  make DESTDIR=${pkgdir} install
}
