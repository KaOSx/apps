
pkgname=kmymoney
pkgver=5.0.7
pkgrel=1
pkgdesc="An finance manager for KDE with HBCI support"
arch=('x86_64')
url="http://www.kmymoney.org/"
license=('GPL')
depends=('karchive' 'kcoreaddons' 'kconfig' 'kconfigwidgets' 'ki18n' 'kcompletion' 'kcmutils' 'kitemmodels' 'kitemviews'
         'kservice' 'kwallet' 'kiconthemes' 'kxmlgui' 'ktextwidgets' 'knotifications' 'kio' 'kholidays' 
         'gpgme' 'kcontacts' 'akonadi-contacts' 'akonadi' 'kidentitymanagement'
         'shared-mime-info' 'boost-libs' 'pulseaudio' 'libofx' 'libical' 'alkimia' 'kdiagram'
         'kactivities' 'aqbanking' 'qtwebengine' 'sqlcipher')
makedepends=('extra-cmake-modules' 'kdoctools' 'docbook-xsl' 'boost')  
groups=('office')
source=("https://download.kde.org/stable/kmymoney/${pkgver}/src/${pkgname}-${pkgver}.tar.xz")
sha256sums=('d5dcac31db410861fe74955109715012bcddfd095cdbb18c91827a77dffeb5c0')

prepare() {
  cd ${pkgname}-${pkgver}

  # lower aqbanking requirement until there actually is a 5.8.0 release
  #sed -i -e 's|find_package(AQBANKING 5.8.0)|find_package(AQBANKING 5.7.8)|' CMakeLists.txt
}

build() {
  mkdir -p build
  cd build

  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_QMLDIR=/usr/lib/qt5/qml \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF \
    -DENABLE_SQLCIPHER=ON \
    -DENABLE_WEBENGINE=ON 
  make
}

package() {
  cd build

  make DESTDIR=${pkgdir} install
}
