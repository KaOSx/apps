
pkgname=atom
pkgver=1.57.0
pkgrel=1
pkgdesc="Hackable text editor from the 21st Century"
arch=('x86_64')
url="https://github.com/atom/atom"
license=('MIT')
depends=('alsa-lib' 'gtk3' 'libnotify' 'libxtst' 'nss' 'libxkbfile')
optdepends=('kde-gtk-config: gtk integration settings for Plasma')
conflicts=('atom-editor')
replaces=('atom-editor')
source=("https://github.com/atom/atom/releases/download/v${pkgver}/atom-amd64.tar.gz"
        'atom.desktop')
md5sums=('d31823ee9ef7020ac5af80323ad7cee5'
         '64c714a8832e5be9e43e532f37ea30ce')

package() {
  cd ${pkgname}-${pkgver}-amd64
  
  install -dm755 ${pkgdir}/usr/bin
  install -dm755 ${pkgdir}/usr/share/atom
  
  mv resources/app/atom.sh ${pkgdir}/usr/bin/atom
  cp -r * ${pkgdir}/usr/share/atom/

  install -Dm644 ${srcdir}/atom.desktop ${pkgdir}/usr/share/applications/atom.desktop
  install -Dm644 atom.png ${pkgdir}/usr/share/pixmaps/atom.png
  install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
  # fix sandboxing issue, introduced with 1.47.0
  # https://github.com/atom/atom/issues/20756
  chmod 4755 ${pkgdir}/usr/share/atom/chrome-sandbox
}
