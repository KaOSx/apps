
pkgbase=digikam
pkgname=('digikam' 'kipi-plugins' 'libkvkontakte')
pkgver=4.7.0
_pkgver=4.7.0-1
pkgrel=1
url="http://digikam.sourceforge.net"
arch=('x86_64')
license=('GPL')
makedepends=('kde-runtime' 'kdegraphics-kgamma' 'kdepimlibs' 'lensfun' 'kdeedu-marble' 'libpgf'
             'liblqr' 'opencv' 'lapack' 'libgpod' 'pkgconfig' 'cmake' 'automoc4' 'baloo'
             'doxygen' 'docbook-xsl' 'qjson' 'kdegraphics-libkexiv2' 'kdegraphics-libkdcraw'
             'kdegraphics-libksane' 'qt-gstreamer' 'boost' 'hugin-tools' 'imagemagick' 'eigen3')
source=("http://download.kde.org/stable/digikam/${pkgname}-${_pkgver}.tar.bz2")
md5sums=('7ddee71d580153125ec08fba6b9b620c')

build() {
   cd ${srcdir}
 
   mkdir -p build
   cd build
   cmake ../${pkgname}-${pkgver} \
     -DCMAKE_BUILD_TYPE=Release \
     -DCMAKE_INSTALL_PREFIX=/usr \
     -DCMAKE_SKIP_RPATH=ON \
     -DBUILD_tests=OFF \
     -DDIGIKAMSC_COMPILE_LIBKFACE=ON \
     -DDIGIKAMSC_COMPILE_LIBKGEOMAP=ON \
     -DDIGIKAMSC_COMPILE_LIBMEDIAWIKI=ON \
     -DDIGIKAMSC_COMPILE_LIBKVKONTAKTE=ON \
     -DENABLE_KDEPIMLIBSSUPPORT=ON \
     -DENABLE_MYSQLSUPPORT=ON
   make 
}

package_digikam() {
  pkgdesc="Digital photo management application for kde"
  depends=('kde-runtime' 'kdegraphics-kgamma' 'kdepimlibs' 'lensfun' 'kdeedu-marble'
           'liblqr' 'opencv' 'kipi-plugins' 'lapack' 'libgpod' 'eigen3' 'baloo' 'libpgf')
  install=digikam.install
  conflicts=('gstreamer0.10-good-plugins')
  replaces=('digikamimageplugins')
  groups=('graphics')

  splitdirs="core doc po"
  for i in ${splitdirs} ; do
      cd ${srcdir}/build/${i}
      make DESTDIR="${pkgdir}" install
  done
  rm -rf "${pkgdir}/usr/share/icons/oxygen"
  rm "${pkgdir}"/usr/share/locale/*/LC_MESSAGES/libkipi.mo
}

package_libkvkontakte() {
  pkgdesc="KDE C++ library for asynchronous interaction with vkontakte.rusocial network"
  depends=('kdelibs')

  cd "${srcdir}"/build/extra/libkvkontakte
  make DESTDIR="${pkgdir}" install
}

package_kipi-plugins() {
  pkgdesc="Kipi plugins and additional libs for digikam and kde apps"
  depends=('kdegraphics-kgamma' 'kdepimlibs' 'opencv' 'libgpod' 'sane' 'hugin-tools'
           'libgphoto2-nolimit' 'libkvkontakte' 'kdegraphics-libkexiv2' 'kdegraphics-libkdcraw'
           'kdegraphics-libksane' 'qt-gstreamer')
  optdepends=('imagemagick: for picture manipulation'
	      'qca: for shwup webservice support')
  install=kipi-plugins.install
  provides=('libkface libkgeomap libmediawiki')
  replaces=('libkface libkgeomap libmediawiki')
  conflicts=('libkface libkgeomap libmediawiki')
  groups=('graphics')

  splitdirs="libkface libkgeomap libmediawiki kipi-plugins"
  for i in ${splitdirs} ; do
      cd ${srcdir}/build/extra/${i}
      make DESTDIR="${pkgdir}" install
  done
}
