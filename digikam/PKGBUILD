
pkgname=digikam
_pkgname=digiKam
pkgver=7.9.0
_pkgver=7.9.0
pkgrel=1
url="https://www.digikam.org/"
arch=('x86_64')
license=('GPL')
pkgdesc="Digital photo management application for kde"
depends=('knotifyconfig' 'knotifications' 'kio' 'ktextwidgets' 'kcoreaddons' 'kwindowsystem'
         'solid' 'kiconthemes' 'kfilemetadata' 'libksane' 'ki18n' 'kitemmodels' 'lensfun' 
         'marble' 'qt5-xmlpatterns' 'qt5-x11extras' 'qtwebengine' 'libgphoto2' 'liblqr' 'opencv'
         'lapack' 'eigen3' 'mariadb' 'akonadi-contacts'
         'kparts' 'kconfig' 'kxmlgui' 'kservice' 'threadweaver' 'karchive' 'libkexiv2' 
         'libkdcraw' 'libmediawiki' 'libtiff' 'image-exiftool'
         'kcalendarcore' 'kwallet' 'phonon-qt5' 'kdepimlibs' 'imagemagick')
makedepends=('pkgconfig' 'extra-cmake-modules' 'kdoctools' 'doxygen' 'boost' 'ruby' 'subversion') # 'clang')
groups=('kde-graphics')
source=("https://download.kde.org/stable/digikam/${_pkgver}/${_pkgname}-${_pkgver}.tar.xz"
        "https://invent.kde.org/graphics/digikam/-/commit/51efe295a222070743187af0367b0bf957879337.diff")
sha256sums=('c3b80abc090da3cbbc42e67a403080d7f5fe0a7c98698735bda556c60314bab4'
            'bb670e79d1638fea2337f68db0a7b4c502faa4b3706b790a7c40404b475d8274')

prepare() {
  cd ${pkgname}-${_pkgver}
  #sed -i -e 's|QString module = QLatin1String(inf->module);|QString module = QLatin1String(inf->magick_module);|' core/tests/dimg/magickloader.cpp
  #akonadi 22.04 support
  #patch -p1 -i ${srcdir}/51efe295a222070743187af0367b0bf957879337.diff
}

build() {
   cmake -B build -S ${pkgname}-${pkgver} \
     -DCMAKE_BUILD_TYPE=Release \
     -DCMAKE_INSTALL_PREFIX=/usr \
     -DKDE_INSTALL_LIBDIR=lib \
     -DCMAKE_SKIP_RPATH=ON \
     -DKDE_INSTALL_QMLDIR=/usr/lib/qt5/qml \
     -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
     -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
     -DBUILD_TESTING=OFF \
     -DENABLE_AKONADICONTACTSUPPORT=ON \
     -DENABLE_MYSQLSUPPORT=ON \
     -DENABLE_KFILEMETADATASUPPORT=ON \
     -DENABLE_MEDIAPLAYER=OFF \
     -DCMAKE_C_COMPILER=gcc \
     -DDIGIKAMSC_CHECKOUT_DOC=ON \
     -DENABLE_APPSTYLES=ON \
     -DENABLE_QWEBENGINE=ON
   cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
