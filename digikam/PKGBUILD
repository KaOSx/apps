
pkgname=digikam
_pkgname=digiKam
pkgver=8.2.0
_pkgver=8.2.0
pkgrel=1
url="https://www.digikam.org/"
arch=('x86_64')
license=('GPL')
pkgdesc="Digital photo management application for kde"
depends=('knotifyconfig' 'knotifications' 'kio' 'ktextwidgets' 'kcoreaddons' 'kwindowsystem'
         'solid' 'kiconthemes' 'kfilemetadata' 'libksane' 'ki18n' 'kitemmodels' 'lensfun' 
         'marble' 'qt5-xmlpatterns' 'qt5-x11extras' 'qtwebengine' 'libgphoto2' 'liblqr' 'opencv'
         'lapack' 'eigen3' 'mariadb' 'kparts' 'kconfig' 'kxmlgui' 'kservice' 'threadweaver'
         'karchive' 'libkexiv2' 'libkdcraw' 'libmediawiki' 'libtiff' 'image-exiftool'
         'kcalendarcore' 'kwallet' 'phonon-qt5' 'imagemagick' 'qt5-networkauth' 'akonadi-contacts') # 'kdepimlibs'
makedepends=('pkgconfig' 'extra-cmake-modules' 'kdoctools' 'doxygen' 'boost' 'ruby' 'subversion') # 'clang')
groups=('kde-graphics')
source=("https://download.kde.org/stable/digikam/${_pkgver}/${_pkgname}-${_pkgver}.tar.xz"
        "https://invent.kde.org/graphics/digikam/-/commit/5b4ba173f6a84a8be1131b1523dc42df95171ec7.diff")
sha256sums=('2f7fcb559b123ed9ecae5a5aef6f4560eee5f49206d9d1746dec9ab6c8fb38bf'
            'e5967f3a1d561d6af1b6698cbae3807f6b32b97492b7f75a359dc75bb0041f58')

prepare() {
  cd ${pkgname}-${_pkgver}
  #sed -i -e 's|QString module = QLatin1String(inf->module);|QString module = QLatin1String(inf->magick_module);|' core/tests/dimg/magickloader.cpp
  #akonadi 23.04 support
  #patch -p1 -i ${srcdir}/5b4ba173f6a84a8be1131b1523dc42df95171ec7.diff
}

build() {
   cmake -B build -S ${pkgname}-${pkgver} \
     -DCMAKE_BUILD_TYPE=Release \
     -DCMAKE_INSTALL_PREFIX=/usr \
     -DKDE_INSTALL_LIBDIR=lib \
     -DCMAKE_SKIP_RPATH=ON \
     -DKDE_INSTALL_QMLDIR=/usr/lib/qt5/qml \
     -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
     -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
     -DBUILD_TESTING=OFF \
     -DENABLE_AKONADICONTACTSUPPORT=ON \
     -DENABLE_MYSQLSUPPORT=ON \
     -DENABLE_KFILEMETADATASUPPORT=ON \
     -DENABLE_MEDIAPLAYER=ON \
     -DCMAKE_C_COMPILER=gcc \
     -DENABLE_APPSTYLES=ON \
     -DENABLE_QWEBENGINE=ON
   cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
