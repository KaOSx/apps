
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=partitionmanager6
_pkgname=partitionmanager
pkgver=${_kdever}
pkgrel=1
pkgdesc="Easily manage disks, partitions and file systems on your KDE Desktop"
arch=('x86_64')
url="https://apps.kde.org/en/partitionmanager"
license=('GPL2')
depends=('kconfig6' 'kconfigwidgets6' 'kcoreaddons6' 'kcrash6' 'kdbusaddons6' 'ki18n6'
         'kjobwidgets6' 'kio6' 'kxmlgui6' 'kwidgetsaddons6' 'kwindowsystem6' 'parted'
         'libatasmart' 'udisks2' 'kpmcore6' 'ntfs-3g' 'dosfstools')
makedepends=('extra-cmake-modules' 'kdoctools6')
#groups=('system')
options=('debug')	
source=($_mirror/${_pkgname}-${_kdever}.tar.xz)
md5sums=(`grep ${_pkgname}-${_kdever}.tar.xz ../kde-sc.md5 | cut -d" " -f1`)

prepare() {
  cd ${_pkgname}-${pkgver}
  # filsystem code from src/fs/filesystem.h, set as default
  sed -i -e 's/FileSystem::Type::Btrfs/FileSystem::Type::Xfs/' src/partitionmanager.kcfg
}

build() {
  cmake -B build -S ${_pkgname}-${pkgver} \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DKDE_INSTALL_LIBDIR=lib \
        -DBUILD_TESTING=OFF \
        -DQT_MAJOR_VERSION=6
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}

