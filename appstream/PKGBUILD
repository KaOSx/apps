 
pkgname=appstream
_pkgname=AppStream
pkgver=0.13.1
pkgrel=1
pkgdesc="Provides a standard for creating app stores across distributions"
arch=('x86_64')
url="https://github.com/ximion/appstream"
license=('GPL')
depends=('libyaml' 'libxml2' 'glib2' 'qt5-base' 'lmdb' 'libsoup')
makedepends=('meson' 'ninja' 'gobject-introspection' 'itstool' 'xmlto' 'gtk-doc' 'gperf' 'qt5-tools') # 'clang')
source=("https://www.freedesktop.org/software/appstream/releases/${_pkgname}-${pkgver}.tar.xz"
        'appstream.hook')
md5sums=('6b865cd6f8865ef186e9bf3b3b60affb'
         '1313427d15dc06b5baf29d8d9f344c76')

build() {
  mkdir -p build
  cd build
  
  #export CC=/usr/bin/clang
  #export CXX=/usr/bin/clang++

   meson ../${_pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    --libdir=lib \
    --libexecdir=lib \
    -Dqt=true \
    -Dstemming=false \
    -Ddocs=false \
    -Dmaintainer=false
  ninja
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install

  install -Dm644 ../appstream.hook ${pkgdir}/usr/share/libalpm/hooks/appstream.hook
}
