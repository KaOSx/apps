
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=konsole
pkgver=${_kdever}
pkgrel=2
pkgdesc="KDE's terminal emulator"
arch=('x86_64')
url='https://github.com/KDE/konsole'
license=('LGPL')
depends=('kbookmarks' 'kcompletion' 'kconfig' 'kconfigwidgets' 'kcoreaddons'
         'kguiaddons' 'kdbusaddons' 'ki18n' 'kiconthemes' 'kinit' 'kio'
         'knotifications' 'knotifyconfig' 'kparts' 'kpty' 'kservice' 
         'ktextwidgets' 'kwidgetsaddons' 'kwindowsystem' 'kxmlgui' 'knewstuff')
makedepends=('extra-cmake-modules' 'kdoctools')
conflicts=('kde-baseapps-konsole')
replaces=('kde-baseapps-konsole')
provides=('kde-baseapps-konsole')
source=("$_mirror/${pkgname}-${pkgver}.tar.xz"
        "https://invent.kde.org/utilities/konsole/-/commit/3b06b6f22a9bdc0297a03ce45aa95642c08b1dd3.diff")
md5sums=(`grep ${pkgname}-$_kdever.tar.xz ../kde-sc.md5 | cut -d" " -f1`
         '15cd21faf776341ecfdd51335f5bdced')

prepare() {
  cd ${pkgname}-${pkgver}
  # revert to make make text selection still visible in all color profiles
  # https://bugs.kde.org/show_bug.cgi?id=435309
  patch -p1 -R -i ${srcdir}/3b06b6f22a9bdc0297a03ce45aa95642c08b1dd3.diff
}
         
build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
    -DKDE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  
  make DESTDIR=${pkgdir} install
}

