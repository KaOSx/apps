
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=konsole
pkgver=${_kdever}
pkgrel=1
pkgdesc="KDE's terminal emulator"
arch=('x86_64')
url='https://konsole.kde.org/'
license=('LGPL')
depends=('kbookmarks' 'kcompletion' 'kconfig' 'kconfigwidgets' 'kcoreaddons'
         'kguiaddons' 'kdbusaddons' 'ki18n' 'kiconthemes' 'kinit' 'kio'
         'knotifications' 'knotifyconfig' 'kparts' 'kpty' 'kservice' 
         'ktextwidgets' 'kwidgetsaddons' 'kwindowsystem' 'kxmlgui' 'knewstuff')
makedepends=('extra-cmake-modules' 'kdoctools')
source=("${_mirror}/${pkgname}-${pkgver}.tar.xz"
        "https://invent.kde.org/utilities/konsole/-/commit/b43548b22c4844db95c628a508199518bd3f4350.diff")
md5sums=(`grep ${pkgname}-$_kdever.tar.xz ../kde-sc.md5 | cut -d" " -f1`
         '2e4098e79c7a72a53886343b27bb00cb')

prepare() {
  cd ${pkgname}-${pkgver}
  # https://bugs.kde.org/show_bug.cgi?id=437791
  patch -p1 -i ${srcdir}/b43548b22c4844db95c628a508199518bd3f4350.diff
}

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
    -DKDE_INSTALL_LIBDIR=lib
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}

