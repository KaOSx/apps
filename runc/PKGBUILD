
# stay with _commit builds, keep in sync with what docker ships
pkgname=runc
pkgver=0.1.1
_commit=b2567b37d7b75eb4cf325b77297b140ea686ce8f
pkgrel=12
arch=('x86_64')
pkgdesc="CLI tool for spawning and running containers according to the OCI specification."
url="http://runc.io/"
license=('APACHE')
depends=('glibc' 'libseccomp')
makedepends=('go')
#source=("https://github.com/opencontainers/runc/archive/v${pkgver}.tar.gz")
source=("https://github.com/opencontainers/runc/archive/${_commit}.zip")
md5sums=('bebf6d596231e25c814f52401f8e3cb9')

prepare() {
    cd ${srcdir}
   
    install -d go/src/github.com/opencontainers
    mv ${pkgname}-${_commit} go/src/github.com/opencontainers/${pkgname}
}

build() {
    cd go/src/github.com/opencontainers/${pkgname}

    export GOPATH=${srcdir}/go/
    export PATH=$PATH:$GOPATH/bin
    
    make
}

package() {
    cd go/src/github.com/opencontainers/${pkgname}
    
    install -Dm755 runc ${pkgdir}/usr/bin/runc
}
