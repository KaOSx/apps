 
pkgname=neochat
pkgver=1.1.0
pkgrel=1
pkgdesc="Client for Matrix, the decentralized communication protocol for instant messaging - Spectral fork."
arch=('x86_64')
url="https://apps.kde.org/en/neochat"
license=('GPL3')
depends=('qt5-multimedia' 'qt5-quickcontrols2' 'qt5-svg' 'kirigami' 'ki18n' 
         'knotifications' 'kconfig' 'kcoreaddons' 'kdbusaddons' 'qtkeychain' 'libquotient' 
         'cmark' 'kquickimageeditor')
makedepends=('extra-cmake-modules')
replaces=('spectral')
conflicts=('spectral')
source=("https://download.kde.org/stable/neochat/${pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('9dc386b4630e630970fb8e6e47ddb034f9e6758996b27b0615a8c2725839b4c2')

prepare() {
  cd ${pkgname}-${pkgver}
  
  sed -i -e 's|include "events/stickerevent.h"|include "stickerevent.h"|' src/neochatroom.cpp
  sed -i -e 's|include <events/stickerevent.h>|include "stickerevent.h"|' src/messageeventmodel.cpp
}

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release 
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
