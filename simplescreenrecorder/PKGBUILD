 
pkgname=simplescreenrecorder
pkgver=0.4.2
pkgrel=2
_commit=65975c6
pkgdesc="Screen recorder that is actually quite complex, but 'simple' in the sense that it's easy to use"
arch=('x86_64')
url="https://www.maartenbaert.be/simplescreenrecorder/"
license=("GPL3")
depends=('qt5-x11extras' 'ffmpeg' 'alsa-lib' 'pulseaudio' 'jack' 'libgl' 'glu' 'libx11' 'libxext' 
         'libxfixes' 'libxi')
makedepends=('qt5-tools' 'cmake')
options=('!libtool')
source=("https://github.com/MaartenBaert/ssr/archive/${pkgver}.tar.gz")
md5sums=('c43eb407d13006e0173f087ba5111214')

build() {
  mkdir -p build
  cd build
  
  cmake ../ssr-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DWITH_QT5=TRUE \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DLRELEASE=/usr/lib/qt5/bin/lrelease
    
  make
}

package() {
  cd build
  
  make DESTDIR=${pkgdir} install
}
