
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=khotkeys
pkgver=${_plasmaver}
pkgrel=1
pkgdesc='KHotKeys'
arch=('x86_64')
url='https://projects.kde.org/projects/kde/workspace/khotkeys'
license=('LGPL')
depends=('kglobalaccel' 'ki18n' 'kio' 'kxmlgui' 'kdelibs4support' 'kdbusaddons' 'kcmutils'
         'plasma-framework' 'plasma-workspace')
makedepends=('extra-cmake-modules' 'kdoctools' 'ninja')
groups=('kdeplasma5')
source=("http://download.kde.org/stable/plasma/${pkgver}/${pkgname}-${pkgver}.tar.xz"
#source=("${pkgname}-${pkgver}.tar.gz::https://github.com/KDE/${pkgname}/archive/${_branch}.tar.gz"
        'dbus_action.diff')
md5sums=(`grep ${pkgname}-${_plasmaver}.tar.xz ../kf5.md5 | cut -d" " -f1`
         'b72371a922acbf0b812d92cd509595a9')

prepare() {
  cd ${pkgname}-${pkgver}
  
  patch -p1 -i ${srcdir}/dbus_action.diff
  sed -i 's|qdbusviewer|qdbusviewer-qt5|' kcm_hotkeys/actions/dbus_action_widget.cpp
}

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF \
    -GNinja
  ninja 
}

package() {
  cd build
  
  DESTDIR=${pkgdir} ninja install
}
