
pkgname=kdev-go
pkgver=1.90.92
_commit=6aee7c564ae3372d66304553f0ea82b5e26ef0fc
pkgrel=2
pkgdesc='Go language support for KDevelop'
arch=('x86_64')
url="https://www.kdevelop.org/"
license=('GPL')
depends=('kdevelop' 'threadweaver' 'ktexteditor' 'ki18n')
makedepends=('extra-cmake-modules')
groups=('kde' 'kdevelop-plugins')
source=("https://github.com/KDE/kdev-go/archive/${_commit}.zip")
md5sums=('a90a735cc191f4d99c00ea56c6573583')

prepare() {
  cd ${pkgname}-${_commit}

  sed -i 's|add_subdirectory(tests)|#add_subdirectory(tests)|' codecompletion/CMakeLists.txt
  sed -i 's|add_subdirectory(tests)|#add_subdirectory(tests)|' duchain/CMakeLists.txt
  sed -i 's|add_subdirectory(test)|#add_subdirectory(test)|' parser/CMakeLists.txt
}

build() {
  mkdir -p build
  cd build 
  
  cmake ../${pkgname}-${_commit} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_QMLDIR=/usr/lib/qt5/qml \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
    -DBUILD_TESTING=OFF 
  make 
}

package() {
  cd build
  
  make DESTDIR=${pkgdir} install
}
