
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=incidenceeditor
pkgver=${_kdever}
pkgrel=1
pkgdesc="Library that provides calendar incidence editor."
arch=('x86_64')
url='https://invent.kde.org/pim/incidenceeditor'
license=('LGPL')
depends=('kcalendarcore6' 'kcodecs6' 'ki18n6' 'kiconthemes6' 'kio6' 'ktextwidgets6'
         'akonadi' 'kldap' 'calendarsupport' 'kcalutils' 'eventviews' 'libkdepim' 
         'kmime' 'akonadi-calendar' 'akonadi-mime' 'pimcommon' 'kpimtextedit' 'kidentitymanagement'
         'kdiagram')
makedepends=('extra-cmake-modules')
groups=('kde' 'kdepim' 'kde-uninstall')
source=($_mirror/${pkgname}-${_kdever}.tar.xz
        "https://invent.kde.org/pim/incidenceeditor/-/commit/c64502a92568abe54e27f6d3adfcb1fe84baac4b.diff"
        "https://invent.kde.org/pim/incidenceeditor/-/commit/d2bda9e3754f7daa8ea876e1d909fe038e03c85b.diff")
md5sums=(`grep ${pkgname}-${_kdever}.tar.xz ../kde-sc.md5 | cut -d" " -f1`
         '8167c4b987888e9b0d96f1d83841e7d0'
         'acd54f99335ad8bb4321bec43b6e6ace')

prepare() {
    cd ${pkgname}-${pkgver}

    patch -p1 -i ${srcdir}/d2bda9e3754f7daa8ea876e1d909fe038e03c85b.diff
    patch -p1 -i ${srcdir}/c64502a92568abe54e27f6d3adfcb1fe84baac4b.diff
}

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DQT_MAJOR_VERSION=6
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
