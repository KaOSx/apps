
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=ktp-common-internals
pkgver=${_kdever}
pkgrel=2
pkgdesc="Common libraries for telepathy-kde"
arch=('x86_64')
url="https://projects.kde.org/projects/extragear/network/telepathy/ktp-common-internals"
license=('GPL')
depends=('kcoreaddons' 'knotifications' 'kio' 'kwidgetsaddons' 'kcmutils' 'knotifyconfig' 
         'ktexteditor' 'kwallet' 'kconfig' 'kwindowsystem' 'kiconthemes' 'kpeople' 
         'telepathy-qt5' 'telepathy-mission-control' 'kcontacts' 'kaccounts-integration'
         'libotr' 'telepathy-logger-qt5' 'telepathy-accounts-signon')
makedepends=('extra-cmake-modules' 'kdoctools' 'git' 'boost')
groups=('kde-telepathy')
conflicts=('kf5-ktp-common-internals')
replaces=('kf5-ktp-common-internals')
provides=('kf5-ktp-common-internals')
source=("$_mirror/${pkgname}-$_kdever.tar.xz"
        "https://invent.kde.org/network/ktp-common-internals/-/commit/804c22c5abdf3492387f6a7c71c76f58625a3f80.diff")
md5sums=(`grep ${pkgname}-$_kdever.tar.xz ../kde-sc.md5 | cut -d" " -f1`
         '0e330b5f26ac837b5118c0c8bce29997')

prepare() {
  cd ${pkgname}-${pkgver}

  patch -p1 -i ${srcdir}/804c22c5abdf3492387f6a7c71c76f58625a3f80.diff
}

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_LIBEXECDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_QMLDIR=/usr/lib/qt5/qml \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
    -DBUILD_TESTING=OFF 
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
