
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=krunner
pkgver=${_kf5ver}
pkgrel=1
pkgdesc='Framework for providing different actions given a string query'
arch=('x86_64')
url='https://projects.kde.org/projects/frameworks/krunner'
license=('LGPL')
depends=('kconfig' 'kcoreaddons' 'ki18n' 'kio' 'kservice' 'plasma-framework' 'solid' 'threadweaver')
makedepends=('extra-cmake-modules' 'kdoctools')
groups=('kf5')
source=("http://download.kde.org/stable/frameworks/${pkgver}/portingAids/${pkgname}-${pkgver}.tar.xz")
md5sums=(`grep ${pkgname}-${_kf5ver}.tar.xz ../kf5.md5 | cut -d" " -f1`)

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DQML_INSTALL_DIR=/usr/lib/qt5/qml \
    -DECM_MKSPECS_INSTALL_DIR=/usr/lib/qt5/mkspecs/modules \
    -DPLUGIN_INSTALL_DIR=/usr/lib/qt5/plugins \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build

  make DESTDIR="${pkgdir}" install
}

