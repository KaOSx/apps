
pkgname=bitcoin
pkgver=26.0
pkgrel=1
arch=('x86_64')
pkgdesc="Bitcoin is a peer-to-peer network based digital currency"
url="https://bitcoin.org/en/"
license=('MIT')
depends=('boost-libs' 'qt5-base' 'miniupnpc' 'qrencode' 'libevent' 'zeromq')
makedepends=('boost' 'qt5-tools')
#source=("git://github.com/bitcoin/bitcoin.git")
source=("https://github.com/bitcoin/bitcoin/archive/v${pkgver}.tar.gz"
        "https://raw.githubusercontent.com/bitcoin-core/packaging/master/debian/bitcoin-qt.desktop")
md5sums=('dd9837a31da6295fc21ed793eb268a7a'
         'a1322d92df28647f88332030f414a0da')

build() {
  cd ${pkgname}-${pkgver}

  ./autogen.sh
  ./configure --prefix=/usr \
      --without-bdb \
      --with-gui=qt5
  make
}


package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  install -Dm644 ../bitcoin-qt.desktop ${pkgdir}/usr/share/applications/bitcoin.desktop
  install -Dm644 share/pixmaps/bitcoin128.png ${pkgdir}/usr/share/pixmaps/bitcoin128.png

  install -Dm644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}
