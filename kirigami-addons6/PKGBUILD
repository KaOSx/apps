 
pkgname=kirigami-addons6
_pkgname=kirigami-addons
pkgver=0.11.0
pkgrel=2
pkgdesc="Add-ons for the Kirigami framework (kf6 version)."
arch=('x86_64')
url="https://invent.kde.org/libraries/kirigami-addons"
license=('GPL3')
depends=('qt6-declarative' 'kirigami6' 'ki18n6')
makedepends=('extra-cmake-modules6')
source=("https://download.kde.org/stable/kirigami-addons/${_pkgname}-${pkgver}.tar.xz"
        "https://invent.kde.org/libraries/kirigami-addons/-/commit/dc6e1ea099622801750ada8b59beded3dc2da5fa.diff")
sha256sums=('05296c5afbe6804308bf9c0d2751f3b748b40d00fa784946d1dcdf3af4bffbad'
            'ca37e7412bc12a105aa28552399ee49b0fe148230e3c2338fba8d792b548557f')

prepare() {
  cd ${_pkgname}-${pkgver}

  patch -p1 -i ${srcdir}/dc6e1ea099622801750ada8b59beded3dc2da5fa.diff
}

build() {
  cmake -B build -S ${_pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE_INSTALL_LIBDIR=lib \
    -DQT_MAJOR_VERSION=6
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
