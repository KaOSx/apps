
_prefix=/usr

pkgname=xbmc
pkgver=12.2
pkgrel=1
pkgdesc="XBMC Media Center"
arch=('x86_64')
url="http://xbmc.org"
license=('GPL' 'custom')
depends=( 'bzip2' 'curl' 'enca' 'faac' 'faad2' 'fontconfig' 'fribidi' 'python2' 'python-pysqlite'
          'glew' 'jasper' 'libcdio' 'libgl' 'libmad' 'libmms' 'libass' 'yajl' 'libpng12' 'x264'
          'libmpeg2' 'libmariadbclient' 'libsamplerate' 'libxinerama' 'libpng' 'libbluedevil'
          'libxrandr' 'libxtst' 'lzo2' 'sdl_image' 'sdl_mixer' 'libvorbis' 'pulseaudio' 'libdvdcss'
          'samba' 'wavpack' 'mesa-demos' 'xorg-utils' 'libmicrohttpd' 'libjpeg-turbo' 'mesa' 'swig'
          'libssh' 'libva' 'libmodplug' 'rtmpdump' 'lsb-release' 'sqlite3' 'libbluray' 'java-runtime'
          'tinyxml' 'taglib' 'libcap' 'glu' 'libusbx')
makedepends=('boost' 'cmake' 'gperf' 'nasm' 'libvdpau' 'zip' 'unzip' 'autoconf' 'automake' 'doxygen')
optdepends=('lirc: remote controller support'
            'unrar: access compressed files without unpacking them'
            'udisks: automount external drives'
            'upower: used to trigger suspend functionality'
            'libvdpau: for nvidia hardware acceleration'
            'lcms'
            'tk')
categories=('multimedia')
screenshot=('http://xbmc.org/wp-content/gallery/back-row/screenshot000.jpg')
install='xbmc.install'
options=(makeflags)
source=("http://mirrors.xbmc.org/releases/source/xbmc-$pkgver.tar.gz"
        'xbmc.service')
md5sums=('489f3877decae4e265ece54f9eaef0ba'
         '76fae229ebc3bcfaab7e7f27e4fb51f5')

build() {
    cd "${srcdir}/xbmc-$pkgver"

    msg "Bootstrapping XBMC"
    ./bootstrap
    
    export PYTHON_VERSION=2 
    msg "Configuring XBMC"
    ./configure --prefix=${_prefix} \
	--enable-vdpau \
	--enable-pulse \
	--disable-avahi \
	--disable-external-ffmpeg \
	--enable-rtmp \
	--enable-libbluray \
	--enable-dvdcss \
	--disable-debug \
	--enable-libusb \
	--enable-external-libraries \
	--enable-pulse \
	--enable-rtmp \
	--enable-ffmpeg_libvorbis \

    msg "Running make"
    make 
    make -C lib/addons/script.module.pil
}

package() {
    cd "${srcdir}/xbmc-$pkgver"
    msg "Running make install"
    make prefix="${pkgdir}${_prefix}" install

    # various fixes
    sed -i -e 's/which lsb_release &> \/dev\/null/\[ -f \/etc\/arch-release ]/g' "${pkgdir}${_prefix}/bin/xbmc"
    sed -i -e "s/lsb_release -a 2> \/dev\/null | sed -e 's\/\^\/    \/'/cat \/etc\/arch-release/g" "${pkgdir}${_prefix}/bin/xbmc"
    sed -i -e 's/python/python2/g' ${pkgdir}${_prefix}/bin/xbmc

    # Tools
    install -D -m 0755 "${srcdir}/xbmc-$pkgver/xbmc-xrandr" "${pkgdir}${_prefix}/share/xbmc/xbmc-xrandr"
    install -D -m 0755 "${srcdir}/xbmc-$pkgver/tools/TexturePacker/TexturePacker" "${pkgdir}${_prefix}/share/xbmc/"

    # Licenses
    install -d -m 0755 "${pkgdir}${_prefix}/share/licenses/${pkgname}"
    for licensef in LICENSE.GPL copying.txt; do
        mv "${pkgdir}${_prefix}/share/doc/xbmc/${licensef}" "${pkgdir}${_prefix}/share/licenses/${pkgname}"
    done

    # Docs
    install -d -m 0755 "${pkgdir}${_prefix}/share/doc/${pkgname}"

    # Cleanup 
    msg "Cleanup unneeded files"
    rm -rf "${pkgdir}/usr/share/xsessions"
    rm -f "${pkgdir}/usr/share/icons/hicolor/icon-theme.cache"
    
    # Systemd service
    install -Dm0644 $srcdir/xbmc.service $pkgdir/usr/lib/systemd/system/xbmc.service

}
