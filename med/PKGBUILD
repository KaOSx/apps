
pkgname=med
pkgver=4.1.1
pkgrel=3
arch=('x86_64')
pkgdesc="Library to store and exchange meshed data or computation results"
url="https://www.salome-platform.org/"
license=('LGPL')
depends=('hdf5' 'openmpi' 'python3')
makedepends=('cmake' 'swig')
source=("https://files.salome-platform.org/Salome/other/${pkgname}-${pkgver}.tar.gz"
        'hdf5.patch')
md5sums=('7f3e223385a762a8690f7735397dfde9'
         '35bb7099a24e6a8ee855beee004bc4ea')

prepare() {
  cd ${pkgname}-${pkgver}_SRC

  patch -p1 -i ${srcdir}/hdf5.patch
}

build() {
  cmake -B build -S ${pkgname}-${pkgver}_SRC \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DMEDFILE_USE_MPI=OFF \
    -DMEDFILE_BUILD_STATIC_LIBS=OFF \
    -DMEDFILE_INSTALL_DOC=OFF \
    -DMEDFILE_BUILD_PYTHON=ON \
    -DMEDFILE_BUILD_TESTS=OFF
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
