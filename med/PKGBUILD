
pkgname=med
pkgver=5.0.0
pkgrel=1
arch=('x86_64')
pkgdesc="Library to store and exchange meshed data or computation results"
url="https://www.salome-platform.org/"
license=('LGPL')
depends=('hdf5' 'openmpi' 'python3')
makedepends=('cmake' 'swig')
source=("https://files.salome-platform.org/Salome/medfile/${pkgname}-${pkgver}.tar.bz2"
        'hdf5.patch')
md5sums=('3c5ae8a37d7971658870b77caad1d73b'
         'b0b15a4dd4a9ad0453726f9da4d4301f')

prepare() {
  cd ${pkgname}-${pkgver}

  patch -p1 -i ${srcdir}/hdf5.patch
}

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DMEDFILE_USE_MPI=OFF \
    -DMEDFILE_BUILD_STATIC_LIBS=OFF \
    -DMEDFILE_INSTALL_DOC=OFF \
    -DMEDFILE_BUILD_PYTHON=ON \
    -DMEDFILE_BUILD_TESTS=OFF
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
