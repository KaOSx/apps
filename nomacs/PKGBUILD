
pkgname=nomacs
pkgver=3.12
pkgrel=2
pkgdesc="Qt based image viewer for all common image formats including RAW and psd images."
arch=('x86_64')
url="http://www.nomacs.org/"
license=('GPL3')
depends=('qt5-svg' 'exiv2' 'libraw' 'opencv' 'qt5-imageformats' 'quazip')
makedepends=('cmake' 'qt5-tools')
source=("https://github.com/nomacs/nomacs/archive/${pkgver}.tar.gz"
        'qdrag.diff')
md5sums=('7b2160cbcf907ee080d696c61b3dc4e8'
         'bdd31541310ed5b6bee1c8f101ef3505')

prepare() {
  cd ${pkgname}-${pkgver}
  #patch -p1 -i $srcdir/qdrag.diff
  sed -i 's|include("cmake/CodeCoverage.cmake")|#include("cmake/CodeCoverage.cmake")|g' ImageLounge/CMakeLists.txt
  sed -i 's|setup_target_for_coverage(${PROJECT_NAME}|#setup_target_for_coverage(${PROJECT_NAME}|g' ImageLounge/CMakeLists.txt

}

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver}/ImageLounge \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DQT_QMAKE_EXECUTABLE=/usr/lib/qt5/bin/qmake \
    -DENABLE_TRANSLATIONS=ON \
    -DUSE_SYSTEM_QUAZIP=ON 
  make
}

package() {
  cd build
  
  make DESTDIR=${pkgdir}/ install
}
