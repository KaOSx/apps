
pkgname=xdg-desktop-portal-gtk
pkgver=1.15.1
pkgrel=1
pkgdesc="Gtk implementation of xdg-desktop-portal"
arch=('x86_64')
url="https://github.com/flatpak/xdg-desktop-portal-gtk"
license=('LGPL')
depends=('gsettings-desktop-schemas' 'gtk3' 'xdg-desktop-portal')
makedepends=('python3' 'meson' 'ninja')
source=("https://github.com/flatpak/xdg-desktop-portal-gtk/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('425551ca5f36451d386d53599d95a3a05b94020f1a4927c5111a2c3ba3a0fe4c')

build() {
  mkdir -p build
  cd build

  meson setup ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    --libexecdir=/usr/lib \
    -Dwallpaper=disabled
  ninja
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
}
