pkgname=mixxx
pkgver=2.3.6
_commit=286a52a432e05fbd7ed2ad029fe2a11f44f1cf85
pkgrel=4
pkgdesc="Free, open source software for digital DJ'ing."
arch=('x86_64')
url='https://www.mixxx.org'
license=('GPL')
depends=('faad2' 'fftw' 'flac' 'libid3tag' 'libmad' 'libmodplug' 'libogg' 'libshout' 'libsndfile' 'libusb' 'libvorbis' 
         'portaudio' 'portmidi' 'protobuf' 'qt5-xmlpatterns' 'qt5-svg' 'qt5-script' 'qt5-x11extras' 'rubberband'
         'taglib' 'wavpack' 'opusfile' 'chromaprint' 'upower' 'lilv' 'libmp4v2' 'soundtouch' 'qtkeychain')
makedepends=('cmake' 'glu' 'qt5-tools') # 'clang')
source=("https://github.com/mixxxdj/mixxx/archive/${pkgver}.tar.gz"
#source=("https://github.com/mixxxdj/mixxx/archive/${_commit}.zip"
        "https://github.com/mixxxdj/mixxx/commit/03fad27e1f0f18ec83c9a4bc5f03f28948cd44fb.diff")
md5sums=('4a8e6aa35bec7240a02f27afa65754d5'
         'b03c4c7b79281f29bba54a4dd5003466')

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF \
    -DINSTALL_USER_UDEV_RULES=OFF \
    -DOPTIMIZE=OFF \
    -DCCACHE_SUPPORT=OFF \
    -DFAAD=ON \
    -DOPUS=ON \
    -DVINYLCONTROL=ON \
    -DWAVPACK=ON
  cmake --build build
}

package() {
  DESTDIR=${pkgdir} cmake --install build

  install -m755 -d ${pkgdir}/usr/lib/udev/rules.d
  install -m644 ${pkgname}-${pkgver}/res/linux/mixxx-usb-uaccess.rules  ${pkgdir}/usr/lib/udev/rules.d/
}
