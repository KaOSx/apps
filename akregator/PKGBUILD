
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=akregator
pkgver=${_kdever}
pkgrel=1
pkgdesc="RSS Feed Reader."
arch=('x86_64')
url='https://github.com/KDE/akregator'
license=('LGPL')
depends=('kcrash' 'kcmutils' 'knotifyconfig' 'kparts' 'ktexteditor' 'kxmlgui' 'kiconthemes'
         'knotifications' 'kwindowsystem'
         'akonadi-mime' 'grantleetheme' 'kontactinterface' 'libkdepim' 'libkleo' 'messagelib' 
         'pimcommon' 'kpimtextedit' 'syndication'
         'grantlee' 'qtwebengine' 'kdepim-addons'
         'akonadi-import-wizard' 'pim-data-exporter' 'kuserfeedback')
makedepends=('extra-cmake-modules' 'kdoctools' 'boost')
groups=('kdepim')
source=($_mirror/${pkgname}-${_kdever}.tar.xz)
md5sums=(`grep ${pkgname}-${_kdever}.tar.xz ../kde-sc.md5 | cut -d" " -f1`)

build() {
  mkdir -p build
  cd build 
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DKDE_INSTALL_QMLDIR=/usr/lib/qt5/qml \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
    -DBUILD_TESTING=OFF 
  make 
}

package() {
  cd build
  
  make DESTDIR=${pkgdir} install
}
