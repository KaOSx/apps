
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=kdevelop
pkgver=${_kdever}
pkgrel=1
pkgdesc="A C/C++ development environment for KDE"
arch=('x86_64')
url="https://www.kdevelop.org/"
license=('GPL')
depends=('kconfig6' 'kdeclarative6' 'kiconthemes6' 'ki18n6' 'kitemmodels6' 'kitemviews6'
         'kjobwidgets6' 'kcmutils6' 'kio6' 'knewstuff6' 'knotifyconfig6' 'kparts6' 'kservice6' 'ktexteditor6' 
         'threadweaver6' 'kxmlgui6' 'kwindowsystem6' 'kcrash6' 'kguiaddons6' 'karchive6' 'knotifications6'
         'purpose6' 'sonnet6' 'ktextwidgets6'
         'libksysguard' 'libkomparediff2' 'libplasma' 'konsole'
         'kdevelop-pg-qt' 'clang' 'llvm' 'heaptrack' 'subversion' 'qt6-webengine' 'qt6-5compat'
         'meson' 'clazy' 'ktexttemplate') # 'okteta' 'krunner' 
makedepends=('extra-cmake-modules' 'kdoctools6' 'qt6-tools' 'perl' 'docbook-xsl' 'boost')
conflicts=('kdevplatform')
replaces=('kdevplatform')
groups=('programming')
#source=($_mirror/${pkgname}-${_kdever}.tar.xz)
#md5sums=(`grep ${pkgname}-${_kdever}.tar.xz ../kde-sc.md5 | cut -d" " -f1`)
source=("https://invent.kde.org/kdevelop/kdevelop/-/archive/work/apol/kf6/kdevelop-work-apol-kf6.tar.bz2")
md5sums=('55d1c23d01b833a99c090a613aff3710')

prepare() {
  cd ${pkgname}-work-apol-kf6

  #patch -p1 -i $srcdir/qt55.diff
  sed -i -e 's|qdbus|qdbus-qt6|g' app/kdevelop!
  sed -i -e 's|ecm_optional_add_subdirectory(qmljs)|# ecm_optional_add_subdirectory(qmljs)|g' plugins/CMakeLists.txt
}

build() {
  cmake -B build -S ${pkgname}-work-apol-kf6 \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DQT_MAJOR_VERSION=6 \
    -DBUILD_WITH_QT6=ON
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
