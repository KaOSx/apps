
pkgname=kdevelop
pkgver=5.3.2
_pkgver=5.3.2
pkgrel=1
pkgdesc="A C/C++ development environment for KDE"
arch=('x86_64')
url="http://www.kdevelop.org/"
license=('GPL')
depends=('kconfig' 'kdeclarative' 'kiconthemes' 'ki18n' 'kitemmodels' 'kitemviews'
         'kjobwidgets' 'kcmutils' 'kio' 'knewstuff' 'knotifyconfig' 'kparts' 'kservice' 'ktexteditor' 
         'threadweaver' 'kxmlgui' 'kwindowsystem' 'kcrash' 'kguiaddons' 'karchive' 'knotifications'
         'krunner' 'libksysguard' 'libkomparediff2' 'plasma-framework' 'okteta' 'grantlee' 'konsole'
         'kdevelop-pg-qt' 'clang' 'llvm' 'heaptrack' 'subversion' 'qt5-webkit' 'qtwebengine' 'purpose')
makedepends=('extra-cmake-modules' 'kdoctools' 'perl' 'docbook-xsl' 'boost')
conflicts=('kdevplatform')
replaces=('kdevplatform')
groups=('programming')
source=("https://download.kde.org/stable/kdevelop/${_pkgver}/src/kdevelop-${pkgver}.tar.xz")      
sha256sums=('08ccd575514187dcbd01ac976a619803410c26bdfabf5d2d5fd52c95b76d6f2a')

prepare() {
  cd ${pkgname}-${pkgver}
  
  #patch -p1 -i $srcdir/qt55.diff
  sed -i -e 's|qdbus|qdbus-qt5|g' app/kdevelop!
}

build() {
  mkdir -p build
  cd build 
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_QMLDIR=/usr/lib/qt5/qml \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF 
  make 
}

package() {
  cd build
  
  make DESTDIR=${pkgdir} install
}
