
pkgname=kwin-effects-cube
pkgver=2.0.0
pkgrel=1
pkgdesc='Desktop cube effect for kwin.'
arch=('x86_64')
url='https://github.com/zzag/kwin-effects-cube'
license=('GPL3')
depends=('kconfig' 'kconfigwidgets' 'kcoreaddons' 'kglobalaccel' 'ki18n' 'kwindowsystem'
         'kxmlgui' 'libepoxy' 'kwin' 'qt5-quick3d')
makedepends=('extra-cmake-modules')
source=("https://github.com/zzag/kwin-effects-cube/archive/refs/tags/$pkgver.tar.gz")
md5sums=('2edfd11bb217595e167cc51df171e5bf')

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build

  install -Dm644 ${pkgname}-${pkgver}/LICENSES/GPL-3.0-only.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
