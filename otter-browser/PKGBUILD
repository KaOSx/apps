pkgname=otter-browser
pkgver=0.9.99.3
pkgrel=2
_commit=07f6853296e3246686ef3eef3daee226282cfab9
pkgdesc='Browser aiming to recreate the best aspects of the classic Opera (12.x) UI using Qt5.'
arch=('x86_64')
url="http://www.otter-browser.org"
license=('GPL3')
depends=('qtwebkit-tp' 'qt5-multimedia' 'desktop-file-utils' 'hicolor-icon-theme' 'hunspell' 'qt5-svg') 
makedepends=('cmake' 'qt5-tools')
groups=('network-web')
source=("https://github.com/OtterBrowser/otter-browser/archive/${_commit}.tar.gz")
md5sums=('6d5ced53e63f0fcc92962bebf46d394e')

prepare() {
  cd ${pkgname}-${_commit}
  
  sed -i -e 's|hunspell-1.5|hunspell-1.7|' cmake/FindHunspell.cmake
}

build() {
  cd ${pkgname}-${_commit}
  /usr/lib/qt5/bin/lrelease resources/translations/*.ts

  mkdir -p build
  cd build
  
  cmake ../ \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_QTWEBENGINE=OFF \
    -DENABLE_QTWEBKIT=ON 
  make
}

package() {
  cd ${pkgname}-${_commit}/build
  make DESTDIR=${pkgdir} install
  
  install -D -m0644 ../../${pkgname}-${_commit}/COPYING  ${pkgdir}/usr/share/licenses/otter-browser/COPYING
}

