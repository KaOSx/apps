
pkgname=icedtea-web
_pkgname=IcedTea-Web
pkgdesc="Provides a Free Software web browser plugin running applets written in the Java programming
         language and an implementation of Java Web Start, originally based on the NetX project."
pkgver=1.8.4
pkgrel=1
arch=('x86_64')
url="http://icedtea.classpath.org/wiki/IcedTea-Web"
license=('GPL2')
depends=('openjdk' 'desktop-file-utils')
makedepends=('zip' 'npapi-sdk' 'rhino' 'junit' 'cups' 'mercurial' 'firefox' 'rust')
groups=('network-web')
#source=("hg+http://icedtea.classpath.org/hg/icedtea-web")
source=("https://github.com/AdoptOpenJDK/IcedTea-Web/archive/icedtea-web-${pkgver}.tar.gz")
#noextract="${pkgname}-$pkgver.tar.gz"
md5sums=('79c0eb78dd8e18bdaab57fede5d67b8c')

_javaver=8
_jvmdir="/usr/lib/jvm/java-${_javaver}-openjdk"

build() {
  #cd ${srcdir}
  #LANG=en_US.UTF-8 bsdtar -x -f ${srcdir}/${pkgname}-${pkgver}.tar.gz
  
  cd ${_pkgname}-${pkgname}-${pkgver}

  ./autogen.sh
  ./configure --prefix="${_jvmdir}" \
              --with-jdk-home="${_jvmdir}" \
              --datarootdir=/usr/share \
              --disable-docs \
              --with-itw-libs=DISTRIBUTION \
              --with-browser-tests=no
  make -j1
}

check() {
  cd ${_pkgname}-${pkgname}-${pkgver}

  #make -k check 
}

package() {
  cd ${_pkgname}-${pkgname}-${pkgver}

  # possible make target (see bottom of Makefile.am: install-exec-local install-data-local
  make install-exec-local install-data-local DESTDIR="${pkgdir}"

  # Install desktop files.
  install -m755 -d "${pkgdir}/usr/share"/{applications,pixmaps}
  install -m644 javaws.png "${pkgdir}/usr/share/pixmaps"
  install -m644 {javaws,itweb-settings}.desktop "${pkgdir}/usr/share/applications"

  # link binaries into /usr/bin + jre/bin
  install -m755 -d "${pkgdir}/usr/bin"
  install -m755 -d "${pkgdir}/${_jvmdir}/jre/bin"
  pushd "${pkgdir}/${_jvmdir}/bin"
    for file in *; do
      ln -sf "${_jvmdir}/bin/${file}" "${pkgdir}/usr/bin"
      ln -sf "${_jvmdir}/bin/${file}" "${pkgdir}/${_jvmdir}/jre/bin"
    done
  popd

  # link the mozilla-plugin - test it here http://www.java.com/en/download/help/testvm.xml
  install -m755 -d "${pkgdir}/usr/lib/mozilla/plugins/"
  ln -sf "${_jvmdir}/lib/IcedTeaPlugin.so" "${pkgdir}/usr/lib/mozilla/plugins/"
}

