
pkgname=amule
pkgver=2.3.2.1
_commit=88364ced28850a14c0df1c2235692956db883b32
pkgrel=11
epoch=2
pkgdesc="An eMule-like client for ed2k p2p network"
arch=('x86_64')
url="http://www.amule.org"
license=('GPL')
depends=('wxgtk' 'gd' 'geoip' 'libupnp' 'crypto++' 'boost-libs')
makedepends=('boost')
source=("https://github.com/amule-project/amule/archive/${_commit}.zip")
#source=("https://sourceforge.net/projects/amule/files/aMule/${pkgver}/aMule-${pkgver}.tar.xz")
md5sums=('23a74c39df129446b266cff36d284692')

build() {
  cd amule-${_commit}
  sed -i 's|pubkey.DEREncode(asink);|pubkey.AccessMaterial().Save(asink);|' src/ClientCreditsList.cpp

  ./autogen.sh
  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --enable-cas \
              --enable-wxcas \
              --enable-amule-daemon \
              --enable-amulecmd \
              --enable-amule-gui \
              --enable-alc \
              --enable-alcc \
              --enable-webserver \
              --disable-debug \
              --enable-optimize \
              --enable-geoip \
              --enable-upnp \
              --with-wx-config=wx-config \
              --with-boost

  make 
}

package() {
  cd amule-${_commit}
  
  make DESTDIR=${pkgdir}/ install 
  # conflict with glibc
  #rm "${pkgdir}/usr/share/locale/locale.alias"
}

