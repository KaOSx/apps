
# stay with _commit builds, keep in sync with what docker ships
pkgname=docker-proxy
pkgver=0.7.99
# LIBNETWORK_COMMIT=7b2b1feb1de4817d522cc372af149ff48d25028e
_commit=dcdf8f176d1e13ad719e913e796fb698d846de98
pkgrel=1
arch=('x86_64')
pkgdesc="Docker container Networking"
url="https://github.com/docker/libnetwork"
license=('APACHE')
depends=('glibc')
makedepends=('go')
source=("https://github.com/docker/libnetwork/archive/${_commit}.zip")
md5sums=('c5cf0d39685380b3f9081c3040f25bda')

build() {
    cd libnetwork-${_commit}
    cp -r ${srcdir}/libnetwork-${_commit}/vendor/ ${srcdir}/libnetwork-${_commit}/src/
    export GOPATH=${srcdir}/libnetwork-${_commit}/
    export PATH=$PATH:$GOPATH/bin
    export GO111MODULE=off

    go build -o "bin/docker-proxy" ./cmd/proxy
}

package() {
    cd libnetwork-${_commit}

    install -Dm755 bin/docker-proxy ${pkgdir}/usr/bin/docker-proxy
}
