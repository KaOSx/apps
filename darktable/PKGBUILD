
pkgname=darktable
pkgver=3.4.1
_pkgver=3.4.1
pkgrel=1
pkgdesc="Open source photography workflow application and RAW developer"
arch=('x86_64')
url="https://www.darktable.org/"
license=('GPL3')
depends=('exiv2' 'lcms2' 'lensfun' 'dbus-glib' 'curl' 'kwallet'
         'libgphoto2' 'openexr' 'sqlite' 'libxslt' 'libsoup' 'json-glib' 'flickcurl' 'librsvg'
         'colord' 'graphicsmagick' 'libwebp' 'gtk3' 'pugixml' 'openjpeg')
makedepends=('cmake' 'clang' 'intltool')
optdepends=('kde-gtk-config: gtk integration settings for Plasma')
options=('!libtool')
source=("https://github.com/darktable-org/darktable/releases/download/release-${pkgver}/darktable-${_pkgver}.tar.xz")
sha256sums=('7fc3f851da9bcd7c5053ecd09f21aa3eb6103be98a6c58f52010b6f22174941e')

prepare() {
  cd ${pkgname}-${_pkgver}
  # https://github.com/darktable-org/darktable/issues/4752
  #./build.sh --disable-opencl
}

build() {
  mkdir -p build
  cd build
  
  CXXFLAGS+=" -fpermissive"
  cmake ../${pkgname}-${_pkgver} \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DBINARY_PACKAGE_BUILD=1 \
      -DBUILD_USERMANUAL=False \
      -DDONT_USE_INTERNAL_LUA=Off \
      -DUSE_COLORD=On \
      -DTESTBUILD_OPENCL_PROGRAMS=Off
  make
}

package() {
  cd build
  
  make DESTDIR=${pkgdir} install
}
