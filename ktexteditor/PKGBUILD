
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=ktexteditor
pkgver=${_kf5ver}
pkgrel=2
pkgdesc='KTextEditor provides a powerful text editor component that you can embed in your application'
arch=('x86_64')
url='https://github.com/KDE/ktexteditor'
license=('LGPL')
depends=('karchive' 'kconfig' 'kguiaddons' 'ki18n' 'kio' 'kparts' 'sonnet' 'kiconthemes'
         'syntax-highlighting' 'editorconfig-core-c' 'libgit2')
makedepends=('extra-cmake-modules')
groups=('kf5')
source=("https://download.kde.org/stable/frameworks/5.90/${pkgname}-${pkgver}.tar.xz"
        "https://invent.kde.org/frameworks/ktexteditor/-/commit/804e49444c093fe58ec0df2ab436565e50dc147e.diff"
        "https://invent.kde.org/frameworks/ktexteditor/-/commit/c80f935c345de2e2fb10635202800839ca9697bf.diff")
md5sums=(`grep ${pkgname}-$_kf5ver.tar.xz ../kf5.md5 | cut -d" " -f1`
         'f4c7eb690bc4c00b6700a3801c1b0857'
         '7fdf3c6394b1459cfc283f60753cd2d5')

prepare() {
  cd ${pkgname}-${pkgver}
  # CVE-2022-23853
  patch -p1 -i ${srcdir}/804e49444c093fe58ec0df2ab436565e50dc147e.diff
  patch -p1 -i ${srcdir}/c80f935c345de2e2fb10635202800839ca9697bf.diff
}

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
