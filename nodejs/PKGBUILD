
pkgname=nodejs
pkgver=11.5.0
pkgrel=1
pkgdesc="Platform built on Chrome's JavaScript runtime for easily building fast, scalable network applications."
arch=('x86_64')
url='http://nodejs.org/'
license=('MIT')
depends=('python2' 'openssl' 'zlib' 'libuv' 'c-ares' 'icu')
makedepends=('procps-ng')
checkdepends=('curl') 
options=('!emptydirs')
source=("https://github.com/nodejs/node/archive/v${pkgver}.tar.gz")
md5sums=('060e7e4ce0700b82e86ebfacc30844a8')

build() {
  cd node-${pkgver}
  
  ./configure --prefix=/usr \
    --with-intl=system-icu \
    --shared-openssl \
    --shared-cares \
    --shared-libuv \
    --experimental-http-parser \
    --shared-zlib 
  make
}

check() {
  cd node-${pkgver}

  make -j1 test
}

package() {
  cd node-${pkgver}
  make install DESTDIR=${pkgdir}


  install -d ${pkgdir}/usr/share/doc/nodejs
  cp -r doc/api/* ${pkgdir}/usr/share/doc/nodejs
  install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/nodejs/LICENSE
}

