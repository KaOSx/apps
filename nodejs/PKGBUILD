
pkgname=nodejs
pkgver=10.3.0
pkgrel=1
pkgdesc="Platform built on Chrome's JavaScript runtime for easily building fast, scalable network applications."
arch=('x86_64')
url='http://nodejs.org/'
license=('MIT')
depends=('python2' 'zlib' 'libuv' 'c-ares') # 'icu' 'openssl'
makedepends=('procps-ng')
checkdepends=('curl') 
options=('!emptydirs')
source=("https://github.com/nodejs/node/archive/v${pkgver}.tar.gz")
md5sums=('9d8f7788d5de215cf1af68a57fb4be86')

build() {
  cd node-${pkgver}
  
  ./configure --prefix=/usr \
    --with-intl=small-icu \
    --without-ssl \
    --shared-cares \
    --shared-zlib 
  make
}

check() {
  cd node-${pkgver}

  make -j1 test
}

package() {
  cd node-${pkgver}
  make install DESTDIR=${pkgdir}


  install -d ${pkgdir}/usr/share/doc/nodejs
  cp -r doc/api/* ${pkgdir}/usr/share/doc/nodejs
  install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/nodejs/LICENSE
}

