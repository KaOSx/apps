
pkgname=zulucrypt
_pkgname=zuluCrypt
pkgver=6.2.0
pkgrel=1
pkgdesc="Simple,feature rich and powerful solution for hard drives encryption."
url="https://mhogomchungu.github.io/zuluCrypt/"
arch=('x86_64')
license=('GPL') 
depends=('cryptsetup' 'libpwquality' 'libsecret' 'libxkbcommon' 'qt5-base' 'kwalletmanager')
makedepends=('cmake')
source=("https://github.com/mhogomchungu/zuluCrypt/releases/download/${pkgver}/${_pkgname}-${pkgver}.tar.xz") 
sha256sums=('090f3d561add1c5a5fc5158bf346155d43e2b42e7c47939a403ae2f8dcf8eaa3')

build() {
  cmake -B build -S ${_pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr/ \
    -DCMAKE_BUILD_TYPE=release \
    -DLIB_SUFFIX=lib \
    -DNOGUI=OFF \
    -DQT5=ON \
    -DHOMEMOUNTPREFIX=OFF \
    -DNOKDE=OFF 
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}

