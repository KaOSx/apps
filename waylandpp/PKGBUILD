
pkgname=waylandpp
pkgver=0.2.10
pkgrel=1
pkgdesc='Library to create C++ bindings for Wayland using the most modern C++ technology currently available'
arch=('x86_64')
url='https://nilsbrause.github.io/waylandpp_docs/'
license=('MIT' 'GPL3')
depends=('pugixml' 'wayland' 'libglvnd')
makedepends=('cmake')
source=("https://github.com/NilsBrause/waylandpp/archive/${pkgver}.tar.gz")
sha256sums=('10dba6359c41c4485dbf6586aefc5d6485b7d5f0da4f199358a4b5ceff69fb02')

build() {
    cmake -B build -S ${pkgname}-${pkgver} \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DBUILD_DOCUMENTATION=OFF
    cmake --build build
}

package() {

    DESTDIR=${pkgdir} cmake --install build
}
