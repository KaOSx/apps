
pkgname=kid3
pkgver=3.8.2
pkgrel=1
pkgdesc="Easily tag multiple MP3, Ogg/Vorbis, FLAC, MPC, MP4/AAC, and more, plus have control over both ID3v1 and ID3v2 tags"
arch=('x86_64')
url="https://kid3.sourceforge.io/"
license=('GPL')
depends=('qt5-declarative' 'libmp4v2' 'chromaprint' 'taglib' 'kio' 'qt5-multimedia')
makedepends=('extra-cmake-modules' 'docbook-xsl' 'qt5-tools' 'kdoctools')
groups=('multimedia')
source=("https://sourceforge.net/projects/kid3/files/kid3/${pkgver}/${pkgname}-${pkgver}.tar.gz")
md5sums=('a7b27ef15eb19b77cf7e689add005581')

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DWITH_APPS="CLI;KDE" \
    -DWITH_ID3LIB=OFF
  make 
}

package() {
  cd build
  
  make DESTDIR=${pkgdir} install
}
