
pkgname=kid3
pkgver=3.9.6
pkgrel=2
pkgdesc="Easily tag multiple MP3, Ogg/Vorbis, FLAC, MPC, MP4/AAC, and more, plus have control over both ID3v1 and ID3v2 tags"
arch=('x86_64')
url="https://kid3.kde.org/"
license=('GPL')
depends=('qt6-declarative' 'libmp4v2' 'chromaprint' 'taglib' 'kio6' 'qt6-multimedia')
makedepends=('extra-cmake-modules' 'docbook-xsl' 'qt6-tools' 'kdoctools6' 'clang')
groups=('multimedia')
source=("https://download.kde.org/stable/kid3/${pkgver}/${pkgname}-${pkgver}.tar.xz")
#source=("https://sourceforge.net/projects/kid3/files/kid3//${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('cf27ffcc4369c9940da7bfc12fd10dfbda526b8187e5b62662abe8069fc26c27')

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DWITH_APPS="CLI;KDE" \
    -DWITH_ID3LIB=OFF \
    -DBUILD_WITH_QT6=ON
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
