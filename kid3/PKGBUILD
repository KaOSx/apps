
pkgname=kid3
pkgver=3.9.4
pkgrel=1
pkgdesc="Easily tag multiple MP3, Ogg/Vorbis, FLAC, MPC, MP4/AAC, and more, plus have control over both ID3v1 and ID3v2 tags"
arch=('x86_64')
url="https://kid3.kde.org/"
license=('GPL')
depends=('qt5-declarative' 'libmp4v2' 'chromaprint' 'taglib' 'kio' 'qt5-multimedia')
makedepends=('extra-cmake-modules' 'docbook-xsl' 'qt5-tools' 'kdoctools')
groups=('multimedia')
source=("https://download.kde.org/stable/kid3/${pkgver}/${pkgname}-${pkgver}.tar.xz")
#source=("https://sourceforge.net/projects/kid3/files/kid3//${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('c410960e96225de0a1c4888c3ea1c6dc2ca245abb691474326dcdc3edbd6a520')

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DWITH_APPS="CLI;KDE" \
    -DWITH_ID3LIB=OFF
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
