
pkgname=godot
pkgver=4.3
_pkgver=4.3-stable
pkgrel=1
pkgdesc="Full featured, open source, MIT licensed, game engine."
arch=('x86_64')
url="https://www.godotengine.org"
license=('MIT')
depends=('alsa-lib' 'glu' 'libx11' 'libxcursor' 'libxinerama' 'libxrandr' 'freetype2'
         'mesa' 'libwebp' 'pulseaudio' 'openssl')
makedepends=('scons') # 'clang')
groups=('games')
#source=("https://downloads.tuxfamily.org/godotengine/${pkgver}/godot-${_pkgver}.tar.xz")
source=("https://github.com/godotengine/godot/releases/download/${_pkgver}/godot-${_pkgver}.tar.xz")
sha256sums=('751e55bfad8e04b846f9cf7b6eb80e058986a2cb1b103fc0fe6a4d8526a20e56')

build() {
    cd ${pkgname}-${_pkgver}
    # https://github.com/godotengine/godot/issues/7676
    #patch -p1 -i ${srcdir}/43bc27e2755f34017148097773da8a6988718aaf.diff

    scons platform=linuxbsd \
          use_llvm=no \
          target=editor \
          production=yes
}

package() {
    cd ${pkgname}-${_pkgver}

    install -Dm755 bin/godot.linuxbsd.editor.x86_64 ${pkgdir}/usr/bin/godot

    install -Dm644 misc/dist/linux/org.godotengine.Godot.desktop ${pkgdir}/usr/share/applications/godot.desktop
    install -Dm644 icon.svg ${pkgdir}/usr/share/pixmaps/godot.svg  
    install -Dm644 LICENSE.txt ${pkgdir}/usr/share/licenses/godot/LICENSE.txt
}

