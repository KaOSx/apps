
pkgname=godot
pkgver=4.0.3
_pkgver=4.0.3-stable
pkgrel=1
pkgdesc="Full featured, open source, MIT licensed, game engine."
arch=('x86_64')
url="https://www.godotengine.org"
license=('MIT')
depends=('alsa-lib' 'glu' 'libx11' 'libxcursor' 'libxinerama' 'libxrandr' 'freetype2'
         'mesa' 'libwebp' 'pulseaudio' 'openssl')
makedepends=('scons') # 'clang')
groups=('games')
source=("https://downloads.tuxfamily.org/godotengine/${pkgver}/godot-${_pkgver}.tar.xz")
sha256sums=('8ce3e2170ac5e043a7ce9c6f839ef67eddd83e24c0728d1afc76e678d4f37552')

build() {
    cd ${pkgname}-${_pkgver}
    # https://github.com/godotengine/godot/issues/7676
    #patch -p1 -i ${srcdir}/43bc27e2755f34017148097773da8a6988718aaf.diff

    scons platform=linuxbsd \
          use_llvm=no \
          target=editor \
          production=yes
}

package() {
    cd ${pkgname}-${_pkgver}

    install -Dm755 bin/godot.linuxbsd.editor.x86_64 ${pkgdir}/usr/bin/godot

    install -Dm644 misc/dist/linux/org.godotengine.Godot.desktop ${pkgdir}/usr/share/applications/godot.desktop
    install -Dm644 icon.svg ${pkgdir}/usr/share/pixmaps/godot.svg  
    install -Dm644 LICENSE.txt ${pkgdir}/usr/share/licenses/godot/LICENSE.txt
}

