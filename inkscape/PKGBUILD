
pkgname=inkscape
pkgver=1.2.1
_commit=2022-07-14_9c6d41e410
pkgrel=5
pkgdesc="A vector-based drawing program - svg compliant"
url="https://inkscape.org/en/"
arch=('x86_64')
license=('GPL' 'LGPL')
depends=('poppler' 'graphicsmagick' 'aspell' 'gc' 'gtkmm3' 'libxslt' 'gsl'
         'desktop-file-utils' 'hicolor-icon-theme' 'python3-lxml' 'python3-numpy'
         'libwpg' 'libvisio' 'libwpd' 'potrace' 'python3' 'dbus-glib' 'double-conversion'
         'libsoup' 'gdl' 'libcdr')
makedepends=('boost' 'pkg-config' 'intltool')
options=('!libtool')
source=("https://inkscape.org/gallery/item/34673/${pkgname}-${pkgver}.tar.xz"
        "https://gitlab.com/inkscape/inkscape/-/commit/f7857a4fc2b916e64d9689ccc89914ad19f3ef0a.diff"
        'poppler22.09.patch')
#source=("https://inkscape.org/en/gallery/item/10682/inkscape-${pkgver}.tar.bz2")
sha256sums=('46ce7da0eba7ca4badc1db70e9cbb67e0adf9bb342687dc6e08b5ca21b8d4c1b'
            '5478635169cdfaf22cde9ce9eceaaa7470fb563f1de47a25366ac503f3e8e748'
            '12fa1d893726ec9f080c99846f1e3d872ee26bb92e387302127a8d019166be6c')

prepare() {
  cd ${pkgname}-${pkgver}_${_commit}
  # poppler 22.09.0 needed
  patch -p1 -i ${srcdir}/poppler22.09.patch
  # poppler 22.04 needed
  #sed -i 's|_POPPLER_FONT_RAW_PTR(font_ptr) font_ptr|_POPPLER_FONT_RAW_PTR(font_ptr) font_ptr.get()|g' src/extension/internal/pdfinput/poppler-transition-api.h
}

build() {
  cmake -B build -S ${pkgname}-${pkgver}_${_commit} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DWITH_LIBCDR=ON
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
