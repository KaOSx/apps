
pkgname=inkscape
pkgver=1.1.2
_commit=2022-02-04_0a00cf5339
pkgrel=4
pkgdesc="A vector-based drawing program - svg compliant"
url="https://inkscape.org/en/"
arch=('x86_64')
license=('GPL' 'LGPL')
depends=('poppler' 'graphicsmagick' 'aspell' 'gc' 'gtkmm3' 'libxslt' 'gsl'
         'desktop-file-utils' 'hicolor-icon-theme' 'python3-lxml' 'python3-numpy'
         'libwpg' 'libvisio' 'libwpd' 'potrace' 'python3' 'dbus-glib' 'double-conversion'
         'libsoup' 'gdl')
makedepends=('boost' 'pkg-config' 'intltool')
options=('!libtool')
source=("https://media.inkscape.org/dl/resources/file/${pkgname}-${pkgver}.tar.xz"
        "https://gitlab.com/inkscape/inkscape/-/commit/15ab83e02b07018c3ffd4952a2623393187659e0.diff")
#source=("https://inkscape.org/en/gallery/item/10682/inkscape-${pkgver}.tar.bz2")
sha256sums=('3ffe54a06d0b25a4cd8b6eb424536ef1ed205be13443a39cd437c8c7b89b96d1'
            '249ad7a025013a593cd48a79b450ebd92221bde17ff030d1b67c4fc959c793ba')

prepare() {
  cd ${pkgname}-${pkgver}_${_commit}
  # poppler 22.03.0 needed
  patch -p1 -i ${srcdir}/15ab83e02b07018c3ffd4952a2623393187659e0.diff
  # poppler 21.11 needed
  #sed -i 's|font->getTag()->getCString(),|font->getTag().c_str(),|g' src/extension/internal/pdfinput/pdf-parser.cpp
}

build() {
  cmake -B build -S ${pkgname}-${pkgver}_${_commit} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DWITH_DBUS=ON \
    -DWITH_LIBCDR=OFF
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
