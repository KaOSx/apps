
pkgname=inkscape
pkgver=1.3.1
_commit=2023-11-16_91b66b0783
pkgrel=2
pkgdesc="A vector-based drawing program - svg compliant"
url="https://inkscape.org/en/"
arch=('x86_64')
license=('GPL' 'LGPL')
depends=('poppler' 'graphicsmagick' 'aspell' 'gc' 'gtkmm3' 'libxslt' 'gsl'
         'desktop-file-utils' 'hicolor-icon-theme' 'python3-lxml' 'python3-numpy'
         'libwpg' 'libvisio' 'libwpd' 'potrace' 'python3' 'dbus-glib' 'double-conversion'
         'libsoup' 'gdl' 'libcdr')
makedepends=('boost' 'pkg-config' 'intltool')
options=('!libtool')
source=("https://media.inkscape.org/dl/resources/file/${pkgname}-${pkgver}.tar.xz"
        "https://gitlab.com/inkscape/inkscape/-/commit//694d8ae43d06efff21adebf377ce614d660b24cd.diff"
        'poppler22.09.patch')
#source=("https://inkscape.org/en/gallery/item/10682/inkscape-${pkgver}.tar.bz2")
sha256sums=('421e0035fe5b3b054a0865dc8235be3f9e6e2dea54190d926b880a4ce05b00d8'
            'c2b6f4e74883e7f6f74283d9cf70997a344159c1e5cb9022237d14936efb3683'
            '12fa1d893726ec9f080c99846f1e3d872ee26bb92e387302127a8d019166be6c')

prepare() {
  cd ${pkgname}-${pkgver}_${_commit}
  # libxml2 2.12 needed
  patch -p1 -i ${srcdir}//694d8ae43d06efff21adebf377ce614d660b24cd.diff
  # poppler 22.04 needed
  #sed -i 's|_POPPLER_FONT_RAW_PTR(font_ptr) font_ptr|_POPPLER_FONT_RAW_PTR(font_ptr) font_ptr.get()|g' src/extension/internal/pdfinput/poppler-transition-api.h
}

build() {
  cmake -B build -S ${pkgname}-${pkgver}_${_commit} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DWITH_LIBCDR=ON
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
