
pkgname=inkscape
pkgver=0.92.4
pkgrel=6
pkgdesc="A vector-based drawing program - svg compliant"
url="https://inkscape.org/en/"
arch=('x86_64')
license=('GPL' 'LGPL')
depends=('poppler' 'imagemagick' 'aspell' 'gc' 'gtkmm' 'libxslt' 'gsl'
         'desktop-file-utils' 'popt' 'hicolor-icon-theme' 'python3-lxml' 'python3-numpy'
         'libwpg' 'libvisio' 'libwpd' 'potrace' 'python3')
makedepends=('boost' 'pkg-config' 'intltool')
options=('!libtool')
source=("https://inkscape.global.ssl.fastly.net/media/resources/file/inkscape-${pkgver}.tar.bz2"
        "https://gitlab.com/inkscape/inkscape/commit/a600c6438fef2f4c06f9a4a7d933d99fb054a973.diff"
        "https://gitlab.com/inkscape/inkscape/commit/fa1c469aa8c005e07bb8676d72af9f7c16fae3e0.diff")
#source=("https://inkscape.org/en/gallery/item/10682/inkscape-${pkgver}.tar.bz2")
md5sums=('ac30f6d5747fd9c620c00dad500f414f'
         '41a33ea04b7901848b275e76fcf7e5c2'
         'add8bdd7b82b7950ca9abc546768f545')

prepare() {
  cd ${pkgname}-${pkgver}
  
  #patch -p1 -i ${srcdir}/a600c6438fef2f4c06f9a4a7d933d99fb054a973.diff
  # poppler 0.7.x needed sed lines
  sed -e 's|gTrue|true|g' -e 's|gFalse|false|g' -e 's|GBool|bool|g' -i src/extension/internal/pdfinput/pdf-parser.*
  sed -e 's|getCString|c_str|g' -i src/extension/internal/pdfinput/*
  sed -e 's|new Lexer(xref, obj)|obj|g' -i src/extension/internal/pdfinput/pdf-parser.cpp
}

build() {
  mkdir -p build
  cd build 
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release 
  make 
}

package() {
  cd build
  
  make DESTDIR=${pkgdir} install
}
