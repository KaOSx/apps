
pkgname=sigil
_pkgname=Sigil
pkgver=2.0.2
_pagever=2.0.2
pkgrel=2
pkgdesc="A multi-platform WYSIWYG ebook editor. It is designed to edit books in ePub format. "
arch=('x86_64')
url="https://github.com/Sigil-Ebook/Sigil"
license=('GPL3')
depends=('qt6-webengine' 'qt6-svg' 'qt6-webchannel' 'qt6-5compat' 'boost-libs' 'xerces-c' 'python3-lxml'
         'python3-pillow' 'python3-six' 'pyqt6-python3' 'hunspell' 'pcre2')
makedepends=('cmake' 'boost' 'qt6-tools' 'clang')
#options=('debug')
groups=('office')
source=("Sigil-${pkgver}.tar.gz::https://github.com/Sigil-Ebook/Sigil/archive/${pkgver}.tar.gz"
        "PageEdit-${_pagever}.tar.gz::https://github.com/Sigil-Ebook/PageEdit/archive/${_pagever}.tar.gz")
#source=("https://github.com/Sigil-Ebook/Sigil/archive/${_commit}.zip")
sha256sums=('d5eaf300dc8307ca0765f9d32d9136e143ca7bcf9e65049499512fdea588e285'
            '01c9bf42421b27dd12a60a3dae8d29bd2328c8b293ff6aeea0fdd9d6175c70f5')

prepare() {
  cd ${_pkgname}-${pkgver}

  # set external editor
  sed -i -e 's|QString new_xeditor_path = ""|QString new_xeditor_path = "/usr/bin/pageedit"|' src/Dialogs/PreferenceWidgets/GeneralSettingsWidget.cpp
}

build() {
  cd ${_pkgname}-${pkgver}
  mkdir -p build 
  cd build

  export CC=/usr/bin/clang
  export CXX=/usr/bin/clang++

  cmake -G "Unix Makefiles" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DUSE_SYSTEM_LIBS=1 \
    -DINSTALL_HICOLOR_ICONS=1 \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_SKIP_RPATH=ON ..
  make

  cd ../..
  cd PageEdit-${_pagever}
  mkdir -p build
  cd build

  cmake -G "Unix Makefiles" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DINSTALL_BUNDLED_DICTS=0 \
    -DCMAKE_SKIP_RPATH=ON ..
  make
}

package() {
  cd ${_pkgname}-${pkgver}/build
  make install DESTDIR=${pkgdir}

  cd ../..

  cd PageEdit-${_pagever}/build
  make install DESTDIR=${pkgdir}
}
