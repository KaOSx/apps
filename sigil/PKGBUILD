
pkgname=sigil
_pkgname=Sigil
pkgver=2.3.1
_pagever=2.3.1
pkgrel=1
pkgdesc="A multi-platform WYSIWYG ebook editor. It is designed to edit books in ePub format. "
arch=('x86_64')
url="https://github.com/Sigil-Ebook/Sigil"
license=('GPL3')
depends=('qt6-webengine' 'qt6-svg' 'qt6-webchannel' 'qt6-5compat' 'python3-lxml'
         'python3-pillow' 'python3-six' 'pyqt6-python3' 'hunspell' 'pcre2')
makedepends=('cmake' 'qt6-tools' 'clang')
#options=('debug')
groups=('office')
source=("Sigil-${pkgver}.tar.gz::https://github.com/Sigil-Ebook/Sigil/archive/${pkgver}.tar.gz"
        "PageEdit-${_pagever}.tar.gz::https://github.com/Sigil-Ebook/PageEdit/archive/${_pagever}.tar.gz")
#source=("https://github.com/Sigil-Ebook/Sigil/archive/${_commit}.zip")
sha256sums=('12e84d9a1fc41e60903557ea03801cd0004d8b75dfcebeb44c3a9b1fd8fa97ea'
            'c7a06ddfa52321d61c14a7fef8e4b5cd2b8c5839f76873a678c54b560e11ddb8')

prepare() {
  cd ${_pkgname}-${pkgver}

  # set external editor
  sed -i -e 's|QString new_xeditor_path = ""|QString new_xeditor_path = "/usr/bin/pageedit"|' src/Dialogs/PreferenceWidgets/GeneralSettingsWidget.cpp
}

build() {
  cd ${_pkgname}-${pkgver}
  mkdir -p build 
  cd build

  export CC=/usr/bin/clang
  export CXX=/usr/bin/clang++

  cmake -G "Unix Makefiles" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DUSE_SYSTEM_LIBS=1 \
    -DINSTALL_HICOLOR_ICONS=1 \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_SKIP_RPATH=ON ..
  make

  cd ../..
  cd PageEdit-${_pagever}
  mkdir -p build
  cd build

  cmake -G "Unix Makefiles" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DINSTALL_BUNDLED_DICTS=0 \
    -DCMAKE_SKIP_RPATH=ON ..
  make
}

package() {
  cd ${_pkgname}-${pkgver}/build
  make install DESTDIR=${pkgdir}

  cd ../..

  cd PageEdit-${_pagever}/build
  make install DESTDIR=${pkgdir}
}
