
pkgname=sigil
_pkgname=Sigil
pkgver=2.0.1
_pagever=2.0.0
pkgrel=1
pkgdesc="A multi-platform WYSIWYG ebook editor. It is designed to edit books in ePub format. "
arch=('x86_64')
url="https://github.com/Sigil-Ebook/Sigil"
license=('GPL3')
depends=('qtwebengine' 'qt5-svg' 'qt5-webchannel' 'qt5-xmlpatterns' 'boost-libs' 'xerces-c' 'python3-lxml'
         'python3-pillow' 'python3-six' 'pyqt5-python3' 'hunspell' 'pcre2')
makedepends=('cmake' 'boost' 'qt5-tools' 'clang')
#options=('debug')
groups=('office')
source=("Sigil-${pkgver}.tar.gz::https://github.com/Sigil-Ebook/Sigil/archive/${pkgver}.tar.gz"
        "PageEdit-${_pagever}.tar.gz::https://github.com/Sigil-Ebook/PageEdit/archive/${_pagever}.tar.gz")
#source=("https://github.com/Sigil-Ebook/Sigil/archive/${_commit}.zip")
sha256sums=('af6da67d939276fb6d8dff4da2926d265565fab58d870f66b3b2e8b3125bd0a8'
            'eafc513eff3bdbaa4d3831f26c7d6aa73134f4a5e9f75c11c9cb45be40eb9858')

prepare() {
  cd ${_pkgname}-${pkgver}

  # set external editor
  sed -i -e 's|QString new_xeditor_path = ""|QString new_xeditor_path = "/usr/bin/pageedit"|' src/Dialogs/PreferenceWidgets/GeneralSettingsWidget.cpp
}

build() {
  cd ${_pkgname}-${pkgver}
  mkdir -p build 
  cd build

  export CC=/usr/bin/clang
  export CXX=/usr/bin/clang++

  cmake -G "Unix Makefiles" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DUSE_SYSTEM_LIBS=1 \
    -DINSTALL_HICOLOR_ICONS=1 \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_SKIP_RPATH=ON ..
  make

  cd ../..
  cd PageEdit-${_pagever}
  mkdir -p build
  cd build

  cmake -G "Unix Makefiles" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_SKIP_RPATH=ON ..
  make
}

package() {
  cd ${_pkgname}-${pkgver}/build
  make install DESTDIR=${pkgdir}

  cd ../..

  cd PageEdit-${_pagever}/build
  make install DESTDIR=${pkgdir}
}
