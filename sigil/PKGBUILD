
pkgname=sigil
_pkgname=Sigil
pkgver=1.6.0
_pagever=1.6.0
pkgrel=1
pkgdesc="A multi-platform WYSIWYG ebook editor. It is designed to edit books in ePub format. "
arch=('x86_64')
url="https://github.com/Sigil-Ebook/Sigil"
license=('GPL3')
depends=('qtwebengine' 'qt5-svg' 'qt5-webchannel' 'qt5-xmlpatterns' 'boost-libs' 'xerces-c' 'python3-lxml'
         'python3-pillow' 'python3-six' 'pyqt5-python3' 'hunspell')
makedepends=('cmake' 'boost' 'qt5-tools' 'clang')
#options=('debug')
groups=('office')
source=("Sigil-${pkgver}.tar.gz::https://github.com/Sigil-Ebook/Sigil/archive/${pkgver}.tar.gz"
        "PageEdit-${_pagever}.tar.gz::https://github.com/Sigil-Ebook/PageEdit/archive/${_pagever}.tar.gz")
#source=("https://github.com/Sigil-Ebook/Sigil/archive/${_commit}.zip")
sha256sums=('ea0e41a56fa98d54c61e2148c6d4b152bd50d8faa59a95c148c8b32b058dacda'
            '6ab8da064faecb43a4226e4c9f917454b6a511e2a911d5c4fdc540ad3a1b9366')

prepare() {
  cd ${_pkgname}-${pkgver}

  # set external editor
  sed -i -e 's|QString new_xeditor_path = ""|QString new_xeditor_path = "/usr/bin/pageedit"|' src/Dialogs/PreferenceWidgets/GeneralSettingsWidget.cpp
}

build() {
  cd ${_pkgname}-${pkgver}
  mkdir -p build 
  cd build
  
  export CC=/usr/bin/clang
  export CXX=/usr/bin/clang++

  cmake -G "Unix Makefiles" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DUSE_SYSTEM_LIBS=1 \
    -DINSTALL_HICOLOR_ICONS=1 \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_SKIP_RPATH=ON ..
  make
  
  cd ../..
  cd PageEdit-${_pagever}
  mkdir -p build
  cd build
  
  cmake -G "Unix Makefiles" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_SKIP_RPATH=ON ..
  make
}

package() {
  cd ${_pkgname}-${pkgver}/build
  make install DESTDIR=${pkgdir}

  cd ../..
  
  cd PageEdit-${_pagever}/build
  make install DESTDIR=${pkgdir}
}
