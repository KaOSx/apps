
# build version at https://dolphin-emu.org/download/ dev version
pkgname=dolphin-emu
pkgver=5.0.16380
pkgrel=2
_commit=8335ec70e5fe253eb21509408ca6b5736ed57dfc
pkgdesc='GameCube, Wii and Triforce emulator'
arch=('x86_64')
url='https://dolphin-emu.org'
license=('GPL2')
depends=('bluez' 'ffmpeg' 'libao' 'mbedtls' 'miniupnpc' 'openal' 'portaudio'
         'libevdev' 'soundtouch' 'qt5-base' 'pulseaudio' 'vulkan-loader'
         'pugixml' 'fmt')
makedepends=('cmake')
groups=('games')
source=("https://github.com/dolphin-emu/dolphin/archive/${_commit}.zip")
#source=("https://github.com/dolphin-emu/dolphin/archive/${pkgver}.tar.gz")
md5sums=('c22ff892d7d5bbdee1d3f48745f58083')

build() {
  cmake -B build -S dolphin-${_commit} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DENABLE_QT=ON \
    -DUSE_MGBA=OFF \
    -DDISTRIBUTOR=KaOS
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build

  install -D -m644 dolphin-${_commit}/Data/51-usb-device.rules -t ${pkgdir}/usr/lib/udev/rules.d/
}


