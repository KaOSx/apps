
# build version at https://dolphin-emu.org/download/ dev version
pkgname=dolphin-emu
pkgver=5.0.18498
pkgrel=4
_commit=46b99671d9158e0ca840c1d8ef249db0f321ced7
pkgdesc='GameCube, Wii and Triforce emulator'
arch=('x86_64')
url='https://dolphin-emu.org'
license=('GPL2')
depends=('bluez' 'ffmpeg' 'libao' 'miniupnpc' 'openal' 'portaudio'
         'libevdev' 'soundtouch' 'qt6-base' 'pulseaudio' 'vulkan-loader'
         'pugixml' 'fmt' 'cubeb')
makedepends=('cmake')
groups=('games')
#source=("https://github.com/dolphin-emu/dolphin/archive/${_commit}.zip"
#        "https://github.com/dolphin-emu/dolphin/commit/66f330e57316257fe81b46f57dad22ea6dee7bae.diff")
source=("git+https://github.com/dolphin-emu/dolphin.git#commit=${_commit}")
md5sums=('SKIP')

prepare() {
  cd dolphin

  git submodule update --init
  #patch -p1 -i ${srcdir}/66f330e57316257fe81b46f57dad22ea6dee7bae.diff
}

build() {
  cmake -B build -S dolphin \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DENABLE_QT=ON \
    -DUSE_MGBA=OFF \
    -DDISTRIBUTOR=KaOS
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build

  install -D -m644 dolphin/Data/51-usb-device.rules -t ${pkgdir}/usr/lib/udev/rules.d/
}


