
# build version at https://dolphin-emu.org/download/ dev version
pkgname=dolphin-emu
pkgver=5.0.16364
pkgrel=1
_commit=dcf8bd7733ae6393b879f4795b46104d67bba651
pkgdesc='GameCube, Wii and Triforce emulator'
arch=('x86_64')
url='https://dolphin-emu.org'
license=('GPL2')
depends=('bluez' 'ffmpeg' 'libao' 'mbedtls' 'miniupnpc' 'openal' 'portaudio'
         'libevdev' 'soundtouch' 'qt5-base' 'pulseaudio' 'vulkan-loader'
         'pugixml' 'fmt')
makedepends=('cmake')
source=("https://github.com/dolphin-emu/dolphin/archive/${_commit}.zip")
#source=("https://github.com/dolphin-emu/dolphin/archive/${pkgver}.tar.gz")
md5sums=('5849d41d4c1b85d9fd6338e4ccb694b0')

build() {
  cmake -B build -S dolphin-${_commit} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DENABLE_QT=ON \
    -DUSE_MGBA=OFF \
    -DDISTRIBUTOR=KaOS
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build

  install -D -m644 dolphin-${_commit}/Data/51-usb-device.rules -t ${pkgdir}/usr/lib/udev/rules.d/
}


