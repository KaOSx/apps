
# build version at https://dolphin-emu.org/download/ dev version
pkgname=dolphin-emu
pkgver=5.0.13614
pkgrel=1
_commit=3e4bf57c696ed1e4f465075ae311653de2cd33b0
pkgdesc='GameCube, Wii and Triforce emulator'
arch=('x86_64')
url='https://dolphin-emu.org'
license=('GPL2')
depends=('bluez' 'ffmpeg' 'libao' 'mbedtls' 'miniupnpc' 'openal' 'portaudio'
         'libevdev' 'soundtouch' 'qt5-base' 'pulseaudio' 'vulkan-loader')
makedepends=('cmake')
source=("https://github.com/dolphin-emu/dolphin/archive/${_commit}.zip")
#source=("https://github.com/dolphin-emu/dolphin/archive/${pkgver}.tar.gz")
md5sums=('cdc8e5b3b0ec5360b4857e9eb9d2548d')
         
build() {
  mkdir -p build 
  cd build
  
  cmake ../dolphin-${_commit}\
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DENABLE_QT2=ON \
    -DENABLE_WX=OFF \
    -DUSE_EGL=ON \
    -DDISTRIBUTOR=KaOS
  make
}

package() {
  cd build

  make DESTDIR=${pkgdir} install
  
  install -D -m644 ../dolphin-${_commit}/Data/51-usb-device.rules -t ${pkgdir}/usr/lib/udev/rules.d/
}


