
pkgname=tor
pkgver=0.4.7.8
pkgrel=1
pkgdesc='Open network that helps you defend against traffic analysis, a form of network surveillance that threatens personal freedom and privacy.'
arch=('x86_64')
url='https://www.torproject.org/'
license=('BSD')
depends=('openssl' 'libevent' 'bash' 'libseccomp' 'zstd')
makedepends=('ca-certificates')
backup=('etc/tor/torrc'
        'etc/tor/torrc-dist')
install='tor.install'
source=("https://dist.torproject.org/${pkgname}-${pkgver}.tar.gz"
        'torrc'
        'tor.service')
sha256sums=('9e9a5c67ad2acdd5f0f8be14ed591fed076b1708abf8344066990a0fa66fe195'
            '2bf76f575e73fedd148c7dd17981693d7585a4ea16650fece526e9bfbdd8e3ef'
            '5a5395106872579473222f678759746784ae9763e5ba337472e8b0fdd9971e82')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install

  install -dm0700 -o 43 -g 43 ${pkgdir}/var/lib/tor

  mv ${pkgdir}/etc/tor/torrc.sample ${pkgdir}/etc/tor/torrc-dist
  install -Dm0644 ${srcdir}/torrc ${pkgdir}/etc/tor/torrc
  install -Dm0644 ${srcdir}/tor.service ${pkgdir}/usr/lib/systemd/system/tor.service

  install -Dm0644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
