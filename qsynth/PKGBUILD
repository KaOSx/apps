
pkgname=qsynth
pkgver=1.0.1
pkgrel=1
pkgdesc="Qt GUI for FluidSynth"
arch=('x86_64')
url="https://qsynth.sourceforge.io/"
license=('GPL')
depends=('fluidsynth' 'qt6-svg')
makedepends=('qt6-tools')
source=("https://www.rncbc.org/archive/${pkgname}-${pkgver}.tar.gz")
md5sums=('f215c338e5d1f76c794c5d736f73ede1')

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCONFIG_XUNIQUE=ON \
    -DCONFIG_QT6=ON
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
