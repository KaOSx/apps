
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=kinfocenter
pkgver=${_plasmaver}
pkgrel=1
pkgdesc='KInfocenter'
arch=('x86_64')
url='https://github.com/KDE/kinfocenter'
license=('LGPL')
depends=('glu' 'kcompletion' 'kconfig' 'kconfigwidgets' 'kcoreaddons' 'kcrash' 'kdbusaddons' 'ki18n' 'kiconthemes'
         'kcmutils' 'kio' 'kservice' 'solid' 'kwidgetsaddons' 'kxmlgui' 'kdeclarative' 'kpackage' 'libraw1394'
         'pciutils' 'kwayland')
makedepends=('extra-cmake-modules' 'kdoctools')
groups=('kdeplasma5')
conflicts=('kde-workspace' 'about-distro')
replaces=('about-distro')
source=("https://download.kde.org/stable/plasma/${pkgver}/${pkgname}-${pkgver}.tar.xz"
#source=("${pkgname}-${pkgver}.tar.gz::https://github.com/KDE/${pkgname}/archive/${_branch}.tar.gz"
        'kcm-about-distrorc'
        'KaOS.png')
md5sums=(`grep ${pkgname}-${_plasmaver}.tar.xz ../kf5.md5 | cut -d" " -f1`
         '0ba2a97277ee242f17fae5b513b335b1'
         '0957d15eee83c30c1351571d3b875bc9')


build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
  
  install -Dm644 ${srcdir}/kcm-about-distrorc ${pkgdir}/etc/xdg/kcm-about-distrorc
  install -Dm644 ${srcdir}/KaOS.png ${pkgdir}/usr/share/${pkgname}/KaOS.png
}

