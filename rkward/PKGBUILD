
pkgname=rkward
pkgver=0.7.1.1
_pkgver=0.7.1b
pkgrel=1
pkgdesc="Provides an easily extensible, easy to use IDE/GUI for R."
arch=('x86_64')
url="https://rkward.kde.org/"
license=('GPL')
depends=('kcoreaddons' 'ki18n' 'kxmlgui' 'ktexteditor' 'kwidgetsaddons' 'kdewebkit' 'kparts' 'knotifications'
         'kwindowsystem' 'kcrash' 'qtwebkit-tp' 'qt5-script' 'r')
makedepends=('extra-cmake-modules' 'kdoctools' 'icu')
groups=('science')
source=("https://download.kde.org/stable/rkward/${_pkgver}/src/rkward-${_pkgver}.tar.gz")
sha256sums=('0cca23b106f569549a005110eeecb2557805049e6659c3a2ebd5eb18e392ac76')

prepare() {
  cd ${pkgname}-${_pkgver}
  
  #sed -i -e 's|V4.2-Based Variant V1.1//EN" "dtd/kdex.dtd|V4.5-Based Variant V1.1//EN" "dtd/kdex45.dtd|' doc/rkwardplugins/index.docbook
  
  #scripts/import_translations.py es fr de nl
}

build() { 
  mkdir -p build
  cd build

  cmake ../${pkgname}-${_pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBEXECDIR=lib \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DR_LIBDIR="/usr/lib/R/library" \
    -DNO_R_XML=1 \
    -DBUILD_TESTING=OFF 
  make
}

package() {
  cd build

  make DESTDIR=${pkgdir} install
}
