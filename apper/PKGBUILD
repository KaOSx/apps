#
# KDE SC Packages for Chakra, part of chakra-project.org
#
# maintainer: Giuseppe Calà <jiveaxe@gmail.com>
 
pkgname=apper
pkgver=0.7.2
pkgrel=1
pkgdesc="KDE tools for PackageKit"
arch=('i686' 'x86_64')
url="http://kde-apps.org/content/show.php/Apper?content=84745"
license=('GPL')
depends=('kde-workspace' 'packagekit-qt2>=0.6.11')
makedepends=('cmake' 'automoc4' 'docbook-xml' 'docbook-xsl')
provides=('kpackagekit' 'appset-qt')
conflicts=('kpackagekit' 'appset-qt')
replaces=('kpackagekit' 'appset-qt')
categories=('system')
install='apper.install'
source=("http://download.kde.org/stable/${pkgname}/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2"
        "remove-unavailable-groups.patch"
        "hide-showOriginCB.patch")
sha256sums=('975fede728e7ab96d8e244ae721a2e15ae40b9fb1cd189a1f4afd46c400b219f'
            '414013e596e6bac17d1d08afb56f473c9a8a8b714efbf644d0dd70575e9c3a1f'
            'eb77e20e5ca52d03aef83889def774e370a192b20e82b0d6497dfcba1b61b00e')
screenshot=('http://kde-apps.org/CONTENT/content-pre1/84745-1.png')
 
build() {
    cd ${pkgname}-${pkgver}
 
    # Remove unavailable groups
    patch -Np1 -i ../remove-unavailable-groups.patch
    
    # Hide showOriginCB
    patch -Np1 -i ../hide-showOriginCB.patch
 
    [ -d "build" ] && rm -rf build
    mkdir build
    cd build
 
    cmake ../ -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr
    make
}
 
package() {
    cd ${pkgname}-${pkgver}
    cd build
    make DESTDIR=${pkgdir} install
}
