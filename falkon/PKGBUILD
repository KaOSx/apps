
pkgname=falkon
pkgver=3.1.0
_pkgver=3.1
pkgrel=2
arch=('x86_64')
pkgdesc="Open source browser based on QtWebengine, written in Qt Framework"
url="https://www.falkon.org/"
license=('GPL3')
depends=('qtwebengine' 'qt5-webchannel' 'qt5-x11extras' 'ki18n' 'kwallet' 'kio' 'kcrash' 'kcoreaddons' 'purpose')
makedepends=('extra-cmake-modules' 'qt5-tools')
groups=('network-web')
conflicts=('qupzilla')
replaces=('qupzilla')
source=("https://download.kde.org/stable/falkon/${_pkgver}/falkon-${pkgver}.tar.xz"
        'speeddial.diff'
        'preferences.diff'
        'browserwindow.diff'
        'bookmarks_json.diff'
        "https://github.com/KDE/falkon/commit/7308b64df6a13d5822f491f2cbc7f39ae6781488.diff"
        "https://invent.kde.org/network/falkon/-/commit/307547749291948c97b459db2c8a0f51a44a4e20.diff"
        "https://invent.kde.org/network/falkon/-/commit/0e25b116cb7d0c12871ae82331b3477b97f23549.diff"
        "https://invent.kde.org/network/falkon/-/commit/f7b372098ed687e9de123c3f6c3c59b066ea6c9d.diff"
        "https://invent.kde.org/network/falkon/-/commit/5eedcb5b44da4895cf5a729d3e0a0dc2f5b45394.diff"
        "https://invent.kde.org/network/falkon/-/commit/3070f7b0325a87fb71cc4d649c1102ebe7046d9a.diff")
sha256sums=('ce743cd80c0e2d525a784e29c9b487f73480119b0567f9ce8ef1f44cca527587'
            '55600254a8a03579b74f0ddc2b3bb1e8a0151e9bc04599c39c84849ce71f238e'
            'caf3c9405c03775febd9c2f137004602a261fd0bb3f4ad4d81889959d4f0ae80'
            'ef2d92476041d1ec9f5c00326c325ade6451aee835945581f905945d77ba1a2c'
            'd8b9ac6ba4303869087e4b1984c375c6c7393afa0d218ba88bbb5dfd1a144298'
            '90ea083c2b0734da9d2490a9552c7ce0a45589013cb4c97c25c49fa51a0cb894'
            '28e609369320501cf08ffea1ed52a687282e0ca61547731710ef35f2f5d717d8'
            '7db0a77c13c740f41e21dbbf6d91307e82e762c2a6cde2a2be079153f99d2fcc'
            'e430adff89864686fd977ea28ad939d1fed9edc20666bd8b10a22183477cfbca'
            '1c7273465c52b2c161b1c3c3f127302aa9cc2eb5667e1d6124dcb12ac4756989'
            'a5178fb1741e43155bb6a14fba76a785078319939e52061156407c763c738107')

prepare() {
  cd ${pkgname}-${pkgver}
  
  patch -R -p1 -i ${srcdir}/7308b64df6a13d5822f491f2cbc7f39ae6781488.diff
  patch -p1 -i ${srcdir}/speeddial.diff
  patch -p0 -i ${srcdir}/preferences.diff
  patch -p0 -i ${srcdir}/browserwindow.diff
  # bug fixes & enhancements not released since 2019
  patch -p1 -i ${srcdir}/307547749291948c97b459db2c8a0f51a44a4e20.diff
  patch -p1 -i ${srcdir}/0e25b116cb7d0c12871ae82331b3477b97f23549.diff
  #patch -p1 -i ${srcdir}/f7b372098ed687e9de123c3f6c3c59b066ea6c9d.diff
  # Qt 5.14 + build fixes
  patch -p1 -i ${srcdir}/5eedcb5b44da4895cf5a729d3e0a0dc2f5b45394.diff
  patch -p1 -i ${srcdir}/3070f7b0325a87fb71cc4d649c1102ebe7046d9a.diff
}

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE_INSTALL_LIBDIR=lib \
    -DENABLE_KDE_FRAMEWORKS_INTEGRATION_PLUGIN=TRUE
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build

  install -Dm644 ${pkgname}-${pkgver}/linux/completion/_falkon ${pkgdir}/usr/share/zsh/site-functions/_falkon
}

