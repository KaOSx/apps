
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=falkon
pkgver=${_kdever}
pkgrel=1
arch=('x86_64')
pkgdesc="Open source browser based on QtWebengine, written in Qt Framework"
url="https://www.falkon.org/"
license=('GPL3')
depends=('qtwebengine' 'qt5-webchannel' 'qt5-x11extras' 'ki18n' 'kwallet' 'kio' 'kcrash' 'kcoreaddons' 'purpose')
makedepends=('extra-cmake-modules' 'qt5-tools')
groups=('network-web')
conflicts=('qupzilla')
replaces=('qupzilla')
source=("${_mirror}/${pkgname}-${_kdever}.tar.xz"
        'speeddial.diff'
        'preferences.diff'
        'browserwindow.diff'
        'bookmarks_json.diff'
        'revert.diff') #taken from commit https://github.com/KDE/falkon/commit/7308b64df6a13d5822f491f2cbc7f39ae6781488
md5sums=(`grep ${pkgname}-${_kdever}.tar.xz ../kde-sc.md5 | cut -d" " -f1`
         '81118fed4d4bb3f4ce2b2757afb19c1e'
         '2addb3a5e484bcfbb19f702d2e86d2fd'
         'cafc9d1284e700ad615265bf39a2bafb'
         'ac21fc6a8c6d85eefcbbb7e7ad93ca1c'
         'e1b0fbbc51de79774757b7b60a482ebe')

prepare() {
  cd ${pkgname}-${pkgver}

  #patch -R -p1 -i ${srcdir}/revert.diff
  patch -p1 -i ${srcdir}/speeddial.diff
  patch -p0 -i ${srcdir}/preferences.diff
  patch -p0 -i ${srcdir}/browserwindow.diff
}

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE_INSTALL_LIBDIR=lib \
    -DENABLE_KDE_FRAMEWORKS_INTEGRATION_PLUGIN=TRUE
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build

  install -Dm644 ${pkgname}-${pkgver}/linux/completion/_falkon ${pkgdir}/usr/share/zsh/site-functions/_falkon
}

