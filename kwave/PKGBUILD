

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=kwave
pkgver=${_kdever}
_commit=0d65624a1eb349ac146f1aee6428de7c7a95fcb9
pkgrel=1
pkgdesc="Sound editor designed for the Plasma Desktop Environment"
arch=('x86_64')
url="https://apps.kde.org/kwave/"
license=('GPL')
depends=('karchive6' 'kcompletion6' 'kconfig6' 'kconfigwidgets6' 'kcoreaddons6' 'kcrash6' 'kdbusaddons6' 
         'ki18n6' 'kiconthemes6' 'kio6' 'ktextwidgets6' 'kxmlgui6' 'kwidgetsaddons6'
         'pulseaudio' 'audiofile' 'flac' 'fftw' 'libvorbis' 'libsamplerate' 'opus')
         # kf6 port not ready yet for 'qt6-multimedia' for mp3 support: 'libmad' 'id3lib'
makedepends=('extra-cmake-modules' 'kdoctools6')
options=('!libtool')
#source=("$_mirror/${pkgname}-${_kdever}.tar.xz")
#md5sums=(`grep ${pkgname}-${_kdever}.tar.xz ../kde-sc.md5 | cut -d" " -f1`)
source=("https://invent.kde.org/multimedia/kwave/-/archive/${_commit}/kwave-${_commit}.tar.bz2")
md5sums=('151f12de3e571a0d5831638df6858a5c')

build() {
  cmake -B build -S ${pkgname}-${_commit} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=/usr/lib \
    -DCMAKE_BUILD_TYPE=Release \
    -DWITH_QT_AUDIO=OFF \
    -DWITH_MP3=OFF  # id3lib not in repo, upstream works on taglib
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
