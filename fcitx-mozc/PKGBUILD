
pkgname=fcitx-mozc
_bldtype=Release
_zipcode_rel=201810
_protobuf_rev=92a7e778e7394386f413cec28d67a07630f784b1
_gyp_rev=4ec6c4e3a94bd04a6da2858163d40b2429b8aad1
_japanese_usage_dictionary_rev=a4a66772e33746b91e99caceecced9a28507e925
_mozc_rev=afb03ddfe72dde4cf2409863a3bfea160f7a66d8
_fcitx_patchver=2.23.2815.102.1
pkgdesc="Japanese Input Method Editor designed for multi-platform"
pkgver=2.23.2815.102
pkgrel=1
arch=('x86_64')
url="https://github.com/google/mozc"
license=('custom')
depends=('qt5-base' 'fcitx' 'zinnia')
makedepends=('pkg-config' 'python2' 'curl' 'gtk2' 'mesa' 'ninja' 'git' 'clang')
source=("mozc::git+https://github.com/google/mozc.git#commit=${_mozc_rev}"
        "japanese_usage_dictionary::git+https://github.com/hiroyuki-komatsu/japanese-usage-dictionary.git#commit=${_japanese_usage_dictionary_rev}"
        "gyp::git+https://chromium.googlesource.com/external/gyp#commit=${_gyp_rev}"
        "protobuf::git+https://github.com/google/protobuf.git#commit=${_protobuf_rev}"
        "https://downloads.sourceforge.net/pnsft-aur/x-ken-all-${_zipcode_rel}.zip"
        "https://downloads.sourceforge.net/pnsft-aur/jigyosyo-${_zipcode_rel}.zip"
        "https://download.fcitx-im.org/fcitx-mozc/fcitx-mozc-${_fcitx_patchver}.patch"
        "https://download.fcitx-im.org/fcitx-mozc/fcitx-mozc-icon.tar.gz"
        "https://github.com/google/mozc/pull/444/commits/82d38f929882a9c62289b179c6fe41efed249987.diff")
md5sums=('SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'bba32a6cb14120c11f6eec6edcd0dd71'
         'd188bd7a873e851ed8f1f1466f1091a5'
         '8ec243464ddb9ecf48924708c0448284'
         '80fe2ef127b70649df22d133822ac8a7'
         'b94e9049789284c00de443ae9ef3baa1')

prepare() {
  cd mozc/src

  rm unix/fcitx -rf
  patch -p2 -i ${srcdir}/fcitx-mozc-${_fcitx_patchver}.patch
  rm unix/fcitx5 -rf
  
  find . -name  \*.py        -type f -exec sed -i -e "1s|python.*$|python2|"  {} +
  find . -regex '.*\.gypi?$' -type f -exec sed -i -e "s|'python'|'python2'|g" {} +
  
  # GCC > 8 needed
  patch -p1 -i ${srcdir}/82d38f929882a9c62289b179c6fe41efed249987.diff

  # disable Japanese zip code in dictionary, python import fails to find dictionary
  #python2 dictionary/gen_zip_code_seed.py --zip_code="${srcdir}/x-ken-all.csv" --jigyosyo="${srcdir}/JIGYOSYO.CSV" >> data/dictionary_oss/dictionary09.txt

  cd ${srcdir}
  for dep in gyp protobuf japanese_usage_dictionary
  do
    cp -a $dep mozc/src/third_party/
  done
}

build() {
  cd mozc/src

  _targets="server/server.gyp:mozc_server gui/gui.gyp:mozc_tool unix/fcitx/fcitx.gyp:fcitx-mozc"

  QTDIR=/usr GYP_DEFINES="document_dir=/usr/share/licenses/$pkgname use_libzinnia=1" python2 build_mozc.py gyp
  python2 build_mozc.py build -c $_bldtype $_targets

  head -n 29 server/mozc_server.cc > LICENSE
}

package() {
  cd mozc/src
  install -D -m 755 out_linux/${_bldtype}/mozc_server ${pkgdir}/usr/lib/mozc/mozc_server
  install    -m 755 out_linux/${_bldtype}/mozc_tool   ${pkgdir}/usr/lib/mozc/mozc_tool

  install -d ${pkgdir}/usr/share/licenses/$pkgname/
  install -m 644 LICENSE data/installer/*.html ${pkgdir}/usr/share/licenses/${pkgname}/

  for mofile in out_linux/${_bldtype}/gen/unix/fcitx/po/*.mo
  do
    filename=`basename $mofile`
    lang=${filename/.mo/}
    install -D -m 644 $mofile ${pkgdir}/usr/share/locale/$lang/LC_MESSAGES/fcitx-mozc.mo
  done

  install -D -m 755 out_linux/${_bldtype}/fcitx-mozc.so ${pkgdir}/usr/lib/fcitx/fcitx-mozc.so
  install -D -m 644 unix/fcitx/fcitx-mozc.conf ${pkgdir}/usr/share/fcitx/addon/fcitx-mozc.conf
  install -D -m 644 unix/fcitx/mozc.conf ${pkgdir}/usr/share/fcitx/inputmethod/mozc.conf

  install -d ${pkgdir}/usr/share/fcitx/mozc/icon
  install -m 644 $srcdir/fcitx-mozc-icons/mozc.png ${pkgdir}/usr/share/fcitx/mozc/icon/mozc.png
  install -m 644 $srcdir/fcitx-mozc-icons/mozc-alpha_full.png ${pkgdir}/usr/share/fcitx/mozc/icon/mozc-alpha_full.png
  install -m 644 $srcdir/fcitx-mozc-icons/mozc-alpha_half.png ${pkgdir}/usr/share/fcitx/mozc/icon/mozc-alpha_half.png
  install -m 644 $srcdir/fcitx-mozc-icons/mozc-direct.png ${pkgdir}/usr/share/fcitx/mozc/icon/mozc-direct.png
  install -m 644 $srcdir/fcitx-mozc-icons/mozc-hiragana.png ${pkgdir}/usr/share/fcitx/mozc/icon/mozc-hiragana.png
  install -m 644 $srcdir/fcitx-mozc-icons/mozc-katakana_full.png ${pkgdir}/usr/share/fcitx/mozc/icon/mozc-katakana_full.png
  install -m 644 $srcdir/fcitx-mozc-icons/mozc-katakana_half.png ${pkgdir}/usr/share/fcitx/mozc/icon/mozc-katakana_half.png
  install -m 644 $srcdir/fcitx-mozc-icons/mozc-dictionary.png ${pkgdir}/usr/share/fcitx/mozc/icon/mozc-dictionary.png
  install -m 644 $srcdir/fcitx-mozc-icons/mozc-properties.png ${pkgdir}/usr/share/fcitx/mozc/icon/mozc-properties.png
  install -m 644 $srcdir/fcitx-mozc-icons/mozc-tool.png ${pkgdir}/usr/share/fcitx/mozc/icon/mozc-tool.png
}

