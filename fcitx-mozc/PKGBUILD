
# version at https://github.com/fcitx/mozc/blob/fcitx/src/data/version/mozc_version_template.bzl

pkgname=fcitx-mozc
_bldtype=Release
_zipcode_rel=202011
_protobuf_rev=fde7cf7
_gyp_rev=caa6002
_japanese_usage_dictionary_rev=e5b3425
_mozc_rev=1882e33b61673b66d63277f82b4c80ae4e506c10
_abseil_rev=0f3bb46
_fcitx_patchver=2.26.4220.100
pkgdesc="Japanese Input Method Editor designed for multi-platform"
pkgver=2.26.4220.100
pkgrel=1
arch=('x86_64')
url="https://github.com/google/mozc"
license=('custom')
depends=('qt5-base' 'fcitx' 'zinnia')
makedepends=('pkg-config' 'python3-six' 'curl' 'gtk2' 'mesa' 'ninja' 'git' 'clang' 'protobuf')
source=("mozc::git+https://github.com/fcitx/mozc.git#commit=${_mozc_rev}"
        "japanese_usage_dictionary::git+https://github.com/hiroyuki-komatsu/japanese-usage-dictionary.git#commit=${_japanese_usage_dictionary_rev}"
        "gyp::git+https://chromium.googlesource.com/external/gyp#commit=${_gyp_rev}"
        "protobuf::git+https://github.com/google/protobuf.git#commit=${_protobuf_rev}"
        "abseil-cpp::git+https://github.com/abseil/abseil-cpp.git#commit=${_abseil_rev}"
        "https://osdn.net/projects/ponsfoot-aur/storage/mozc/jigyosyo-${_zipcode_rel}.zip"
        "https://osdn.net/projects/ponsfoot-aur/storage/mozc/x-ken-all-${_zipcode_rel}.zip"
        #"https://download.fcitx-im.org/fcitx-mozc/fcitx-mozc-${_fcitx_patchver}.patch"
        "https://download.fcitx-im.org/fcitx-mozc/fcitx-mozc-icon.tar.gz"
        "https://github.com/google/mozc/pull/444/commits/82d38f929882a9c62289b179c6fe41efed249987.diff")
md5sums=('SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'c5f9c8f0149f5350cfd9dfcfa876aed4'
         '669ae868ea978ca06909a2d4562eaf0a'
         '80fe2ef127b70649df22d133822ac8a7'
         'b94e9049789284c00de443ae9ef3baa1')

prepare() {
  cd mozc/src

  #rm unix/fcitx -rf
  #patch -p2 -i ${srcdir}/fcitx-mozc-${_fcitx_patchver}.patch
  rm unix/fcitx5 -rf
  
  sed "/stdlib=libc++/d;/-lc++/d" -i gyp/common.gypi
  
  # GCC > 8 needed
  #patch -p1 -i ${srcdir}/82d38f929882a9c62289b179c6fe41efed249987.diff

  # disable Japanese zip code in dictionary, python import fails to find dictionary
  #python dictionary/gen_zip_code_seed.py --zip_code="${srcdir}/x-ken-all.csv" --jigyosyo="${srcdir}/JIGYOSYO.CSV" >> data/dictionary_oss/dictionary09.txt

  cd ${srcdir}
  for dep in gyp abseil-cpp japanese_usage_dictionary
  do
    cp -a $dep mozc/src/third_party/
  done
}

build() {
  cd mozc/src

  _targets="server/server.gyp:mozc_server gui/gui.gyp:mozc_tool unix/fcitx/fcitx.gyp:fcitx-mozc"

  QTDIR=/usr GYP_DEFINES="document_dir=/usr/share/licenses/$pkgname use_libzinnia=1 use_libprotobuf=1" python3 build_mozc.py gyp
  python3 build_mozc.py build -c $_bldtype $_targets

  head -n 29 server/mozc_server.cc > LICENSE
}

package() {
  cd mozc/src
  install -D -m 755 out_linux/${_bldtype}/mozc_server ${pkgdir}/usr/lib/mozc/mozc_server
  install    -m 755 out_linux/${_bldtype}/mozc_tool   ${pkgdir}/usr/lib/mozc/mozc_tool

  install -d ${pkgdir}/usr/share/licenses/$pkgname/
  install -m 644 LICENSE data/installer/*.html ${pkgdir}/usr/share/licenses/${pkgname}/

  for mofile in out_linux/${_bldtype}/gen/unix/fcitx/po/*.mo
  do
    filename=`basename $mofile`
    lang=${filename/.mo/}
    install -D -m 644 $mofile ${pkgdir}/usr/share/locale/$lang/LC_MESSAGES/fcitx-mozc.mo
  done

  install -D -m 755 out_linux/${_bldtype}/fcitx-mozc.so ${pkgdir}/usr/lib/fcitx/fcitx-mozc.so
  install -D -m 644 unix/fcitx/fcitx-mozc.conf ${pkgdir}/usr/share/fcitx/addon/fcitx-mozc.conf
  install -D -m 644 unix/fcitx/mozc.conf ${pkgdir}/usr/share/fcitx/inputmethod/mozc.conf

  install -d ${pkgdir}/usr/share/fcitx/mozc/icon
  install -m 644 $srcdir/fcitx-mozc-icons/mozc.png ${pkgdir}/usr/share/fcitx/mozc/icon/mozc.png
  install -m 644 $srcdir/fcitx-mozc-icons/mozc-alpha_full.png ${pkgdir}/usr/share/fcitx/mozc/icon/mozc-alpha_full.png
  install -m 644 $srcdir/fcitx-mozc-icons/mozc-alpha_half.png ${pkgdir}/usr/share/fcitx/mozc/icon/mozc-alpha_half.png
  install -m 644 $srcdir/fcitx-mozc-icons/mozc-direct.png ${pkgdir}/usr/share/fcitx/mozc/icon/mozc-direct.png
  install -m 644 $srcdir/fcitx-mozc-icons/mozc-hiragana.png ${pkgdir}/usr/share/fcitx/mozc/icon/mozc-hiragana.png
  install -m 644 $srcdir/fcitx-mozc-icons/mozc-katakana_full.png ${pkgdir}/usr/share/fcitx/mozc/icon/mozc-katakana_full.png
  install -m 644 $srcdir/fcitx-mozc-icons/mozc-katakana_half.png ${pkgdir}/usr/share/fcitx/mozc/icon/mozc-katakana_half.png
  install -m 644 $srcdir/fcitx-mozc-icons/mozc-dictionary.png ${pkgdir}/usr/share/fcitx/mozc/icon/mozc-dictionary.png
  install -m 644 $srcdir/fcitx-mozc-icons/mozc-properties.png ${pkgdir}/usr/share/fcitx/mozc/icon/mozc-properties.png
  install -m 644 $srcdir/fcitx-mozc-icons/mozc-tool.png ${pkgdir}/usr/share/fcitx/mozc/icon/mozc-tool.png
}

