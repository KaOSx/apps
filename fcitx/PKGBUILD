
pkgname=fcitx
pkgver=4.2.9.6
pkgrel=2
pkgdesc="A Flexible Input Method Framework"
arch=('x86_64')
license=('GPL')
url="https://github.com/fcitx/"
depends=('pango' 'dbus' 'opencc' 'iso-codes' 'icu' 'libxkbfile' 'enchant' 'gettext'
         'shared-mime-info' 'hicolor-icon-theme' 'libxinerama' 'libxkbcommon')
makedepends=('extra-cmake-modules' 'doxygen' 'gobject-introspection')
groups=('localization')
install=fcitx.install
source=("https://download.fcitx-im.org/${pkgname}/${pkgname}-${pkgver}_dict.tar.xz")
md5sums=('29e18ea4144c4e059dc2c56fb62d55e5')

prepare() {
  cd  ${pkgname}-${pkgver}
  
  sed -i -e 's|PC_ENCHANT enchant|PC_ENCHANT enchant-2|' cmake/FindEnchant.cmake
}

build() { 
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver} \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_BUILD_TYPE=Release \
      -DSYSCONFDIR=/etc \
      -DENABLE_QT=Off \
      -DENABLE_GTK2_IM_MODULE=Off \
      -DENABLE_GTK3_IM_MODULE=Off \
      -DFORCE_ENCHANT=ON \
      -DENABLE_OPENCC=On \
      -DENABLE_GIR=Off
  make
}

package(){
    cd build
    
    make DESTDIR=${pkgdir} install
}

