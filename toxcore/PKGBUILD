
pkgname=toxcore
pkgver=0.2.17
pkgrel=1
pkgdesc='Easy to use, all-in-one communication platform that ensures full privacy and secure message delivery.'
arch=('x86_64')
url='https://tox.chat'
license=('GPL3')
depends=('systemd' 'libconfig' 'libsndfile' 'libsodium' 'libvpx' 'opus' 'opencv' 'portaudio'
         'msgpack-c')
makedepends=('check')
options=('!staticlibs')
source=("https://github.com/TokTok/c-toxcore/archive/v${pkgver}.tar.gz")
md5sums=('e22f6ce064d3c2b437a07a602ccfe224')

build() {
  cd c-${pkgname}-${pkgver}

  ./autogen.sh
  ./configure \
    --prefix=/usr \
    --enable-tests \
    --enable-daemon
  make
}

check() {
  cd c-${pkgname}-${pkgver}
  # tox_test 
  make check 
}

package() {
  cd c-${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install
}

