
# odd numbers are for development
pkgname=flatpak
pkgver=1.8.2
pkgrel=1
pkgdesc="System for building, distributing and running sandboxed desktop applications on Linux."
url="http://flatpak.org"
arch=('x86_64')
license=('GPL')
depends=('bubblewrap' 'elfutils' 'fuse' 'glib2' 'json-glib' 'libarchive' 'ostree'
         'polkit' 'xorg-xauth' 'python3' 'patch' 'systemd' 'appstream-glib')
makedepends=('intltool' 'xmlto' 'libdwarf' 'dconf' 'socat' 'python3-pyparsing')
install=flatpak.install
source=("https://github.com/flatpak/flatpak/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('7926625df7c2282a5ee1a8b3c317af53d40a663b1bc6b18a2dc8747e265085b0')

build() {
  cd ${pkgname}-${pkgver}

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib/${pkgname} \
    --disable-static \
    --disable-gtk-doc \
    --with-system-bubblewrap \
    --with-priv-mode=setuid \
    --with-dbus-config-dir=/usr/share/dbus-1/system.d \
    --with-system-install-dir=/var/lib/flatpak

  make
}

check() {
  cd ${pkgname}-${pkgver}
  # 4 docs tests fail
  make -k check || :
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install

  install -d -o root -g 102 -m 700 ${pkgdir}/usr/share/polkit-1/rules.d
}
