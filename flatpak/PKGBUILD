

# odd numbers are for development
pkgname=flatpak
pkgver=1.15.8
pkgrel=1
pkgdesc="System for building, distributing and running sandboxed desktop applications on Linux."
url="https://flatpak.org"
arch=('x86_64')
license=('GPL')
depends=('bubblewrap' 'dconf' 'elfutils' 'fuse3' 'glib2' 'json-glib' 'libarchive' 'ostree'
         'polkit' 'xorg-xauth' 'python3' 'patch' 'systemd' 'appstream' 'xdg-desktop-portal-kde'
         'libsodium')
makedepends=('intltool' 'xmlto' 'socat' 'python3-pyparsing' 'meson' 'ninja' 'python3-packaging')
optdepends=('xdg-desktop-portal-gtk: Integration for GTK based Flatpaks')
install=flatpak.install
source=("https://github.com/flatpak/flatpak/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('e89bcf42fd1eb0fadf14c8b5845bc31cb78a2624f3bdc9bcdd007cc75022e4d3')

build() {
  mkdir -p build
  cd build

  meson setup ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    --libexecdir=/usr/lib/${pkgname} \
    -Ddbus_config_dir=/usr/share/dbus-1/system.d \
    -Dsystem_bubblewrap=bwrap \
    -Dsystem_install_dir=/var/lib/flatpak \
    -Ddocbook_docs=disabled \
    -Dtests=false
  ninja
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
}
