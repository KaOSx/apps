pkgname=gmailfeed
pkgver=2.1
_commit=7e9f3455169e0800afb265e93406ab632683aff3
pkgrel=2
pkgdesc="Plasmoid that provides a list of unread emails from your Gmail inbox and notifies when you receive new messages"
arch=('x86_64')
url="https://github.com/anthon38/gmailfeed"
license=('GPL')
depends=('plasma-framework' 'hicolor-icon-theme' 'knotifications' 'kaccounts-integration')
makedepends=('extra-cmake-modules' 'intltool')
groups=('network-email')
source=("https://github.com/anthon38/gmailfeed/archive/${_commit}.zip")
#source=("https://github.com/Intika-KDE-Plasmoids/plasmoid-ultimate-gmail-feed/archive/v${pkgver}.tar.gz")
md5sums=('6b76b15bda6f5d59503a9d75292bd77a')

build() {
  cmake -B build -S gmailfeed-${_commit} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_LIBEXECDIR=lib
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
