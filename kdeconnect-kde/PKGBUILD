
pkgname=kdeconnect-kde
pkgver=1.0
_pkgver=1.0
pkgrel=2
epoch=1
pkgdesc='KDE Connect adds communication between KDE and your smartphone'
url='https://github.com/KDE/kdeconnect-kde'
license=('GPL')
arch=('x86_64')
depends=('kcmutils' 'kconfigwidgets' 'kiconthemes' 'kdbusaddons' 'knotifications' 
         'kio' 'ki18n' 'kdeclarative' 'kwayland' 'qca-qt5' 'libfakekey'
         'qt5-quickcontrols' 'telepathy-qt5' 'kirigami')
makedepends=('extra-cmake-modules' 'kdoctools')
conflicts=('kf5-kdeconnect-kde')
replaces=('kf5-kdeconnect-kde')
source=("http://download.kde.org/stable/kdeconnect/${_pkgver}/src/${pkgname}-${pkgver}.tar.xz")
md5sums=('f5d09557300665de4144de24dc57ced4')

prepare() {
  cd $pkgname-${pkgver}
  #sed -i -e 's|import QtQuick.Layouts 1.2|import QtQuick.Layouts 1.1|' ${srcdir}/${pkgname}/app/qml/DeviceDelegate.qml
  #sed -i -e 's|import QtQuick.Layouts 1.2|import QtQuick.Layouts 1.1|' ${srcdir}/${pkgname}/app/qml/mpris.qml
}

build() {
  mkdir -p build
  cd build 
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_LIBEXECDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF \
    -DEXPERIMENTALAPP_ENABLED=ON
  make 
}

package() {
  cd build
  
  make DESTDIR=${pkgdir} install
}
