
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=kdeconnect-kde
pkgver=${_kdever}
pkgrel=2
epoch=1
pkgdesc='KDE Connect adds communication between KDE and your smartphone'
url='https://invent.kde.org/network/kdeconnect-kde'
license=('GPL')
arch=('x86_64')
depends=('ki18n' 'kconfigwidgets' 'kdbusaddons' 'kiconthemes' 'knotifications' 'kio' 'kcmutils'
         'kservice' 'solid' 'kirigami' 'kpeoplevcard' 'kwindowsystem' 'kdeclarative' 'kwayland'
         'qqc2-desktop-style' 'qca-qt5' 'libfakekey' 'qt5-quickcontrols' 'sshfs' 'pulseaudio-qt')
makedepends=('extra-cmake-modules' 'kdoctools')
source=("$_mirror/${pkgname}-${_kdever}.tar.xz"
        'openssh8.2.patch')
md5sums=(`grep ${pkgname}-${_kdever}.tar.xz ../kde-sc.md5 | cut -d" " -f1`
         'ec00e96b184e6e0ab3641dab9dbac9e8')

prepare() {
  cd ${pkgname}-${pkgver}
  #sed -i -e 's|import QtQuick.Layouts 1.2|import QtQuick.Layouts 1.1|' ${srcdir}/${pkgname}/app/qml/DeviceDelegate.qml
  # file browsing crashes with openssh 8.2, fix from ArchLinux
  patch -p1 -i ${srcdir}/openssh8.2.patch
}

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF 
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
