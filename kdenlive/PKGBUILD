
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=kdenlive
pkgver=${_kdever}
pkgrel=2
pkgdesc="Free and Open Source video editing application, based on MLT Framework and KDE Frameworks 6."
arch=('x86_64')
url="https://www.kdenlive.org/"
license=('GPL')
depends=('karchive6' 'kbookmarks6' 'kcodecs' 'kcoreaddons6' 'kconfig6' 'kconfigwidgets6' 'kdbusaddons6' 'kio6'
         'kwidgetsaddons6' 'knotifyconfig6' 'knewstuff6' 'kxmlgui6' 'knotifications6'
         'kguiaddons6' 'ktextwidgets6' 'kiconthemes6' 'solid6' 'kfilemetadata6' 'kcrash6' 'purpose6' 'ki18n6'
         'mlt' 'dvgrab' 'frei0r-plugins' 'qt6-multimedia' 'qt6-networkauth' 'qt6-svg' 'qt6-tools' 'qt6-5compat') 
makedepends=('extra-cmake-modules' 'kdoctools6' 'gettext' 'docbook-xsl')
conflicts=('kdenlive6')
replaces=('kdenlive6')
optdepends=("dvdauthor: for creating dvds")
groups=('multimedia')
source=("$_mirror/${pkgname}-${_kdever}.tar.xz"
        "https://invent.kde.org/multimedia/kdenlive/-/commit/8be0e826471332bb739344ebe1859298c46e9e0f.diff") 
md5sums=(`grep ${pkgname}-${_kdever}.tar.xz ../kde-sc.md5 | cut -d" " -f1`
         'ce7c39a40fb70b6e218350e29f1e0a9f')

prepare() {
  cd ${pkgname}-${pkgver}

  patch -p1 -i ${srcdir}/8be0e826471332bb739344ebe1859298c46e9e0f.diff
  #sed -e "s|QtGraphicalEffects 1.15|Qt5Compat.GraphicalEffects|" -i src/timeline2/view/qml/TrackHead.qml
}

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt6/plugins \
    -DBUILD_TESTING=OFF \
    -DQT_MAJOR_VERSION=6
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
