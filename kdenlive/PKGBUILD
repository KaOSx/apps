
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=kdenlive
pkgver=${_kdever}
pkgrel=2
pkgdesc="Free and Open Source video editing application, based on MLT Framework and KDE Frameworks 5."
arch=('x86_64')
url="https://www.kdenlive.org/"
license=('GPL')
depends=('karchive' 'kbookmarks' 'kcoreaddons' 'kconfig' 'kconfigwidgets' 'kdbusaddons' 'kio'
         'kwidgetsaddons' 'knotifyconfig' 'knewstuff' 'kxmlgui' 'knotifications'
         'kguiaddons' 'ktextwidgets' 'kiconthemes' 'kfilemetadata' 'kcrash' 'purpose' 'ki18n'
         'mlt' 'dvgrab' 'frei0r-plugins' 'qt5-networkauth' 'qt5-tools') 
makedepends=('extra-cmake-modules' 'kdoctools' 'gettext' 'docbook-xsl')
optdepends=("dvdauthor: for creating dvds")
groups=('multimedia')
source=("$_mirror/${pkgname}-$_kdever.tar.xz"
        "https://invent.kde.org/multimedia/kdenlive/-/commit/364e23f6d1ba104e40623297b82a0ee873942925.diff") 
md5sums=(`grep ${pkgname}-$_kdever.tar.xz ../kde-sc.md5 | cut -d" " -f1`
         '27f4dda2f5d1f0df81244a401961218c')

prepare() {
  cd ${pkgname}-${pkgver}
  # https://bugs.kde.org/show_bug.cgi?id=468962
  patch -p1 -i ${srcdir}/364e23f6d1ba104e40623297b82a0ee873942925.diff
}

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_QMLDIR=/usr/lib/qt5/qml \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF 
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
