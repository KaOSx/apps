
pkgname=qtkeychain
pkgver=0.14.3
pkgrel=1
arch=('x86_64')
pkgdesc="Qt API to store passwords and other secret data securely"
url="https://github.com/frankosterfeld/qtkeychain"
license=('BSD')
depends=('qt5-base' 'kwallet')
makedepends=('cmake' 'qt5-tools' 'icu')
source=("https://github.com/frankosterfeld/qtkeychain/archive/${pkgver}.tar.gz")
md5sums=('269fe04b6d9d3a22841695a25f1d70e6')

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_LIBEXECDIR=lib/${pkgname} \
    -DLIBSECRET_SUPPORT=Off
  cmake --build build
}

package() {
  DESTDIR=${pkgdir} cmake --install build

  install -D -m644 ${pkgname}-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
