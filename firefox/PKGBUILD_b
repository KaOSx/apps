
pkgname=firefox
pkgdesc='Standalone web browser from mozilla.org'
url='http://www.mozilla.org/projects/firefox'
pkgver=40.0.3
pkgrel=1
arch=('x86_64')
license=('MPL' 'GPL' 'LGPL')
groups=('network-web')
install=firefox.install
depends=('desktop-file-utils' 'libxt' 'mime-types' 'nss' 'shared-mime-info' 'gtk2' 'networkmanager'
         'gst-plugins-good' 'gst-plugins-bad')
#source=("http://download-installer.cdn.mozilla.net/pub/firefox/releases/${pkgver}/linux-x86_64/en-US/firefox-${pkgver}.tar.bz2"
source=("https://ftp.mozilla.org/pub/mozilla.org/firefox/releases/${pkgver}/linux-x86_64/en-US/firefox-${pkgver}.tar.bz2"
        'firefox.desktop' 
        'firefox-safe.desktop')
md5sums=('a1538a8a5b4db97f6faa4e535de5877f'
         'db722eb1f9778aab80159cdbd9f0b56d'
         '2f004e44dd2ef3138367ea5b1bc1a219')

package() {
  cd "${srcdir}"
  mkdir -p "${pkgdir}"/{usr/{bin,share/{applications,pixmaps}},opt}
  cp -r firefox "${pkgdir}/opt/firefox-${pkgver}"

  ln -s /opt/firefox-${pkgver}/firefox "${pkgdir}/usr/bin/firefox"
  install -m644 "${srcdir}"/{firefox.desktop,firefox-safe.desktop} "${pkgdir}/usr/share/applications/"
  install -m644 "${srcdir}/firefox/browser/icons/mozicon128.png" "${pkgdir}/usr/share/pixmaps/${pkgname}-icon.png"
}
