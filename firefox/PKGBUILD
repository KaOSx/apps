
pkgname=firefox
pkgdesc='Standalone web browser from mozilla.org'
url='https://www.mozilla.org/en-US/firefox/'
pkgver=105.0.3
# Check nss version in build repo
_nssver=3.83
pkgrel=1
arch=('x86_64')
license=('MPL' 'GPL' 'LGPL')
groups=('network-web')
#install=firefox.install
depends=('desktop-file-utils' 'libxt' 'mime-types' "nss>=${_nssver}" 'shared-mime-info' 'gtk3' 'networkmanager'
         'gst-plugins-good' 'gst-plugins-bad' 'gst-libav' 'startup-notification'
         'dbus-glib' 'alsa-lib' 'hicolor-icon-theme' 'libevent' 'pulseaudio' 'libjpeg-turbo') # 'icu')
makedepends=('unzip' 'zip' 'diffutils' 'python3' 'python3-psutil' 'yasm' 'mesa' 'imake' 'xorg-server' 
             'inetutils' 'autoconf2.13' 'rust' 'clang' 'nodejs' 'cbindgen' 'nasm' 'sqlite') # 'libvpx' 
optdepends=('kde-gtk-config: gtk integration settings for Plasma')
options=('!emptydirs' '!makeflags')
#source=("https://hg.mozilla.org/releases/mozilla-release/archive/${_commit}.tar.bz2"
source=("https://ftp.mozilla.org/pub/firefox/releases/${pkgver}/source/${pkgname}-${pkgver}.source.tar.xz"
        'mozconfig'
        'firefox.desktop'
        'firefox-install-dir.patch'
        'vendor.js'
        'stylo.patch')
sha256sums=('f2fa1e03aecdd4dca0bcda94fd228d3a9ef3635862a2c140f8982d32ae7761e7'
            '5401a208af2ceb7fef22c4435d9f619502ca63098eaf0e5abbdcce5163cf78f1'
            'e74e001006982efd7e55bf9cde80eafddec755cb8bd4d0030b4518f0062ca742'
            'd86e41d87363656ee62e12543e2f5181aadcff448e406ef3218e91865ae775cd'
            '858642c81833ccfd7ac89af2c159e8d8fca0b80e4fea2840afe64b0393bf9ed6'
            '46104bf0afde6d7fb74630425915364a11b05f016f16e14ff0e666f322c9e940')

prepare() {
  cd ${pkgname}-${pkgver}
  # run prior to building:
  export LC_ALL="en_US.UTF-8"

  cp ../mozconfig .mozconfig
  #patch -p1 -i ${srcdir}/firefox-install-dir.patch

  mkdir -p ${srcdir}/mozbuild

  sed -e "s|psutil>=5.4.2,<=5.8.0|psutil>=5.4.2,<=5.9.5|" -i python/sites/mach.txt
}

build() {
  cd ${pkgname}-${pkgver}

  export MOZBUILD_STATE_PATH=${srcdir}/mozbuild
  export MACH_BUILD_PYTHON_NATIVE_PACKAGE_SOURCE="system"
  unset CPPFLAGS

  #make -f client.mk build # MOZ_PGO=0
  # https://developer.mozilla.org/en-US/docs/Mozilla/Developer_guide/mach
  ./mach build
}

package() {
  cd ${pkgname}-${pkgver}
  #make -f client.mk DESTDIR=${pkgdir} INSTALL_SDK= install
  DESTDIR=${pkgdir} ./mach install

  install -Dm644 ../vendor.js ${pkgdir}/usr/lib/firefox/browser/defaults/preferences/vendor.js

  for i in 16 22 24 32 48 256; do
      install -Dm644 browser/branding/official/default$i.png \
        ${pkgdir}/usr/share/icons/hicolor/${i}x${i}/apps/firefox.png
  done
  #install -Dm644 browser/branding/official/content/icon64.png ${pkgdir}/usr/share/icons/hicolor/64x64/apps/firefox.png
  #install -Dm644 browser/branding/official/mozicon128.png ${pkgdir}/usr/share/icons/hicolor/128x128/apps/firefox.png
  #install -Dm644 browser/branding/official/content/about-logo.png ${pkgdir}/usr/share/icons/hicolor/192x192/apps/firefox.png
  #install -Dm644 browser/branding/official/content/about-logo@2x.png ${pkgdir}/usr/share/icons/hicolor/384x384/apps/firefox.png

  install -Dm644 ../firefox.desktop ${pkgdir}/usr/share/applications/firefox.desktop

  ln -sf firefox ${pkgdir}/usr/lib/firefox/firefox-bin
}
