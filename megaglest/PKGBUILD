
pkgname=megaglest
pkgver=3.13.0
pkgrel=8
pkgdesc="Entertaining free (freeware and free software) and open source cross-platform 3D real-time strategy (RTS) game"
arch=('x86_64')
url="https://github.com/MegaGlest/megaglest-source"
license=('GPL3')
depends=('curl' 'megaglest-data' 'xerces-c' 'sdl2' 'libvorbis' 'openal' 'libgl' 'lua' 'icu' 'ftgl' 'glew'
         'miniupnpc' 'glu' 'libx11' 'vlc')
makedepends=('ftjam' 'cmake' 'git')
#source=("https://github.com/basxto/megaglest-source/tarball/qt4/megaglest-source-${pkgver}.tar.gz")
source=("https://github.com/MegaGlest/megaglest-source/archive/${pkgver}.tar.gz"
        "https://github.com/MegaGlest/megaglest-source/commit/a85d12aed1983eb69d630b431b81ae656d83b3ac.patch")
md5sums=('85bad907c20747b6f446e79e756b5ae0'
         '86bd248278059f1d3052e08d103edd15')

build() {
  cd megaglest-source-${pkgver}
  # miniupnpc 2.0 needed patch
  #patch -p1 -i $srcdir/a85d12aed1983eb69d630b431b81ae656d83b3ac.patch

  mkdir build 
  cd build

  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_CXX_FLAGS="-lX11" \
    -DWANT_GIT_STAMP=0 \
    -DBUILD_MEGAGLEST_MAP_EDITOR=Off \
    -DBUILD_MEGAGLEST_MODEL_IMPORT_EXPORT_TOOLS=Off \
    -DBUILD_MEGAGLEST_MODEL_VIEWER=Off
    
  make VERBOSE=1
}

package() {
  cd megaglest-source-${pkgver}/build

  make DESTDIR="$pkgdir" install
}
