
pkgname=focuswriter
pkgver=1.8.8
pkgrel=2
pkgdesc="A simple fullscreen word processor."
arch=('x86_64')
url="https://gottcode.org/focuswriter/"
license=('GPL3')
depends=('libzip' 'qt6-declarative' 'sdl_mixer' 'qt6-multimedia' 'hunspell' 'icu')
makedepends=('cmake' 'qt6-tools')
groups=('office')
source=("https://gottcode.org/focuswriter/${pkgname}-${pkgver}.tar.bz2"
        "https://github.com/gottcode/focuswriter/commit/3e06c2c6b6935f2c88a7e201e4a5a51a326570a4.diff")
sha256sums=('cd34c97978bee62bc730c8369b183153240a7ea47fb13507c60be15b105259ff'
            '2082118d0ce873b65b4e2c651abb959af9895d9273dff9676b2a4e3073e8a841')

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
