 
pkgname=kdiff3
pkgver=1.9.2
pkgrel=1
pkgdesc='Utility for comparing and merging files and directories'
arch=('x86_64')
url='https://invent.kde.org/sdk/kdiff3'
license=('LGPL')
depends=('ki18n' 'kcoreaddons' 'kcrash' 'kiconthemes' 'kparts' 'kwidgetsaddons' 'kio' 'clang-tools-extra')
makedepends=('extra-cmake-modules' 'kdoctools' 'boost')
groups=('programming')
source=("https://download.kde.org/stable/kdiff3/${pkgname}-${pkgver}.tar.xz")
sha256sums=('f1775f1ca501080363707b2923d493fc314ae4f0b00b172d4cad9b4d3cc2d805')

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DENABLE_CLANG_TIDY=ON \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
