 
pkgname=kdiff3
pkgver=1.10.3
pkgrel=1
pkgdesc='Utility for comparing and merging files and directories'
arch=('x86_64')
url='https://invent.kde.org/sdk/kdiff3'
license=('LGPL')
depends=('ki18n' 'kcoreaddons' 'kcrash' 'kiconthemes' 'kparts' 'kwidgetsaddons' 'kio' 'clang')
makedepends=('extra-cmake-modules' 'kdoctools' 'boost')
groups=('programming')
source=("https://download.kde.org/stable/kdiff3/${pkgname}-${pkgver}.tar.xz")
sha256sums=('d6a46bcb8d999f842aa7f6b393079e2022aa1f7e1318c56a64bd63cd0e049f50')

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DENABLE_CLANG_TIDY=ON \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
