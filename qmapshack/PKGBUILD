
pkgname=qmapshack
pkgver=1.15.0
pkgrel=3
pkgdesc="Plan your next outdoor trip or to visualize and archive all the GPS recordings, next generation of the QLandkarte GT application"
arch=('x86_64')
url="https://github.com/Maproom/qmapshack/wiki"
license=('GPL')
depends=('gdal' 'libdmtx' 'libexif' 'mariadb' 'mesa' 'postgresql-libs'
         'shared-mime-info' 'unixodbc' 'glu' 'qtwebengine' 'proj' 'qt5-script' 
         'routino' 'quazip')
makedepends=('cmake' 'extra-cmake-modules' 'qt5-tools')
groups=('utils')
source=("https://github.com/Maproom/qmapshack/archive/V_${pkgver}.tar.gz"
        'proj.patch'
        'quazip.patch'
        'qt5.15.diff')
md5sums=('c9f9dc0d2c317b8bf3d36bdff2b3e762'
         '1a2192dfa8b63fce503a59d6917918a5'
         'cbdc25ae8121f347ddfedc10501d589e'
         '148595b47465d4a465faafa1c5e8a336')

prepare() {
  cd ${pkgname}-V_${pkgver}
  
  patch -p1 -i ${srcdir}/proj.patch
  patch -p1 -i ${srcdir}/quazip.patch
  patch -p1 -i ${srcdir}/qt5.15.diff
  #sed -i 's|PROJ4|PROJ|' CMakeLists.txt
}

build() {
  mkdir -p build
  cd build
 
  cmake ../${pkgname}-V_${pkgver} \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_BUILD_TYPE=Release 
      
  make
}

package(){
  cd build
  make DESTDIR=${pkgdir} install
  
  install -Dm644 ../${pkgname}-V_${pkgver}/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
