
pkgname=kshutdown
pkgver=4.2
_pkgver=4.2
pkgrel=1
pkgdesc="Graphical shutdown utility that allows you to turn off or suspend a computer at a specified time."
url="http://kshutdown.sourceforge.net/"
arch=('x86_64')
license=('GPL')
depends=('kconfig' 'kconfigwidgets' 'kdbusaddons' 'kglobalaccel' 'ki18n' 'kidletime' 'knotifications'
         'knotifyconfig' 'kxmlgui')
makedepends=('extra-cmake-modules')
groups=('kde-utils') 
source=("https://sourceforge.net/projects/kshutdown/files/KShutdown/${_pkgver}/kshutdown-source-${_pkgver}.zip")
sha256sums=('48e537b593fd2470c0cfeca7c2c48ac3221fdb48b0fb89bfb898e4faf531ee46')

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${_pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKS_KF5=TRUE
  make
}

package() {
  cd build
  
  make DESTDIR=${pkgdir} install
}
