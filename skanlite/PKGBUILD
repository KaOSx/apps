
pkgname=skanlite
pkgver=2.1.0.1
_pkgver=2.1
pkgrel=2
pkgdesc="Image Scanning Application for KDE"
arch=('x86_64')
url='https://www.kde.org/applications/graphics/skanlite'
license=('LGPL')
depends=('kio' 'libksane' 'sane')
makedepends=('pkgconfig' 'extra-cmake-modules' 'kdoctools')
groups=('kde-graphics')
source=("https://download.kde.org/stable/skanlite/${_pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('b4733e55b706eaf749522aa81ec47bfd65a95c2fec994c1c47e0c4b628e70442')

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make 
}

package() {
  cd build
  
  make DESTDIR=${pkgdir} install
}
