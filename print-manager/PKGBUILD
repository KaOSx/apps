
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=print-manager
pkgver=${_kdever}
pkgrel=2
pkgdesc="A KDE tool for managing print jobs and printers"
url="https://invent.kde.org/utilities/print-manager"
arch=('x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kconfig' 'kconfigwidgets' 'kcoreaddons' 'kdbusaddons' 'kiconthemes'
         'ki18n' 'kcmutils' 'kio' 'knotifications' 'plasma-framework'
         'kwidgetsaddons' 'kwindowsystem' 'libcups' 'qt5-tools' 'samba')
makedepends=('extra-cmake-modules' 'kdoctools')
groups=('kde' 'kde-utils' 'kde-uninstall')
source=("$_mirror/${pkgname}-$_kdever.tar.xz"
        's-c-p.diff')
md5sums=(`grep ${pkgname}-${_kdever}.tar.xz ../kde-sc.md5 | cut -d" " -f1`
         '67a5a9e3c84d53d61fbec8ce44094d68')

prepare() {
  cd ${pkgname}-${pkgver}

  patch -p1 -i ${srcdir}/s-c-p.diff
}

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF 
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
