
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=print-manager
pkgver=${_plasmaver}
pkgrel=1
pkgdesc="A KDE tool for managing print jobs and printers"
url="https://invent.kde.org/utilities/print-manager"
arch=('x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kconfig6' 'kconfigwidgets6' 'kcoreaddons6' 'kdbusaddons6' 'kiconthemes6'
         'ki18n6' 'kcmutils6' 'kio6' 'knotifications6' 'libplasma'
         'kwidgetsaddons6' 'kwindowsystem6' 'kirigami-addons' 'libcups' 'qt6-tools' 'samba' 'qt6-5compat')
makedepends=('extra-cmake-modules' 'kdoctools6')
conflicts=('print-manager6')
replaces=('print-manager6')
groups=('kdeplasma6')
source=("https://download.kde.org/stable/plasma/${pkgver}/${pkgname}-${pkgver}.tar.xz"
        's-c-p.diff')
md5sums=(`grep ${pkgname}-${_plasmaver}.tar.xz ../kf6.md5 | cut -d" " -f1`
         '4f6e48d697411a51950ab93db1bd2f49')

prepare() {
  cd ${pkgname}-${pkgver}

  patch -p1 -i ${srcdir}/s-c-p.diff
}

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DQT_MAJOR_VERSION=6
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
