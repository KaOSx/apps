
pkgname=isoimagewriter
pkgver=1.0.0
pkgrel=2
arch=('x86_64')
pkgdesc="Program to write hybrid ISO files onto USB disks"
url="https://invent.kde.org/utilities/isoimagewriter"
license=('GPL2')
depends=('ki18n6' 'kcoreaddons6' 'kwidgetsaddons6' 'kiconthemes6' 'karchive6'
         'kcrash6' 'solid6' 'gpgme-qt6')
makedepends=('extra-cmake-modules6') 
source=("https://download.kde.org/stable/isoimagewriter/${pkgver}/${pkgname}-${pkgver}.tar.xz"
        'isoverifier.diff'
        'mainwindow.diff')
sha256sums=('a69022303ec1beb6b7b4f0d68e59e4199d3c9868767cb9eb23c6dd1999e06a5d'
            '75f1c2e8ec97d0af0c0b68a77df66f8f5c8c9bf8dee1efe2d69bcaa2fe27f67e'
            '7ae2371c029d0ff0343e4c7e1d1d9256886e5a8a84870ce18a69438c8400f87a')

prepare() {
  cd ${pkgname}-${pkgver}

  patch -p1 -i ${srcdir}/isoverifier.diff
  patch -p1 -i ${srcdir}/mainwindow.diff
}

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_BUILD_TYPE=Release \
      -DBUILD_TESTING=OFF \
      -DQT_MAJOR_VERSION=6
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
