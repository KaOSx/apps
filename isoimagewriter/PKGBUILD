
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=isoimagewriter
pkgver=${_kdever}
pkgrel=1
arch=('x86_64')
pkgdesc="Program to write hybrid ISO files onto USB disks"
url="https://apps.kde.org/isoimagewriter/"
license=('GPL2')
depends=('ki18n6' 'kcoreaddons6' 'kwidgetsaddons6' 'kiconthemes6' 'karchive6'
         'kcrash6' 'solid6' 'gpgme-qt6')
makedepends=('extra-cmake-modules6') 
source=("https://download.kde.org/stable/applications/${_kdever}/src/${pkgname}-${_kdever}.tar.xz"
        'isoverifier.diff'
        'mainwindow.diff')
md5sums=(`grep ${pkgname}-$_kdever.tar.xz ../kde-sc.md5 | cut -d" " -f1`
         '6a46cc4146a3c2f2a0f5568a76f99ab0'
         '6f83e0906c132bb1bf5117a0ab5262d5')

prepare() {
  cd ${pkgname}-${pkgver}

  patch -p1 -i ${srcdir}/isoverifier.diff
  patch -p1 -i ${srcdir}/mainwindow.diff
}

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_BUILD_TYPE=Release \
      -DBUILD_TESTING=OFF \
      -DQT_MAJOR_VERSION=6
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
