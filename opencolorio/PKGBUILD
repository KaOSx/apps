
pkgname=opencolorio
pkgver=2.3.0
pkgrel=1
pkgdesc="A color management framework for visual effects and animation"
arch=('x86_64')
url="https://opencolorio.org"
license=('BSD')
depends=('python3' 'lcms2' 'yaml-cpp' 'openexr' 'glew' 'freeglut')
makedepends=('cmake' 'boost')
groups=('color')
source=("https://github.com/AcademySoftwareFoundation/OpenColorIO/archive/v${pkgver}.tar.gz")
md5sums=('c05f24a516fe82d381c4b0d471e12ad5')

prepare() {
  cd OpenColorIO-${pkgver}

  sed -i 's|yaml-cpp 0.7.0|yaml-cpp 0.8.0|' share/cmake/modules/FindExtPackages.cmake
  sed -i 's|yaml-cpp 0.7.0|yaml-cpp 0.8.0|' share/cmake/modules/Findyaml-cpp.cmake
  sed -i 's|get_target_property(yaml-cpp_LIBRARY yaml-cpp LOCATION)||' share/cmake/modules/Findyaml-cpp.cmake
}

build() {
  cmake -B build -S OpenColorIO-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DOCIO_BUILD_TESTS=OFF \
    -DOCIO_BUILD_GPU_TESTS=OFF
  cmake --build build
}

package() {
  DESTDIR=${pkgdir} cmake --install build

  install -Dm644 OpenColorIO-${pkgver}/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

