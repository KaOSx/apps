
pkgname=qgis
_pkgname=QGIS
pkgver=3.18.1
_commit=7dbe4943d606b8acd879dd6ed9e38542e3ab0bab
pkgrel=1
pkgdesc='Quantum GIS is a Geographic Information System (GIS) that supports vector, raster & database formats'
url='https://qgis.org/'
license=('GPL')
arch=('x86_64')
depends=('mariadb' 'postgresql-libs' 'sqlite' 'curl' 'giflib' 'gsl' 'grass'
         'cfitsio' 'qwt' 'proj' 'gdal' 'geos' 'libspatialite' 'spatialindex' 'python3-qscintilla-qt5' 
         'postgis' 'fcgi' 'qt5-svg' 'qt5-script' 'python3-six' 'python3-psycopg2' 'python3-dateutil'
         'python3-pytz' 'python3-jinja' 'python3-future' 'pyqt5-python3' 'qca-qt5' 'qt5-xmlpatterns'
         'python3-numpy' 'python3-httplib2' 'python3-requests' 'pyyaml'
         'libzip' 'qtkeychain' 'exiv2' 'protobuf' 'hdf5')
makedepends=('cmake'  'netcdf' 'sip3' 'qt5-tools') 
source=("https://qgis.org/downloads/${pkgname}-${pkgver}.tar.bz2"
        'qt5.15.diff'
        "https://github.com/qgis/QGIS/commit/fc1ac8bef8dcc3194857ecd32519aca4867b4fa1.diff"
        "https://github.com/qgis/QGIS/commit/e11801d34e11dd1e7edfbcb2f3cda7bf476ea599.diff")
sha256sums=('b2934927f1c9314dbe83aff0c900d2c51fb2f980fb2d313f4bcb1736b92fbaa7'
            'b0b34ab28bf56f5a60366847df59f755cbb26af597c378959aa79f8c068c1b03'
            '96197890f155659fd7cfb34e6cf9149b650230949d4c88b5d2b62738031c23ac'
            '809eb89f7869109f26a142014f934597b4393e9dd0db5cf185f0ef12c866e7c8') 
         
prepare() {
     cd ${pkgname}-${pkgver}
     
     # proj 8
     patch -p1 -i ${srcdir}/fc1ac8bef8dcc3194857ecd32519aca4867b4fa1.diff
     #patch -p1 -i ${srcdir}/e11801d34e11dd1e7edfbcb2f3cda7bf476ea599.diff
     sed -i -e 's|qt/Qca-qt5/QtCrypto|qt5/Qca-qt5/QtCrypto|' cmake/FindQCA.cmake
     sed -i -e 's|set(QWT_LIBRARY_NAMES qwt-qt5 qwt6-qt5)|set(QWT_LIBRARY_NAMES qwt qwt6)|' cmake/FindQwt.cmake
     #sed -i -e 's|libqt5scintilla2|libqtscintilla2|' cmake/FindQScintilla.cmake
     
     sed -i 's/\(env \|\/usr\/bin\/\)python$/&3/' $(find . -iname "*.py")
}

 build() {
    cmake -B build -S ${pkgname}-${pkgver} \
        -DENABLE_TESTS=OFF \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_SKIP_RPATH=ON \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DGRASS_PREFIX=/opt/grass \
        -DQGIS_MANUAL_SUBDIR=share/man \
        -DPYTHON_EXECUTABLE=/usr/bin/python3 \
        -DWITH_QWTPOLAR=FALSE \
        -DWITH_QTWEBKIT=FALSE
    cmake --build build
}

package() {

    DESTDIR=${pkgdir} cmake --install build

    #python3 -m compileall -q ${pkgdir}
}
