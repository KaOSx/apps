
pkgname=qgis
_pkgname=QGIS
pkgver=3.22.0
_commit=7dbe4943d606b8acd879dd6ed9e38542e3ab0bab
pkgrel=1
pkgdesc='Quantum GIS is a Geographic Information System (GIS) that supports vector, raster & database formats'
url='https://qgis.org/'
license=('GPL')
arch=('x86_64')
depends=('mariadb' 'postgresql-libs' 'sqlite' 'curl' 'giflib' 'gsl' 
         'cfitsio' 'qwt' 'proj' 'gdal' 'geos' 'libspatialite' 'spatialindex' 'python3-qscintilla-qt5' 
         'postgis' 'fcgi' 'qt5-svg' 'qt5-script' 'python3-six' 'python3-psycopg2' 'python3-dateutil'
         'python3-pytz' 'python3-jinja' 'python3-future' 'pyqt5-python3' 'qca-qt5' 'qt5-xmlpatterns'
         'python3-numpy' 'python3-httplib2' 'python3-requests' 'pyyaml'
         'libzip' 'qtkeychain' 'exiv2' 'protobuf' 'hdf5' 'grass')
makedepends=('cmake'  'netcdf' 'sip3' 'qt5-tools') 
source=("https://qgis.org/downloads/${pkgname}-${pkgver}.tar.bz2"
        'qt5.15.diff'
        "https://github.com/qgis/QGIS/commit/6f9cbde7c782274ebe5875da7dbac98d68e9827b.diff"
        "https://github.com/qgis/QGIS/commit/581cb40603dd3daca2916b564a4cd2630d005556.diff")
sha256sums=('cf0c169863f332aab67d8c4943e14b73a564f0254bf54015f5826c6427e6785b'
            'b0b34ab28bf56f5a60366847df59f755cbb26af597c378959aa79f8c068c1b03'
            '1064c4c37b0a385a4c9b4b1bcd5109ba89f2aaec4bb9279f17d895222342eddc'
            '2ec84d5bf9c0622f3d67e3cbd942f6b91a62fc1b10efe3ffd4bfa48e2bc4c135') 
         
prepare() {
     cd ${pkgname}-${pkgver}

     # qwt 6.2
     #patch -p1 -i ${srcdir}/6f9cbde7c782274ebe5875da7dbac98d68e9827b.diff
     #patch -p1 -i ${srcdir}/581cb40603dd3daca2916b564a4cd2630d005556.diff

     sed -i -e 's|qt/Qca-qt5/QtCrypto|qt5/Qca-qt5/QtCrypto|' cmake/FindQCA.cmake
     sed -i -e 's|set(QWT_LIBRARY_NAMES qwt-qt5 qwt6-qt5)|set(QWT_LIBRARY_NAMES qwt qwt6)|' cmake/FindQwt.cmake
     #sed -i -e 's|libqt5scintilla2|libqtscintilla2|' cmake/FindQScintilla.cmake

     sed -i 's/\(env \|\/usr\/bin\/\)python$/&3/' $(find . -iname "*.py")
}

 build() {
    cmake -B build -S ${pkgname}-${pkgver} \
        -DENABLE_TESTS=OFF \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_SKIP_RPATH=ON \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DQGIS_MANUAL_SUBDIR=share/man \
        -DWITH_QWTPOLAR=FALSE \
        -DWITH_INTERNAL_QWTPOLAR=FALSE \
        -DWITH_QTWEBKIT=FALSE
    cmake --build build
}

package() {

    DESTDIR=${pkgdir} cmake --install build

    #python3 -m compileall -q ${pkgdir}
}
