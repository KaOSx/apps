
pkgname=qgis
_pkgname=QGIS
pkgver=3.12.3
_commit=7dbe4943d606b8acd879dd6ed9e38542e3ab0bab
pkgrel=1
pkgdesc='Quantum GIS is a Geographic Information System (GIS) that supports vector, raster & database formats'
url='https://qgis.org/'
license=('GPL')
arch=('x86_64')
depends=('mariadb' 'postgresql-libs' 'sqlite' 'curl' 'giflib' 'gsl' 'grass'
         'cfitsio' 'qwt' 'proj' 'gdal' 'geos' 'libspatialite' 'spatialindex' 'python3-qscintilla-qt5' 
         'postgis' 'fcgi' 'qt5-svg' 'qt5-script' 'python3-six' 'python3-psycopg2' 'python3-dateutil'
         'python3-pytz' 'python3-jinja' 'python3-future' 'pyqt5-python3' 'qca-qt5' 'qt5-xmlpatterns'
         'python3-numpy' 'python3-httplib2' 'python3-requests' 'pyyaml'
         'libzip' 'qtkeychain' 'exiv2')
makedepends=('cmake'  'netcdf' 'sip3' 'qt5-tools') 
source=("https://qgis.org/downloads/${pkgname}-${pkgver}.tar.bz2"
        'qt5.15.diff')
sha256sums=('c2b53815f9b994e1662995d1f25f90628156b996758f5471bffb74ab29a95220'
            'b0b34ab28bf56f5a60366847df59f755cbb26af597c378959aa79f8c068c1b03') 
         
prepare() {
     cd ${pkgname}-${pkgver}
     # needed patch for Qt 5.15
     patch -p1 -i ${srcdir}/qt5.15.diff
     sed -i -e 's|qt/Qca-qt5/QtCrypto|qt5/Qca-qt5/QtCrypto|' cmake/FindQCA.cmake
     sed -i -e 's|set(QWT_LIBRARY_NAMES qwt-qt5 qwt6-qt5)|set(QWT_LIBRARY_NAMES qwt qwt6)|' cmake/FindQwt.cmake
     #sed -i -e 's|libqt5scintilla2|libqtscintilla2|' cmake/FindQScintilla.cmake
     
     sed -i 's/\(env \|\/usr\/bin\/\)python$/&3/' $(find . -iname "*.py")
}

 build() {
      mkdir -p build
      cd build
     
      cmake ../${pkgname}-${pkgver} \
        -DENABLE_TESTS=OFF \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_SKIP_RPATH=ON \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DGRASS_PREFIX=/opt/grass \
        -DQGIS_MANUAL_SUBDIR=share/man \
        -DPYTHON_EXECUTABLE=/usr/bin/python3 \
        -DWITH_QWTPOLAR=FALSE \
        -DWITH_QTWEBKIT=FALSE
     
      make -j4
}

package() {
      cd build
     
      make DESTDIR=${pkgdir} install
     
      #python3 -m compileall -q ${pkgdir}
}
