
pkgname=weston
pkgver=14.0.1
pkgrel=1
pkgdesc='Reference implementation of a Wayland compositor'
arch=('x86_64')
url='https://wayland.freedesktop.org'
license=('MIT')
options=('!libtool')
depends=('libxkbcommon' 'mesa' 'pango' 'poppler-glib' 'mtdev' 'libxcursor' 'glu' 'libunwind'
         'pixman' 'xkeyboard-config' 'colord' 'libwebp' 'libva' 'libinput' 'wayland-protocols'
         'freerdp' 'xwayland' 'pipewire')
makedepends=('meson' 'ninja')
source=("https://gitlab.freedesktop.org/wayland/weston/-/releases/${pkgver}/downloads/${pkgname}-${pkgver}.tar.xz")
sha256sums=('a8150505b126a59df781fe8c30c8e6f87da7013e179039eb844a5bbbcc7c79b3')

build() {
  mkdir -p build
  cd build

  meson setup ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --sysconfdir=/etc \
    --buildtype=release \
    --libexec=lib/weston \
    -Ddemo-clients=true \
    -Dsimple-clients=all \
    -Dbackend-rdp=true \
    -Dbackend-drm=false \
    -Dbackend-default=wayland \
    -Dremoting=false \
    -Dpipewire=false
   ninja
}

package() {
  cd build  
  DESTDIR=${pkgdir} ninja install

  install -Dm644 ../${pkgname}-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}
