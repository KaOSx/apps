
pkgname=weston
pkgver=11.0.1
pkgrel=1
pkgdesc='Reference implementation of a Wayland compositor'
arch=('x86_64')
url='https://wayland.freedesktop.org'
license=('MIT')
options=('!libtool')
depends=('libxkbcommon' 'mesa' 'pango' 'poppler-glib' 'mtdev' 'libxcursor' 'glu' 'libunwind'
         'pixman' 'xkeyboard-config' 'colord' 'libwebp' 'libva' 'libinput' 'wayland-protocols'
         'freerdp' 'xwayland' 'pipewire')
makedepends=('meson' 'ninja')
source=("https://gitlab.freedesktop.org/wayland/weston/uploads/f5648c818fba5432edc3ea63c4db4813/${pkgname}-${pkgver}.tar.xz")
sha256sums=('a413f68c252957fc3191c3650823ec356ae8c124ccc0cb440da5cdc4e2cb9e57')

build() {
  mkdir -p build
  cd build

  meson ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --sysconfdir=/etc \
    --buildtype=release \
    --libexec=lib/weston \
    -Ddemo-clients=true \
    -Dsimple-clients=all \
    -Dbackend-rdp=true \
    -Dpipewire=true \
    -Dlauncher-logind=true \
    -Dlauncher-libseat=false
   ninja
}

package() {
  cd build  
  DESTDIR=${pkgdir} ninja install

  install -Dm644 ../${pkgname}-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}
