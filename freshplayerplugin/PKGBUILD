
pkgname=freshplayerplugin
pkgver=0.3.11
pkgrel=4
pkgdesc='ppapi2npapi compatibility layer.'
arch=('x86_64' )
url='https://github.com/i-rinat/freshplayerplugin'
license=('MIT')
depends=('alsa-lib' 'ffmpeg' 'freetype2' 'gtk2' 'jack' 'libevent' 'libgl' 'v4l-utils' 
         'openssl' 'pango' 'pulseaudio' 'pepper-flash' 'icu' 'libva')
makedepends=('cmake' 'ragel')
source=("https://github.com/i-rinat/freshplayerplugin/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz"
        "https://github.com/i-rinat/freshplayerplugin/commit/934aa9c24d34f8203744b56e4ac6e8599446ca02.diff")
md5sums=('c34383e281135b7d40e29444af189d34'
         'f5364ead775363f3305365bc956cf2d1')

prepare() {
  cd ${pkgname}-${pkgver}
  
  #patch -p1 -i ${srcdir}/934aa9c24d34f8203744b56e4ac6e8599446ca02.diff
}

build() {
  mkdir -p build
  cd build

  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_PULSEAUDIO=1
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install

  install -Dm644 ../${pkgname}-${pkgver}/data/freshwrapper.conf.example ${pkgdir}/usr/share/${pkgname}/freshwrapper.conf.example
  install -Dm644 ../${pkgname}-${pkgver}/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
