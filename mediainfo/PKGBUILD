
pkgname=mediainfo
_pkgname=MediaInfo
pkgver=24.11
pkgrel=1
pkgdesc="Convenient unified display of the most relevant technical and tag data for video and audio files."
arch=('x86_64')
url="https://github.com/MediaArea/MediaInfo"
license=('GPL')
depends=('mediainfolib' 'qt6-base')
makedepends=('libtool')
#source=("https://github.com/MediaArea/MediaInfo/archive/v${pkgver}.tar.gz")
source=("https://old.mediaarea.net/download/source/mediainfo/${pkgver}/${pkgname}_${pkgver}.tar.xz"
        "https://github.com/MediaArea/MediaInfo/commit/86a0a008277b421e8eb897793f224979f4c5f515.diff")
md5sums=('66ba129bd968f7945a0f9ad709322d3e'
         'e0dd29dffa45829e97f38b85382bdc71')

prepare() {
  cd ${_pkgname}

  patch -p1 -R -i ${srcdir}/86a0a008277b421e8eb897793f224979f4c5f515.diff
}

build() {
  cd ${_pkgname}/Project/GNU/CLI

  ./autogen.sh
  ./configure --prefix=/usr
  make

  cd ../../QMake/GUI

  /usr/lib/qt6/bin/qmake
  make
}

package() {
  cd ${_pkgname}/Project/GNU/CLI
  make DESTDIR=${pkgdir} install

  cd ../../QMake/GUI
  make INSTALL_ROOT=${pkgdir} install

 install -D -m644 ../../GNU/GUI/mediainfo-gui.desktop ${pkgdir}/usr/share/applications/mediainfo-gui.desktop
 install -Dm 644 ../../../Source/Resource/Image/MediaInfo.svg ${pkgdir}/usr/share/icons/hicolor/scalable/apps/mediainfo.svg
}
