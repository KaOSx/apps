
pkgname=qtcurve-gtk
_pkgname=qtcurve
pkgver=1.9.1
_commit=0020a42c02a3c488fef23d33a27fea41f302ba98
pkgrel=1
arch=('x86_64')
license=('GPL')
pkgdesc='The QtCurve style engine for GTK.'
url='https://projects.kde.org/projects/playground/base/qtcurve'
depends=('qtcurve')
makedepends=('extra-cmake-modules' 'gtk2')
source=("https://github.com/KDE/qtcurve/archive/${_commit}.zip"
        'kdeglobals')
md5sums=('36d0057ee3766062964d11797c505e13'
         '8ca968e7f09201b8d2376d131a8e55df')

build() {
	cmake -B build -S ${_pkgname}-${_commit} \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DENABLE_GTK2=true \
        -DQTC_QT5_ENABLE_KDE=false \
        -DQTC_QT4_ENABLE_KDE=false \
        -DENABLE_QT5=false \
        -DENABLE_QT4=false
	cmake --build build
}

package() {
	DESTDIR=${pkgdir} cmake --install build 
	
	rm -r ${pkgdir}/usr/lib/libqtcurve-utils.so*
	rm -r ${pkgdir}/usr/share/themes/QtCurve/gtk-2.0/kdeglobals
	install -D -m644 ${srcdir}/kdeglobals ${pkgdir}/usr/share/themes/QtCurve/gtk-2.0/
}
