
pkgname=libfilezilla
pkgver=0.37.2
pkgrel=1
pkgdesc="Small C++ library, offering a typesafe, multi-threaded event system and process handling for spawning child processes."
arch=('x86_64')
url="https://lib.filezilla-project.org/"
license=('GPL2')
depends=('gcc' 'nettle')
source=("https://download.filezilla-project.org/libfilezilla/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('a8902977bae5f442045faa8a68c2f85e667a2371b1e643fde49e592fa9544b0a672c476246b01b2ba7b52390b37b0f1f33e5196be4499eddf5783f225091c93a')

build() {
  cd ${pkgname}-${pkgver}
  sed -e "s|<uint8_t> random_bytes(size_t size)|<uint8_t> FZ_PUBLIC_SYMBOL random_bytes(size_t size)|" -i lib/libfilezilla/util.hpp

  ./configure --prefix=/usr \
    --disable-static 
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install
}
