
pkgname=koko
pkgver=21.08
pkgrel=1
pkgdesc="An Image Gallery application"
arch=('x86_64')
url='https://invent.kde.org/graphics/koko'
license=('LGPL-3.0')
depends=('ki18n' 'kdeclarative' 'kconfig' 'kdbusaddons' 'kio' 'kguiaddons' 'kcoreaddons'
         'knotifications' 'kfilemetadata' 'kdbusaddons' 'kirigami' 'exiv2' 'qt5-location'
         'kquickimageeditor')
makedepends=('extra-cmake-modules' 'kdoctools')
source=("https://download.kde.org/stable/plasma-mobile/${pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('d79c2d298662dca14226c9c9d3c86901f4c85d1f0ac3396da6cdd470106514eb')

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
