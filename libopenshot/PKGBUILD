
pkgname=libopenshot
pkgver=0.2.5
pkgrel=7
pkgdesc="Open-source project dedicated to delivering high quality video editing, animation, and playback solutions to the world."
arch=('x86_64')
url="https://www.openshot.org/"
license=('LGPL3')
depends=('libopenshot-audio' 'ffmpeg' 'imagemagick' 'libx11' 'libxcursor' 'libxinerama' 'python3'
         'x264' 'qt5-multimedia' 'zeromq' 'jsoncpp') # build fails with gcc > 6.4 & ruby 'ruby' 
makedepends=('cmake' 'swig') # 'doxygen')
# http://bazaar.launchpad.net/~openshot.code/libopenshot/trunk/files
source=("https://github.com/OpenShot/libopenshot/archive/v${pkgver}.tar.gz"
        "https://github.com/OpenShot/libopenshot/commit/9922479945def383d7d425242ee8de8b519456c0.diff")
md5sums=('2e271cf3ad03e6116564fa1bcc7bf46b'
         'be81bb2f7693bc75bbec2a93c9afebc8')

prepare () {
  cd ${pkgname}-${pkgver}

  #sed -i 's|add_subdirectory(tests)|#add_subdirectory(tests)|' CMakeLists.txt
  # gcc 10 needed
  patch -p1 -i ${srcdir}/9922479945def383d7d425242ee8de8b519456c0.diff
}

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DMAGICKCORE_HDRI_ENABLE=1 \
    -DENABLE_RUBY=OFF \
    -DDISABLE_TESTS=ON \
    -DDOXYGEN_FOUND=NO
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
