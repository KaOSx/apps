
pkgname=opencascade
pkgver=7.5.3
_commit=fecb042498514186bd37fa621cdcf09eb61899a3
pkgrel=1
epoch=1
pkgdesc="Open CASCADE Technology, 3D modeling & numerical simulation"
arch=('x86_64')
url="http://dev.opencascade.org/"
license=('custom')
depends=('tk' 'mesa' 'java-runtime' 'libxmu' 'ftgl' 'vtk' 'qtwebkit-tp' 'gl2ps')
makedepends=('java-environment' 'cmake')
options=('!libtool')
source=("opencascade-${pkgver}.tar.gz::https://git.dev.opencascade.org/gitweb/?p=occt.git;a=snapshot;h=${_commit};sf=tgz"
        'env.sh'
        'opencascade.sh'
        'opencascade.conf'
        'cmake.patch'
        "https://gitweb.gentoo.org/repo/gentoo.git/plain/sci-libs/opencascade/files/opencascade-7.5.1-fix-AllValues-name-collision-with-vtk-9.0.patch"
        "https://gitweb.gentoo.org/repo/gentoo.git/plain/sci-libs/opencascade/files/opencascade-7.5.1-0005-fix-write-permissions-on-scripts.patch")
md5sums=('8fc0891ab20752de80c148c3eed5c570'
         '545f3842f87750dd04569f93f1921e07'
         'fc4b64a9425c02d4daeaeb1425e0a70d'
         '2924ecf57c95d25888f51071fdc72ad0'
         '0424cd6d6f8ce1a96b9883ef229b7048'
         '04d22ea4398d2a26e81fd02d01e6eb1d'
         '9c052ce94a6c7587e8df088ec1b30bae')

prepare() {
  cd occt-fecb042

  patch -p1 -i ${srcdir}/cmake.patch
  patch -p1 -i ${srcdir}/opencascade-7.5.1-fix-AllValues-name-collision-with-vtk-9.0.patch
  patch -p1 -i ${srcdir}/opencascade-7.5.1-0005-fix-write-permissions-on-scripts.patch
  sed -i -e 's|/lib$|/lib(64)?$|' adm/templates/OpenCASCADEConfig.cmake.in
}
         
build() {
  cmake -B build -S occt-fecb042 \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DINSTALL_DIR_BIN=/usr/bin \
    -DINSTALL_DIR_LIB=/usr/lib \
    -DINSTALL_DIR_CMAKE=/usr/lib/cmake \
    -DBUILD_RELEASE_DISABLE_EXCEPTIONS=OFF \
    -DUSE_FFMPEG=ON \
    -DUSE_VTK=ON \
    -D3RDPARTY_VTK_DIR=/usr \
    -D3RDPARTY_VTK_INCLUDE_DIR=/usr/include/vtk-9.0 \
    -D3RDPARTY_VTK_LIBRARY_DIR=/usr/lib
  cmake --build build
}

package() {
  DESTDIR=${pkgdir} cmake --install build

  #cp -r src/UnitsAPI/ "${pkgdir}/opt/${pkgname}/src"
  #install -D -m644 ${srcdir}/opencascade.conf ${pkgdir}/etc/ld.so.conf.d/opencascade.conf
  install -D -m 755 ${srcdir}/opencascade.sh ${pkgdir}/etc/profile.d/opencascade.sh
  #install -m 755 ${srcdir}/env.sh ${pkgdir}/opt/${pkgname}
  
  install -dm755 ${pkgdir}/usr/share/licenses/${pkgname}/
  install -m644 occt-fecb042/LICENSE_LGPL_21.txt  ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 occt-fecb042/OCCT_LGPL_EXCEPTION.txt ${pkgdir}/usr/share/licenses/${pkgname}
}

