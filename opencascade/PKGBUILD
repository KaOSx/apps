
pkgname=opencascade
pkgver=7.7.0
_pkgver=7_7_0
_commit=185d29b92f6764ffa9fc195b7dbe7bba3c4ac855
__commit=185d29b
_vtk=9.2
pkgrel=1
epoch=1
pkgdesc="Open CASCADE Technology, 3D modeling & numerical simulation"
arch=('x86_64')
url="https://dev.opencascade.org/"
license=('custom')
depends=('tk' 'mesa' 'java-runtime' 'libx11' 'ftgl' 'vtk' 'qt5-base' 'gl2ps')
makedepends=('java-environment' 'cmake')
options=('!libtool')
source=("opencascade-${pkgver}.tar.gz::https://git.dev.opencascade.org/gitweb/?p=occt.git;a=snapshot;h=${_commit};sf=tgz"
#source=("https://github.com/Open-Cascade-SAS/OCCT/archive/refs/tags/V${_pkgver}.tar.gz"
        'env.sh'
        'opencascade.sh'
        'opencascade.conf'
        'cmake.patch'
        "https://gitweb.gentoo.org/repo/gentoo.git/plain/sci-libs/opencascade/files/opencascade-7.7.0-add-missing-include-limits.patch"
        "https://gitweb.gentoo.org/repo/gentoo.git/plain/sci-libs/opencascade/files/opencascade-7.7.0-build-against-vtk-9.2.patch")
md5sums=('bc958cd8e8244ddfa392e851cced90e1'
         '545f3842f87750dd04569f93f1921e07'
         'fc4b64a9425c02d4daeaeb1425e0a70d'
         '2924ecf57c95d25888f51071fdc72ad0'
         '09458e891de56cfe6bdd1f1186b11a19'
         '6aeec32a4aa607d9d5ef58aa65bc27d0'
         '724bd7cb464d8f76c5a840e287cdac59')

prepare() {
  cd occt-${__commit}

  patch -p1 -i ${srcdir}/cmake.patch
  patch -p1 -i ${srcdir}/opencascade-7.7.0-add-missing-include-limits.patch
  patch -p1 -i ${srcdir}/opencascade-7.7.0-build-against-vtk-9.2.patch
  sed -i -e 's|/lib$|/lib(64)?$|' adm/templates/OpenCASCADEConfig.cmake.in
}
         
build() {
  cmake -B build -S occt-${__commit} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_RELEASE_DISABLE_EXCEPTIONS=OFF \
    -DUSE_FFMPEG=ON \
    -DUSE_VTK=ON \
    -D3RDPARTY_VTK_DIR=/usr \
    -D3RDPARTY_VTK_INCLUDE_DIR=/usr/include/vtk-${_vtk} \
    -D3RDPARTY_VTK_LIBRARY_DIR=/usr/lib
  cmake --build build
}

package() {
  DESTDIR=${pkgdir} cmake --install build

  #cp -r src/UnitsAPI/ "${pkgdir}/opt/${pkgname}/src"
  #install -D -m644 ${srcdir}/opencascade.conf ${pkgdir}/etc/ld.so.conf.d/opencascade.conf
  install -D -m 755 ${srcdir}/opencascade.sh ${pkgdir}/etc/profile.d/opencascade.sh
  #install -m 755 ${srcdir}/env.sh ${pkgdir}/opt/${pkgname}
  
  install -dm755 ${pkgdir}/usr/share/licenses/${pkgname}/
  install -m644 occt-${__commit}/LICENSE_LGPL_21.txt  ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 occt-${__commit}/OCCT_LGPL_EXCEPTION.txt ${pkgdir}/usr/share/licenses/${pkgname}
}

