
pkgname=kile
pkgver=2.9.93
_commit=f863281649aad416a9f9a352cfaea7471519103e
pkgrel=4
pkgdesc="User friendly TeX/LaTeX frontend for KDE."
arch=('x86_64')
license=('GPL2')
url="https://kile.sourceforge.io/"
depends=('kcodecs6' 'kconfig6' 'kcoreaddons6' 'kcrash6' 'kdbusaddons6' 'kguiaddons6' 'ki18n6'
         'kiconthemes6' 'kio6' 'kparts6' 'ktexteditor6' 'kwindowsystem6' 'kxmlgui6' 'ktextwidgets6'
         'texlive-core' 'docbook-xsl' 'okular' 'qt6-5compat')
makedepends=('extra-cmake-modules' 'kdoctools6')
groups=('office')
source=("https://invent.kde.org/office/kile/-/archive/${_commit}/kile-${_commit}.tar.bz2")
#source=("https://sourceforge.net/projects/kile/files/unstable/kile-3.0b3/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('8cf28a467610f28cffdf6397443df704a97f59f0ca74f833a1e7ff46bbb72225')

build() {
  cmake -B build -S ${pkgname}-${_commit} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DBUILD_TESTING=OFF \
    -DQT_MAJOR_VERSION=6
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
