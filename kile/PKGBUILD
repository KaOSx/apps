
pkgname=kile
pkgver=2.9.94
_commit=f863281649aad416a9f9a352cfaea7471519103e
pkgrel=1
pkgdesc="User friendly TeX/LaTeX frontend for KDE."
arch=('x86_64')
license=('GPL2')
url="https://kile.sourceforge.io/"
depends=('kcodecs6' 'kconfig6' 'kcoreaddons6' 'kcrash6' 'kdbusaddons6' 'kguiaddons6' 'ki18n6'
         'kiconthemes6' 'kio6' 'kparts6' 'ktexteditor6' 'kwindowsystem6' 'kxmlgui6' 'ktextwidgets6'
         'texlive-core' 'docbook-xsl' 'okular' 'qt6-5compat')
makedepends=('extra-cmake-modules' 'kdoctools6')
groups=('office')
#source=("https://invent.kde.org/office/kile/-/archive/${_commit}/kile-${_commit}.tar.bz2")
source=("https://sourceforge.net/projects/kile/files/unstable/kile-3.0b4/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('53c6742bd83fb095cbdc898b03fdf8e15ab680a81693ccaf9c5076b93ad1a3ca')

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DBUILD_TESTING=OFF \
    -DQT_MAJOR_VERSION=6
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
