
pkgname=system-config-printer
pkgver=1.5.17
_commit=340ec91a5702ccb75c6cde3a8ba634a01eab59d5
pkgrel=1
pkgdesc="A CUPS printer configuration tool and status applet"
url="https://github.com/OpenPrinting/system-config-printer"
arch=('x86_64')
license=('GPL')
depends=('pycups' 'dbus-python3' 'pysmbc' 'libusb' 'python3-pycurl' 'systemd'
         'python3-gobject3' 'dbus-glib' 'libnotify' 'libsecret')
makedepends=('intltool' 'xmlto' 'docbook-xsl' 'desktop-file-utils' 'libxml2')
optdepends=('print-manager: for the administration tool in KDE System Settings')
provides=('system-config-printer-common')
source=("https://github.com/OpenPrinting/system-config-printer/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.xz")
#source=("https://github.com/OpenPrinting/system-config-printer/archive/${_commit}.zip")
md5sums=('27b2f246dce23fbc6b9f0d768ba7bdad')

build() {
  cd ${pkgname}-${pkgver}
  #sed -i "s|cups.socket|org.cups.cupsd.socket|g" udev/configure-printer@.service.in
  #sed -i "s|from gi.repository import GnomeKeyring|#from gi.repository import GnomeKeyring|g" jobviewer.py

  ./configure --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --with-udev-rules \
    --with-udevdir=/usr/lib/udev
  make 
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install

  rm -r ${pkgdir}/usr/share/applications
  rm -r ${pkgdir}/etc/xdg
  rm -r ${pkgdir}/usr/bin/system-config*

  sed -i "s|Exec=/usr/local/bin/scp-dbus-service|Exec=/usr/bin/scp-dbus-service|g" \
    ${pkgdir}/usr/share/dbus-1/services/org.fedoraproject.Config.Printing.service
}
