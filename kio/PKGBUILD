
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=kio
pkgver=${_kf5ver}
pkgrel=3
pkgdesc='Framework that implements almost all the file management functions you will ever need'
arch=('x86_64')
url='https://invent.kde.org/frameworks/kio'
license=('LGPL')
depends=('karchive' 'kconfig' 'kcoreaddons' 'kcrash' 'kdbusaddons' 'ki18n' 'kservice' 'solid'
         'kbookmarks' 'kcompletion' 'kconfigwidgets' 'kiconthemes' 'kitemviews' 'kjobwidgets'
         'kwidgetsaddons' 'kwindowsystem' 'kauth' 'kwallet' 'knotifications' 'krb5' 'qt5-script')
makedepends=('extra-cmake-modules' 'kdoctools' 'qt5-tools')
groups=('kf5')
source=("https://download.kde.org/stable/frameworks/5.82/${pkgname}-${pkgver}.tar.xz"
        "https://invent.kde.org/frameworks/kio/-/commit/c19876052ecec18a87a82f5950e8909e22e895ba.diff")
md5sums=(`grep ${pkgname}-${_kf5ver}.tar.xz ../kf5.md5 | cut -d" " -f1`
         '113790d3e2b94145245cef2dd2c302dd')

prepare() {
  cd ${pkgname}-${pkgver}

  patch -p1 -i ${srcdir}/c19876052ecec18a87a82f5950e8909e22e895ba.diff
}
         
build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_LIBEXECDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
