
pkgname=muse
pkgver=4.1
_pkgver=4.1.0
pkgrel=1
_commit=db294aed5c1f449a2a58e3f82aff3602c95806fc
pkgdesc="MIDI/Audio sequencer with recording and editing capabilities, aims to be a complete multitrack virtual studio for Linux."
arch=('x86_64')
url='https://muse-sequencer.github.io/'
license=('GPL2')
depends=('qt5-svg' 'qt5-tools' 'dssi' 'fluidsynth' 'jack' 'ladspa' 'liblo' 'libsamplerate' 'libsndfile'
         'lv2' 'lilv' 'rubberband' 'liblrdf') 
makedepends=('cmake')
groups=('multimedia')
source=("https://github.com/muse-sequencer/muse/releases/download/${_pkgver}/${pkgname}-${pkgver}.tar.gz")
md5sums=('e378b6d95926c03a77b8d75dba547ec5')

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DENABLE_EXPERIMENTAL=OFF \
    -DENABLE_PYTHON=OFF \
    -DENABLE_LASH=OFF \
    -DENABLE_RTAUDIO=OFF \
    -DENABLE_LV2_GTK2=OFF \
    -DENABLE_INSTPATCH=OFF
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
