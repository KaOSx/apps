
pkgname=kodi
pkgver=21.0
_pkgver=21.0-Omega
pkgrel=2
pkgdesc="Open source (GPL) software media player and entertainment hub"
arch=('x86_64')
url="https://kodi.tv/"
license=('GPL' 'custom')
depends=('bzip2' 'curl' 'enca' 'faac' 'faad2' 'fontconfig' 'fribidi' 
          'glew' 'libgl' 'libmad' 'libmms' 'libass' 'yajl' 'x264' 'libcdio'
          'libmpeg2' 'libmariadbclient' 'libsamplerate' 'libpng' 'bluez'
          'libxrandr' 'libxtst' 'lzo2' 'sdl2' 'pulseaudio' 'libdvdcss'
          'samba' 'wavpack' 'mesa-demos' 'xorg-utils' 'libmicrohttpd' 'libjpeg-turbo' 'mesa' 'swig'
          'libssh' 'libva' 'libmodplug' 'lsb-release' 'sqlite3' 'libbluray' 'java-runtime'
          'tinyxml' 'tinyxml2' 'taglib' 'libcap' 'glu' 'libusb' 'hicolor-icon-theme' 'libtiff' 'avahi'
          'gnutls' 'nettle' 'libnfs' 'dcadec' 'lcms2' 'flatbuffers' 'giflib' 'fstrcmp'
          'python3-pillow' 'python3-simplejson' 'wayland-protocols' 'waylandpp' 'pipewire' 'dav1d' 'libplist'
          'libdisplay-info') # 'python-pysqlite' 'ffmpeg'
makedepends=('boost' 'cmake' 'gperf' 'nasm' 'libvdpau' 'zip' 'unzip' 'cmake'
             'doxygen' 'upower' 'ghostscript' 'googletest' 'pcre')
optdepends=('unrar: access compressed files without unpacking them'
            'upower: used to trigger suspend functionality'
            'libvdpau: for nvidia hardware acceleration'
            'tk')
groups=('multimedia')
options=('makeflags')
#source=("http://mirrors.xbmc.org/releases/source/xbmc-$pkgver.tar.gz"
source=("https://github.com/xbmc/xbmc/archive/${_pkgver}.tar.gz"
        "https://gitweb.gentoo.org/repo/gentoo.git/plain/media-tv/kodi/files/kodi-20.2-binutils-2.41.patch")
md5sums=('74501a89f0ea23d2908a9b983ab3d6f8'
         'f3b447f59ff95a19e32f98d95444025c')

prepare() {
  cd xbmc-${_pkgver}

  #patch -p1 -i ${srcdir}/kodi-20.2-binutils-2.41.patch
}

build() {
    cmake -B build -S xbmc-${_pkgver} \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=/usr/lib \
        -DENABLE_EVENTCLIENTS=OFF \
        -DENABLE_OPTICAL=OFF \
        -DENABLE_PYTHON=ON \
        -DENABLE_INTERNAL_FMT=ON \
        -DENABLE_INTERNAL_CROSSGUID=ON \
        -DENABLE_INTERNAL_FLATBUFFERS=OFF \
        -DENABLE_INTERNAL_FSTRCMP=OFF \
        -DENABLE_INTERNAL_RapidJSON=ON \
        -DENABLE_INTERNAL_FFMPEG=ON \
        -DENABLE_INTERNAL_SPDLOG=ON \
        -DENABLE_DVDCSS=OFF \
        -DAPP_RENDER_SYSTEM=gl
    cmake --build build
}

package() {

    DESTDIR=${pkgdir} cmake --install build

    # various fixes
    sed -i -e "s/lsb_release -a 2> \/dev\/null | sed -e 's\/\^\/    \/'/cat \/etc\/KaOS-release/g" ${pkgdir}/usr/bin/kodi

    install -Dm644 xbmc-${_pkgver}/LICENSE.md ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.md
}
