
pkgname=kodi
pkgver=18.9
_pkgver=18.9-Leia
pkgrel=1
pkgdesc="Open source (GPL) software media player and entertainment hub"
arch=('x86_64')
url="https://kodi.tv/"
license=('GPL' 'custom')
depends=( 'bzip2' 'curl' 'enca' 'faac' 'faad2' 'fontconfig' 'fribidi' 
          'glew' 'libgl' 'libmad' 'libmms' 'libass' 'yajl' 'x264' 'libcdio'
          'libmpeg2' 'libmariadbclient' 'libsamplerate' 'libpng' 'bluez'
          'libxrandr' 'libxtst' 'lzo2' 'sdl2' 'pulseaudio' 'libdvdcss'
          'samba' 'wavpack' 'mesa-demos' 'xorg-utils' 'libmicrohttpd' 'libjpeg-turbo' 'mesa' 'swig'
          'libssh' 'libva' 'libmodplug' 'lsb-release' 'sqlite3' 'libbluray' 'java-runtime'
          'tinyxml' 'taglib' 'libcap' 'glu' 'libusb' 'hicolor-icon-theme' 'libtiff' 'avahi'
          'gnutls' 'nettle' 'libnfs' 'dcadec' 'lcms2' 'flatbuffers' 'giflib' 'ffmpeg' 'fstrcmp') #'python2-pillow' 'python2-simplejson' 'python2' 'python-pysqlite'
makedepends=('boost' 'cmake' 'gperf' 'nasm' 'libvdpau' 'zip' 'unzip' 'cmake'
             'doxygen' 'upower' 'ghostscript')
optdepends=('unrar: access compressed files without unpacking them'
            'upower: used to trigger suspend functionality'
            'libvdpau: for nvidia hardware acceleration'
            'tk')
groups=('multimedia')
options=('makeflags')
#source=("http://mirrors.xbmc.org/releases/source/xbmc-$pkgver.tar.gz"
source=("https://github.com/xbmc/xbmc/archive/${_pkgver}.tar.gz")
md5sums=('b90fdfd3bdb9dd879debabaa09259cb3')

build() {
    mkdir -p build
    cd build
    
    cmake ../xbmc-${_pkgver} \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=/usr/lib \
        -DENABLE_EVENTCLIENTS=OFF \
        -DENABLE_OPTICAL=OFF \
        -DENABLE_PYTHON=OFF \
        -DENABLE_INTERNAL_FMT=ON \
        -DENABLE_INTERNAL_CROSSGUID=ON \
        -DENABLE_INTERNAL_FLATBUFFERS=OFF \
        -DENABLE_INTERNAL_FSTRCMP=OFF \
        -DENABLE_INTERNAL_RapidJSON=ON \
        -DENABLE_INTERNAL_FFMPEG=OFF \
        -DENABLE_DVDCSS=OFF
    make -j4 VERBOSE=1
  
    # use when switched to cmake build
    #make -C ../xbmc-${_pkgver}/tools/depends/target/binary-addons PREFIX=/usr
}

package() {
    cd build

    make DESTDIR=${pkgdir} install

    # various fixes
    sed -i -e "s/lsb_release -a 2> \/dev\/null | sed -e 's\/\^\/    \/'/cat \/etc\/KaOS-release/g" ${pkgdir}/usr/bin/kodi

    install -Dm644 ../xbmc-${_pkgver}/LICENSE.md ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.md
    
    # move from xbmc to kodi leaves conflicting symlinks in place of real files, pacman not handling it
    #rm -r ${pkgdir}/usr/share/xbmc
    #rm -r ${pkgdir}/usr/lib/xbmc
}
