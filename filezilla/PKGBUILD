
pkgname=filezilla
_pkgname=FileZilla
pkgver=3.34.0
pkgrel=1
pkgdesc="Fast and reliable FTP, FTPS and SFTP client."
arch=('x86_64')
url="https://filezilla-project.org/"
license=('GPL2')
depends=('dbus' 'hicolor-icon-theme' 'libidn' 'libjpeg-turbo' 'xdg-utils' 'wxgtk' 'sqlite'
         'libfilezilla' 'pugixml' 'gnutls')
source=("https://dl3.cdn.filezilla-project.org/client/${_pkgname}_${pkgver}_src.tar.bz2"
        'filezilla.desktop')
sha512sums=('577118bd053c3aedbe77082b138c30abc6a759b20ea905abc4e9bef965cbca3cf73023186c389ded7d906b7cf76e4f3d4462c00fae62c7b1ae695ad171c88764'
            'a9d36573bc30a476e75f33f04f8aef990bdb03a151e7a4b72cdc69c351fc7313da8a84fc2e43383d3945b78a510d01b31a820c3615a185ead3343606285c893b')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr \
    --disable-manualupdatecheck \
    --disable-autoupdatecheck \
    --disable-static \
    --with-pugixml=system
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  
  rm ${pkgdir}/usr/share/applications/filezilla.desktop
  install -D -m644 ${srcdir}/filezilla.desktop ${pkgdir}/usr/share/applications/filezilla.desktop
}
