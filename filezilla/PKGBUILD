
pkgname=filezilla
_pkgname=FileZilla
pkgver=3.65.0rc1
_pkgver=3.65.0-rc1
pkgrel=1
pkgdesc="Fast and reliable FTP, FTPS and SFTP client."
arch=('x86_64')
url="https://filezilla-project.org/"
license=('GPL2')
depends=('dbus' 'hicolor-icon-theme' 'libidn' 'libjpeg-turbo' 'xdg-utils' 'sqlite'
         'libfilezilla' 'pugixml' 'gnutls' 'nettle' 'wxwidgets' 'gdk-pixbuf2')
source=("https://download.filezilla-project.org/client/${_pkgname}_${_pkgver}_src.tar.xz"
        'filezilla.desktop'
        "https://raw.githubusercontent.com/archlinux/svntogit-community/packages/filezilla/trunk/wxwidgets-3.2.patch")
sha512sums=('f707af2dcc768def8147282caff1c37207b71a7dd2347b8d0bb723957ebe67508ea49c165bde83aa629ac775fa522a7a9d58023c37834b01cc5a8c51fee07acd'
            'a9d36573bc30a476e75f33f04f8aef990bdb03a151e7a4b72cdc69c351fc7313da8a84fc2e43383d3945b78a510d01b31a820c3615a185ead3343606285c893b'
            '0f92bd0180830fdb54cfb2eff2b6ebb3413aaace2b901300ca2f192b5abffea28f511eb26dea4eb8fb2ff8b951d15b4c39bef77cad59542b9f3632bbddd79183')

build() {
  cd ${pkgname}-${_pkgver}
  #patch -p1 -i ${srcdir}/wxwidgets-3.2.patch

  autoreconf -vi
  ./configure --prefix=/usr \
    --disable-manualupdatecheck \
    --disable-autoupdatecheck \
    --disable-static \
    --with-pugixml=system
  make
}

package() {
  cd ${pkgname}-${_pkgver}
  make DESTDIR=${pkgdir} install
  
  rm ${pkgdir}/usr/share/applications/filezilla.desktop
  install -D -m644 ${srcdir}/filezilla.desktop ${pkgdir}/usr/share/applications/filezilla.desktop
}
