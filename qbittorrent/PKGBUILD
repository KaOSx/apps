
pkgname=qbittorrent
pkgver=4.6.4
_commit=46e2e5342bb5b6f7a479f9ec7746e58d28ff783e
pkgrel=2
pkgdesc="Bittorrent client programmed in C++ / Qt that uses libtorrent (sometimes called libtorrent-rasterbar)"
arch=('x86_64')
url="https://www.qbittorrent.org/"
license=('GPL')
depends=('qt6-svg' 'libtorrent-rasterbar' 'xdg-utils' 'boost-libs' 'python3')
makedepends=('boost' 'which' 'qt6-tools' 'cmake')
groups=('network')
source=("https://sourceforge.net/projects/qbittorrent/files/${pkgname}/${pkgname}-${pkgver}/${pkgname}-${pkgver}.tar.xz"
        "https://github.com/qbittorrent/qBittorrent/commit/029b1c755057343c9e095bfb4e5c1d41fede70ae.diff")
#source=("https://github.com/qbittorrent/qBittorrent/archive/${_commit}.zip")
sha256sums=('8e62a24145582a0b36e8268a2e574c5d61a396d28a7d02b899ca59f2244a8913'
            '9da44f287529563e20b5b0db1b6eebdcdc9854db2b6c435cec8fa497b1dcb013')

prepare() {
  cd ${pkgname}-${pkgver}

  patch -p1 -i ${srcdir}/029b1c755057343c9e095bfb4e5c1d41fede70ae.diff
}

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DQT6=ON
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
