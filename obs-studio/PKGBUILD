
pkgname=obs-studio
pkgver=30.0.2
pkgrel=1
pkgdesc="Free and open source software for video recording and live streaming."
arch=('x86_64')
url="https://obsproject.com/"
license=('GPL2')
depends=('ffmpeg' 'jansson' 'libxinerama' 'libxkbcommon' 'qt6-svg' 'qt6-wayland'
         'libxcomposite' 'x264' 'pulseaudio' 'vlc' 'jack2' 'mbedtls' 'pipewire'
         'python3')
makedepends=('cmake' 'swig')
source=("https://github.com/obsproject/obs-studio/archive/${pkgver}.tar.gz"
        'disable-some-plugins.patch')
md5sums=('143181a6ff673326ca68533e2bcb4352'
         '1487ec1fecad66b2e7b6fb4aa6943df8')

prepare() {
  cd ${pkgname}-${pkgver}

  patch -p1 -i ${srcdir}/disable-some-plugins.patch
  #sed -i "s|add_subdirectory(obs-outputs)|#add_subdirectory(obs-outputs)|g" plugins/CMakeLists.txt
}

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DUNIX_STRUCTURE=1 \
    -DOBS_VERSION_OVERRIDE=${pkgver} \
    -DWITH_RTMPS=ON \
    -DBUILD_VST=OFF \
    -DENABLE_NEW_MPEGTS_OUTPUT=OFF \
    -DENABLE_SCRIPTING_LUA=OFF \
    -DENABLE_SCRIPTING_PYTHON=ON \
    -DENABLE_PLUGINS=ON \
    -DENABLE_JACK=ON \
    -DENABLE_QSV11=OFF \
    -DENABLE_BROWSER=OFF \
    -DENABLE_WEBRTC=OFF
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
