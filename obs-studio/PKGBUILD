
pkgname=obs-studio
_pkgname=OBS-Studio
pkgver=30.2.2
pkgrel=1
pkgdesc="Free and open source software for video recording and live streaming."
arch=('x86_64')
url="https://obsproject.com/"
license=('GPL2')
depends=('ffmpeg' 'jansson' 'libxinerama' 'libxkbcommon' 'qt6-svg' 'qt6-wayland'
         'libxcomposite' 'x264' 'pulseaudio' 'jack2' 'mbedtls' 'pipewire'
         'python3')
makedepends=('cmake' 'swig' 'uthash' 'nlohmann-json')
source=("https://github.com/obsproject/obs-studio/releases/download/${pkgver}/OBS-Studio-${pkgver}-Sources.tar.gz"
        'disable-some-plugins.patch'
        'buildnumber.cmake')
md5sums=('bcc022a036306a7ec974b2bbe33ebe20'
         '1c83ef8f37f4fffd418c6937d856eca0'
         '31b99f3f5e9b36af1518b7a8f1ed48a4')

prepare() {
  cd ${pkgname}-${pkgver}-sources

  #patch -p1 -i ${srcdir}/disable-some-plugins.patch
  #sed -i "s|add_subdirectory(obs-outputs)|#add_subdirectory(obs-outputs)|g" plugins/CMakeLists.txt
  cp ${srcdir}/buildnumber.cmake cmake/common/
}

build() {
  cmake -B build -S ${pkgname}-${pkgver}-sources \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DUNIX_STRUCTURE=1 \
    -DOBS_CMAKE_VERSION=3 \
    -DOBS_VERSION_OVERRIDE=${pkgver} \
    -DWITH_RTMPS=ON \
    -DBUILD_VST=OFF \
    -DENABLE_NEW_MPEGTS_OUTPUT=OFF \
    -DENABLE_SCRIPTING_LUA=OFF \
    -DENABLE_SCRIPTING_PYTHON=ON \
    -DENABLE_JACK=ON \
    -DENABLE_QSV11=OFF \
    -DENABLE_BROWSER=OFF \
    -DENABLE_WEBRTC=OFF \
    -DENABLE_WEBSOCKET=OFF \
    -DENABLE_VLC=OFF \
    -DENABLE_AJA=OFF \
    -DENABLE_WHATSNEW=OFF \
    -DENABLE_NATIVE_NVENC=OFF
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
