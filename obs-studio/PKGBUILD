
pkgname=obs-studio
pkgver=29.1.3
pkgrel=2
pkgdesc="Free and open source software for video recording and live streaming."
arch=('x86_64')
url="https://obsproject.com/"
license=('GPL2')
depends=('ffmpeg' 'jansson' 'libxinerama' 'libxkbcommon' 'qt6-svg' 'qt6-wayland'
         'libxcomposite' 'x264' 'pulseaudio' 'vlc' 'jack2' 'mbedtls' 'pipewire'
         'python3')
makedepends=('cmake' 'swig')
source=("https://github.com/obsproject/obs-studio/archive/${pkgver}.tar.gz"
        'disable-some-plugins.patch')
md5sums=('5597636f9c66342566f47d68aa4c6693'
         '21223b4d96e9094284e85fc91790b993')

prepare() {
  cd ${pkgname}-${pkgver}

  patch -p1 -i ${srcdir}/disable-some-plugins.patch
  #sed -i "s|add_subdirectory(obs-outputs)|#add_subdirectory(obs-outputs)|g" plugins/CMakeLists.txt
}

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DUNIX_STRUCTURE=1 \
    -DOBS_VERSION_OVERRIDE=${pkgver} \
    -DWITH_RTMPS=ON \
    -DBUILD_VST=OFF \
    -DENABLE_NEW_MPEGTS_OUTPUT=OFF \
    -DENABLE_SCRIPTING_LUA=OFF \
    -DENABLE_SCRIPTING_PYTHON=ON \
    -DENABLE_PLUGINS=ON \
    -DENABLE_JACK=ON
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
