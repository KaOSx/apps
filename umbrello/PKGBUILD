
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=umbrello
pkgver=${_kdever}
pkgrel=1
pkgdesc='UML modeling tool for KDE'
url='https://apps.kde.org/umbrello/'
arch=('x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('karchive' 'kauth' 'kcompletion' 'kconfig' 'kcoreaddons' 'kcrash' 'kguiaddons' 'ki18n' 'kiconthemes'
         'kio' 'kparts' 'kservice' 'ktexteditor' 'kwindowsystem' 'kwidgetsaddons' 'kxmlgui')
makedepends=('extra-cmake-modules' 'kdoctools' 'boost') # 'clang')
groups=('kde' 'kde-sdk' 'kde-uninstall' 'programming' )
source=("$_mirror/${pkgname}-$_kdever.tar.xz")
md5sums=(`grep ${pkgname}-${_kdever}.tar.xz ../kde-sc.md5 | cut -d" " -f1`)

prepare() {
  cd ${pkgname}-${pkgver}
  sed -i -e 's|ecm_optional_add_subdirectory(unittests)|#ecm_optional_add_subdirectory(unittests)|' CMakeLists.txt
}

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF \
    -DBUILD_UNITTESTS=0
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}

