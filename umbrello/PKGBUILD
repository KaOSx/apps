
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=umbrello
pkgver=${_kdever}
pkgrel=1
pkgdesc='UML modeling tool for KDE'
url='https://kde.org/applications/development/org.kde.umbrello'
arch=('x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('karchive' 'kauth' 'kcompletion' 'kconfig' 'kcoreaddons' 'kguiaddons' 'ki18n' 'kiconthemes' 'kinit'
         'kio' 'kparts' 'kservice' 'ktexteditor' 'kwindowsystem' 'kwidgetsaddons' 'kxmlgui' 'qt5-webkit' 'kdelibs4support')
makedepends=('extra-cmake-modules' 'kdoctools' 'boost') # 'clang')
groups=('kde' 'kde-sdk' 'kde-uninstall' 'programming' )
source=("$_mirror/${pkgname}-$_kdever.tar.xz")
md5sums=(`grep ${pkgname}-${_kdever}.tar.xz ../kde-sc.md5 | cut -d" " -f1`)

prepare() {
  cd ${pkgname}-${pkgver}
  sed -i -e 's|ecm_optional_add_subdirectory(unittests)|#ecm_optional_add_subdirectory(unittests)|' CMakeLists.txt
}

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF \
    -DBUILD_UNITTESTS=0 \
    -DBUILD_KF5=ON
  make
}

package() {
  cd build

  make DESTDIR=${pkgdir} install
}

