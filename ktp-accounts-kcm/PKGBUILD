
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=ktp-accounts-kcm
pkgver=${_kdever}
pkgrel=2
pkgdesc="KCM Module for configuring Telepathy Instant Messaging Accounts"
arch=('x86_64')
url="https://invent.kde.org/network/ktp-accounts-kcm"
license=('GPL')
depends=('kcmutils' 'kio' 'ki18n' 'kwidgetsaddons' 'kcoreaddons' 'kconfigwidgets' 'kiconthemes'
         'kitemviews' 'ktp-common-internals' 'kaccounts-providers'
         'telepathy-gabble' 'telepathy-haze' 'telepathy-morse')
makedepends=('extra-cmake-modules' 'kdoctools' 'git' 'boost' 'intltool')
groups=('kde-telepathy')
source=("${_mirror}/${pkgname}-$_kdever.tar.xz")
md5sums=(`grep ${pkgname}-$_kdever.tar.xz ../kde-sc.md5 | cut -d" " -f1`)

prepare() {
  cd ${pkgname}-${pkgver}
  sed -i -e 's|add_subdirectory (sipe)|#add_subdirectory (sipe)|' plugins/CMakeLists.txt
}

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_QMLDIR=/usr/lib/qt5/qml \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF 
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
  # remove sipe protocol from accounts view
  rm ${pkgdir}/usr/share/accounts/providers/kde/ktp-sipe.provider
  rm ${pkgdir}/usr/share/accounts/providers/kde/ktp-sipe-haze.provider
}
