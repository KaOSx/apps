
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=ktp-accounts-kcm
pkgver=${_kdever}
pkgrel=1
pkgdesc="KCM Module for configuring Telepathy Instant Messaging Accounts"
arch=('x86_64')
url="https://github.com/KDE/ktp-accounts-kcm"
license=('GPL')
depends=('kcmutils' 'kio' 'ki18n' 'kwidgetsaddons' 'kcoreaddons' 'kconfigwidgets' 'kiconthemes'
         'kitemviews' 'ktp-common-internals' 'kaccounts-providers'
         'telepathy-gabble' 'telepathy-haze' 'telepathy-morse')
makedepends=('extra-cmake-modules' 'kdoctools' 'git' 'boost' 'intltool')
groups=('kde-telepathy')
conflicts=('kf5-ktp-accounts-kcm')
replaces=('kf5-ktp-accounts-kcm')
source=("$_mirror/${pkgname}-$_kdever.tar.xz")
md5sums=(`grep ${pkgname}-$_kdever.tar.xz ../kde-sc.md5 | cut -d" " -f1`)

prepare() {
  cd ${pkgname}-${pkgver}
  sed -i -e 's|add_subdirectory (sipe)|#add_subdirectory (sipe)|' plugins/CMakeLists.txt
}

build() {
  mkdir -p build
  cd build

  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DSYSCONF_INSTALL_DIR=/etc \
    -DQML_INSTALL_DIR=/usr/lib/qt5/qml \
    -DPLUGIN_INSTALL_DIR=/usr/lib/qt5/plugins \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF 
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
  # remove sipe protocol from accounts view
  rm ${pkgdir}/usr/share/accounts/providers/kde/ktp-sipe.provider
  rm ${pkgdir}/usr/share/accounts/providers/kde/ktp-sipe-haze.provider
}
