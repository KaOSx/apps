

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=plasma-nm
pkgver=${_plasmaver}
pkgrel=2
pkgdesc="Plasma applet written in QML for managing network connections"
arch=('x86_64')
url="https://invent.kde.org/plasma/plasma-nm"
license=('GPL')
depends=('modemmanager-qt' 'networkmanager-qt' 'ki18n' 'kwindowsystem' 'kservice' 'kcompletion' 'kwidgetsaddons'
         'kio' 'kcoreaddons' 'kwallet' 'kitemviews' 'kxmlgui' 'kconfigwidgets' 'kiconthemes' 'solid' 'prison'
         'kdbusaddons' 'knotifications' 'kdeclarative' 'kinit' 'plasma-framework' 
         'mobile-broadband-provider-info' 'openconnect' 'qca-qt5')
makedepends=('extra-cmake-modules' 'kdoctools' 'qt5-tools')
groups=('kdeplasma5')
source=("https://download.kde.org/stable/plasma/${pkgver}/${pkgname}-${pkgver}.tar.xz")
md5sums=(`grep ${pkgname}-${_plasmaver}.tar.xz ../kf5.md5 | cut -d" " -f1`)

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
        -DCMAKE_BUILD_TYPE=Release \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_QMLDIR=/usr/lib/qt5/qml \
        -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
        -DBUILD_TESTING=OFF ..
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
