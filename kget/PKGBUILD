
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=kget
pkgver=${_kdever}
pkgrel=1
pkgdesc='Download Manager'
url='https://apps.kde.org/kget/'
arch=('x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-network')
depends=('kcompletion6' 'kconfig6' 'kconfigwidgets6' 'kcoreaddons6' 'kcrash6' 'kdbusaddons6' 'ki18n6' 'kiconthemes6'
         'kitemviews6' 'kcmutils6' 'kio6' 'knotifications6' 'knotifyconfig6' 'kstatusnotifieritem'
         'kparts6' 'ktextwidgets6' 'kwallet6' 'kwidgetsaddons6' 'kwindowsystem6' 'kxmlgui6'
         'libmms' 'gpgme-qt6' 'libktorrent')
makedepends=('extra-cmake-modules' 'kdoctools6' 'boost')
source=("$_mirror/${pkgname}-${_kdever}.tar.xz")
md5sums=(`grep ${pkgname}-${_kdever}.tar.xz ../kde-sc.md5 | cut -d" " -f1`)

prepare() {
  cd ${pkgname}-${pkgver}

  sed -i -e 's|KF6::ConfigWidgets|KF6::ConfigWidgets KF6::KCMUtils|' transfer-plugins/mmsthreads/CMakeLists.txt
}

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF 
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}

