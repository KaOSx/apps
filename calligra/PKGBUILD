
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgbase=calligra
pkgname=('calligra' 'calligra-l10n')
pkgver=${_kdever}
pkgrel=1
arch=('x86_64')
pkgdesc="Suite is a collection of office applications linked together by a common base."
url='https://www.calligra-suite.org'
license=('FDL1.2' 'GPL2' 'LGPL')
makedepends=('karchive6' 'kcompletion6' 'kconfig6' 'kconfigwidgets6' 'kcoreaddons6' 'kcrash6' 'kdbusaddons6'
             'kguiaddons6' 'ki18n6' 'kiconthemes6' 'kitemviews6' 'kjobwidgets6' 'kcmutils6' 'kio6' 'knotifications6'
             'knotifyconfig6' 'sonnet6' 'ktextwidgets6' 'kwidgetsaddons6' 'kwindowsystem6' 'kxmlgui6'
             'eigen3' 'freetds' 'libvisio' 'libgsf' 'libwpd' 'libwpg' 'libwps' 'pstoedit' 'gsl' 
             'lcms2' 'libodfgen' 'fftw' 'hicolor-icon-theme' 'libgit2'
             'sqlite3' 'libkdcraw6' 'qt6-declarative' 'qt6-svg' 'qt6-webengine' 'qtkeychain-qt6' 'kdiagram'
             'openexr' 'vc' 'akonadi-contacts' 'poppler' 'okular'
             'extra-cmake-modules' 'kdoctools6' 'boost' 'docbook-xsl') # 'clang' vc fails clang  'kreport' 'kcalendarcore6' 'kcontacts6'
source=("$_mirror/${pkgname}-${_kdever}.tar.xz") 
md5sums=(`grep ${pkgname}-${_kdever}.tar.xz ../kde-sc.md5 | cut -d" " -f1`)

prepare() {
  cd ${pkgbase}-${pkgver}
  #patch -p1 -i ${srcdir}/eab11436712f92afc6e7b14b25d1f6df217d79d7.diff
  #sed -i 's|add_subdirectory( import )|#add_subdirectory( import )|' filters/words/wordperfect/CMakeLists.txt
}

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=FALSE \
    -DPACKAGERS_BUILD=true \
    -DBUILD_UNMAINTAINED=FALSE \
    -DQT_MAJOR_VERSION=6
  cmake --build build
}

package_calligra() {
  pkgdesc="Suite is a collection of office applications linked together by a common base."
  depends=('karchive6' 'kcompletion6' 'kconfig6' 'kconfigwidgets6' 'kcoreaddons6' 'kcrash6' 'kdbusaddons6'
           'kguiaddons6' 'ki18n6' 'kiconthemes6' 'kitemviews6' 'kjobwidgets6' 'kcmutils6' 'kio6' 'knotifications6'
           'knotifyconfig6' 'sonnet6' 'ktextwidgets6' 'kwidgetsaddons6' 'kwindowsystem6' 'kxmlgui6'
           'eigen3' 'freetds' 'libvisio' 'libgsf' 'libwpd' 'libwpg' 'libwps' 'pstoedit' 'gsl' 
           'lcms2' 'libodfgen' 'fftw' 'hicolor-icon-theme' 'sqlite3' 'libkdcraw6'
           'exiv2' 'qt6-declarative' 'kdiagram' 'okular' 'poppler'
           'openexr' 'vc' 'akonadi-contacts') #  'kcalendarcore6' 'kcontacts6'  'kreport'

  DESTDIR=${pkgdir} cmake --install build

  #rm ${pkgdir}/usr/share/applications/calligra_filter_odt2docx.desktop
  #use in calligra-l10n
  mv ${pkgdir}/usr/share/locale ${srcdir}/
}

package_calligra-l10n() {
  pkgdesc="Localization for Calligra"
  depends=('ki18n6')
  groups=('localization')

  install -d -m755 ${pkgdir}/usr/share/locale
  mv ${srcdir}/locale ${pkgdir}/usr/share
}
