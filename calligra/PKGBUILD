
pkgbase=calligra
pkgname=('calligra' 'calligra-l10n')
pkgver=3.1.0
pkgrel=9
arch=('x86_64')
pkgdesc="Suite is a collection of office applications linked together by a common base."
url='https://www.calligra-suite.org'
license=('FDL1.2' 'GPL2' 'LGPL')
makedepends=('karchive' 'kcodecs' 'kconfig' 'kcoreaddons' 'kdbusaddons' 'kguiaddons' 'ki18n' 'kitemviews' 'kwidgetsaddons'
         'threadweaver' 'kcompletion' 'kiconthemes' 'kparts' 'kxmlgui' 'kross' 'kconfigwidgets' 'ktexteditor' 
         'kdelibs4support' 'kio' 'knotifyconfig' 'knotifications' 'ktextwidgets'
         'eigen3' 'freetds' 'libvisio' 'libgsf' 'libwpd' 'libwpg' 'libwps' 'pstoedit' 'gsl' 
         'lcms2' 'libodfgen' 'poppler' 'libspnav' 'fftw' 'hicolor-icon-theme' 
         'sqlite3' 'libkdcraw' 'exiv2' 'qca-qt5' 'qt5-declarative' 'marble' 'kactivities' 'kdiagram' 'kdeclarative' 'kcalcore'
         'kcontacts' 'akonadi-contacts' 'kholidays' 'okular' 'openexr' 'vc' 'kreport' 'qt5-webkit'
         'extra-cmake-modules' 'kdoctools' 'boost' 'docbook-xsl') # 'clang' vc fails clang
source=("https://download.kde.org/stable/${pkgbase}/${pkgver}/${pkgbase}-${pkgver}.tar.xz"
        'qt5.11.diff'
        "https://github.com/KDE/calligra/commit/ee83e0f2c251072e47a2799619cdc79efe67e651.diff"
        "https://phabricator.kde.org/file/data/vx2azt6a2bivrayd3zzz/PHID-FILE-35m3uqi7zi5g7gubh27w/D15403.diff")
#source=("https://share.kde.org/index.php/s/yanrJWiQFB3rrvc/download?path=%2F&files=${pkgbase}-${pkgver}.tar.xz")
md5sums=('9016615f62fc75bb44aa139a231c3be4'
         '55bb5393ce7d24ae3f33285b571934ae'
         '6383d2ee97037661374a6305a5c4a7fe'
         'c4dd370efc79834db2ab0258a7d0f902')

prepare() {
  cd ${pkgbase}-${pkgver}
  patch -p1 -i ${srcdir}/qt5.11.diff
  patch -p1 -i ${srcdir}/ee83e0f2c251072e47a2799619cdc79efe67e651.diff
  patch -p1 -i ${srcdir}/D15403.diff
  #sed -i 's|KReport 3.0.0 EXACT QUIET|KReport 3.0 QUIET|' CMakeLists.txt
  #sed -i 's|KPropertyWidgets 3.0.0 EXACT QUIET|KPropertyWidgets 3.0 QUIET|' CMakeLists.txt
}

build() {
  mkdir -p build
  cd build
  
  #export CC=/usr/bin/clang
  #export CXX=/usr/bin/clang++
  
  cmake ../${pkgbase}-${pkgver} \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DPRODUCTSET=DESKTOP \
    -DBUILD_TESTING=FALSE \
    -DPACKAGERS_BUILD=true 
    #-DBUILD_UNMAINTAINED=TRUE
  make
}

package_calligra() {
  pkgdesc="Suite is a collection of office applications linked together by a common base."
  depends=('karchive' 'kcodecs' 'kconfig' 'kcoreaddons' 'kdbusaddons' 'kguiaddons' 'ki18n' 'kitemviews' 'kwidgetsaddons'
         'threadweaver' 'kcompletion' 'kiconthemes' 'kparts' 'kxmlgui' 'kross' 'kconfigwidgets' 'ktexteditor' 
         'kdelibs4support' 'kio' 'knotifyconfig' 'knotifications' 'ktextwidgets'
         'eigen3' 'freetds' 'libvisio' 'libgsf' 'libwpd' 'libwpg' 'libwps' 'pstoedit' 'gsl' 
         'lcms2' 'libodfgen' 'poppler' 'libspnav' 'fftw' 'hicolor-icon-theme' 
         'sqlite3' 'libkdcraw' 'exiv2' 'qca-qt5' 'qt5-declarative' 'marble' 'kactivities' 'kdiagram' 'kdeclarative' 'kcalcore'
         'kcontacts' 'akonadi-contacts' 'kholidays' 'okular' 'openexr' 'vc' 'kreport' 'qt5-webkit')
         
  cd build
  
  make DESTDIR=${pkgdir} install
  
  #rm ${pkgdir}/usr/share/applications/calligra_filter_odt2docx.desktop
  #use in calligra-l10n
  mv ${pkgdir}/usr/share/locale ${srcdir}/
}

package_calligra-l10n() {
  pkgdesc="Localization for Calligra"
  depends=('ki18n')
  groups=('localization')
  
  install -d -m755 ${pkgdir}/usr/share/locale
  mv ${srcdir}/locale ${pkgdir}/usr/share
}
