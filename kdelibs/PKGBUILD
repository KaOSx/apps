#
# KDE SC Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org
# maintainer Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname="kdelibs"
arch=('x86_64')
pkgver=${_kdever}
pkgrel=1
pkgdesc="KDE Core Libraries"
url="http://www.kde.org"
license=('GPL' 'LGPL' 'FDL')
options=('docs' '!splithdr' 'splitdbg' 'log')
depends=("${_qtpkg}>=${_qtver}" "kde-common>=${_kdever}" "oxygen-icons>=${_kdever}" 
	    'shared-mime-info' 'upower' 'udisks<2.0' 'xz>=5.0.0' 'enchant' 'jasper' 'openexr' 'giflib'
	    'strigi' 'libxtst' 'soprano>=2.8' 'ca-certificates' 'xdg-utils' 'qca' 'polkit-qt>=0.98.1'
	    'libxss' 'phonon' 'shared-desktop-ontologies>=0.7' 'attica>=0.2.0' 'krb5' 'libxcursor' 'libutempter'
	    'hicolor-icon-theme' 'libdbusmenu-qt' 'grantlee>=0.3.0' 'media-player-info' 'qtwebkit' 'systemd')
makedepends=('pkg-config' 'cmake' 'automoc4' 'intltool' 'avahi' 'libgl' 'aspell' 'hspell' 'shared-mime-info' 
             'docbook-xsl' 'docbook-xml' 'bzip2>=1.0.6' 'libzip')
optdepends=('phonon-gstreamer: GStreamer Phonon backend (default)'
	    'phonon-xine:      Xine Phonon backend'
	    'phonon-mplayer:   MPlayer Phonon backend'
	    'phonon-vlc:       VLC Phonon backend'
	    'avahi:            Service discovery on your local network'
	    'aspell:           Spell checking'
	    'hspell:           Hebrew spell checking')
provides=("${pkgname}=${_kdever}" "kdelibs-doc")
replaces=('kdelibs-experimental' 'arts' 'kdelibs-doc')
groups=("kde" "kde-minimal" "kde-uninstall")
conflicts=("kdelibs-doc")
install=${pkgname}.install
source=($_mirror/${pkgname}-$_kdever.tar.xz
	01_chakra_tag.patch
	02_kde_applications_menu.patch
	03_chakra_menu.patch
	MergeDir.patch)
md5sums=(`grep ${pkgname}-$_kdever.tar.xz ../kde-sc.md5 | cut -d" " -f1`
         'e9ac43de85dc73309ab6c89ebcf5dc50'   # 01_chakra_tag.patch
         'e94450ba5430ea9c1e33bad9ae38ca2d'   # 02_kde_applications_menu.patch
         '53b85403e4dd8f9146933c52ca23243e'   # 03_chakra_menu.path
         'f442698bae9d4ffb26f6df9457d7fc5a')  # MergeDir.patch
        
build() {
	# main patches
	msg "applying main patchset ..."
	cd ${srcdir}/${pkgname}-${pkgver}
	patch -Np1 -i ${srcdir}/01_chakra_tag.patch
	patch -Np1 -i ${srcdir}/02_kde_applications_menu.patch
	patch -Np1 -i ${srcdir}/03_chakra_menu.patch
	# right positioning of applications' entries in kmenu
	patch -p1 -i ${srcdir}/MergeDir.patch

        cd ..
	rm -rf build
	mkdir -p build && cd build

	cmake ../${pkgname}-${pkgver} \
		-DCMAKE_BUILD_TYPE=${_build_type} \
		-DCMAKE_INSTALL_PREFIX=${_installprefix} \
		-DSYSCONF_INSTALL_DIR=/etc \
		-DHTML_INSTALL_DIR=/usr/share/doc/kde/html \
		-DKDE_DISTRIBUTION_TEXT='Chakra ' \
		-DKDE_DEFAULT_HOME='.kde4' \
		-DWITH_FAM=OFF \
                -DWITH_HUpnp=OFF \
		-DCMAKE_SKIP_RPATH=ON \
		-DWITH_SOLID_UDISKS2=OFF \
		-DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed'

	make VERBOSE=1
}

package() {
	cd ${srcdir}/build

	make DESTDIR=${pkgdir} install

	# link cert bundle to the one from ca-certificates
	rm -f ${pkgdir}/usr/share/apps/kssl/ca-bundle.crt
	ln -sf /etc/ssl/certs/ca-certificates.crt ${pkgdir}/usr/share/apps/kssl/ca-bundle.crt
}
