
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=sink
pkgver=0.9.0
pkgrel=4
pkgdesc="Data access layer handling synchronization, caching and indexing."
arch=('x86_64')
url='https://invent.kde.org/pim/sink/'
license=('LGPL')
depends=('kmime' 'kcontacts' 'kcalendarcore' 'kasync' 'lmdb' 'kdav2' 'kimap2' 'curl'
         'xapian-core' 'libgit2')
makedepends=('extra-cmake-modules' 'flatbuffers')
#source=("https://download.kde.org/unstable/sink/${pkgver}/src/${pkgname}-${pkgver}.tar.xz")
source=("https://invent.kde.org/pim/sink/-/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.bz2")
sha256sums=('2b1a363a7464f5b309e08493c005f3dfef7493a16720f95842eebcce8767f0da')

prepare() {
  cd ${pkgname}-v${pkgver}

  sed -i -e 's|add_subdirectory(tests)|#add_subdirectory(tests)|' CMakeLists.txt
  sed -e '/CMAKE_CXX_STANDARD/d' -i CMakeLists.txt
  sed -i -e 's|add_subdirectory(tests)|#add_subdirectory(tests)|' examples/imapresource/CMakeLists.txt
  sed -i -e 's|add_subdirectory(tests)|#add_subdirectory(tests)|' examples/maildirresource/CMakeLists.txt
  sed -i -e 's|add_subdirectory(tests)|#add_subdirectory(tests)|' examples/mailtransportresource/CMakeLists.txt
  sed -i -e 's|add_subdirectory(tests)|#add_subdirectory(tests)|' examples/caldavresource/CMakeLists.txt
  sed -i -e 's|add_subdirectory(tests)|#add_subdirectory(tests)|' examples/carddavresource/CMakeLists.txt
}

build() {
  cmake -B build -S ${pkgname}-v${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_QMLDIR=/usr/lib/qt5/qml \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins 
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
