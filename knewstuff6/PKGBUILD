
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=knewstuff6
_pkgname=knewstuff
pkgver=${_kf6ver}
pkgrel=1
pkgdesc='Framework for downloading and sharing additional application data'
arch=('x86_64')
url='https://invent.kde.org/frameworks/knewstuff'
license=('LGPL')
depends=('karchive6' 'kcompletion6' 'kconfig6' 'kcoreaddons6' 'ki18n6' 'kiconthemes6' 'kio6'
         'kitemviews6' 'kpackage6' 'kservice6' 'ktextwidgets6' 'kwidgetsaddons6' 'kxmlgui6'
         'attica' 'kirigami6' 'syndication6')
makedepends=('extra-cmake-modules' 'qt6-tools')
groups=('kf6')
source=("https://download.kde.org/stable/frameworks/5.99/${_pkgname}-${pkgver}.tar.xz")
md5sums=(`grep ${_pkgname}-${_kf6ver}.tar.xz ../kf6.md5 | cut -d" " -f1`)

prepare () {
  cd ${_pkgname}-${pkgver}

  sed -e "s|activeBackgroundColor:|//activeBackgroundColor:|" -i src/qtquick/qml/DownloadItemsSheet.qml
  sed -e "s|activeTextColor:|//activeTextColor:|" -i src/qtquick/qml/DownloadItemsSheet.qml
}

build() {
  cmake -B build -S ${_pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
