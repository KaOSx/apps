
pkgname=qt-jpegxl-image-plugin
pkgver=0.6.3
_commit=1ed974e2f1a9e79939eb2f7660a0b4f65d812dc8
pkgrel=2
pkgdesc='Qt plug-in to allow Qt and KDE based applications to read/write JXL images'
arch=('x86_64')
url="https://github.com/novomesk/qt-jpegxl-image-plugin"
license=('GPL3')
depends=('qt6-base' 'jpeg-xl')
makedepends=('extra-cmake-modules')
source=("https://github.com/novomesk/qt-jpegxl-image-plugin/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('56b263b2f16bf85e5cfb4b67f2017c224aba57e6e7aa1b226242097735c2af28')


build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_WITH_QT6=ON
  cmake --build build
}

package() {
  DESTDIR=${pkgdir} cmake --install build

  install -Dm644 ${pkgname}-${pkgver}/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
