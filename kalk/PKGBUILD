
pkgname=kalk
pkgver=22.04
pkgrel=1
arch=('x86_64')
pkgdesc="Powerful cross-platform calculator application built with the Kirigami framework"
url="https://invent.kde.org/plasma-mobile/kalk"
license=('GPLv3')
depends=('qt5-quickcontrols2' 'qtfeedback' 'kconfig' 'kirigami' 'ki18n'
         'kcoreaddons' 'kunitconversion')
makedepends=('extra-cmake-modules' 'qt5-tools')
source=("https://download.kde.org/stable/plasma-mobile/${pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('e8aafd63a918e0f31064b082460c38215affe9c356fee9b8ba1ecf29cf625829')

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_BUILD_TYPE=Release \
      -DBUILD_TESTING=OFF
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
