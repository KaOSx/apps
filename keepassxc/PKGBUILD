
pkgname=keepassxc
pkgver=2.7.4
pkgrel=2
pkgdesc="Cross-platform community-driven port of the Windows application Keepass Password Safe"
arch=('x86_64')
url="https://www.keepassxc.org/"
license=('GPL2')
depends=('qt5-x11extras' 'qt5-svg' 'libxtst' 'shared-mime-info' 'botan' 'argon2'
         'zlib' 'qrencode')
makedepends=('cmake' 'intltool' 'qt5-tools')
groups=('accessories')
replaces=('keepassx')
provides=('keepassx')
conflicts=('keepassx')
source=("https://github.com/keepassxreboot/keepassxc/releases/download/${pkgver}/${pkgname}-${pkgver}-src.tar.xz")
sha256sums=('560052961da0389327e759171f660230dfa4e0f4e1fab6139600fb85c6e5dece')

prepare() {
  cd ${pkgname}-${pkgver}

  #sed -i 's|NAMES quazip5 quazip|NAMES quazip5 quazip quazip1-qt5|' cmake/FindQuaZip.cmake
  #sed -i 's|PATH_SUFFIXES quazip5 quazip|PATH_SUFFIXES quazip5 quazip QuaZip-Qt5-1.1/quazip|' cmake/FindQuaZip.cmake
}

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DWITH_GUI_TESTS=OFF \
    -DWITH_XC_BROWSER=ON \
    -DWITH_XC_NETWORKING=ON \
    -DWITH_XC_AUTOTYPE=ON \
    -DWITH_XC_SSHAGENT=ON \
    -DWITH_XC_KEESHARE=ON \
    -DWITH_XC_FDOSECRETS=ON \
    -DWITH_XC_DOCS=OFF \
    -DWITH_XC_UPDATECHECK=OFF
  cmake --build build
}

check() {
    cd build
    # 1 fail 39 - testcli (Subprocess aborted)
    make test
}

package() {

    DESTDIR=${pkgdir} cmake --install build
}
