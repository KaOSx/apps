
pkgname=keepassxc
pkgver=2.5.4
pkgrel=1
pkgdesc="Cross-platform community-driven port of the Windows application Keepass Password Safe"
arch=('x86_64')
url="http://www.keepassxc.org/"
license=('GPL2')
depends=('qt5-x11extras' 'qt5-svg' 'libxtst' 'shared-mime-info' 'libsodium' 'argon2'
         'quazip' 'qrencode')
makedepends=('cmake' 'intltool' 'qt5-tools')
groups=('accessories')
replaces=('keepassx')
provides=('keepassx')
conflicts=('keepassx')
source=("https://github.com/keepassxreboot/keepassxc/releases/download/${pkgver}/${pkgname}-${pkgver}-src.tar.xz")
sha256sums=('a55e0801c318b02b1ac4e16e9b7a87ccfa7b039ea60d2c62610bd1bbbdd6cd4a')

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DWITH_GUI_TESTS=OFF \
    -DWITH_XC_BROWSER=ON \
    -DWITH_XC_NETWORKING=ON \
    -DWITH_XC_AUTOTYPE=ON \
    -DWITH_XC_SSHAGENT=ON \
    -DWITH_XC_KEESHARE=ON
  make 
}

check() {
    cd build
    
    make test
}

package() {
    cd build
    
    make DESTDIR=${pkgdir} install 
}
