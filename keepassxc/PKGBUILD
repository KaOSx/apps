
pkgname=keepassxc
pkgver=2.7.8
pkgrel=1
pkgdesc="Cross-platform community-driven port of the Windows application Keepass Password Safe"
arch=('x86_64')
url="https://www.keepassxc.org/"
license=('GPL2')
depends=('qt5-x11extras' 'qt5-svg' 'libxtst' 'shared-mime-info' 'botan' 'argon2'
         'zlib' 'qrencode')
makedepends=('cmake' 'intltool' 'qt5-tools')
groups=('accessories')
replaces=('keepassx')
provides=('keepassx')
conflicts=('keepassx')
source=("https://github.com/keepassxreboot/keepassxc/releases/download/${pkgver}/${pkgname}-${pkgver}-src.tar.xz"
        "https://github.com/keepassxreboot/keepassxc/commit/05ab5b17002b5f699e4c05c77e11d5f586848e74.diff")
sha256sums=('87d3101712b3c8656a24b908ad5b7e2529bc01717cb4156f53ba195fb81783a3'
            'cb85e79ce632c9c2953aa045ed4d697c06f677a6ba27db79dcf032ab851045ee')

prepare() {
  cd ${pkgname}-${pkgver}

  #sed -i 's|NAMES quazip5 quazip|NAMES quazip5 quazip quazip1-qt5|' cmake/FindQuaZip.cmake
  #patch -p1 -i ${srcdir}/05ab5b17002b5f699e4c05c77e11d5f586848e74.diff
}

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DWITH_GUI_TESTS=OFF \
    -DWITH_XC_BROWSER=ON \
    -DWITH_XC_NETWORKING=ON \
    -DWITH_XC_AUTOTYPE=ON \
    -DWITH_XC_SSHAGENT=ON \
    -DWITH_XC_KEESHARE=ON \
    -DWITH_XC_FDOSECRETS=ON \
    -DWITH_XC_DOCS=OFF \
    -DWITH_XC_UPDATECHECK=OFF
  cmake --build build
}

check() {
    cd build
    # 1 fail 39 - testcli (Subprocess aborted)
    make test
}

package() {

    DESTDIR=${pkgdir} cmake --install build
}
