
pkgname=mc
pkgver=4.8.30
pkgrel=1
pkgdesc="Text based filemanager/shell that emulates Norton Commander"
arch=('x86_64')
url="https://www.midnight-commander.org"
license=('GPL')
depends=('e2fsprogs' 'glib2' 'pcre2' 'gpm' 'libssh2' 'zip' 'unrar' 'perl'
         'gawk' 'aspell' 'samba' 'slang')
makedepends=('libxt' 'libx11')
optdepends=('p7zip: support for 7zip archives'
            'mtools: ms-dos tool')
groups=('system')
options=('!emptydirs' '!makeflags')
source=("https://www.midnight-commander.org/downloads/${pkgname}-${pkgver}.tar.xz")
sha256sums=('5ebc3cb2144b970c5149fda556c4ad50b78780494696cdf2d14a53204c95c7df')

build() {
	cd ${pkgname}-${pkgver}

	./configure \
	    --prefix=/usr \
	    --sysconfdir=/etc \
	    --enable-background \
	    --with-x \
	    --libexecdir=/usr/lib \
	    --with-screen=slang \
	    --with-search-engine=pcre2
	make 
}

package() {
	cd ${pkgname}-${pkgver}
	make DESTDIR=${pkgdir} install

	sed -i 's|op_has_zipinfo = 0|op_has_zipinfo = 1|' ${pkgdir}/usr/lib/mc/extfs.d/uzip
}
