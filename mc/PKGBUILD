
pkgname=mc
pkgver=4.8.29
pkgrel=1
pkgdesc="Text based filemanager/shell that emulates Norton Commander"
arch=('x86_64')
url="https://www.midnight-commander.org"
license=('GPL')
depends=('e2fsprogs' 'glib2' 'pcre' 'gpm' 'libssh2' 'zip' 'unrar' 'perl'
         'gawk' 'aspell' 'samba' 'slang')
makedepends=('libxt' 'libx11')
optdepends=('p7zip: support for 7zip archives'
            'mtools: ms-dos tool')
groups=('system')
options=('!emptydirs' '!makeflags')
source=("https://www.midnight-commander.org/downloads/${pkgname}-${pkgver}.tar.xz")
sha256sums=('01d8a3b94f58180cca5bf17257b5078d1fd6fd27a9b5c0e970ec767549540ad4')

build() {
	cd ${pkgname}-${pkgver}

	./configure \
	    --prefix=/usr \
	    --sysconfdir=/etc \
	    --enable-background \
	    --with-x \
	    --enable-vfs-smb \
	    --libexecdir=/usr/lib \
	    --with-screen=slang

	make 
}

package() {
	cd ${pkgname}-${pkgver}
	make DESTDIR=${pkgdir} install

	sed -i 's|op_has_zipinfo = 0|op_has_zipinfo = 1|' ${pkgdir}/usr/lib/mc/extfs.d/uzip
}
