
pkgname=kvantum
_pkgname=Kvantum
pkgver=0.20.1
_commit=0ef502f301cbc4d89dcecc36d425cff56679a402
pkgrel=1
pkgdesc="SVG-based Qt5 theme engine plus a config tool and extra themes"
arch=('x86_64')
url="https://github.com/tsujan/Kvantum"
license=('GPL3')
depends=('qt5-svg' 'qt5-x11extras' 'kwindowsystem' 'libxext')
makedepends=('cmake' 'qt5-tools')
source=("https://github.com/tsujan/Kvantum/archive/V${pkgver}.tar.gz")
#source=("https://github.com/tsujan/Kvantum/archive/${_commit}.zip")
md5sums=('4f72145346409392800c4138978ff2ba')

build() {
    cmake -B build -S ${_pkgname}-${pkgver}/${_pkgname} \
        -DCMAKE_INSTALL_PREFIX=/usr
    cmake --build build
}

package() {
    DESTDIR=${pkgdir} cmake --install build
    
    install -Dm644 ${_pkgname}-${pkgver}/${_pkgname}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
} 
