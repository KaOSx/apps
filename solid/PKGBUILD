
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=solid
pkgver=${_kf5ver}
pkgrel=1
pkgdesc='Solid'
arch=('x86_64')
url='https://projects.kde.org/projects/frameworks/solid'
license=('LGPL')
depends=('media-player-info' 'qt5-declarative' 'upower' 'udisks2')
makedepends=('extra-cmake-modules' 'qt5-tools')
groups=('kf5')
source=("http://download.kde.org/stable/frameworks/${pkgver}/${pkgname}-${pkgver}.tar.xz"
        "https://raw.githubusercontent.com/frugalware/frugalware-current/master/source/kf5/solid/sort_cores.patch"
        "https://github.com/KDE/solid/commit/d735708ff11c40ee6b9bee64544250d55067403f.diff")
md5sums=(`grep ${pkgname}-$_kf5ver.tar.xz ../kf5.md5 | cut -d" " -f1`
         'e086776ae503f25f40b558ff0dd8579a'
         'a35c10865323a6af99de3def36abdd17')

prepare () {
  cd ${pkgname}-${pkgver}
  # patch from frugalware, make sure cpu's show correct
  patch -p1 -i ${srcdir}/sort_cores.patch
  # https://bugs.kde.org/show_bug.cgi?id=370975
  #patch -p1 -i ${srcdir}/d735708ff11c40ee6b9bee64544250d55067403f.diff
}

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_QMLDIR=/usr/lib/qt5/qml \
    -DECM_MKSPECS_INSTALL_DIR=/usr/lib/qt5/mkspecs/modules \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
    -DBUILD_TESTING=OFF 
  make
}

package() {
  cd build

  make DESTDIR=${pkgdir} install
}

