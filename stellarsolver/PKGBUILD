 
pkgname=stellarsolver
pkgver=2.2
pkgrel=1
pkgdesc='Astrometric Plate Solver for Mac, Linux, and Windows, built on Astrometry.net and SEP (sextractor)'
arch=('x86_64')
url='https://github.com/rlancaste/stellarsolver'
license=('GPLv3')
depends=('cfitsio' 'gsl' 'qt5-base' 'wcslib')
makedepends=('cmake')
source=("https://github.com/rlancaste/stellarsolver/archive/${pkgver}.tar.gz")
sha256sums=('c3a1e9456ff31b3803d13538dbf1c87be11c2c9d98010354ac65f144a9eb647f')

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr 
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
  install -Dm644 ${pkgname}-${pkgver}/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
