
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=xdg-desktop-portal-kde
arch=('x86_64')
pkgver=${_plasmaver}
pkgrel=1
pkgdesc="A backend implementation for xdg-desktop-portal that is using Qt/KDE"
url="https://github.com/KDE/xdg-desktop-portal-kde"
license=('GPL' 'LGPL' 'FDL')
depends=('kcoreaddons' 'kconfig' 'ki18n' 'kdeclarative' 'kio' 'kirigami' 'knotifications'
         'plasma-framework' 'kwayland' 'kwidgetsaddons' 'kwindowsystem' 
         'libepoxy' 'pipewire' 'xdg-desktop-portal' 'libcups')
makedepends=('extra-cmake-modules')
source=($_mirror/${pkgname}-${_plasmaver}.tar.xz)
md5sums=(`grep ${pkgname}-${_plasmaver}.tar.xz ../kf5.md5 | cut -d" " -f1`)

build() {
  mkdir -p build
  cd build

  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_QMLDIR=/usr/lib/qt5/qml \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
    -DKDE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF 
  make
}

package() {
  cd build

  make DESTDIR=${pkgdir} install
}
