
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=print-manager6
_pkgname=print-manager
pkgver=${_plasmaver}
pkgrel=1
pkgdesc="A KDE tool for managing print jobs and printers"
url="https://invent.kde.org/utilities/print-manager"
arch=('x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kconfig6' 'kconfigwidgets6' 'kcoreaddons6' 'kdbusaddons6' 'kiconthemes6'
         'ki18n6' 'kcmutils6' 'kio6' 'knotifications6' 'plasma-framework6'
         'kwidgetsaddons6' 'kwindowsystem6' 'libcups' 'qt6-tools' 'samba' 'qt6-5compat')
makedepends=('extra-cmake-modules' 'kdoctools6')
groups=('kdeplasma6')
source=("https://download.kde.org/stable/plasma/${pkgver}/${_pkgname}-${pkgver}.tar.xz"
        's-c-p.diff')
md5sums=(`grep ${_pkgname}-${_plasmaver}.tar.xz ../kf6.md5 | cut -d" " -f1`
         '9a9e6ca561f286fec2b8262c7c05eb02')

prepare() {
  cd ${_pkgname}-${pkgver}

  patch -p1 -i ${srcdir}/s-c-p.diff
}

build() {
  cmake -B build -S ${_pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DQT_MAJOR_VERSION=6
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
