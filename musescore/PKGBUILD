
pkgname=musescore
pkgver=4.4.1
pkgrel=1
pkgdesc="Music notation and composition software written in Qt"
arch=('x86_64')
url="https://www.musescore.org/en/"
license=('GPL')
depends=('qt6-networkauth' 'qt6-svg' 'qt6-tools' 'qt6-declarative' 'qt6-5compat' 'qt6-scxml'
         'openssl' 'pulseaudio' 'lame' 'portaudio' 'jack2' 'portmidi')
makedepends=('cmake' 'doxygen' 'texlive-core' 'qt5-script')
groups=('multimedia')
source=("https://github.com/musescore/MuseScore/archive/v${pkgver}.tar.gz")
#source=("https://download.musescore.com/releases/MuseScore-${pkgver}/MuseScore-${pkgver}.zip")
md5sums=('0cf0611f7cb9cc1e7b0f3b6acee058ac')

prepare() {
  cd MuseScore-${pkgver}
  #sed -i 's|QT_QMAKE_EXECUTABLE qmake|QT_QMAKE_EXECUTABLE qmake-qt5|' build/FindQt5.cmake
  #patch -p1 -R -i ${srcdir}/7a465c3727bc28ed12e9614b50684cb18e5525e7.diff
}

build() {
  PATH=/usr/lib/qt5/bin:${PATH}
  cmake -B build -S MuseScore-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DMUE_BUILD_CRASHPAD_CLIENT=OFF \
    -DMUE_CRASH_REPORT_URL="https://kaosx.us/docs/bugs/" \
    -DMUE_BUILD_UNIT_TESTS=OFF \
    -DMUE_ENABLE_FILE_ASSOCIATION=ON
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build

  # package mscore fonts so they can be used by other apps
  cd MuseScore-${pkgver}
  install -m755 -d ${pkgdir}/usr/share/fonts/mscore
  install -m644 fonts/mscoreTab.*tf ${pkgdir}/usr/share/fonts/mscore/
  install -m644 fonts/musejazz/MuseJazz.otf ${pkgdir}/usr/share/fonts/mscore/
  #install -m644 fonts/mscore/mscore.otf ${pkgdir}/usr/share/fonts/mscore/
  install -m644 fonts/mscore/*.ttf ${pkgdir}/usr/share/fonts/mscore/
  
  # remove opus files, no cmake option yet to disable third party, see:
  # https://github.com/musescore/MuseScore/issues/11572
  rm -r ${pkgdir}/usr/include/opus
  rm ${pkgdir}/usr/lib/libopus.a
  rm ${pkgdir}/usr/lib/pkgconfig/opus.pc
  rm -r ${pkgdir}/usr/lib/cmake/Opus
}
