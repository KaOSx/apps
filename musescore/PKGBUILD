
pkgname=musescore
pkgver=4.0
pkgrel=1
pkgdesc="Music notation and composition software written in Qt"
arch=('x86_64')
url="https://www.musescore.org/en/"
license=('GPL')
depends=('qtwebengine' 'qt5-networkauth' 'qt5-svg' 'qt5-tools' 'qt5-xmlpatterns' 'qt5-quickcontrols2'
         'qt5-graphicaleffects' 'qt5-x11extras' 'openssl' 'pulseaudio' 'lame' 'portaudio' 'jack2' 'portmidi')
makedepends=('cmake' 'doxygen' 'texlive-core' 'qt5-script')
groups=('multimedia')
source=("https://github.com/musescore/MuseScore/archive/v${pkgver}.tar.gz"
#source=("https://download.musescore.com/releases/MuseScore-${pkgver}/MuseScore-${pkgver}.zip"
        "https://github.com/musescore/MuseScore/commit/7a465c3727bc28ed12e9614b50684cb18e5525e7.diff")
md5sums=('44bab4cfbb2cc9821a3602e6f5c6a387'
         '624ad150d4cff71168553522f0236df4')

prepare() {
  cd MuseScore-${pkgver}
  #sed -i 's|QT_QMAKE_EXECUTABLE qmake|QT_QMAKE_EXECUTABLE qmake-qt5|' build/FindQt5.cmake
  #patch -p1 -R -i ${srcdir}/7a465c3727bc28ed12e9614b50684cb18e5525e7.diff
}

build() {
  PATH=/usr/lib/qt5/bin:${PATH}
  cmake -B build -S MuseScore-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCRASH_REPORT_URL="https://kaosx.us/docs/bugs/" \
    -DUSE_SYSTEM_FREETYPE=ON \
    -DBUILD_UNIT_TESTS=OFF \
    -DBUILD_AUTOUPDATE=OFF
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build

  # package mscore fonts so they can be used by other apps
  cd MuseScore-${pkgver}
  install -m755 -d ${pkgdir}/usr/share/fonts/mscore
  install -m644 fonts/mscoreTab.*tf ${pkgdir}/usr/share/fonts/mscore/
  install -m644 fonts/musejazz/MuseJazz.otf ${pkgdir}/usr/share/fonts/mscore/
  #install -m644 fonts/mscore/mscore.otf ${pkgdir}/usr/share/fonts/mscore/
  install -m644 fonts/mscore/*.ttf ${pkgdir}/usr/share/fonts/mscore/
  
  # remove opus files, no cmake option yet to disable third party, see:
  # https://github.com/musescore/MuseScore/issues/11572
  rm -r ${pkgdir}/usr/include/opus
  rm ${pkgdir}/usr/lib/libopus.a
}
