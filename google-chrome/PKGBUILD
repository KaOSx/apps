
pkgbase=google-chrome
pkgname=('google-chrome' 'widevine')
pkgver=88.0.4324.27
pkgrel=1
arch=('x86_64')
url="http://dev.chromium.org/getting-involved/dev-channel"
license=('custom:chrome')
options=('!strip' '!emptydirs')
makedepends=('gcc-libs' 'gtk3' 'nss' 'libjpeg-turbo' 'freetype2' 'cairo' 'libxslt'
            'libpng' 'alsa-lib' 'libxss' 'xdg-utils')
# version:  curl -sL "https://dl.google.com/linux/chrome/rpm/stable/x86_64/repodata/other.xml.gz" | gzip -df | tr ' ' '\n' | grep -e name= -e ver= | cut -d '"' -f2 | sed 'N;s/\n/ /'
# sha256 https://dl.google.com/linux/chrome/deb/dists/stable/main/binary-amd64/Packages
source=("google-chrome-unstable_${pkgver}_amd64.deb::https://dl.google.com/linux/direct/google-chrome-unstable_current_amd64.deb"
        "https://www.google.com/chrome/intl/en/eula_text.html"
        'google-chrome-unstable.desktop')
sha256sums=('a49ad1b5f3335135aa53f56d56b811c64e9490ca66160852f7fe9820883ca22b'
            '300cf241df356b4e80ad3228d999a4d287e233a2194c653a34b6f8cc29a4081f'
            '6a5c0dd62112dcc3d9d750974d10d9e78b5964011cfe9e281c7e5acacb42b8cf')


package_google-chrome() {
    pkgdesc="Google Chrome Browser, developer preview channel for Linux"
    depends=('gcc-libs' 'gtk3' 'nss' 'libjpeg-turbo' 'freetype2' 'cairo' 'libxslt'
             'libpng' 'alsa-lib' 'libxss' 'xdg-utils' 'widevine')
    optdepends=('kde-gtk-config: gtk integration settings for Plasma')
    groups=('network-web')
    install=$pkgname.install
    
    bsdtar -xf data.tar.xz -C ${pkgdir}/ 

    # Adding man page
    gzip ${pkgdir}/usr/share/man/man1/google-chrome-unstable.1.gz
    
    # icons to /usr/share/icons/hicolor/ too
    for i in 16 24 32 48 64 128 256; do
    install -Dm644 ${pkgdir}/opt/google/chrome-unstable/product_logo_${i}_dev.png ${pkgdir}/usr/share/icons/hicolor/${i}x${i}/apps/google-chrome.png
    done

    # License
    install -Dm644 ${srcdir}/eula_text.html ${pkgdir}/usr/share/licenses/google-chrome/eula_text.html
    
    rm -r ${pkgdir}/etc/cron.daily/ ${pkgdir}/opt/google/chrome-unstable/cron/
    rm ${pkgdir}/opt/google/chrome-unstable/product_logo_*.png
    
    # use in widevine package
    rm -r ${pkgdir}/opt/google/chrome-unstable/WidevineCdm 
    
    rm ${pkgdir}/usr/share/applications/google-chrome-unstable.desktop
    install -m755 $srcdir/google-chrome-unstable.desktop ${pkgdir}/usr/share/applications/google-chrome-unstable.desktop
    # symlink no longer provided, back with 55
    #ln -s /opt/google/chrome-unstable/google-chrome-unstable ${pkgdir}/usr/bin/google-chrome-unstable
}

package_widevine() {
    pkgdesc="Google Chrome's plugin for viewing premium video content"
    depends=('pulseaudio')
    
    bsdtar -xf data.tar.xz
    
    install -Dm644 opt/google/chrome-unstable/WidevineCdm/LICENSE ${pkgdir}/usr/share/licenses/widevine/LICENSE
    install -Dm644 opt/google/chrome-unstable/WidevineCdm/_platform_specific/linux_x64/libwidevinecdm.so ${pkgdir}/opt/google/chrome-unstable/WidevineCdm/_platform_specific/linux_x64/libwidevinecdm.so
    
    mkdir -p ${pkgdir}/usr/lib/chromium
    cd ${pkgdir}
    ln -s /opt/google/chrome-unstable/libwidevinecdm.so ${pkgdir}/usr/lib/chromium/libwidevinecdm.so
}
