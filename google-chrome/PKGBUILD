
pkgbase=google-chrome
pkgname=('google-chrome' 'widevine')
pkgver=69.0.3464.0
pkgrel=1
arch=('x86_64')
url="http://dev.chromium.org/getting-involved/dev-channel"
license=('custom:chrome')
options=('!strip' '!emptydirs')
makedepends=('gcc-libs' 'gtk3' 'nss' 'libjpeg-turbo' 'freetype2' 'cairo' 'libxslt'
            'libpng' 'alsa-lib' 'libxss' 'xdg-utils')
# version:  curl -s https://dl.google.com/linux/chrome/rpm/stable/x86_64/repodata/other.xml.gz | gzip -df | awk -F\" '/pkgid/{ sub(".*-","",$4); print $4": "$10 }'
source=("google-chrome-unstable_${pkgver}_amd64.deb::https://dl.google.com/linux/direct/google-chrome-unstable_current_amd64.deb"
         "https://www.google.com/chrome/intl/en/eula_text.html"
         'google-chrome-unstable.desktop')
md5sums=('ed98c25bc30a6ae4db651b3c76db2a01'
         '65c1f3c6893b31db816d594ba5591956'
         'dc490bcda18c1379056700057800cc8f')


package_google-chrome() {
    pkgdesc="Google Chrome Browser, developer preview channel for Linux"
    depends=('gcc-libs' 'gtk3' 'nss' 'libjpeg-turbo' 'freetype2' 'cairo' 'libxslt'
             'libpng' 'alsa-lib' 'libxss' 'xdg-utils' 'widevine')
    groups=('network-web')
    install=$pkgname.install
    
    bsdtar -xf data.tar.xz -C ${pkgdir}/ 

    # Adding man page
    gzip ${pkgdir}/usr/share/man/man1/google-chrome-unstable.1.gz
    
    # icons to /usr/share/icons/hicolor/ too
    for i in 16 22 24 32 48 64 128 256; do
    install -Dm644 ${pkgdir}/opt/google/chrome-unstable/product_logo_${i}_dev.png ${pkgdir}/usr/share/icons/hicolor/${i}x${i}/apps/google-chrome.png
    done

    # License
    install -Dm644 $srcdir/eula_text.html ${pkgdir}/usr/share/licenses/google-chrome/eula_text.html
    
    rm -r ${pkgdir}/etc/cron.daily/ ${pkgdir}/opt/google/chrome-unstable/cron/
    rm ${pkgdir}/opt/google/chrome-unstable/product_logo_*.png
    
    # use in widevine package
    rm ${pkgdir}/opt/google/chrome-unstable/libwidevinecdm.so
    #rm ${pkgdir}/opt/google/chrome-unstable/libwidevinecdmadapter.so
    
    rm ${pkgdir}/usr/share/applications/google-chrome-unstable.desktop
    install -m755 $srcdir/google-chrome-unstable.desktop ${pkgdir}/usr/share/applications/google-chrome-unstable.desktop
    # symlink no longer provided, back with 55
    #ln -s /opt/google/chrome-unstable/google-chrome-unstable ${pkgdir}/usr/bin/google-chrome-unstable
}

package_widevine() {
    pkgdesc="Google Chrome's plugin for viewing premium video content"
    depends=('pulseaudio')
    
    bsdtar -xf data.tar.xz
    
    install -Dm644 opt/google/chrome-unstable/libwidevinecdm.so ${pkgdir}/opt/google/chrome-unstable/libwidevinecdm.so
    #install -Dm755 opt/google/chrome-unstable/libwidevinecdmadapter.so ${pkgdir}/opt/google/chrome-unstable/libwidevinecdmadapter.so
    
    mkdir -p ${pkgdir}/usr/lib/chromium
    cd ${pkgdir}
    ln -s /opt/google/chrome-unstable/libwidevinecdm.so ${pkgdir}/usr/lib/chromium/libwidevinecdm.so
    #ln -s /opt/google/chrome-unstable/libwidevinecdmadapter.so ${pkgdir}/usr/lib/chromium/libwidevinecdmadapter.so
    
    install -Dm644 $srcdir/eula_text.html ${pkgdir}/usr/share/licenses/widevine/eula_text.html
}
