
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=akonadi-search
pkgver=${_kdever}
pkgrel=1
pkgdesc="Libraries and daemons to implement searching in Akonadi."
arch=('x86_64')
url='https://projects.kde.org/projects/playground/pim/akonadi-search'
license=('LGPL')
depends=('xapian-core' 'akonadi' 'ki18n' 'kconfig' 'kcrash' 'kcmutils' 'krunner' 'akonadi-mime'
         'kcontacts' 'kmime' 'kcalcore')
makedepends=('extra-cmake-modules' 'kdoctools' 'boost')
groups=('kdepim')
source=($_mirror/${pkgname}-${_kdever}.tar.xz)
md5sums=(`grep ${pkgname}-${_kdever}.tar.xz ../kde-sc.md5 | cut -d" " -f1`)

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
    -DKDE_INSTALL_LIBDIR=lib 
  make
}

package() {
  cd build

  make DESTDIR=${pkgdir} install
  # conflicts with kf5-baloo for now
  #rm -r ${pkgdir}/usr/lib/qt5/plugins/kf5/baloo/
}
