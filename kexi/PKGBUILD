
pkgname=kexi
pkgver=3.2.0
pkgrel=2
pkgdesc="Visual database applications creator"
arch=('x86_64')
url='http://kexi-project.org/'
license=('FDL1.2' 'GPL2' 'LGPL')
depends=('karchive' 'kcodecs' 'kcompletion' 'kconfig' 'kconfigwidgets' 'kcoreaddons' 
         'kguiaddons' 'ki18n' 'kiconthemes' 'kitemviews' 'kio' 'ktexteditor' 'ktextwidgets' 
         'kwidgetsaddons' 'kxmlgui' 'kcrash' 'marble' 'breeze-icons'
         'kproperty' 'kreport' 'kdb'
         'libmariadbclient' 'sqlite3' 'qtwebkit-tp' 'freetds' 'postgresql') # will switch to qtwebengine
makedepends=('extra-cmake-modules' 'kdoctools' 'qt5-tools') # 'clang') 
source=("https://download.kde.org/stable/kexi/src/${pkgname}-${pkgver}.tar.xz"
        "https://invent.kde.org/office/kexi/-/commit/511d99b7745a6ce87a208bdbf69e631f1f136d53.diff"
        "https://invent.kde.org/office/kexi/-/commit/73d3fa8e7a1028c89b20c74f6444a76a722d8041.diff")
sha256sums=('7a813f9ea815a09c1e733d0e7dc879c64eee85f075389e87e6aab99cf0c1c1ff'
            '929070ff47302fb5a8dac4a972c769511193d1619ed5ecab4905b8a7458a0efa'
            'c0288f55b39d78f850ee222e1f6e1dad4c324f47473b85baaee4d4c64e985e47')

prepare() {
  cd ${pkgname}-${pkgver}
  
  patch -p1 -i ${srcdir}/511d99b7745a6ce87a208bdbf69e631f1f136d53.diff
  patch -p1 -i ${srcdir}/73d3fa8e7a1028c89b20c74f6444a76a722d8041.diff
  sed -i -e 's|KexiMarble ${MARBLE_MIN_VERSION}|Marble ${MARBLE_MIN_VERSION}|' CMakeLists.txt
}

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=FALSE 
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}

