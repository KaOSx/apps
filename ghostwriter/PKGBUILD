

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=ghostwriter
pkgver=${_kdever}
pkgrel=1
pkgdesc="Distraction-free text editor for Markdown."
arch=('x86_64')
url="https://ghostwriter.kde.org/"
license=('GPL3')
depends=('cmark' 'qt6-svg' 'qt6-webengine' 'sonnet6' 'kcoreaddons6' 'kxmlgui6'
         'kconfigwidgets6' 'kwidgetsaddons6')
makedepends=('extra-cmake-modules' 'kdoctools6' 'qt6-tools')
groups=('office')
source=("https://download.kde.org/stable/applications/${_kdever}/src/${pkgname}-${_kdever}.tar.xz")
md5sums=(`grep ${pkgname}-${_kdever}.tar.xz ../kde-sc.md5 | cut -d" " -f1`)

prepare() {
  cd ${pkgname}-${pkgver}

  sed -i 's|GW_DARK_MODE_KEY, QVariant(true)|GW_DARK_MODE_KEY, QVariant(false)|' src/settings/appsettings.cpp
}

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DBUILD_TESTING=OFF \
    -DQT_MAJOR_VERSION=6
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
