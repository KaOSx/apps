
pkgname=clazy
pkgver=1.11
pkgrel=6
pkgdesc="Compiler plugin, Qt oriented code checker based on clang framework"
url="https://invent.kde.org/sdk/clazy"
license=('GPL2')
depends=('clang')
makedepends=('cmake' 'llvm')
arch=('x86_64')
source=("https://download.kde.org/stable/clazy/${pkgver}/src/${pkgname}-${pkgver}.tar.xz"
        "https://invent.kde.org/sdk/clazy/-/commit/20fca52da739ebefa47e35f6b338bb99a0da3cfe.diff"
        "https://invent.kde.org/sdk/clazy/-/commit/a05ac7eb6f6198c3f478bd7b5b4bfc062a8d63cc.diff"
        "https://invent.kde.org/sdk/clazy/-/commit/a9aabd821d8be7932c954e1cdd8d4e10f38f8785.diff")
sha256sums=('66165df33be8785218720c8947aa9099bae6d06c90b1501953d9f95fdfa0120a'
            'b6f76075f9ecd9fad0d1bea84c3868de07d128df6d24c99d2de761e5718429f5'
            '4257ed252eee84e1fa2b2b072d6cd3ff01a0a8d82a4b2f224ef783d88e341510'
            '15816ee3b0af43e9c4e2d81fd1811b4ed14f384dca32057ea58930961627af1c')

prepare() {
  cd ${pkgname}-${pkgver}
  # LLVM 15 needed fix
  patch -p1 -i ${srcdir}/20fca52da739ebefa47e35f6b338bb99a0da3cfe.diff
  # LLVM 16 needed fix
  patch -p1 -i ${srcdir}/a05ac7eb6f6198c3f478bd7b5b4bfc062a8d63cc.diff
  # LLVM 17 needed fix
  #patch -p1 -i ${srcdir}/a9aabd821d8be7932c954e1cdd8d4e10f38f8785.diff
  sed -i -e 's|val.countPopulation()|val.popcount()|g' src/checks/manuallevel/unexpected-flag-enumerator-value.cpp
  sed -i -e 's|!initVal.isNullValue()|!initVal.isZero()|g' src/checks/manuallevel/unexpected-flag-enumerator-value.cpp
}

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
