
pkgname=clazy
pkgver=1.12
pkgrel=2
pkgdesc="Compiler plugin, Qt oriented code checker based on clang framework"
url="https://invent.kde.org/sdk/clazy"
license=('GPL2')
depends=('clang')
makedepends=('cmake' 'llvm')
arch=('x86_64')
source=("https://download.kde.org/stable/clazy/${pkgver}/src/${pkgname}-${pkgver}.tar.xz"
        'llvm19.diff')
sha256sums=('611749141d07ce1e006f8a1253f9b2dbd5b7b44d2d5322d471d62430ec2849ac'
            '9fe5f9232d9f85b1b2612d308df55c01fae0a03bd95589033205a72b5e4020cb')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i ${srcdir}/llvm19.diff
  #sed -i -e 's|isPure()|isPureVirtual()|g' src/checks/level2/virtual-call-ctor.cpp
}

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
