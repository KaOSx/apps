
pkgname=powerline
pkgver=2.8.4
pkgrel=1
pkgdesc='Statusline plugin for vim, provides statuslines and prompts for other applications, including zsh and bash'
url='https://powerline.readthedocs.io/en/latest/overview.html'
license=('MIT')
arch=('x86_64')
depends=('python3')
makedepends=('python3-build' 'python3-installer' 'python3-setuptools' 'python3-wheel')
install=powerline.install
source=("https://github.com/powerline/powerline/archive/${pkgver}.tar.gz")
md5sums=('59ad22961ed0e97014d476431586aba9')

build() {
    cd ${pkgname}-${pkgver}

    python3 -m build -nw
}

package() {
    cd ${pkgname}-${pkgver}

    python3 -m installer --destdir=${pkgdir} dist/*.whl

    install -dm755 ${pkgdir}/etc/fonts/conf.d
    install -Dm644 font/PowerlineSymbols.otf ${pkgdir}/usr/share/fonts/OTF/PowerlineSymbols.otf
    install -Dm644 font/10-powerline-symbols.conf ${pkgdir}/etc/fonts/conf.avail/10-powerline-symbols.conf
    ln -s ../conf.avail/10-powerline-symbols.conf ${pkgdir}/etc/fonts/conf.d/10-powerline-symbols.conf

    install -Dm644 powerline/bindings/bash/powerline.sh ${pkgdir}/usr/share/bash/powerline.sh
    install -Dm644 powerline/bindings/fish/powerline-setup.fish ${pkgdir}/usr/share/fish/powerline-setup.fish
    install -Dm644 powerline/bindings/vim/plugin/powerline.vim ${pkgdir}/usr/share/vim/vimfiles/plugin/powerline.vim
    install -Dm644 powerline/bindings/zsh/powerline.zsh ${pkgdir}/usr/share/zsh/site-contrib/powerline.zsh

    install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

