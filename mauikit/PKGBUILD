 
pkgname=mauikit
pkgver=3.0.1
pkgrel=1
pkgdesc='Modular front-end framework for developing fast and compelling user experiences'
arch=('x86_64')
url="https://mauikit.org/"
license=('GPL3')
depends=('ki18n' 'kconfig' 'kcoreaddons' 'knotifications' 'qt5-svg' 'qt5-quickcontrols2'
         'qt5-graphicaleffects' 'mauiman')
makedepends=('extra-cmake-modules')
source=("https://download.kde.org/stable/maui/mauikit/${pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('abfd89308dd3686f4fc3580b71ffa6eba536d4ad2d7726c79c62f23147d09d6e')

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
