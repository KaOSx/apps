 
pkgname=mauikit
pkgver=2.1.0
pkgrel=1
pkgdesc='Modular front-end framework for developing fast and compelling user experiences'
arch=('x86_64')
url="https://mauikit.org/"
license=('GPL3')
depends=('ki18n' 'kconfig' 'kcoreaddons' 'knotifications' 'qt5-svg' 'qt5-quickcontrols2'
         'qt5-graphicaleffects')
makedepends=('extra-cmake-modules')
source=("https://download.kde.org/stable/maui/mauikit/${pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('577b999de9ee07e7db26b6686890bed9324d64823c2014315193ed39e183e355')

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
