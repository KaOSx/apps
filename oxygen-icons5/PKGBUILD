
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

_origname=oxygen-icons
pkgname=('oxygen-icons5' 'oxygen-icons5-svg')
arch=('x86_64')
pkgver=6.1.0
pkgrel=1
url="https://invent.kde.org/frameworks/oxygen-icons"
license=('GPL' 'LGPL' 'FDL')
depends=('qt6-base')
makedepends=('extra-cmake-modules')
source=("https://download.kde.org/stable/oxygen-icons/${_origname}-${pkgver}.tar.xz")
sha256sums=('16ca971079c5067c4507cabf1b619dc87dd6b326fd5c2dd9f5d43810f2174d68')

build () {
  cmake -B build -S ${_origname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package_oxygen-icons5() {
    pkgdesc="Oxygen Icons theme"
    conflicts=('oxygen-icons')
    replaces=('oxygen-icons')
    provides=('oxygen-icons')

    DESTDIR=${pkgdir} cmake --install build
    # conflicts with kdepim
    #rm ${pkgdir}/usr/share/icons/oxygen/*/mimetypes/x-mail-distribution-list.png
}

package_oxygen-icons5-svg() {
    pkgdesc="Oxygen (Emot)Icons - SVG Sources"
    conflicts=('oxygen-icons-svg')
    replaces=('oxygen-icons-svg')

    cd $_origname-${pkgver}
    find scalable -type f ! -name '*.sh' -exec \
        install -D -m644 "{}" "${pkgdir}/usr/share/icons/oxygen/{}" \;
}
