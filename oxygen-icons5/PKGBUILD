
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

_origname=oxygen-icons5
pkgname=('oxygen-icons5' 'oxygen-icons5-svg')
arch=('x86_64')
pkgver=${_kf5ver}
pkgrel=1
pkgdesc="split package"
url="http://www.kde.org"
license=('GPL' 'LGPL' 'FDL')
makedepends=('extra-cmake-modules')
source=($_mirror/${_origname}-$_kf5ver.tar.xz)
md5sums=(`grep ${_origname}-$_kf5ver.tar.xz ../kf5.md5 | cut -d" " -f1`)

build () {  
  mkdir -p build
  cd build
  
  cmake ../${_origname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DQML_INSTALL_DIR=/usr/lib/qt5/qml \
    -DPLUGIN_INSTALL_DIR=/usr/lib/qt5/plugins \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON 
  make
}

package_oxygen-icons5() {
    pkgdesc="Oxygen Icons theme"
    conflicts=('oxygen-icons')
    replaces=('oxygen-icons')
    provides=('oxygen-icons')
	
    cd build
	
    make DESTDIR=${pkgdir} install 
    # conflicts with kdepim
    #rm ${pkgdir}/usr/share/icons/oxygen/*/mimetypes/x-mail-distribution-list.png
}

package_oxygen-icons5-svg() {
    pkgdesc="Oxygen (Emot)Icons - SVG Sources"
    conflicts=('oxygen-icons-svg')
    replaces=('oxygen-icons-svg')
	
    cd $_origname-${pkgver}
    find scalable -type f ! -name '*.sh' -exec \
        install -D -m644 "{}" "${pkgdir}/usr/share/icons/oxygen/{}" \;
}
