
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

_origname=oxygen-icons
pkgname=('oxygen-icons5' 'oxygen-icons5-svg')
arch=('x86_64')
pkgver=5.245.0
pkgrel=1
url="https://invent.kde.org/frameworks/oxygen-icons"
license=('GPL' 'LGPL' 'FDL')
depends=('qt6-base')
makedepends=('extra-cmake-modules')
source=("https://download.kde.org/unstable/oxygen-icons/${_origname}-${pkgver}.tar.xz")
sha256sums=('b082a1a9a6d06cdeee2863555951609e95dd499f133035d04719a16f8500497f')

build () {
  cmake -B build -S ${_origname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DQML_INSTALL_DIR=/usr/lib/qt5/qml \
    -DPLUGIN_INSTALL_DIR=/usr/lib/qt5/plugins \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON 
  cmake --build build
}

package_oxygen-icons5() {
    pkgdesc="Oxygen Icons theme"
    conflicts=('oxygen-icons')
    replaces=('oxygen-icons')
    provides=('oxygen-icons')

    DESTDIR=${pkgdir} cmake --install build
    # conflicts with kdepim
    #rm ${pkgdir}/usr/share/icons/oxygen/*/mimetypes/x-mail-distribution-list.png
}

package_oxygen-icons5-svg() {
    pkgdesc="Oxygen (Emot)Icons - SVG Sources"
    conflicts=('oxygen-icons-svg')
    replaces=('oxygen-icons-svg')

    cd $_origname-${pkgver}
    find scalable -type f ! -name '*.sh' -exec \
        install -D -m644 "{}" "${pkgdir}/usr/share/icons/oxygen/{}" \;
}
