
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=ark6
_pkgname=ark
arch=('x86_64')
pkgver=${_kdever}
pkgrel=2
pkgdesc="Graphical file compression/decompression utility with support for multiple formats, including tar, gzip, bzip2, rar and zip, as well as CD-ROM images"
url="https://apps.kde.org/ark/"
license=('GPL' 'LGPL' 'FDL')
depends=('karchive6' 'kconfig6' 'kcrash6' 'kdbusaddons6' 'ki18n6' 'kitemmodels6' 'kio6' 'kservice6' 
         'kparts6' 'kpty6' 'kwidgetsaddons6' 'kiconthemes6'
         'libarchive' 'p7zip' 'zip' 'unzip' 'unrar' 'libzip' 'lzop')
makedepends=('extra-cmake-modules6' 'kdoctools6')
#groups=('kde' 'kde-utils' 'kde-uninstall' 'utils')
source=($_mirror/${_pkgname}-${_kdever}.tar.xz
        "https://invent.kde.org/utilities/ark/-/commit/e0e998ed4aad8b8c5dffaed8a49b0b97dd78a27b.diff")
md5sums=(`grep ${_pkgname}-${_kdever}.tar.xz ../kde-sc.md5 | cut -d" " -f1`
         '2878a958ede72ee6415a9c13ae5fc1b1')

prepare() {
  cd ${_pkgname}-${pkgver}

  patch -p1 -i ${srcdir}/e0e998ed4aad8b8c5dffaed8a49b0b97dd78a27b.diff
  
  #sed -i 's|application/x-rar|application/vnd.rar|' plugins/clirarplugin/CMakeLists.txt
}

build() {  
  cmake -B build -S ${_pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DBUILD_TESTING=OFF \
    -DQT_MAJOR_VERSION=6
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
