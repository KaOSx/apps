
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname="kde-runtime"    
arch=('x86_64')
pkgver=${_kdever}
pkgrel=1
pkgdesc="KDE Runtime"
url="http://www.kde.org"
license=('GPL' 'LGPL' 'FDL')
options=('docs')
depends=("kdelibs>=${_kdever}" "nepomuk-core>=${_kdever}" "kactivities>=${_kdever}" 'oxygen-icons'
         'xorg-xauth' 'hicolor-icon-theme' 'samba' 'rarian' 'libssh' 'pulseaudio' 'libssh'
         'libcanberra' 'sound-theme-freedesktop')
makedepends=('pkgconfig' 'cmake' 'automoc4' "kdepimlibs>=${_kdever}" 'openslp' 'docbook-xsl'
             'networkmanager' 'xine-lib' 'libcanberra-pulse' 'soprano')
optdepends=('gdb: drkonq crash handler')
groups=("kde" "kde-minimal" "kde-uninstall")
replaces=('kdebase-runtime' 'kde-runtime-doc')
provides=('kdebase-runtime' 'kde-runtime-doc')
conflicts=('kdebase-runtime' 'kde-runtime-doc')
install=kde-runtime.install
noextract=('branding.svgz')

source=($_mirror/${pkgname}-$_kdever.tar.xz
	'branding.svgz')

md5sums=(`grep ${pkgname}-$_kdever.tar.xz ../kde-sc.md5 | cut -d" " -f1`
        '68cd8eb57db22c2f2ed554b721253bc2')   #  branding.svgz

build() {
	cd ${pkgname}-${pkgver}
 
	cp -f ${srcdir}/branding.svgz ${srcdir}/${pkgname}-${pkgver}/desktoptheme/oxygen/widgets/
	cp -f ${srcdir}/branding.svgz ${srcdir}/${pkgname}-${pkgver}/desktoptheme/air/widgets/

        rm -rf build
        mkdir build && cd build

	msg "starting build ..."
	cmake .. -DCMAKE_BUILD_TYPE=${_build_type} \
		-DCMAKE_INSTALL_PREFIX=${_installprefix} \
		-DWITH_PulseAudio=ON \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed'

	make
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}/build
	make DESTDIR=${pkgdir} install

	# put kdesu into the path
	cp ${pkgdir}/usr/lib/kde4/libexec/kdesu ${pkgdir}/usr/bin/kdesu

	# WORKAROUND, conflict with hicolor-icon-theme
	rm -rf ${pkgdir}/usr/share/icons/hicolor/index.theme
	rm -rf ${pkgdir}/usr/share/icons/default.kde4
}
