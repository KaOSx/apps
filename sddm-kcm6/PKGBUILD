
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=sddm-kcm6
_pkgname=sddm-kcm
pkgver=${_plasmaver}
pkgrel=1
epoch=1
pkgdesc="SDDM configuration module for KDE Plasma"
arch=('x86_64')
url="https://invent.kde.org/plasma/sddm-kcm"
license=("GPL")
depends=('kcoreaddons6' 'ki18n6' 'kxmlgui6' 'kauth6' 'kconfigwidgets6' 'kio6' 'karchive6' 'knewstuff6' 
         'kdeclarative6' 'kcmutils6' 'sddm' 'libx11' 'libxcb' 'libxcursor')
makedepends=('extra-cmake-modules' 'kdoctools6' 'qt6-tools')
groups=('kdeplasma6')
source=("https://download.kde.org/stable/plasma/${pkgver}/${_pkgname}-${pkgver}.tar.xz")
md5sums=(`grep ${_pkgname}-${_plasmaver}.tar.xz ../kf5.md5 | cut -d" " -f1`)

build() {
  cmake -B build -S ${_pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_LIBEXECDIR=/usr/lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_WITH_QT6=ON \
    -DEXCLUDE_DEPRECATED_BEFORE_AND_AT=${_kf5ver} \
    -DQT_MAJOR_VERSION=6
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
