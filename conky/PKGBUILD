
pkgname=conky
pkgver=1.11.6
pkgrel=1
pkgdesc="Advanced, highly configurable system monitor for X based on torsmo"
arch=('x86_64')
url="https://github.com/brndnmtthws/conky/wiki"
license=('custom')
depends=('alsa-lib' 'libxml2' 'curl' 'wireless_tools' 'libxft' 'glib2' 'libxdamage' 'imlib2' 'lua'
         'mpd' 'libxinerama')
makedepends=('pkgconfig' 'cmake')
groups=('utils')
backup=('etc/conky/conky.conf' 'etc/conky/conky_no_x11.conf')
source=("https://github.com/brndnmtthws/conky/archive/v${pkgver}.tar.gz"
        'conky.conf')
md5sums=('8e7a58c1ecbe65628475361cc16a3938'
         '3e8bc1cf7cf7587ccc5b333ef33284e0')
    
prepare() {
  cd ${pkgname}-${pkgver}
  
  sed -i -e 's|REQUIRED lua>=5.3 lua5.3 lua-5.3 lua53 lua5.2 lua-5.2 lua52|REQUIRED lua|g' 3rdparty/toluapp/CMakeLists.txt
  sed -i -e 's|lua>=5.3|lua|g' cmake/ConkyPlatformChecks.cmake
}
         
build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DDOC_PATH=/etc/conky \
    -DBUILD_WLAN=true \
    -DBUILD_IMLIB2=true \
    -DBUILD_MPD=true \
    -DBUILD_CURL=true \
    -DBUILD_RSS=true \
    -DBUILD_XDBE=true
  make
}


package() {
  cd build
  make DESTDIR=${pkgdir} install 
  
  #rm ${pkgdir}/etc/conky/conky.conf
  install -D -m755 ${srcdir}/conky.conf ${pkgdir}/etc/conky/conky.conf
  
  install -D -m644 ../${pkgname}-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
         

