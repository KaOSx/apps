#
# KDE SC Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=qhull
pkgver=2010.1
pkgrel=1
pkgdesc="A general dimension code for computing convex hulls and related structures"
arch=('i686' 'x86_64')
url="http://www.qhull.org/"
license=('custom')
depends=('glibc')
makedepends=('cmake')
source=(http://www.qhull.org/download/$pkgname-$pkgver-src.tgz
	qhull-2010.1-cmake-install.patch
	qhull-2010.1-overflows.patch)
options=('!libtool')
sha1sums=('ed480912e03efdd767195c06c7e8c252c78112b1'
          '0a7747eb13c2e07c25882c1f0f5c0dfb4ef1c8c0'
          '21295d57af2f8cdfc775512fa44fe90f49d89c41')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # gentoo patches
  patch -Np1 -i ${srcdir}/qhull-2010.1-cmake-install.patch
  patch -Np1 -i ${srcdir}/qhull-2010.1-overflows.patch

  [ -d build ] || mkdir build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DDOC_INSTALL_DIR=/usr/share/doc \
	-DLIB_INSTALL_DIR=/usr/lib ..
  make
}

package() { 
  cd "$srcdir/$pkgname-$pkgver/build"

  make DESTDIR=$pkgdir install

  # install manpages
  install -Dm644 $srcdir/$pkgname-$pkgver/html/rbox.man $pkgdir/usr/share/man/man1/rbox.1
  install -Dm644 $srcdir/$pkgname-$pkgver/html/qhull.man $pkgdir/usr/share/man/man1/qhull.1

  # Install license
  install -Dm644 $srcdir/$pkgname-$pkgver/COPYING.txt "$pkgdir/usr/share/licenses/$pkgname/license.txt"
}

