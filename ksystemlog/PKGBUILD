
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=ksystemlog
pkgver=${_kdever}
pkgrel=2
pkgdesc='KDE SystemLog Application'
url='https://apps.kde.org/ksystemlog/'
arch=('x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kxmlgui6' 'kcoreaddons6' 'kwidgetsaddons6' 'kitemviews6' 'kio6' 'kconfig6' 'karchive6'
         'ki18n6' 'kcompletion6' 'ktextwidgets6' 'qt6-5compat')
makedepends=('extra-cmake-modules' 'kdoctools6')
groups=('kde' 'kde-admin' 'kde-uninstall')
source=("http://download.kde.org/stable/applications/$_kdever/src/${pkgname}-$_kdever.tar.xz"
        'syslog-path.patch')
md5sums=(`grep ${pkgname}-$_kdever.tar.xz ../kde-sc.md5 | cut -d" " -f1`
         '04abba8db538ab4bebac678d19482bd5')

prepare() {
  cd $pkgname-${pkgver}
  
  sed -i -e 's|<Action name="kernelLogMode" />|<Action name="cupsLogMode" />|' src/ksystemlogui.rc
  sed -i -e 's|i18n("System Log")|i18n("Pacman Log")|' src/modes/system/systemLogMode.cpp
  patch -p1 -i ${srcdir}/syslog-path.patch
}

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DQT_MAJOR_VERSION=6
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
