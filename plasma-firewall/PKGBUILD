
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=plasma-firewall
pkgver=${_plasmaver}
pkgrel=2
arch=('x86_64')
pkgdesc="Firewall KCM that provides users with a graphical user interface (GUI) for the ufw (Uncomplicated Firewall)"
url="https://invent.kde.org/network/plasma-firewall"
license=('BSD-3-Clause')
depends=('kcoreaddons' 'kcmutils' 'ki18n' 'plasma-framework' 'kdeclarative'
         'kauth' 'kconfig' 'kio' 'knewstuff' 'kconfigwidgets' 'python3' 'ufw')
makedepends=('extra-cmake-modules' 'kdoctools')
groups=('utils')
conflicts=('nomad-firewall' 'nx-firewall')
replaces=('nomad-firewall' 'nx-firewall')
provides=('nomad-firewall' 'nx-firewall')
source=("https://download.kde.org/unstable/plasma/${pkgver}/${pkgname}-${pkgver}.tar.xz"
        'ufw_enable.diff')
md5sums=(`grep ${pkgname}-${_plasmaver}.tar.xz ../kf5.md5 | cut -d" " -f1`
         '9735b8e07c2640d79fd57f9e45fa5784')

prepare() {
  cd ${pkgname}-${pkgver}
  
  patch -p1 -i ${srcdir}/ufw_enable.diff
  sed -i 's|findExecutable("ss")|findExecutable("/usr/sbin/ss")|' kcm/backends/netstat/netstathelper.cpp
  sed -i 's|QStringLiteral("ss")|QStringLiteral("/usr/sbin/ss")|' kcm/backends/netstat/netstathelper.cpp
}

build() {  
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_LIBEXECDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build 
}
