
pkgname=avogadrolibs
pkgver=1.93.0
pkgrel=7
pkgdesc="Libraries providing 3D rendering, visualization, analysis and data processing useful in computational chemistry and related areas."
arch=('x86_64')
url="https://www.openchemistry.org/projects/avogadro2/"
license=('GPL2')
depends=('qt5-base' 'glew' 'hdf5' 'spglib' 'libarchive') #'jsoncpp' 
makedepends=('cmake' 'boost' 'eigen3')
groups=('education')
conflicts=('avogadro')
replaces=('avogadro')
source=("https://github.com/OpenChemistry/avogadrolibs/archive/${pkgver}.tar.gz"
        "https://github.com/OpenChemistry/avogadrolibs/pull/488/commits/32e3dd8ce9f48f74e3960dc9afcc614496d87c7f.diff")
md5sums=('d27c6f1a4653577a063260969d3e72ce'
         'd9c78a3ad61228af53d802d6e25ea70b')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i ${srcdir}/32e3dd8ce9f48f74e3960dc9afcc614496d87c7f.diff
  #sed -i 's|NAMES spglib|NAMES symspg|' cmake/FindSpglib.cmake
}

build() {
  mkdir build
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DUSE_MOLEQUEUE=OFF \
    -DUSE_LIBMSYM=OFF \
    -DUSE_OPENGL=ON \
    -DUSE_QT=ON \
    -DUSE_HDF5=ON \
    -DUSE_VTK=OFF \
    -DUSE_MMTF=OFF
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
  
  # use system jsoncpp
  #rm ${pkgdir}/usr/lib/libjsoncpp.a
  
  install -Dm644 ../${pkgname}-${pkgver}/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
