
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=kservice
pkgver=${_kf5ver}
pkgrel=1
pkgdesc='KService provides a plugin framework for handling desktop services'
arch=('x86_64')
url='https://github.com/KDE/kservice'
license=('LGPL')
depends=('ki18n' 'kconfig' 'kcrash' 'kdbusaddons')
makedepends=('extra-cmake-modules' 'kdoctools')
groups=('kf5')
source=("https://download.kde.org/stable/frameworks/5.50/${pkgname}-${pkgver}.tar.xz")
md5sums=(`grep ${pkgname}-${_kf5ver}.tar.xz ../kf5.md5 | cut -d" " -f1`)

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DECM_MKSPECS_INSTALL_DIR=/usr/lib/qt5/mkspecs/modules \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  
  make DESTDIR="${pkgdir}" install
}
