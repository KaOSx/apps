#
# KDE SC Packages for Chakra, part of chakra-project.org
#
# maintainer: Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname="kdeutils-ark"
_pkgname="ark"
arch=('x86_64')
pkgver=${_kdever}
pkgrel=1
pkgdesc="Archiver for various compression formats"
url="http://www.kde.org"
license=('GPL' 'LGPL' 'FDL')
options=('docs' '!splithdr' 'splitdbg' 'log')
depends=("kde-runtime>=${_kdever}" "kde-baseapps>=${_kdever}" 'libarchive' 'qjson')
optdepends=('p7zip' 'zip' 'unzip' 'unrar')
conflicts=("kdeutils-common" "kdeutils-doc")
groups=("kde" "kdeutils" "kde-uninstall")
categories=('utils')
provides=('ark')
install=kdeutils.install

makedepends=('pkg-config' 'cmake' 'automoc4' "kdelibs>=${_kdever}"
             "kde-baseapps>=${_kdever}"  "kde-workspace>=${_kdever}" 
	     'libarchive' 'p7zip' 'zip' 'unzip' 'unrar' 'qjson' 'docbook-xsl')

source=($_mirror/${_pkgname}-$_kdever.tar.xz)
md5sums=(`grep ${_pkgname}-$_kdever.tar.xz ../kde-sc.md5 | cut -d" " -f1`)


build() {
	cd ${srcdir}/${_pkgname}-${pkgver}

	msg "starting build ..."
	cmake . -DCMAKE_BUILD_TYPE=${_build_type} \
		-DCMAKE_INSTALL_PREFIX=${_installprefix} \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed'

	make
}

package() {
	cd ${srcdir}/${_pkgname}-${pkgver}
	make DESTDIR=${pkgdir} install
}
