
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=kinfocenter6
_pkgname=kinfocenter
pkgver=${_plasmaver}
pkgrel=1
pkgdesc='View information about your computers hardware'
arch=('x86_64')
url='https://invent.kde.org/plasma/kinfocenter'
license=('LGPL')
depends=('glu' 'kconfig6' 'kconfigwidgets6' 'kcoreaddons6' 'ki18n6' 'kcmutils6' 'kio6'
         'kservice6' 'solid6' 'kwidgetsaddons6' 'kdeclarative6' 'kpackage6'
         'libraw1394' 'pciutils' 'wayland-utils' 'systemsettings6' 'vulkan-tools'
         'xorg-utils' 'mesa-demos' 'procps-ng' 'fwupd' 'dmidecode')
makedepends=('extra-cmake-modules' 'kdoctools6')
groups=('kdeplasma6')
source=("https://download.kde.org/stable/plasma/${pkgver}/${_pkgname}-${pkgver}.tar.xz"
        'kcm-about-distrorc'
        'KaOS.png')
md5sums=(`grep ${_pkgname}-${_plasmaver}.tar.xz ../kf5.md5 | cut -d" " -f1`
         'f4e373cde99c52125bafc3bea2a7afd9'
         '0957d15eee83c30c1351571d3b875bc9')

build() {
  cmake -B build -S ${_pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_LIBEXECDIR=/usr/lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_WITH_QT6=ON \
    -DEXCLUDE_DEPRECATED_BEFORE_AND_AT=${_kf5ver} \
    -DQT_MAJOR_VERSION=6
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build

  install -Dm644 ${srcdir}/kcm-about-distrorc ${pkgdir}/etc/xdg/kcm-about-distrorc
  install -Dm644 ${srcdir}/KaOS.png ${pkgdir}/usr/share/${pkgname}/KaOS.png
}

