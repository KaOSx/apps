
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=okteta
pkgver=0.26.6
pkgrel=1
epoch=1
pkgdesc="KDE hex editor for viewing and editing the raw data of files"
arch=('x86_64')
url="https://projects.kde.org/projects/kde/kdesdk/okteta"
license=('LGPL')
depends=('kcmutils' 'kcodecs' 'kcompletion' 'kconfigwidgets' 'kdbusaddons' 'ki18n' 'knewstuff' 
         'kparts' 'kservice' 'kwidgetsaddons' 'kxmlgui' 'qca-qt5' 'qt5-tools')
makedepends=('extra-cmake-modules' 'kdoctools')
source=("https://download.kde.org/stable/okteta/${pkgver}/src/${pkgname}-${pkgver}.tar.xz")
sha256sums=('c569cd5b55503e46df1a5b5c9162b08888c426aa52c6f3f2f926c658bee0144c')

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_QMLDIR=/usr/lib/qt5/qml \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
