
pkgname=qmmp
pkgver=2.1.1
_pkgver=2.1
_commit=dev-code-5352-branches-qmmp-1.0
pkgrel=5
pkgdesc="Qt based audio-player"
arch=('x86_64')
url="https://qmmp.ylsoftware.com/"
license=('GPL')
depends=('alsa-lib' 'curl' 'hicolor-icon-theme' 'libmad' 'libvorbis' 'libogg'  'qt6-base'
         'taglib' 'xdg-utils' 'enca' 'opusfile' 'qt6-multimedia' 'libcdio' 'libarchive' 'solid'
         'flac' 'jack2' 'libmpcdec' 'libmodplug' 'libsndfile' 'wavpack' 'libcdio-paranoia'
         'pipewire')
makedepends=('cmake' 'ffmpeg' 'libsamplerate' 'mesa' 'qt6-tools')
groups=('multimedia')
source=("https://qmmp.ylsoftware.com/files/qmmp/${_pkgver}/qmmp-${pkgver}.tar.bz2")
#source=("http://sourceforge.net/code-snapshots/svn/q/qm/qmmp-dev/code/qmmp-${_commit}.zip")
md5sums=('617ac31fa859d426646a7b61db1eaf36')

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DUSE_CDA=0 \
    -DUSE_SKINNED=0 \
    -DUSE_QSUI=1
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
