

pkgname=konversation
pkgver=1.7.7
_pkgver=1.7.7
pkgrel=1
pkgdesc="A user friendly IRC client for KDE"
arch=('x86_64')
url="https://konversation.kde.org"
license=('GPL2' 'FDL')
depends=('karchive' 'kbookmarks' 'kconfig' 'kconfigwidgets' 'kcoreaddons' 'kcrash' 'kemoticons' 'ki18n' 
         'kidletime' 'knotifyconfig' 'kio' 'kparts' 'solid' 'kwallet' 'kwidgetsaddons' 'kglobalaccel'
         'kdbusaddons' 'knotifications' 'kwindowsystem' 'kiconthemes' 'kitemviews'
         'phonon-qt5' 'qca-qt5')
makedepends=('extra-cmake-modules' 'kdoctools')
groups=('communication')
source=("https://download.kde.org/stable/${pkgname}/${_pkgver}/src/${pkgname}-${_pkgver}.tar.xz"
        "https://invent.kde.org/network/konversation/-/commit/461c7dd504299871d6e9068116125b9bd26e8985.diff")
sha256sums=('47ec161c1385981a8b99d28c419e88b24823dc01b68fb1623894818974c218a7'
            '70da99f78949ea0112bb16503f5bb0f206739dcaea03c09dfbc60385fb57b49e')

prepare() {
  cd ${pkgname}-${_pkgver}
  # set default channel to KaOS
  sed -i -e 's|setName(QStringLiteral("#konversation")|setName(QStringLiteral("#kaosx")|' src/config/preferences.cpp
  
  #patch -p1 -i ${srcdir}/461c7dd504299871d6e9068116125b9bd26e8985.diff
}

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${_pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_QMLDIR=/usr/lib/qt5/qml \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
    -DBUILD_TESTING=OFF 
  make
}

package() {
  cd build
  
  make DESTDIR=${pkgdir} install
}
