
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=konversation
pkgver=${_kdever}
pkgrel=3
pkgdesc="A user friendly IRC client for KDE"
arch=('x86_64')
url="https://konversation.kde.org"
license=('GPL2' 'FDL')
depends=('karchive6' 'kbookmarks6' 'kcodecs6' 'kconfig6' 'kconfigwidgets6' 'kcoreaddons6'
         'kcrash6' 'kdbusaddons6' 'ki18n6' 'kidletime6' 'kitemviews6' 'kio6' 'knewstuff6'
         'knotifications6' 'knotifyconfig6' 'kparts6' 'kstatusnotifieritem' 'ktextwidgets6'
         'kwallet6' 'kwidgetsaddons6' 'kwindowsystem6' 'kglobalaccel6'
         'phonon-qt6' 'qca-qt6' 'qt6-5compat' 'qt6-tools')
makedepends=('extra-cmake-modules' 'kdoctools6')
groups=('communication')
conflicts=('konversation6')
replaces=('konversation6')
source=($_mirror/${pkgname}-${_kdever}.tar.xz
        "https://invent.kde.org/network/konversation/-/commit/1d554cb2c29e57bfd58b9aed7643dcb60fbf8659.diff")
md5sums=(`grep ${pkgname}-${_kdever}.tar.xz ../kde-sc.md5 | cut -d" " -f1`
         '3f09d3385b9460a8cb3a87ca57c1eed1')

prepare() {
  cd ${pkgname}-${pkgver}
  # set default channel to KaOS
  sed -i -e 's|setName(QStringLiteral("#konversation")|setName(QStringLiteral("#kaos")|' src/config/preferences.cpp

  patch -p1 -i ${srcdir}/1d554cb2c29e57bfd58b9aed7643dcb60fbf8659.diff
}

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF 
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
