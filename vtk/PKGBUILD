
pkgname=vtk
pkgver=9.0.3
_pkgver=9.0
pkgrel=1
pkgdesc='Open-source software system for 3D computer graphics, image processing, and visualization.'
arch=('x86_64')
url='https://gitlab.kitware.com/vtk/vtk'
license=('BSD')
depends=('java-environment' 'gnuplot' 'tk' 'python3-matplotlib' 'unixodbc' 'gdal' 'openmpi' 'mariadb'
         'glew' 'ffmpeg' 'python3-six' 'qtwebkit-tp' 'qt5-x11extras' 'jsoncpp' 'double-conversion'
         'netcdf' 'libtiff' 'libtheora' 'gl2ps')
makedepends=('boost' 'cmake' 'qt5-tools')
options=('!emptydirs')
source=("https://www.vtk.org/files/release/${_pkgver}/VTK-${pkgver}.tar.gz"
        "https://www.vtk.org/files/release/${_pkgver}/VTKData-${pkgver}.tar.gz"
        "https://www.vtk.org/files/release/${_pkgver}/VTKLargeData-${pkgver}.tar.gz"
        'gdal2.patch')
md5sums=('1abe6e2d7988193cf7d64c4d84287956'
         'd4c1eed39fab9a2583b0f61e869314dd'
         '2499ab7975d13e0fb74659685b20ce76'
         'e85edd330937a2395a233e0cb6be0b83')

prepare() {
  cd VTK-${pkgver}

  #patch -p1 -i ${srcdir}/gdal2.patch
}

build() {
  mkdir -p build
  cd build

  #export JAVA_HOME=/usr/lib/jvm/default

  # use system libs
  local cmake_system_flags=""
  for lib in EXPAT FREETYPE JPEG PNG TIFF ZLIB OGGTHEORA SIX JSONCPP GLEW DOUBLECONVERSION LZ4 LZMA; do
    cmake_system_flags+="-DVTK_USE_EXTERNAL_${lib}:BOOL=ON "
  done

  cmake ../VTK-${pkgver} \
    -Wno-dev \
    -DCMAKE_SKIP_INSTALL_RPATH=ON \
    -DBUILD_SHARED_LIBS:BOOL=ON \
    -DCMAKE_INSTALL_PREFIX:FILEPATH=/usr \
    -DBUILD_EXAMPLES:BOOL=ON \
    -DVTK_USE_FFMPEG_ENCODER:BOOL=ON \
    -DVTK_BUILD_ALL_MODULES:BOOL=ON \
    -DVTK_ENABLE_OSPRAY=OFF \
    -DVTK_MODULE_ENABLE_VTK_DomainsMicroscopy="DONT_WANT" \
    -DVTK_MODULE_ENABLE_VTK_FiltersOpenTURNS="DONT_WANT" \
    -DVTK_MODULE_ENABLE_VTK_IOADIOS2="DONT_WANT" \
    -DVTK_MODULE_ENABLE_VTK_IOLAS="DONT_WANT" \
    -DVTK_MODULE_ENABLE_VTK_IOPDAL="DONT_WANT" \
    -DVTK_MODULE_ENABLE_VTK_MomentInvariants="DONT_WANT" \
    -DVTK_MODULE_ENABLE_VTK_PoissonReconstruction="DONT_WANT" \
    -DVTK_MODULE_ENABLE_VTK_Powercrust="DONT_WANT" \
    -DVTK_MODULE_ENABLE_VTK_RenderingOpenVR="DONT_WANT" \
    -DVTK_MODULE_ENABLE_VTK_SignedTensor="DONT_WANT" \
    -DVTK_MODULE_ENABLE_VTK_SplineDrivenImageSlicer="DONT_WANT" \
    -DVTK_MODULE_ENABLE_VTK_vtkDICOM="DONT_WANT" \
    -DVTK_USE_LARGE_DATA:BOOL=ON \
    -DVTK_WRAP_JAVA:BOOL=ON \
    -DVTK_WRAP_PYTHON:BOOL=ON \
    -DVTK_PYTHON_VERSION="3" \
    -DVTK_WRAP_TCL:BOOL=ON \
    -DCMAKE_CXX_FLAGS="-D__STDC_CONSTANT_MACROS" \
    -DVTK_CUSTOM_LIBRARY_SUFFIX="" \
    -DVTK_INSTALL_INCLUDE_DIR:PATH=include/vtk \
    -DVTK_RENDERING_BACKEND="OpenGL2" \
    -DVTK_USE_SYSTEM_GL2PS=ON \
    ${cmake_system_flags} \
    -DCMAKE_BUILD_TYPE=Release 

  make
}

package() {
  cd build

  make DESTDIR=${pkgdir} install

  install -dv ${pkgdir}/usr/share/java/vtk
  mv -v ${pkgdir}/usr/lib/java/vtk.jar ${pkgdir}/usr/share/java/vtk
  #rm -rf ${pkgdir}/usr/lib/vtk-${_pkgver}/java

  install -dv ${pkgdir}/usr/share/licenses/vtk
  install -m644 ${srcdir}/VTK-${pkgver}/Copyright.txt ${pkgdir}/usr/share/licenses/vtk

  #install -dv ${pkgdir}/usr/lib/qt5
  #mv ${pkgdir}/usr/plugins ${pkgdir}/usr/lib/qt5/plugins
}

