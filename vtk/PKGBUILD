
pkgname=vtk
pkgver=9.2.6
_pkgver=9.2
pkgrel=2
pkgdesc='Open-source software system for 3D computer graphics, image processing, and visualization.'
arch=('x86_64')
url='https://gitlab.kitware.com/vtk/vtk'
license=('BSD')
depends=('java-environment' 'gnuplot' 'tk' 'python3-matplotlib' 'unixodbc' 'gdal' 'openmpi' 'mariadb'
         'glew' 'ffmpeg' 'python3-six' 'qt5-x11extras' 'jsoncpp' 'double-conversion'
         'netcdf' 'libtiff' 'libtheora' 'gl2ps' 'fmt')
makedepends=('boost' 'cmake' 'qt5-tools')
options=('!emptydirs')
source=("https://www.vtk.org/files/release/${_pkgver}/VTK-${pkgver}.tar.gz"
        "https://www.vtk.org/files/release/${_pkgver}/VTKData-${pkgver}.tar.gz"
        "https://www.vtk.org/files/release/${_pkgver}/VTKLargeData-${pkgver}.tar.gz"
        'gdal2.patch')
sha256sums=('06fc8d49c4e56f498c40fcb38a563ed8d4ec31358d0101e8988f0bb4d539dd12'
            '032c4b827173f859c898403d25360dc99409a4674559ad58f48828f23a6258b8'
            'b1dbb25f50e325f6df5df465c395eea6d252071e1b3c1fe0803f9b75a7c0e3ea'
            '57ae622a7e281887f7948f4d5a53ff0e0301bbdf70594691dd74f20f14b6e882')

prepare() {
  cd VTK-${pkgver}

  #patch -p1 -i ${srcdir}/gdal2.patch
}

build() {
  mkdir -p build
  cd build

  #export JAVA_HOME=/usr/lib/jvm/default

  # use system libs
  local cmake_system_flags=""
  for lib in EXPAT FMT FREETYPE JPEG PNG TIFF ZLIB OGGTHEORA SIX JSONCPP GLEW DOUBLECONVERSION LZ4 LZMA; do
    cmake_system_flags+="-DVTK_USE_EXTERNAL_${lib}=ON "
  done

  cmake ../VTK-${pkgver} \
    -Wno-dev \
    -DCMAKE_SKIP_INSTALL_RPATH=ON \
    -DBUILD_SHARED_LIBS=ON \
    -DCMAKE_INSTALL_PREFIX:FILEPATH=/usr \
    -DBUILD_EXAMPLES=ON \
    -DVTK_USE_FFMPEG_ENCODER=ON \
    -DVTK_BUILD_ALL_MODULES=OFF \
    -DVTK_ENABLE_OSPRAY=OFF \
    -DVTK_MODULE_ENABLE_VTK_DomainsMicroscopy=NO \
    -DVTK_MODULE_ENABLE_VTK_fides=NO \
    -DVTK_MODULE_ENABLE_VTK_FiltersOpenTURNS=NO \
    -DVTK_MODULE_ENABLE_VTK_IOADIOS2=NO \
    -DVTK_MODULE_ENABLE_VTK_IOLAS=NO \
    -DVTK_MODULE_ENABLE_VTK_IOPDAL=NO \
    -DVTK_MODULE_ENABLE_VTK_IOOpenVDB=NO \
    -DVTK_MODULE_ENABLE_VTK_MomentInvariants=NO \
    -DVTK_MODULE_ENABLE_VTK_PoissonReconstruction=NO \
    -DVTK_MODULE_ENABLE_VTK_Powercrust=NO \
    -DVTK_MODULE_ENABLE_VTK_RenderingOpenVR=NO \
    -DVTK_MODULE_ENABLE_VTK_SignedTensor=NO \
    -DVTK_MODULE_ENABLE_VTK_SplineDrivenImageSlicer=NO \
    -DVTK_MODULE_ENABLE_VTK_vtkDICOM=NO \
    -DVTK_USE_LARGE_DATA=ON \
    -DVTK_WRAP_JAVA=ON \
    -DVTK_WRAP_PYTHON=ON \
    -DVTK_PYTHON_VERSION="3" \
    -DVTK_WRAP_TCL=ON \
    -DCMAKE_CXX_FLAGS="-D__STDC_CONSTANT_MACROS" \
    -DVTK_CUSTOM_LIBRARY_SUFFIX="" \
    -DVTK_INSTALL_INCLUDE_DIR:PATH=include/vtk \
    -DVTK_RENDERING_BACKEND="OpenGL2" \
    -DVTK_USE_SYSTEM_GL2PS=ON \
    ${cmake_system_flags} \
    -DCMAKE_BUILD_TYPE=Release 

  make
}

package() {
  cd build

  make DESTDIR=${pkgdir} install

  install -dv ${pkgdir}/usr/share/java/vtk
  mv -v ${pkgdir}/usr/lib/java/vtk.jar ${pkgdir}/usr/share/java/vtk
  #rm -rf ${pkgdir}/usr/lib/vtk-${_pkgver}/java

  install -dv ${pkgdir}/usr/share/licenses/vtk
  install -m644 ${srcdir}/VTK-${pkgver}/Copyright.txt ${pkgdir}/usr/share/licenses/vtk

  #install -dv ${pkgdir}/usr/lib/qt5
  #mv ${pkgdir}/usr/plugins ${pkgdir}/usr/lib/qt5/plugins
}

