
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=plasma-desktop6
_pkgname=plasma-desktop
pkgver=${_plasmaver}
pkgrel=1
pkgdesc='KDE Plasma Desktop'
arch=('x86_64')
url='https://invent.kde.org/plasma/plasma-desktop'
license=('LGPL')
depends=('qt6-svg' 'libxkbfile' 'libusb' 'libcanberra' 'polkit-kde-agent-6'
         'systemsettings6' 'ksystemstats6' 'powerdevil6' 'kmenuedit6' 'kpeople6' 
         'kdbusaddons6' 'qqc2-desktop-style6' 'xorg-server' 'signon-oauth2'
         'kactivities-stats6' 'xf86-input-evdev' 
         'xf86-input-libinput' 'xdg-user-dirs' 'plasma-integration6') #'kaccounts-integration' 'kio-fuse'
makedepends=('extra-cmake-modules6' 'kdoctools6' 'xf86-input-synaptics' 'boost')
groups=('kdeplasma6')
#source=("https://download.kde.org/stable/plasma/${pkgver}/${_pkgname}-${pkgver}.tar.xz"
source=("git+https://invent.kde.org/plasma/${_pkgname}.git"
        'layout_desktop_panel.diff'
        'main_desktop_kicker.diff'
        'main_desktop_kickoff.diff'
        "https://invent.kde.org/plasma/plasma-desktop/-/commit/1dfba7045c82ae2553be2cb7018fcd893c19d7bc.diff")
#md5sums=(`grep ${_pkgname}-${_plasmaver}.tar.xz ../kf5.md5 | cut -d" " -f1`
md5sums=('SKIP'
         '6e13e785600f1a6c1e181ab2934f1558'
         'dbd6e948c39b65c3e84df7d291d9bf47'
         'bc7c07f2754de81b64ead90aa20b1020'
         '46b4248270d75f1c7e481145c5c797a7')

prepare() {
  cd ${_pkgname}
  # adjust default settings for panel, kickoff, kicker & desktop view
  patch -p1 -i ${srcdir}/layout_desktop_panel.diff
  patch -p1 -i ${srcdir}/main_desktop_kicker.diff
  patch -p1 -i ${srcdir}/main_desktop_kickoff.diff
  sed -i 's|org.kde.plasma.folder|org.kde.desktopcontainment|' desktoppackage/contents/defaults
  #patch -p1 -i ${srcdir}/1dfba7045c82ae2553be2cb7018fcd893c19d7bc.diff
}

build() {
  cmake -B build -S ${_pkgname} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_LIBEXECDIR=/usr/lib \
    -DBUILD_TESTING=OFF \
    -DQT_MAJOR_VERSION=6 \
    -DCMAKE_DISABLE_FIND_PACKAGE_PackageKitQt6=ON
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
