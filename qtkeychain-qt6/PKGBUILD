
pkgname=qtkeychain-qt6
_pkgname=qtkeychain
pkgver=0.14.1
pkgrel=2
arch=('x86_64')
pkgdesc="Qt API to store passwords and other secret data securely"
url="https://github.com/frankosterfeld/qtkeychain"
license=('BSD')
depends=('qt6-base' 'kwallet6')
makedepends=('cmake' 'qt6-tools' 'icu' 'clang')
source=("https://github.com/frankosterfeld/qtkeychain/archive/${pkgver}.tar.gz")
md5sums=('3abe32290c1537346731ba53facdd4d7')

build() {
  cmake -B build -S ${_pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_LIBEXECDIR=lib/${pkgname} \
    -DBUILD_WITH_QT6=On \
    -DLIBSECRET_SUPPORT=Off
  cmake --build build
}

package() {
  DESTDIR=${pkgdir} cmake --install build

  install -D -m644 ${_pkgname}-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
