
pkgname=krita
pkgver=4.0.4
_pkgver=4.0.4
pkgrel=1
pkgdesc="Program for sketching and painting, offering an end–to–end solution for creating digital painting files from scratch"
arch=('x86_64')
url='https://krita.org/'
license=('FDL1.2' 'GPL2' 'LGPL')
depends=('karchive' 'kcompletion' 'kconfig' 'kcoreaddons' 'kguiaddons' 'ki18n' 
         'kiconthemes' 'kitemmodels' 'kitemviews' 'ktextwidgets' 'kwidgetsaddons' 'kwindowsystem'
         'kio' 'kimageformats'
         'eigen3' 'gsl' 'lcms2' 'openexr' 'vc' 'opencolorio' 'poppler-qt5' 'hicolor-icon-theme' 'exiv2'
         'libraw' 'qca-qt5' 'qt5-svg' 'boost-libs' 'gmic-qt' 'pyqt5-python3')
makedepends=('extra-cmake-modules' 'kdoctools' 'boost' 'qt5-tools') # 'clang') 
conflicts=('krita-l10n')
replaces=('krita-l10n')
source=("https://download.kde.org/stable/krita/${pkgver}/${pkgname}-${pkgver}.tar.gz"
        "https://github.com/KDE/krita/commit/2f59d0d1d91e3f79342c20d0df68aa9a51817e8d.diff"
        'launch_krita.sh')
sha256sums=('f3d3f27873381897dcbf84b68497812cdbe5ae1bb49eb3826be13cd568577a7c'
            '8b42fa7d830006b747ee4fe620d911430d11baf438a3a065939e687a74174cd2'
            'df345142ede5ec8f2d3422850dd78059944220f163fa30b5cdefdc9bd54235ff')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -i 's|X-KDE-Import": "image/x-krita-raw|X-KDE-Import": "image/x-nikon-nef,image/x-canon-cr2,image/x-sony-sr2,image/x-canon-crw,image/x-pentax-pef,image/x-sigma-x3f,image/x-kodak-kdc,image/x-minolta-mrw,image/x-sony-arw,image/x-kodak-k25,image/x-kodak-dcr,image/x-olympus-orf,image/x-panasonic-raw,image/x-panasonic-raw2,image/x-fuji-raf,image/x-sony-srf,image/x-adobe-dng|' plugins/impex/raw/krita_raw_import.json
  # Qt5.9 needed patch
  #patch -p1 -i ${srcdir}/2f59d0d1d91e3f79342c20d0df68aa9a51817e8d.diff
}

build() {
  mkdir -p build
  cd build
  
  #export CC=/usr/bin/clang
  #export CXX=/usr/bin/clang++
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=FALSE 
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
  # don't override system defaults
  install -Dm755 ${srcdir}/launch_krita.sh ${pkgdir}/usr/bin/launch_krita.sh
  sed 's|Exec=krita %U|Exec=launch_krita.sh|' -i ${pkgdir}/usr/share/applications/org.kde.krita.desktop
}

