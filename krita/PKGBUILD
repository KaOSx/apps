
pkgname=krita
pkgver=4.4.1
_pkgver=4.4.1
pkgrel=3
pkgdesc="Program for sketching and painting, offering an end–to–end solution for creating digital painting files from scratch"
arch=('x86_64')
url='https://krita.org/'
license=('FDL1.2' 'GPL2' 'LGPL')
depends=('karchive' 'kcompletion' 'kconfig' 'kcoreaddons' 'kguiaddons' 'ki18n' 
         'kiconthemes' 'kitemmodels' 'kitemviews' 'ktextwidgets' 'kwidgetsaddons' 'kwindowsystem'
         'kimageformats'
         'eigen3' 'gsl' 'lcms2' 'openexr' 'vc' 'poppler-qt5' 'hicolor-icon-theme' 'exiv2'
         'libraw' 'qca-qt5' 'qt5-svg' 'boost-libs' 'gmic-qt' 'pyqt5-python3' 'quazip' 'qt5-tools' 
         'opencolorio' 'kseexpr')
makedepends=('extra-cmake-modules' 'kdoctools' 'boost') # 'clang') 
conflicts=('krita-l10n')
replaces=('krita-l10n')
source=("https://download.kde.org/stable/krita/${_pkgver}/${pkgname}-${pkgver}.tar.xz"
        "https://invent.kde.org/graphics/krita/-/commit/bdebaf91575fa837697b694219a18c46b5af3f83.diff"
        'launch_krita.sh')
md5sums=('55d11e4951b39b47b083eb56112118f4'
         '2b7ebe822988be40680dc2db4bae168e'
         '673003bb648af0d4f801043ee0a17c2a')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -i 's|X-KDE-Import": "image/x-krita-raw|X-KDE-Import": "image/x-nikon-nef,image/x-canon-cr2,image/x-sony-sr2,image/x-canon-crw,image/x-pentax-pef,image/x-sigma-x3f,image/x-kodak-kdc,image/x-minolta-mrw,image/x-sony-arw,image/x-kodak-k25,image/x-kodak-dcr,image/x-olympus-orf,image/x-panasonic-raw,image/x-panasonic-raw2,image/x-fuji-raf,image/x-sony-srf,image/x-adobe-dng|' plugins/impex/raw/krita_raw_import.json
  # boost 0.73.0 needed, fix from Gentoo
  sed -e "/CMAKE_CXX_STANDARD/s/11/14/" -i CMakeLists.txt
  # Qt 5.15 needed
  patch -p1 -i ${srcdir}/bdebaf91575fa837697b694219a18c46b5af3f83.diff
}

build() {
  mkdir -p build
  cd build
  
  #export CC=/usr/bin/clang
  #export CXX=/usr/bin/clang++
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_KRITA_QT_DESIGNER_PLUGINS=ON \
    -DBUILD_TESTING=FALSE 
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
  # don't override system defaults
  install -Dm755 ${srcdir}/launch_krita.sh ${pkgdir}/usr/bin/launch_krita.sh
  sed 's|Exec=krita %F|Exec=launch_krita.sh|' -i ${pkgdir}/usr/share/applications/org.kde.krita.desktop
}

