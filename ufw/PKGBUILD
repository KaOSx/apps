
pkgname=ufw
pkgver=0.33
pkgrel=1
pkgdesc="Uncomplicated and easy to use CLI tool for managing a netfilter firewall"
arch=('x86_64')
url="https://launchpad.net/ufw"
license=('GPL3')
depends=('iptables' 'python2')
backup=('etc/ufw/before.rules'
        'etc/ufw/before6.rules'
        'etc/ufw/after.rules'
        'etc/ufw/after6.rules'
        'etc/ufw/ufw.conf'
        'etc/ufw/user.rules'
        'etc/ufw/sysctl.conf'
        'etc/default/ufw'
        'usr/lib/ufw/user.rules'
        'usr/lib/ufw/user6.rules')
categories=('network')
source=("http://launchpad.net/${pkgname}/${pkgver}/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}.service")
md5sums=('3747b453d76709e5a99da209fc0bb5f5'
         '2c85da635f53c6817be8c1941a9a35fb')

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -e 's|/lib|/usr/lib|' -i setup.py
  python2 setup.py install --root="${pkgdir}"

  # Install systemd unit
  install -D -m644 "${srcdir}/${pkgname}.service" "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"

  # Move ufw to /usr/bin
  install -D -m755 "${pkgdir}/usr/sbin/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
  rm -rf "${pkgdir}/usr/sbin"

  # Fix permissions
  chmod 644 "${pkgdir}/etc/ufw"/*.rules "${pkgdir}/usr/lib/ufw"/*.rules

  # Absolute paths are bad!
  sed -e 's#/bin/echo#echo#g' -i "${pkgdir}/usr/lib/ufw/ufw-init-functions"
}
