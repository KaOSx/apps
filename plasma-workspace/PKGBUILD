
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=plasma-workspace
pkgver=${_plasmaver}
pkgrel=4
pkgdesc='Provides the interface and basic tools for the KDE Plasma Workspace'
arch=('x86_64')
url='https://projects.kde.org/projects/kde/workspace/plasma-workspace'
license=('LGPL')
depends=('plasma-framework' 'frameworkintegration' 'knotifications' 'knotifyconfig' 'krunner' 'kdesu' 'knewstuff' 
         'kwallet' 'kidletime' 'kdeclarative' 'ki18n' 'kcmutils' 'ktextwidgets' 'kcrash' 'kglobalaccel'
         'kdbusaddons' 'kwayland' 'kcoreaddons' 'kpeople' 'kactivities-stats' 'kactivities' 'kio' 'prison'
         'kpackage' 'kguiaddons' 'karchive' 'kitemmodels' 'kiconthemes' 'kunitconversion' 'kinit'
         'libdbusmenu-qt5' 'glu' 'libxkbfile' 'libxcursor' 'lm_sensors' 'libraw1394'
         'libusb' 'pciutils' 'libbsd' 'kf5-attica'
         'kemoticons' 'libxrandr' 'kded' 'kwin' 'libksysguard' 'ktexteditor'
         'khelpcenter' 'kio-extras' 'baloo' 'milou' 'libkscreen' 'breeze' 'gpsd' 'libqalculate' 'qt5-tools'
         'oxygen-icons5' 'sddm-kcm' 'networkmanager-qt' 'kxmlrpcclient' 'kscreenlocker'
         'kactivitymanagerd' 'iso-codes' 'kholidays' 'drkonqi' 'kuserfeedback'
         'kquickcharts' 'kwayland-integration' 'pipewire' 'layer-shell-qt' 'accountsservice')
makedepends=('extra-cmake-modules' 'kdoctools' 'boost')
conflicts=('plasma-l10n')
replaces=('plasma-l10n')
provides=('plasma-l10n')
groups=('kdeplasma5')
options=('debug')
source=("https://download.kde.org/stable/plasma/${pkgver}/${pkgname}-${pkgver}.tar.xz"
        '10-kf5'
        'kde.pam'
        'kde-np.pam'
        'kscreensaver.pam'
        "https://invent.kde.org/plasma/plasma-workspace/-/commit/f77b7284e39b14b0e9a8b4d2b77c0b93b2c9ea59.diff"
        "https://invent.kde.org/plasma/plasma-workspace/-/commit/4d3f99558cff95259590e70dfbf854a479f772ce.diff")
md5sums=(`grep ${pkgname}-${_plasmaver}.tar.xz ../kf5.md5 | cut -d" " -f1`
         '280f3ca356b59607074da2343a4155b2'
         '6589fb28bd20d5ec1b4a7b9db9fc4209'
         '5a035421a9bfaa353caf04250c2b285c'
         '367a3538f54db71f108b34cfa31088ac'
         '390a151febe90a7e130ffefc8b573516'
         '0944d8474ee44abcdb2e35bc20cec43d')

prepare () {
  cd ${pkgname}-${pkgver}

  patch -p1 -i ${srcdir}/f77b7284e39b14b0e9a8b4d2b77c0b93b2c9ea59.diff
  patch -p1 -i ${srcdir}/4d3f99558cff95259590e70dfbf854a479f772ce.diff
  #sed -i 's|<default>1</default>|<default>2</default>|' applets/systemtray/package/contents/config/main.xml
}

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_LIBEXECDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_QMLDIR=/usr/lib/qt5/qml \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF \
    -DCMAKE_DISABLE_FIND_PACKAGE_PackageKitQt5=ON
  cmake --build build
}

package() {
  DESTDIR=${pkgdir} cmake --install build

  #sed 's|qdbus|qdbus-qt5|' -i $pkgdir/usr/lib/startplasma

  install -Dm755 ${srcdir}/10-kf5 ${pkgdir}/etc/X11/xinit/xinitrc.d/10-kf5

  # install pam configuration, comment out for now, all provided by sddm
  #install -D -m644 ${srcdir}/kde.pam ${pkgdir}/etc/pam.d/kde
  #install -D -m644 ${srcdir}/kde-np.pam ${pkgdir}/etc/pam.d/kde-np
  #install -D -m644 ${srcdir}/kscreensaver.pam ${pkgdir}/etc/pam.d/kscreensaver
}
