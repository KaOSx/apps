
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=plasma-workspace
pkgver=${_plasmaver}
pkgrel=2
pkgdesc='Various components needed to run a Plasma-based environment.'
arch=('x86_64')
url='https://invent.kde.org/plasma/plasma-workspace'
license=('LGPL')
depends=('kauth6' 'kparts6' 'libplasma' 'krunner6' 'knotifications6' 'knotifyconfig6' 'knewstuff6'
         'kwallet6' 'kidletime6' 'kdeclarative6' 'ki18n6' 'kcmutils6' 'ktextwidgets6' 'kcrash6'
         'kglobalaccel6' 'kdbusaddons6' 'kwayland' 'kcoreaddons6' 'kpeople6' 'plasma-activities-stats'
         'plasma-activities' 'kio6' 'prison6' 'kpackage6' 'kguiaddons6' 'karchive6' 'kitemmodels6' 'kiconthemes6' 'kunitconversion6' 'ktexteditor6' 'kded6' 'kpipewire' 'networkmanager-qt6' 'kirigami-addons' 'kquickcharts6'
         'kuserfeedback6' 'libksysguard' 'baloo6' 'kwin' 'libkscreen' 'kscreenlocker' 'layer-shell-qt'
         'kholidays6' 'breeze' 'attica' 'frameworkintegration6' 'milou' 'kdesu6'
         'libqalculate' 'phonon-qt6' 'qt6-wayland' 'qt6-tools' 'plasma-wayland-protocols' 'icu'
         'glu' 'libxkbfile' 'libxcursor' 'lm_sensors' 'libraw1394' 'libusb' 'pciutils' 'libbsd' 'libxrandr'
         'gpsd' 'iso-codes' 'oxygen-icons5' 'sddm-kcm' 'kactivitymanagerd' 'drkonqi' 'plasma5support'
         'kio-extras' 'libkexiv6' 'khelpcenter' 'accountsservice' 'kio-fuse') # 'libdbusmenu-qt' 
makedepends=('extra-cmake-modules' 'kdoctools6' 'boost')
conflicts=('plasma-workspace6')
replaces=('plasma-workspace6')
groups=('kdeplasma6')
options=('debug')
source=("https://download.kde.org/stable/plasma/${pkgver}/${pkgname}-${pkgver}.tar.xz"
        'shellcorona.diff'
        "https://invent.kde.org/plasma/plasma-workspace/-/commit/5841566a33f10236e396222d40451aa3baff0f2a.diff")
md5sums=(`grep ${pkgname}-${_plasmaver}.tar.xz ../kf6.md5 | cut -d" " -f1`
         '1913544f6cbf179033c3a1fdceac1b7e'
         '1fe9968bcd29c205072dd79ba2aeeed2')

prepare () {
  cd ${pkgname}-${pkgver}

  patch -p1 -i ${srcdir}/shellcorona.diff
  # remove PLasma RC banner
  patch -p1 -i ${srcdir}/5841566a33f10236e396222d40451aa3baff0f2a.diff
  #sed -i 's|<default>@KDE_INSTALL_FULL_WALLPAPERDIR@/Next/</default>|<default>@KDE_INSTALL_FULL_WALLPAPERDIR@/midna/</default>|' wallpapers/image/main.xml.in
}

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_LIBEXECDIR=/usr/lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
