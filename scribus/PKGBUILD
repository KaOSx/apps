
pkgname=scribus
_pkgname=Scribus
_commit=3742559924136c2471ab15081c5b600dd5feaeb0
pkgver=1.5.7
pkgrel=14
pkgdesc="A desktop publishing program"
arch=('x86_64')
url="https://www.scribus.net"
license=('GPL')
depends=('libcups' 'lcms2' 'qt5-declarative' 'ghostscript' 'libart-lgpl' 'python3' 'libxml2' 'hyphen'
         'cairo' 'desktop-file-utils' 'shared-mime-info' 'ruby' 'aspell' 'podofo' 'librevenge' 'poppler'
         'hunspell' 'graphicsmagick' 'libvisio' 'icu')
makedepends=('cmake' 'boost' 'qt5-tools')
groups=('office')
options=('!libtool' '!makeflags')
source=("https://sourceforge.net/projects/scribus/files/scribus-devel/${pkgver}/${pkgname}-${pkgver}.tar.xz"
#source=("https://github.com/scribusproject/scribus/archive/${_commit}.zip"
         "https://github.com/scribusproject/scribus/commit/1b546978bc4ea0b2a73fbe4d7cf947887e865162.diff"
         "https://github.com/scribusproject/scribus/commit/68ec41169eaceea4a6e1d6f359762a191c7e61d5.diff")
sha256sums=('318316b2cfc7a76191d3e0d3f8c2265147daea0570162028e243c292d826f8ce'
            'c3f3214ead6948d6d709b99725fb05410a3d1a0532f6b8bfd92f3d5b75187ba0'
            'd5db592b1f69c549348173d2cd3a815756a7c3e5c81bdbc9d3323400fb691a09')

prepare() {
  cd ${pkgname}-${pkgver}

  #sed -i 's|pBase->SetOwner(cs->GetOwner());||' scribus/pdf_analyzer.cpp
  # harfbuzz 3 needed
  patch -p1 -i ${srcdir}/1b546978bc4ea0b2a73fbe4d7cf947887e865162.diff
  patch -p1 -i ${srcdir}/68ec41169eaceea4a6e1d6f359762a191c7e61d5.diff
}

build() {
	cmake -B build -S ${pkgname}-${pkgver} \
	   -DCMAKE_INSTALL_PREFIX=/usr \
	   -DCMAKE_BUILD_TYPE=Release \
	   -DCMAKE_SKIP_RPATH=ON \
	   -DWANT_GRAPHICSMAGICK=1 \
	   -DLIB_SUFFIX=""
	cmake --build build
}

package() {

    DESTDIR=${pkgdir} cmake --install build
}
