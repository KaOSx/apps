
pkgname=scribus
_pkgname=Scribus
_commit=3742559924136c2471ab15081c5b600dd5feaeb0
pkgver=1.6.1
pkgrel=3
pkgdesc="A desktop publishing program"
arch=('x86_64')
url="https://www.scribus.net"
license=('GPL')
depends=('libcups' 'lcms2' 'qt5-declarative' 'ghostscript' 'libart-lgpl' 'python3' 'libxml2' 'hyphen'
         'cairo' 'desktop-file-utils' 'shared-mime-info' 'ruby' 'aspell' 'podofo' 'librevenge' 'poppler'
         'hunspell' 'graphicsmagick' 'libvisio' 'icu')
makedepends=('cmake' 'boost' 'qt5-tools')
groups=('office')
options=('!libtool' '!makeflags')
source=("https://sourceforge.net/projects/scribus/files/scribus/${pkgver}/${pkgname}-${pkgver}.tar.xz"
#source=("https://github.com/scribusproject/scribus/archive/${_commit}.zip"
         "https://github.com/scribusproject/scribus/pull/177/commits/dc2470990ab73c87fdd60068c99518ced01a3edd.diff"
         'poppler-22.09.patch')
sha256sums=('e09dd78e6db61d01b9321108fededbccd6ec0ab352dd5bafdb8b041f0ef79e99'
            '0c615b740d174d503f459182b00a93703d592ced860158e15812bb22d965e13d'
            'b4bde3e84587252dbfd3a2f310de076929565e2d67898656fbe31f83f10410cf')

prepare() {
  cd ${pkgname}-${pkgver}

  #sed -i 's|pBase->SetOwner(cs->GetOwner());||' scribus/pdf_analyzer.cpp
  # poppler 24.03
  patch -p1 -i ${srcdir}/dc2470990ab73c87fdd60068c99518ced01a3edd.diff
}

build() {
	cmake -B build -S ${pkgname}-${pkgver} \
	   -DCMAKE_INSTALL_PREFIX=/usr \
	   -DCMAKE_BUILD_TYPE=Release \
	   -DCMAKE_SKIP_RPATH=ON \
	   -DWANT_GRAPHICSMAGICK=1 \
	   -DWANT_CPP17=ON \
	   -DLIB_SUFFIX=""
	cmake --build build
}

package() {

    DESTDIR=${pkgdir} cmake --install build
}
