
pkgname=scribus
_pkgname=Scribus
_commit=3742559924136c2471ab15081c5b600dd5feaeb0
pkgver=1.5.6.1
pkgrel=16
pkgdesc="A desktop publishing program"
arch=('x86_64')
url="https://www.scribus.net"
license=('GPL')
depends=('libcups' 'lcms2' 'qt5-declarative' 'ghostscript' 'libart-lgpl' 'python3' 'libxml2' 'hyphen'
         'cairo' 'desktop-file-utils' 'shared-mime-info' 'ruby' 'aspell' 'podofo' 'librevenge' 'poppler'
         'hunspell' 'graphicsmagick' 'libvisio' 'icu')
makedepends=('cmake' 'boost' 'qt5-tools')
groups=('office')
options=('!libtool' '!makeflags')
source=("https://sourceforge.net/projects/scribus/files/scribus-devel/${pkgver}/${pkgname}-${pkgver}.tar.xz"
#source=("https://github.com/scribusproject/scribus/archive/${_commit}.zip"
        "https://github.com/scribusproject/scribus/commit/7ce0ac16fd42d61ef9082b27822c7d9d79c7fef7.diff"
        "https://github.com/scribusproject/scribus/commit/6b9ff916959bcb941866f0bd86da639a421337f8.diff")
sha256sums=('d4257695539cfa40dead8abdaf04c51e34d4d74bcad5a2c934d08e6e9d43b7ab'
            '93c57b873c044131cba1ec07028fdad2da061906c3c4023e91dae85a2dc2c729'
            'c08934b9e111f153b646398983f669a2b179fdb86a30c9dd6515f7b5636ddf03')

prepare() {
  cd ${pkgname}-${pkgver}
  
  sed -i 's|pBase->SetOwner(cs->GetOwner());||' scribus/pdf_analyzer.cpp
  # poppler 21.03.0 needed
  patch -p1 -i ${srcdir}/7ce0ac16fd42d61ef9082b27822c7d9d79c7fef7.diff
  patch -p1 -i ${srcdir}/6b9ff916959bcb941866f0bd86da639a421337f8.diff
}

build() {
	cmake -B build -S ${pkgname}-${pkgver} \
	   -DCMAKE_INSTALL_PREFIX=/usr \
	   -DCMAKE_BUILD_TYPE=Release \
	   -DCMAKE_SKIP_RPATH=ON \
	   -DWANT_GRAPHICSMAGICK=1 \
	   -DLIB_SUFFIX=""
	cmake --build build
}

package() {

    DESTDIR=${pkgdir} cmake --install build
}
