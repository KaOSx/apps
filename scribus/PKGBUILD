
pkgname=scribus
_pkgname=Scribus
_commit=3742559924136c2471ab15081c5b600dd5feaeb0
pkgver=1.5.8
pkgrel=8
pkgdesc="A desktop publishing program"
arch=('x86_64')
url="https://www.scribus.net"
license=('GPL')
depends=('libcups' 'lcms2' 'qt5-declarative' 'ghostscript' 'libart-lgpl' 'python3' 'libxml2' 'hyphen'
         'cairo' 'desktop-file-utils' 'shared-mime-info' 'ruby' 'aspell' 'podofo' 'librevenge' 'poppler'
         'hunspell' 'graphicsmagick' 'libvisio' 'icu')
makedepends=('cmake' 'boost' 'qt5-tools')
groups=('office')
options=('!libtool' '!makeflags')
source=("https://sourceforge.net/projects/scribus/files/scribus-devel/${pkgver}/${pkgname}-${pkgver}.tar.xz"
#source=("https://github.com/scribusproject/scribus/archive/${_commit}.zip"
         "https://github.com/scribusproject/scribus/commit/85c0dff3422fa3c26fbc2e8d8561f597ec24bd92.diff"
         "https://github.com/scribusproject/scribus/commit/f75c1613db67f4067643d0218a2db3235e42ec9f.diff"
         "https://github.com/scribusproject/scribus/commit/f19410ac3b27e33dd62105746784e61e85b90a1d.diff"
         "https://github.com/scribusproject/scribus/commit/f2237b8f0b5cf7690e864a22ef7a63a6d769fa36.diff")
sha256sums=('47816e8fcf6d05788ff16aa4499f97ff22431c777a7789149b0a88b451e16b74'
            '388350c269e4c8f149ed71850d7905cb947f4da5a92b638766bf5b903ee605e2'
            '348c023fb934d92249d78dff908b1790f21231941053ebf35b03f925ec7e4397'
            'a485b7b3cd0c3a86edf372572d8e31e78c75c6a10411a5341ac728c7fd5ed061'
            '0845f08c50732336d17cdf2a9596a2137320a4534f5671636aa012c48ff7e7c2')

prepare() {
  cd ${pkgname}-${pkgver}

  #sed -i 's|pBase->SetOwner(cs->GetOwner());||' scribus/pdf_analyzer.cpp
  # poppler 22.02.0 needed
  patch -p1 -i ${srcdir}/85c0dff3422fa3c26fbc2e8d8561f597ec24bd92.diff
  patch -p1 -i ${srcdir}/f75c1613db67f4067643d0218a2db3235e42ec9f.diff
  # poppler 22.03.0 needed
  patch -p1 -i ${srcdir}/f19410ac3b27e33dd62105746784e61e85b90a1d.diff
  # poppler 22.04.0 needed
  patch -p1 -i ${srcdir}/f2237b8f0b5cf7690e864a22ef7a63a6d769fa36.diff
}

build() {
	cmake -B build -S ${pkgname}-${pkgver} \
	   -DCMAKE_INSTALL_PREFIX=/usr \
	   -DCMAKE_BUILD_TYPE=Release \
	   -DCMAKE_SKIP_RPATH=ON \
	   -DWANT_GRAPHICSMAGICK=1 \
	   -DWANT_CPP17=ON \
	   -DLIB_SUFFIX=""
	cmake --build build
}

package() {

    DESTDIR=${pkgdir} cmake --install build
}
