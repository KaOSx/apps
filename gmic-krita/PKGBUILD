
pkgname=gmic-krita
pkgver=3.1.4.1
pkgrel=1
pkgdesc="Plug-in to bring the full-featured open-source framework for image processing -G'MIC- to Krita"
url="https://gmic.eu/"
license=('custom:CeCILL')
arch=('x86_64')
depends=('qt5-base' 'libpng' 'zlib' 'fftw' 'curl' 'kcoreaddons' 'krita')
makedepends=('cmake' 'qt5-tools' 'extra-cmake-modules')
provides=('gmic-qt')
replaces=('gmic-qt')
conflicts=('gmic-qt')
source=("https://github.com/amyspark/gmic/releases/download/v${pkgver}/gmic-${pkgver}-patched.tar.xz"
        "https://github.com/amyspark/gmic/commit/b2a1c72b45b68720072d93e5e53c77000f611075.diff")
sha256sums=('e655b8affd3f9e0db7e9d4e25ba7c4d63cb958c84bca2da37fa58e310bbf7e9d'
            '07fdbc702c6e89f71e06ffbebd025ca0ebac72e09b174fe2ef4922010b6a9e2e')

prepare() {
  cd gmic-${pkgver}-patched

  #patch -p1 -R -i ${srcdir}/b2a1c72b45b68720072d93e5e53c77000f611075.diff
}

build() {
  cmake -B build -S gmic-${pkgver}-patched/gmic-qt \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DGMIC_QT_HOST=krita-plugin \
    -DENABLE_SYSTEM_GMIC=FALSE
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
  install -Dm644 gmic-${pkgver}-patched/gmic-qt/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}
